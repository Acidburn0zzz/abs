# Contributor: Wolfgang Bumiller <archbsd at bumiller dot com>
pkgname=pkgdepdb
pkgver=0.1.4.1
pkgrel=1
_pkgver=0.1.4.1
pkgdesc="maintain library dependencies of packages in a database"
license=(BSD)
arch=(i686 x86_64)
url=http://github.com/Blub/pkgdepdb
depends=(libarchive pacman)
# THREAD=yes + clang have issues
options=(!fbsd10fix !clang)
conflicts=('readpkgelf')
replaces=('readpkgelf')
source=("${pkgname}-${pkgver}-${pkgrel}.tar.gz::https://github.com/Blub/${pkgname}/archive/${_pkgver}.tar.gz"
        pkgdepdb.conf
        absd-fetchdb.sh)

_db_url='http://users.archbsd.net/~blub/repo-report/current/dep.db.gz'

build() {
	cd "$srcdir/$pkgname-$_pkgver"
	make SYSCONFDIR=/etc ALPM=yes REGEX=yes THREADS=yes
	sed -e "s@%%URL%%@${_db_url}@" "${srcdir}/absd-fetchdb.sh" \
	  > "${srcdir}/absd-fetchdb"
}

package() {
	cd $pkgname-$_pkgver
	make DESTDIR="${pkgdir}" \
		SYSCONFDIR=/etc \
		PREFIX=/usr \
		install

	install -dm755 "${pkgdir}/usr/bin"
	install -m755 "${srcdir}/absd-fetchdb" "${pkgdir}/usr/bin/absd-fetchdb"

	install -dm755 "${pkgdir}/etc"
	install -m644 "${srcdir}/pkgdepdb.conf" "${pkgdir}/etc/pkgdepdb.conf"
}
sha1sums=('428a12fe22ee548283e4d515bc40966119ccea31'
          '5f8fcc613162076b305c480886490e6fa9e9cdec'
          '6fa5dc4c07d921861bd3e254d3eeb545b89c8cfb')
