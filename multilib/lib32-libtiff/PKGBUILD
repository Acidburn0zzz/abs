# Port: graphics/tiff

_pkgbasename=libtiff
pkgname=lib32-libtiff
pkgver=4.0.6
pkgrel=1
pkgdesc="Library for manipulation of TIFF images (32-bit)"
arch=('i386' 'amd64')
url="http://www.remotesensing.org/libtiff/"
license=('custom')
depends=('freebsd-lib32' 'lib32-libjpeg' $_pkgbasename)
options=('!pathfix' 'pathfix32')
patchdir=("tiff-${pkgver}")
source=(ftp://ftp.remotesensing.org/pub/libtiff/tiff-${pkgver}.tar.gz)
sha1sums=('280e27704eaca5f592b82e71ac0c78b87395e2de')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${srcdir}/tiff-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man --libdir=/usr/lib32
  gmake
}

package() {
  cd "${srcdir}/tiff-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
  
  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
