# Port: accessibility/atk
_pkgbasename=atk
pkgname=lib32-atk
pkgver=2.18.0
pkgrel=1
pkgdesc="A library providing a set of interfaces for accessibility (32-bit)"
arch=('i386' 'amd64')
license=('LGPL')
depends=('lib32-glib2>=2.34.3' $_pkgbasename)
options=('!docs' '!pathfix' 'pathfix32')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/${pkgver:0:4}/${_pkgbasename}-${pkgver}.tar.xz)
url='http://www.gtk.org/'
sha256sums=('ce6c48d77bf951083029d5a396dd552d836fff3c1715d3a7022e917e46d0c92b')

build() {
  export CC="$CC -m32"
  export CXX="$CXX -m32"
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig:/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 \
    --disable-introspection
  gmake
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  cd "${pkgdir}/usr/lib32"
}
