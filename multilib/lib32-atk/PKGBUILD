# $Id: PKGBUILD 88730 2013-04-22 07:34:11Z lcarlier $
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Maintainer: Mikko Seppälä <t-r-a-y@mbnet.fi>

_pkgbasename=atk
pkgname=lib32-$_pkgbasename
pkgver=2.12.0
pkgrel=2
pkgdesc="A library providing a set of interfaces for accessibility (32-bit)"
arch=('i386' 'amd64')
license=('LGPL')
depends=('lib32-glib2>=2.34.3' $_pkgbasename)
options=('!docs' '!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/${pkgver%.*}/${_pkgbasename}-${pkgver}.tar.xz)
url='http://www.gtk.org/'
sha256sums=('48a8431974639c5a59c24fcd3ece1a19709872d5dfe78907524d9f5e9993f18f')

prepare() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  find . -name libtool -exec cp /usr/bin/libtool '{}' \;
  find . -name ltmain.sh -exec cp /usr/share/libtool/config/ltmain.sh '{}' \;
}

build() {
  export CC="$CC -m32"
  export CXX="$CXX -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 \
    --disable-introspection
  gmake
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  cd "${pkgdir}/usr/lib32"
}
