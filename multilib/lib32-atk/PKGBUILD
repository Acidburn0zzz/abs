# $Id: PKGBUILD 88730 2013-04-22 07:34:11Z lcarlier $
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Maintainer: Mikko Seppälä <t-r-a-y@mbnet.fi>

_pkgbasename=atk
pkgname=lib32-$_pkgbasename
pkgver=2.10.0
pkgrel=4
pkgdesc="A library providing a set of interfaces for accessibility (32-bit)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-glib2>=2.34.3' $_pkgbasename)
makedepends=('lib32-devel')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/${pkgver%.*}/${_pkgbasename}-${pkgver}.tar.xz)
url='http://www.gtk.org/'
sha256sums=('636917a5036bc851d8491194645d284798ec118919a828be5e713b6ecc5b50b0')

prepare() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  find . -name libtool -exec cp /usr/bin/libtool '{}' \;
  find . -name ltmain.sh -exec cp /usr/share/libtool/config/ltmain.sh '{}' \;
}

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 \
    --disable-introspection
  gmake
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  cd "${pkgdir}/usr/lib32"
}
