# Port: x11/libxcb

_pkgbasename=libxcb
pkgname=lib32-$_pkgbasename
pkgver=1.11.1
pkgrel=1
pkgdesc="X11 client-side library (32-bit)"
arch=('i386' 'amd64')
url="http://xcb.freedesktop.org/"
depends=('lib32-libxdmcp' 'lib32-libxau' 'freebsd-lib32' lib32-libpthread-stubs $_pkgbasename)
makedepends=('pkgconf' 'libxslt' 'python2'
             'autoconf')
options=('!pathfix' 'pathfix32')
license=('custom')
patchdir=("${_pkgbasename}-${pkgver}")
source=($url/dist/$_pkgbasename-$pkgver.tar.bz2)
sha1sums=('28e47aa699d2c2cffed31d1aa2473a9fefe77f17')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  find . -type f | xargs sed -i '' -e 's|-pR|-R|g' -e 's|-fpR|-fR|g'

  export CC="clang -m32"
  export CXX="clang -m32"

  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  PYTHON=/usr/bin/python2 ./autogen.sh \
    --prefix=/usr \
    --enable-xinput \
    --enable-xkb \
    --libdir=/usr/lib32 \
    --disable-static LIBS="`pkg-config --libs pthread-stubs`"
  gmake
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname" 
}
