# Port: graphics/cairo

_pkgbasename=cairo
pkgname=lib32-cairo
pkgver=1.14.6
pkgrel=1
pkgdesc="Cairo vector graphics library (32-bit)"
arch=('i386' 'amd64')
url="http://cairographics.org/"
license=('LGPL' 'MPL')
# we need to avoid old libgl in [extra]
makedepends=('lib32-devel' 'lib32-mesa>=10.2' 'lib32-mesa-libgl>=10.2' 'mesa-libgl')
depends=('lib32-libpng' 'lib32-libxext' 'lib32-libxrender' 'lib32-fontconfig'
         'lib32-pixman' 'lib32-glib2' 'lib32-libxcb' ${_pkgbasename})
source=(http://cairographics.org/releases/${_pkgbasename}-${pkgver}.tar.xz)
options=('!pathfix' 'pathfix32' 'libtoolfix')
sha1sums=('0a59324e6cbe031b5b898ff8b9e2ffceb9d114f5')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i '' -e '/strings/s/ - / -a /' \
    -e '/test/s/==/=/g' \
    -e '/LIBS/s/-lrt//' -e '/LIBS/s|-ldld||' \
    -e '/LIBS/s|-ldl||' \
    configure

  sed -i '' -e 's|-lcairo|-lcairo -lpthread|' \
    src/*.pc.in

  sed -i '' -e '/@CAIRO_HAS_PNG_FUNCTIONS_TRUE@.*=/d' \
    Makefile.in

  sed -i '' -e 's|-ldl||g' \
    util/*/Makefile.in \
    boilerplate/Makefile.in

}

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"

./configure --prefix=/usr \
      --libdir=/usr/lib32 \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-static \
      --enable-tee \
      --disable-gl \
      --disable-egl \
      --disable-directfb \
      --enable-xcb \
      --enable-gobject
      # --disable-xlib-xcb

  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
