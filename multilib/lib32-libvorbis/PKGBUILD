# $Id: PKGBUILD 63966 2012-02-08 18:15:30Z pschmitz $

_pkgbasename=libvorbis
pkgname=lib32-$_pkgbasename
pkgver=1.3.5
pkgrel=1
pkgdesc="Vorbis codec library (32-bit)"
arch=('amd64')
license=('custom')
url="http://www.xiph.org/ogg/vorbis/"
depends=('lib32-libogg' $_pkgbasename)
makedepends=(lib32-devel)
options=('!pathfix' 'pathfix32' 'libtoolfix')
source=(http://downloads.xiph.org/releases/vorbis/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('7220e089f3be3412a2317d6fde9e3944')

build() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  #-march=i386 optimizes too much, strip it out
  CFLAGS=${CFLAGS/-march=$CARCH} ./configure --prefix=/usr --disable-static --libdir=/usr/lib32
  gmake
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
