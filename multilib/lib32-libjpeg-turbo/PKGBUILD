# $Id: PKGBUILD 93059 2013-06-23 14:59:30Z svenstaro $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>

_pkgbasename=libjpeg-turbo
pkgname=lib32-$_pkgbasename
pkgver=1.4.1
pkgrel=2
pkgdesc="libjpeg derivative with accelerated baseline JPEG compression and decompression (32-bit)"
arch=('i386' 'amd64')
url="http://libjpeg-turbo.virtualgl.org/"
license=('GPL' 'custom')
depends=('freebsd-lib32' $_pkgbasename)
makedepends=('nasm' lib32-devel)
provides=('lib32-libjpeg=8.0.2' 'lib32-turbojpeg')
conflicts=('lib32-libjpeg' 'lib32-turbojpeg')
replaces=('lib32-libjpeg' 'lib32-turbojpeg')
options=('!pathfix' 'pathfix32')
source=(http://sourceforge.net/projects/$_pkgbasename/files/$pkgver/$_pkgbasename-$pkgver.tar.gz)
sha1sums=('363a149f644211462c45138a19674f38100036d3')

build() {
  cd "$_pkgbasename-$pkgver"

  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  # it doesn't seem necessary....
  # sed -i "s|NAFLAGS='-felf64 -DELF -D__x86_64__'|NAFLAGS='-felf32 -DELF -D__x86_64__'|" configure
  ./configure --prefix=/usr --with-jpeg8 --mandir=/usr/share/man --libdir=/usr/lib32 --without-simd 

  gmake
}

package() {
  cd "$_pkgbasename-$pkgver"

  gmake -j1 DESTDIR="$pkgdir/" docdir=/usr/share/doc/libjpeg-turbo \
    exampledir=/usr/share/doc/libjpeg-turbo install

  rm -rf "${pkgdir}"/usr/{include,share,bin,sbin}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
  cd "${pkgdir}/usr/lib32/"
  ln -s libjpeg.so.8.2 "libjpeg.so"
}
