# Port: graphics/jpeg-turbo

_pkgbasename=libjpeg-turbo
pkgname=lib32-libjpeg-turbo
pkgver=1.4.2
pkgrel=2
pkgdesc="libjpeg derivative with accelerated baseline JPEG compression and decompression (32-bit)"
arch=('i386' 'amd64')
url="http://libjpeg-turbo.virtualgl.org/"
license=('GPL' 'custom')
depends=('freebsd-lib32' $_pkgbasename)
makedepends=('nasm')
provides=('lib32-libjpeg=8.0.2' 'lib32-turbojpeg')
conflicts=('lib32-libjpeg' 'lib32-turbojpeg')
replaces=('lib32-libjpeg' 'lib32-turbojpeg')
options=('!pathfix' 'pathfix32')
patchdir=("$_pkgbasename-$pkgver")
source=(http://sourceforge.net/projects/$_pkgbasename/files/$pkgver/$_pkgbasename-$pkgver.tar.gz)
sha1sums=('2666158ccd5318513f875867bbc4af52f6eb9f0b')

build() {
  cd "$_pkgbasename-$pkgver"

  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig:/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  ./configure --prefix=/usr --with-jpeg8 --mandir=/usr/share/man --libdir=/usr/lib32 --without-simd 

  gmake
}

package() {
  cd "$_pkgbasename-$pkgver"

  gmake -j1 DESTDIR="$pkgdir/" docdir=/usr/share/doc/libjpeg-turbo \
    exampledir=/usr/share/doc/libjpeg-turbo install

  rm -rf "${pkgdir}"/usr/{include,share,bin,sbin}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
