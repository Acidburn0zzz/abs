# Port: x11/libXi

_pkgbasename=libxi
pkgname=lib32-libxi
pkgver=1.7.6
pkgrel=1
pkgdesc="X11 Input extension library (32-bit)"
arch=('i386' 'amd64')
url="http://xorg.freedesktop.org"
depends=('lib32-libxext' 'lib32-libxfixes' 'inputproto' $_pkgbasename)
makedepends=('pkgconf' 'xorg-util-macros' automake)
options=('!pathfix' 'pathfix32')
license=('custom')
source=(${url}/releases/individual/lib/libXi-${pkgver}.tar.bz2)
sha256sums=('1f32a552cec0f056c0260bdb32e853cec0673d2f40646ce932ad5a9f0205b7ac')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${srcdir}/libXi-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --disable-static \
    --libdir=/usr/lib32 --enable-malloc0returnsnull

  gmake
}

package() {
  cd "${srcdir}/libXi-${pkgver}"

  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
