# $Id: PKGBUILD 93400 2013-07-03 08:15:50Z lcarlier $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxi
pkgname=lib32-$_pkgbasename
pkgver=1.7.5
pkgrel=1
pkgdesc="X11 Input extension library (32-bit)"
arch=('i386' 'amd64')
url="http://xorg.freedesktop.org"
depends=('lib32-libxext' 'lib32-libxfixes' 'inputproto' $_pkgbasename)
makedepends=('pkgconf' 'xorg-util-macros' lib32-devel automake)
options=('!pathfix' 'pathfix32')
license=('custom')
source=(${url}/releases/individual/lib/libXi-${pkgver}.tar.bz2)
sha256sums=('d964d7deb5d8f7d6b9c358969c625073d7ab273dbda94693130b3540bc0ca229')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${srcdir}/libXi-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --disable-static \
    --libdir=/usr/lib32

  gmake
}

package() {
  cd "${srcdir}/libXi-${pkgver}"

  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
