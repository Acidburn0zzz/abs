_pkgbasename=libdrm
pkgname=lib32-$_pkgbasename
pkgver=2.4.65
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services (32-bit)"
arch=(x86_64)
license=('custom')
depends=('lib32-libpciaccess' $_pkgbasename 'lib32-libpthread-stubs')
makedepends=('lib32-devel' 'lib32-cairo')
options=('!libtool')
url="http://dri.freedesktop.org/"
port_patches=(
	patch-configure
	patch-tests_modetest_Makefile.in
	patch-tests_modetest_modetest.c
	patch-tests_modetest_strchrnul.c
)
source=(http://dri.freedesktop.org/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.bz2
	${port_patches[@]})
sha256sums=('e20fbbe092177a8422913d8884a1255477456ab5b10b07389fa891a4dce54030'
            '4fa2a5f05a82042827ad6dafe48a419a8a2b118695e4ee5613f9a46f785b2ba9'
            '374aadae17acb72e82e66fdabcbf9756d6da64ebc312fcf25b068a2bf0eddc48'
            '5016eb84619b10275f2c2ae785cc6d7cc580b696989b06cc43cd215bd8844fff'
            'abb6821f559992111c366199a75f8c0277c7f21d09c27f0d34e8158a6a04083a')

prepare() {
  cd "${_pkgbasename}-${pkgver}"

  # man/Makefile,in is missing
  sed -i '' -e '/86|x86_64) INTEL=yes/ s@x86_64@&|amd64@' configure

  #libtoolize --force
  autoreconf --force --install

  # NOTE: HAS TO BE AFTER autoreconf
  for i in ${_port_patches[@]}; do
    msg "port patch $i"
    patch -p0 -i "${srcdir}/$i"
  done

  gsed -i -e '/#include <sys\/mman\.h>/i#define ERESTART (-1)' \
    libkms/vmwgfx.c
}

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  # libtoolize --force
  ./configure --prefix=/usr --libdir=/usr/lib32 \
      --enable-libkms --disable-cairo-tests

  gmake
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
