# Port: graphics/libdrm

_pkgbasename=libdrm
pkgname=lib32-libdrm
pkgver=2.4.66
pkgrel=2
pkgdesc="Userspace interface to kernel DRM services (32-bit)"
arch=('amd64')
license=('custom')
depends=('lib32-libpciaccess' $_pkgbasename 'lib32-libpthread-stubs')
options=('libtoolfix' '!pathfix' 'pathfix32')
patchdir=("$_pkgbasename-$pkgver")
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('79cb8e988749794edfb2d777b298d5292eff353bbbb71ed813589e61d2bc2d76')

prepare() {
  cd $_pkgbasename-$pkgver

  # man/Makefile,in is missing
  sed -i '' -e 's|{libdir}/pkgconfig|{prefix}/libdata32/pkgconfig|g' \
    -e 's,i?86|x86_64),i?86|amd64|x86_64),g' \
    configure

}

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  # libtoolize --force
  ./configure --prefix=/usr --libdir=/usr/lib32 \
    --enable-libkms \
    --disable-vmwgfx 

  gmake
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
