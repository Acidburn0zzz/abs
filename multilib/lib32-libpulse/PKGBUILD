# Port: audio/pulseaudio

_pkgbasename=libpulse
pkgname=lib32-libpulse
pkgdesc="A featureful, general-purpose sound server (32-bit client libraries)"
pkgver=8.0
pkgrel=1
arch=('amd64')
url="http://pulseaudio.org/"
license=(GPL LGPL)
depends=(lib32-dbus-core lib32-json-c
         lib32-libxtst lib32-libsm lib32-libsndfile
         $_pkgbasename)
makedepends=(intltool lib32-glib2 lib32-speex lib32-gtk2 lib32-libltdl)
optdepends=('lib32-alsa-plugins: ALSA support')
provides=("lib32-pulseaudio=$pkgver")
conflicts=(lib32-pulseaudio)
replaces=(lib32-pulseaudio)
options=(!emptydirs !pathfix pathfix32 libtoolfix)
patchdir=("pulseaudio-$pkgver")
source=(http://freedesktop.org/software/pulseaudio/releases/pulseaudio-$pkgver.tar.xz)
sha256sums=('690eefe28633466cfd1ab9d85ebfa9376f6b622deec6bfee5091ac9737cd1989')

_libs="libpulse.la libpulse-simple.la libpulse-mainloop-glib.la"
_pkglibs="libpulsecommon-${pkgver}.la libpulsedsp.la"

prepare() {
  cd pulseaudio-$pkgver

  sed -i '' -e 's|%%LOCALBASE%%|/usr/|g' \
        src/daemon/default.pa.in
}

build() {
  cd pulseaudio-$pkgver

  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib32 \
    --localstatedir=/var \
    --with-database=simple \
    --without-caps \
    --disable-udev \
    --disable-gtk2 \
    --disable-lirc \
    --disable-tcpwrap \
    --disable-rpath \
    --disable-default-build-tests

  gmake -C src $_libs $_pkglibs
}

package() {
  cd pulseaudio-$pkgver
  gmake -j1 -C src DESTDIR="$pkgdir" \
    lib_LTLIBRARIES="$_libs" pkglib_LTLIBRARIES="$_pkglibs" \
    install-libLTLIBRARIES install-pkglibLTLIBRARIES
  gmake -j1 DESTDIR="$pkgdir" install-pkgconfigDATA
}
