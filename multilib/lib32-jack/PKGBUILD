# $Id: PKGBUILD 56009 2011-09-28 20:00:34Z bluewind $
# Maintainer: tobias <tobias@archlinux.net>
# Maintainer: Ray Rashif <schivmeister@gmail.com>
# Contributor: Robert Emil Berge <robert@rebi.no>

_pkgbasename=jack
pkgname=lib32-$_pkgbasename
_longname=jack-audio-connection-kit
pkgver=0.121.3
pkgrel=8
pkgdesc="A low-latency audio server (32-bit)"
arch=('i386' 'amd64')
license=('GPL' 'LGPL')
depends=('freebsd-lib32' "$_pkgbasename=$pkgver")
makedepends=(lib32-devel)
url="http://jackaudio.org/"
options=('!libtool')
_port_patches=(
        patch-drivers-alsa-midi-alsa_rawmidi.c
        patch-drivers-alsa_memops.c
        patch-drivers-dummy-dummy_driver.c
        patch-drivers-dummy-dummy_driver.h
        patch-drivers-oss-oss_driver.c
        patch-jack.pc.in
        patch-jackd_engine.c)
source=("http://distcache.FreeBSD.org/ports-distfiles/$_longname-$pkgver.tar.gz"
        "${_port_patches[@]}")
md5sums=('35f470f7422c37b33eb965033f7a42e8'
         '3ee25edeee16b80e99e4bfde36670c77'
         '5ce061874dd4ea4f7617131ef63b2f96'
         'd76aacaa5720599ceec138ac11dd6a87'
         'd225060bd5e674c7a98ba4ea16194995'
         '06503a33a3b98d10e369b1b4d60f3058'
         '4983c713973124294328e7cf65134cb4'
         '0a0a93c472b09e96a136eeef3362c759')

prepare() {
  cd "$srcdir/$_longname-$pkgver"

  for i in "${_port_patches[@]}"; do
    patch -p0 -i ${srcdir}/$i
  done

  sed -i '' -e 's|define USE_MLOCK|undef USE_MLOCK|' \
      configure
  sed -i '' -e "s,-lpthread,-pthread," \
      configure \
      libjack/Makefile.in \
      jackd/Makefile.in \
      example-clients/Makefile.in \
      jack.pc.in
  sed -i '' -e "s,-lrt,," libjack/Makefile.in \
      jackd/Makefile.in \
      example-clients/Makefile.in
  sed -i '' -e "s,-ldl,," jackd/Makefile.in \
      example-clients/Makefile.in
  sed -i '' -e 's|md5sum|md5 -q|g' \
      jackd/Makefile.in
}

build() {
  cd ${srcdir}/${_longname}-${pkgver}

  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  ./configure --prefix=/usr --libdir=/usr/lib32
  gmake -C libjack
}

package() {
  cd ${srcdir}/${_longname}-${pkgver}

  gmake DESTDIR=${pkgdir} install-pkgconfigDATA
  gmake -C libjack DESTDIR=${pkgdir} install
}

# vim:set ts=2 sw=2 et:
