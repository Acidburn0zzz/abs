# Port: x11-toolkits/libXt
_pkgbasename=libxt
pkgname=lib32-libxt
pkgver=1.1.5
pkgrel=1
pkgdesc="X11 toolkit intrinsics library (32-bit)"
arch=('i386' 'amd64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libsm' 'lib32-libx11' $_pkgbasename)
makedepends=('xorg-util-macros')
options=('!pathfix' 'pathfix32')
source=(${url}/releases/individual/lib/libXt-${pkgver}.tar.bz2)
sha1sums=('c79e2c4f7de5259a2ade458817a139b66a043d59')

prepare() {
  cd "libXt-${pkgver}"
  sed -i '' -e 's/test.*-traditional.*;/true;/' \
    configure
}

build() {
  cd "libXt-${pkgver}"

  export CC="clang -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --libdir=/usr/lib32
  gmake
}

package() {
  cd "${srcdir}/libXt-${pkgver}"

  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname" 
}
