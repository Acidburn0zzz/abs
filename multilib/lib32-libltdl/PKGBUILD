# $Id: PKGBUILD 168333 2012-10-08 22:50:22Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>
# Contributor: Wolfgang Bumiller <blub@speed.at>

# Arch PKGBUILD:
# # NOTE: requires rebuild with each new gcc version

_pkgbasename=libtool
pkgname=('lib32-libltdl')
pkgver=2.4.6
pkgrel=1
pkgdesc="A system independent dlopen wrapper for GNU libtool (32-bit)"
arch=('amd64')
url="http://www.gnu.org/software/libtool"
license=('GPL')
depends=(freebsd-lib32 libltdl)
replaces=(lib32-libtool)
provides=("lib32-libtool=$pkgver-$pkgrel")
conflicts=(lib32-libtool)
makedepends=('lib32-devel')
source=(ftp://ftp.gnu.org/pub/gnu/libtool/${_pkgbasename}-${pkgver}.tar.xz{,.sig})
md5sums=('1bfb9b923f2c1339b4d2ce1807064aa5'
         'SKIP')

prepare() {
  cd ${_pkgbasename}-${pkgver}

  sed -i "" -e 's|sed gsed|sed|g' \
            -e 's|CC=cc|CC=${CC}|g' \
      configure libltdl/configure

}

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd ${srcdir}/${_pkgbasename}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --libdir=/usr/lib32

  find . -type f | xargs touch -f

  make
}

package_lib32-libltdl() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  make DESTDIR=${pkgdir} install-libLTLIBRARIES install-includeHEADERS \
                         install-ltdlincludeHEADERS install-data-local
  rm -rf ${pkgdir}/usr/{share,include}
}
