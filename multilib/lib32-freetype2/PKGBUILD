# Port: print/freetype2

_pkgbasename=freetype2
pkgname=lib32-$_pkgbasename
pkgver=2.6.3
pkgrel=2
pkgdesc="TrueType font rendering library (32-bit)"
arch=('amd64')
license=('GPL')
url="http://freetype.sourceforge.net"
depends=('freebsd-lib32' $_pkgbasename 'lib32-libpng')
BREAK_DEPENDENCY_CYCLES=1
if ! ((BREAK_DEPENDENCY_CYCLES)); then
  depends+=('lib32-harfbuzz')
fi
options=('!pathfix' 'pathfix32')
source=(http://download.savannah.gnu.org/releases/freetype/freetype-${pkgver}.tar.bz2{,.sig}
	"0001-Enable-table-validation-modules.patch"
	"0002-Enable-subpixel-rendering.patch"
	"0003-Enable-subpixel-hinting.patch"
	"0004-Mask-subpixel-hinting-with-an-env-var.patch")
md5sums=('0037b25a8c090bc8a1218e867b32beb1'
         'SKIP'
         '65adad2bff23068d5663d11dad3ce0b9'
         '3e9ba05d51b4a15575fdebb303c0cb69'
         '6b5af58ea0144bc9adfc170ced06197c'
         'fb0e6544a2100d63a903826e4051b806')

prepare() {
  cd "${srcdir}/freetype-${pkgver}"

  patch -Np1 -i "${srcdir}/0001-Enable-table-validation-modules.patch"
  patch -Np1 -i "${srcdir}/0002-Enable-subpixel-rendering.patch"

  # https://bugs.archlinux.org/task/35274
  patch -Np1 -i "${srcdir}/0003-Enable-subpixel-hinting.patch"
  # Provide a way to enable the above patch at runtime.
  # Hopefully just a temporary measure until fontconfig picks up
  # the necessary configurables.
  patch -Np1 -i "${srcdir}/0004-Mask-subpixel-hinting-with-an-env-var.patch"

}

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "freetype-${pkgver}"

if ((BREAK_DEPENDENCY_CYCLES)); then
  conf_args="--without-harfbuzz"
fi

  GNUMAKE=$(which gmake) \
  ./configure --prefix=/usr --libdir=/usr/lib32 --without-png "${conf_args}"
  gmake
}

check() {
  cd "${srcdir}/freetype-${pkgver}"
  gmake -k check
}

package() {
  cd "${srcdir}/freetype-${pkgver}"

  gmake -j1 DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
