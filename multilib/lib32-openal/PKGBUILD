# Port: audio/openal-soft

_pkgbasename=openal
pkgname=lib32-openal
pkgver=1.16.0
pkgrel=2
pkgdesc="OpenAL audio library for use with opengl (32-bit)"
arch=('i386' 'amd64')
url="http://www.openal.org/"
license=('LGPL')
depends=('freebsd-lib32' $_pkgbasename)
makedepends=('lib32-sdl' 'lib32-libpulse' 'pkgconf' 'cmake')
options=('libtoolfix' '!pathfix' 'pathfix32')
patchdir=("${_pkgbasename}-soft-${pkgver}")
source=(http://kcat.strangesoft.net/openal-releases/openal-soft-${pkgver}.tar.bz2)
md5sums=('14db4b0d12f07252070b4fe5eb5911f6')

build() {

  export CC="clang -m32"
  export CXX="clang++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  mkdir -p ${_pkgbasename}-soft-${pkgver}/build
  cd ${srcdir}/${_pkgbasename}-soft-${pkgver}/build

  cmake -D CMAKE_INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release -DALSOFT_REQUIRE_OSS:BOOL=ON -D LIB_SUFFIX=32 ..
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-soft-${pkgver}/build
  make DESTDIR=${pkgdir}/ install 

  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
