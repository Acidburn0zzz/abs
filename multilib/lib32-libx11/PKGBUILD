_pkgbasename=libx11
pkgname=lib32-$_pkgbasename
pkgver=1.6.3
pkgrel=1
pkgdesc="X11 client-side library (32-bit)"
arch=('i386' 'amd64')
url="http://xorg.freedesktop.org/"
depends=('lib32-libxcb' $_pkgbasename)
makedepends=('xorg-util-macros' 'xextproto' 'xtrans' 'inputproto' 'lib32-devel')
options=('!libtool' '!pathfix' 'pathfix32')
license=('custom:XFREE86')
source=(${url}/releases/individual/lib/libX11-${pkgver}.tar.bz2)
sha256sums=('cf31a7c39f2f52e8ebd0db95640384e63451f9b014eed2bb7f5de03e8adc8111')

build() {
  cd "${srcdir}/libX11-${pkgver}"
  export CC="clang -m32"
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig"
  export CPP=clang-cpp
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  sed -i '' -e 's/test.*-traditional.*;/true;/' \
    configure
  sed -i '' -e '/^install-data-am:/,/^$/ s/install-specDATA//' \
      nls/Makefile.in

  ./configure --prefix=/usr --disable-static --disable-xf86bigfont \
      --libdir=/usr/lib32 --disable-specs
  gmake
}

check() {
  cd "${srcdir}/libX11-${pkgver}"

  gmake check
}

package() {
  cd "${srcdir}/libX11-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
