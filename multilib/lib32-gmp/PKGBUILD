#

_pkgbasename=gmp
pkgname=lib32-$_pkgbasename
pkgver=6.0.0
pkgrel=5
pkgdesc="A free library for arbitrary precision arithmetic (32-bit)"
arch=('i386' 'amd64')
url="http://gmplib.org/"
depends=('freebsd-lib32' $_pkgbasename 'lib32-gcc-libs')
license=('LGPL3')
options=('!libtool' 'libtoolfix' '!pathfix' 'pathfix32')
source=(ftp://ftp.gmplib.org/pub/gmp-${pkgver}/gmp-${pkgver}a.tar.xz{,.sig})
md5sums=('1e6da4e434553d2811437aa42c7f7c76'
         'SKIP')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  # so the tests can run:
  gsed -i 's/shlibpath_var=.*/shlibpath_var=LD_32_LIBRARY_PATH/g' configure

  export ABI=32
  ./configure \
    --prefix=/usr --infodir=/usr/share/info \
    --enable-cxx --libdir=/usr/lib32 \
    --includedir=/usr/lib32/gmp

  #Put gmp.h in the same folder as gmpxx.h
  gsed -i 's/$(exec_prefix)\/include/$\(includedir\)/' Makefile

  gmake
}

check() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  gmake check
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  gmake -j1 DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
