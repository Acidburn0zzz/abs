# Port: dns/libidn
_pkgbasename=libidn
pkgname=lib32-libidn
pkgver=1.33
pkgrel=1
pkgdesc="Implementation of the Stringprep, Punycode and IDNA specifications (32 bit)"
url="http://www.gnu.org/software/libidn/"
arch=('i386' 'amd64')
license=('GPL3' 'LGPL')
depends=('freebsd-lib32' "$_pkgbasename>=$pkgver")
options=('libtoolfix' '!pathfix' 'pathfix32')
patchdir=("${_pkgbasename}-${pkgver}")
source=(http://ftp.gnu.org/gnu/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.gz)
sha256sums=('44a7aab635bb721ceef6beecc4d49dfd19478325e1b47f3196f7d2acc4930e19')

build() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"
  ./configure --prefix=/usr --libdir=/usr/lib32 CC='clang -m32'
  gmake
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  gmake DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/{bin,include,share}
}
