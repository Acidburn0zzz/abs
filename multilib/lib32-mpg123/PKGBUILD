# Port: audio/mpg123

_pkgbasename=mpg123
pkgname=lib32-$_pkgbasename
pkgver=1.23.6
pkgrel=1
pkgdesc="A console based real time MPEG Audio Player for Layer 1, 2 and 3 (32-bit)"
arch=('i386' 'amd64')
url="http://sourceforge.net/projects/mpg123"
license=('GPL2' 'LGPL2.1')
depends=('freebsd-lib32' 'lib32-libltdl' $_pkgbasename)
options=('libtoolfix' '!pathfix' 'pathfix32')
source=(http://downloads.sourceforge.net/sourceforge/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.bz2)
md5sums=('ed22a3e664f076fa05131a3454ef8166')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  
  ./configure --prefix=/usr \
    --with-audio="oss" \
    --with-cpu=i586 \
    --libdir=/usr/lib32 \
    --with-module-suffix=.so \
    --with-default-audio=oss 

  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
