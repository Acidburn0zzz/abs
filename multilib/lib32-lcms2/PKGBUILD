# Port: graphics/lcms2

_pkgbasename=lcms2
pkgname=lib32-lcms2
pkgver=2.7
pkgrel=1
pkgdesc="Small-footprint color management engine, version 2 (32-bit)"
arch=('i386' 'amd64')
license=('MIT')
depends=('lib32-libtiff' ${_pkgbasename})
url="http://www.littlecms.com"
options=('libtoolfix' '!pathfix' 'pathfix32')
patchdir=("${_pkgbasename}-${pkgver}")
source=(http://downloads.sourceforge.net/sourceforge/lcms/${_pkgbasename}-${pkgver}.tar.gz)
sha1sums=('625f0d74bad4a0f6f917120fd992437d26f754d2')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/libdata32/pkgconfig"

  cd ${_pkgbasename}-${pkgver}

  ./configure --prefix=/usr --libdir=/usr/lib32
  gmake
}

package() {
  cd ${_pkgbasename}-${pkgver}

  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "${pkgdir}/usr/share/licenses"
  ln -s ${_pkgbasename} "${pkgdir}/usr/share/licenses/${pkgname}"
}
