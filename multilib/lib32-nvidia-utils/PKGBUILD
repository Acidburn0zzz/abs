# $Id: PKGBUILD 93241 2013-06-29 10:14:01Z foutrelis $
# Maintainer: Thomas Baechler <thomas@archlinux.org>
# Contributor: James Rayner <iphitus@gmail.com>

_pkgbasename=nvidia-utils
pkgbase=lib32-$_pkgbasename
pkgname=('lib32-nvidia-utils') # 'lib32-opencl-nvidia')
pkgver=355.11
pkgrel=1
arch=('i386' 'amd64')
url="http://www.nvidia.com/"
license=('custom')
options=('!strip')

_arch='x86'
_pkg="NVIDIA-FreeBSD-${_arch}-${pkgver}"
source=("ftp://download.nvidia.com/XFree86/FreeBSD-${_arch}/${pkgver}/${_pkg}.tar.gz")
sha1sums=('877896005aecb65ca25fa72f8a45df236acc80d2')

prepare() {
    cd "${srcdir}/NVIDIA-FreeBSD-${_arch}-${pkgver}/"
# Don't try compiling the kernel module:
    sed -i '' -e 's/SUBDIR=.*src/SUBDIR=/' Makefile
}

build() {
    cd "${srcdir}/NVIDIA-FreeBSD-${_arch}-${pkgver}/"
    unset CFLAGS LDFLAGS CPPFLAGS CXXFLAGS

    make -j1
}

create_links() {
    # create soname links
    for _lib in $(find "${pkgdir}" -name '*.so*' | grep -v 'xorg/'); do
        _soname=$(dirname "${_lib}")/$(readelf -d "${_lib}" | sed -nr 's/.*Library soname: \[(.*)\].*/\1/p')
        _base=$(echo ${_soname} | sed -r 's/(.*).so.*/\1.so/')
        [[ -e "${_soname}" ]] || ln -s $(basename "${_lib}") "${_soname}"
        [[ -e "${_base}" ]] || ln -s $(basename "${_soname}") "${_base}"
    done
}


package() {
    pkgdesc="NVIDIA drivers utilities (32-bit)"
    depends=('nvidia-utils')
    conflicts=('lib32-libgl')
    provides=('lib32-libgl')

    cd "${_pkg}/obj"


    # GLX extension module for X
    ginstall -D -m755 "libglx.so.1" "${pkgdir}/usr/lib32/nvidia/xorg/modules/extensions/libglx.so.1"
    ln -s "libglx.so.1" "${pkgdir}/usr/lib32/nvidia/xorg/modules/extensions/libglx.so"	# X doesn't find glx otherwise

    # OpenGL libraries
    ginstall -D -m755 "libEGL.so.1" "${pkgdir}/usr/lib32/nvidia/libEGL.so.1"
    ginstall -D -m755 "libGLESv1_CM.so.1" "${pkgdir}/usr/lib32/libGLESv1_CM.so.1"
    ginstall -D -m755 "libGLESv2.so.2" "${pkgdir}/usr/lib32/libGLESv2.so.2"

    # OpenGL core library
    ginstall -D -m755 "libnvidia-glcore.so.1" "${pkgdir}/usr/lib32/libnvidia-glcore.so.1"
    ginstall -D -m755 "libnvidia-eglcore.so.1" "${pkgdir}/usr/lib32/libnvidia-eglcore.so.1"
    ginstall -D -m755 "libnvidia-glsi.so.1" "${pkgdir}/usr/lib32/libnvidia-glsi.so.1"

    # VDPAU
    ginstall -D -m755 "libvdpau_nvidia.so.1" "${pkgdir}/usr/lib32/vdpau/libvdpau_nvidia.so.1"

    # nvidia-tls library
    ginstall -D -m755 "libnvidia-tls.so.1" "${pkgdir}/usr/lib32/libnvidia-tls.so.1"

    create_links

    mkdir -p "${pkgdir}/usr/lib32"
    ln -s "/usr/lib32/nvidia/libGL.so.1" "${pkgdir}/usr/lib32/libGL.so.1"
    ln -s "libGL.so.1" "${pkgdir}/usr/lib32/libGL.so"

    ln -s "/usr/lib32/nvidia/libEGL.so.1" "${pkgdir}/usr/lib32/libEGL.so.1"
    ln -s "libEGL.so.1" "${pkgdir}/usr/lib32/libEGL.so"

    rm -rf "${pkgdir}"/usr/{include,share,bin}
    mkdir -p "${pkgdir}/usr/share/licenses"
    ln -s $_pkgbasename "${pkgdir}/usr/share/licenses/${pkgname}"
}

