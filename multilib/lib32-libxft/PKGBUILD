# Port: x11-fonts/libxft
_pkgbasename=libxft
pkgname=lib32-libxft
pkgver=2.3.2
pkgrel=1
pkgdesc="FreeType-based font drawing library for X (32-bit)"
arch=('i386' 'amd64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('lib32-fontconfig' 'lib32-libxrender')
options=('!pathfix' 'pathfix32')
source=(${url}/releases/individual/lib/libXft-${pkgver}.tar.bz2)
sha256sums=('f5a3c824761df351ca91827ac221090943ef28b248573486050de89f4bfcdc4c')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd ${srcdir}/libXft-${pkgver}
  ./configure --prefix=/usr \
    --libdir=/usr/lib32 --disable-static
  gmake
}

package() {
  cd "${srcdir}/libXft-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{bin,include,share}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
