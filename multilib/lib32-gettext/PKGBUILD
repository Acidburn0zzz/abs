_pkgbasename=gettext-runtime
_pkgname=gettext
pkgname=lib32-$_pkgbasename
pkgver=0.19.3
pkgrel=1
pkgdesc="GNU internationalization library (32-bit)"
arch=('amd64')
url="http://www.gnu.org/software/gettext/"
license=('GPL')
depends=(lib32-devel $_pkgbasename)
optdepends=('cvs: for autopoint tool')
options=(!libtool !docs '!pathfix' 'pathfix32')
patchdir=("${_pkgname}-${pkgver}/gettext-runtime")
source=(ftp://ftp.gnu.org/pub/gnu/gettext/${_pkgname}-${pkgver}.tar.gz{,.sig})
md5sums=('c365029ffc866fc4e485d9e5ca60b260'
         'SKIP')

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  find . -name configure -print | xargs sed -i '' -e 's|mkdir gmkdir|mkdir|'
}

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${srcdir}/${_pkgname}-${pkgver}"

  EMACS="no" \
  ./configure --prefix=/usr --libdir=/usr/lib32 \
              --disable-csharp --disable-threads --disable-openmp \
              --with-included-libcroco \
              --without-git
  gmake
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
