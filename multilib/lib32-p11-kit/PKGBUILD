# Port: security/p11-kit

_pkgbasename=p11-kit
pkgname=lib32-p11-kit
pkgver=0.23.2
pkgrel=1
pkgdesc="Library to work with PKCS#11 modules (32-bit)"
arch=('i386' 'amd64')
url="http://p11-glue.freedesktop.org"
license=('BSD')
depends=(freebsd-lib32 lib32-libtasn1 lib32-libffi $_pkgbasename ca_root_nss)
options=('!pathfix' 'pathfix32' 'libtoolfix')
source=($url/releases/$_pkgbasename-$pkgver.tar.gz{,.sig})
md5sums=('738af2442331fc22f440df9bee9b062a'
         'SKIP')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "$_pkgbasename-$pkgver"
  autoreconf
  ./configure --prefix=/usr --libdir=/usr/lib32 --sysconfdir=/etc \
    --with-module-path=/usr/lib32/pkcs11 \
    --with-trust-paths=/usr/share/certs/ca-root-nss.crt \
    LDFLAGS="-pthread"

  gmake
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  gmake DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/etc
  rm -rf "${pkgdir}"/usr/{bin,include,share}
  install -dm755 $pkgdir/usr/share/licenses/$pkgname
  install -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}

# vim:set ts=2 sw=2 et:
