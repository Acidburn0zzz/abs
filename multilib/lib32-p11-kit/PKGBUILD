# $Id: PKGBUILD 91811 2013-05-28 11:37:37Z lcarlier $
# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Christoph Vigano <mail@cvigano.de>

_pkgbasename=p11-kit
pkgname=lib32-$_pkgbasename
pkgver=0.20.1
pkgrel=2
pkgdesc="Library to work with PKCS#11 modules (32-bit)"
arch=(x86_64)
url="http://p11-glue.freedesktop.org"
license=('BSD')
depends=(freebsd-lib32 lib32-libtasn1 $_pkgbasename)
makedepends=(lib32-devel lib32-libffi)
options=(!libtool)
source=($url/releases/$_pkgbasename-$pkgver.tar.gz{,.sig})
md5sums=('88c651137f76a167336639371eafd8cc'
         'SKIP')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "$_pkgbasename-$pkgver"
  autoreconf
  ./configure --prefix=/usr --libdir=/usr/lib32 --sysconfdir=/etc \
    --with-module-path=/usr/lib32/pkcs11 \
    --without-trust-paths
  gmake
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  gmake DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/etc
  rm -rf "${pkgdir}"/usr/{bin,include,share}
  install -dm755 $pkgdir/usr/share/licenses/$pkgname
  install -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}

# vim:set ts=2 sw=2 et:
