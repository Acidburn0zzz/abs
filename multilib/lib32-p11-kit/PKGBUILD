# $Id: PKGBUILD 91811 2013-05-28 11:37:37Z lcarlier $
# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Christoph Vigano <mail@cvigano.de>

_pkgbasename=p11-kit
pkgname=lib32-$_pkgbasename
pkgver=0.20.2
pkgrel=1
pkgdesc="Library to work with PKCS#11 modules (32-bit)"
arch=(x86_64)
url="http://p11-glue.freedesktop.org"
license=('BSD')
depends=(freebsd-lib32 lib32-libtasn1 lib32-libffi $_pkgbasename ca_root_nss)
makedepends=(lib32-devel)
options=(!libtool)
source=($url/releases/$_pkgbasename-$pkgver.tar.gz{,.sig})
md5sums=('757b97ee4ac0ce598661a90cd784c4f1'
         'SKIP')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "$_pkgbasename-$pkgver"
  autoreconf
  ./configure --prefix=/usr --libdir=/usr/lib32 --sysconfdir=/etc \
    --with-module-path=/usr/lib32/pkcs11 \
    --with-trust-paths=/usr/share/certs/ca-root-nss.crt \
    LDFLAGS="-pthread"

  gmake
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  gmake DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/etc
  rm -rf "${pkgdir}"/usr/{bin,include,share}
  install -dm755 $pkgdir/usr/share/licenses/$pkgname
  install -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}

# vim:set ts=2 sw=2 et:
