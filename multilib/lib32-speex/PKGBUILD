# $Id: PKGBUILD 64032 2012-02-08 19:00:07Z pschmitz $
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

_pkgbasename=speex
pkgname=lib32-$_pkgbasename
pkgver=1.2rc2
pkgrel=1
pkgdesc="A free codec for free speech (32-bit)"
arch=('i386' 'amd64')
license=('BSD')
depends=('freebsd-lib32' 'lib32-libogg' $_pkgbasename)
makedepends=(lib32-devel)
options=('!pathfix' 'pathfix32' 'libtoolfix')
source=(http://downloads.us.xiph.org/releases/$_pkgbasename/$_pkgbasename-$pkgver.tar.gz)
md5sums=('6ae7db3bab01e1d4b86bacfa8ca33e81')
url="http://www.speex.org/"

build() {
  cd $srcdir/speex-$pkgver

  export CC="clang -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libdir=/usr/lib32 \
    --with-ogg-libraries=/usr/lib32
  make
}

package() {
  cd $srcdir/speex-$pkgver
  make -j1 DESTDIR=$pkgdir install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
