# Port: security/libtasn1

_pkgbase=libtasn1
pkgname=lib32-libtasn1
pkgver=4.9
pkgrel=1
pkgdesc="The ASN.1 library used in GNUTLS (32 bit)"
arch=('i386' 'amd64')
license=('GPL3' 'LGPL')
url="http://www.gnu.org/software/libtasn1/"
depends=('freebsd-lib32' ${_pkgbase})
options=('!pathfix' 'pathfix32' 'libtoolfix')
source=(http://ftp.gnu.org/gnu/libtasn1/${_pkgbase}-${pkgver}.tar.gz{,.sig})
validpgpkeys=('1F42418905D8206AA754CCDC29EE58B996865171') #Nikos Mavrogiannopoulos <nmav@gnutls.org>
sha256sums=('4f6f7a8fd691ac2b8307c8ca365bad711db607d4ad5966f6938a9d2ecd65c920'
            'SKIP')

build() {
  export CC='clang -m32'
  export CXX='clang -m32'
  export CPP='clang-cpp'
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig:/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd ${_pkgbase}-${pkgver}

  ./configure --prefix=/usr \
    --libdir=/usr/lib32 \
    --with-packager=PacBSD \
    --with-packager-bug-reports="https://bugs.pacbsd.org/" \
    --with-packager-version="${pkgver}-${pkgrel}" \
    --disable-gcc-warnings
  gmake
}

package() {
  cd ${_pkgbase}-${pkgver}

  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/usr/"{bin,include,share}
}
