_pkgbase=libtasn1
pkgname=lib32-${_pkgbase}
pkgver=4.5
pkgrel=2
pkgdesc="The ASN.1 library used in GNUTLS (32 bit)"
arch=('amd64')
license=('GPL3' 'LGPL')
url="http://www.gnu.org/software/libtasn1/"
depends=('freebsd-lib32' ${_pkgbase})
makedepends=('lib32-gcc-libs')
options=('!pathfix' 'pathfix32')
source=(http://ftp.gnu.org/gnu/libtasn1/${_pkgbase}-${pkgver}.tar.gz{,.sig})
md5sums=('81d272697545e82d39f6bd14854b68f0'
         'SKIP')

build() {
  export CC='clang -m32'
  export CXX='clang -m32'
  export CPP='clang-cpp'
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd ${_pkgbase}-${pkgver}

  ./configure --prefix=/usr --libdir=/usr/lib32
  gmake
}

package() {
  cd ${_pkgbase}-${pkgver}

  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/usr/"{bin,include,share}
}
