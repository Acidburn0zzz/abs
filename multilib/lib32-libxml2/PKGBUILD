# Port: textproc/libxml2

_pkgbasename=libxml2
pkgname=lib32-$_pkgbasename
pkgver=2.9.3
pkgrel=2
pkgdesc="XML parsing library, version 2 (32-bit)"
arch=('amd64')
license=('custom')
depends=('freebsd-lib32' $_pkgbasename)
url="http://www.xmlsoft.org/"
patchdir=("${_pkgbasename}-${pkgver}")
options=("!pathfix" "pathfix32")
source=(ftp://ftp.xmlsoft.org/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.gz
        http://www.w3.org/XML/Test/xmlts20080205.tar.gz)
md5sums=('daece17e045f1c107610e137ab50c179'
         'b255be9a1c7f7021e52448e4ec8d7a0d')

prepare() {
  cd "${_pkgbasename}-${pkgver}"
  for d in . doc doc/devhelp doc/examples
  do
    sed -i '' -e '/^install-data-am:/ s|install-data-local||' \
      ${d}/Makefile.in
  done

}

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${_pkgbasename}-${pkgver}"
  #autoreconf -fi
  ./configure --prefix=/usr --with-threads --with-history --libdir=/usr/lib32 --without-lzma --without-python
  gmake
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin,etc} "$pkgdir/usr/lib32/xml2Conf.sh"
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
