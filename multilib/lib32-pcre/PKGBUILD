# $Id: PKGBUILD 92531 2013-06-07 17:48:10Z bluewind $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

_pkgbasename=pcre
pkgname=lib32-$_pkgbasename
pkgver=8.37
pkgrel=1
pkgdesc="A library that implements Perl 5-style regular expressions (32-bit)"
arch=('amd64')
url="http://pcre.sourceforge.net"
license=('custom')
depends=('freebsd-lib32' $_pkgbasename 'lib32-gcc-libs')
options=('!libtool')
source=(ftp://ftp.csx.cam.ac.uk/pub/software/programming/pcre/${_pkgbasename}-${pkgver}.tar.bz2{,.sig})
md5sums=('ed91be292cb01d21bc7e526816c26981'
         'SKIP')

prepare() {
  cd ${_pkgbasename}-${pkgver}

  sed -i '' -e 's|GREP "\\-L"|GREP "conftest\\."|g' configure

  # so the tests can run:
  gsed -i 's/shlibpath_var=.*/shlibpath_var=LD_32_LIBRARY_PATH/g' configure
}

build() {
  cd ${_pkgbasename}-${pkgver}
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  ./configure --prefix=/usr --libdir=/usr/lib32 \
    --enable-utf --enable-unicode-properties --enable-pcre8 --enable-pcre16 --enable-pcre32 --enable-jit
  gmake
}

check() {
  cd "${srcdir}"/${_pkgbasename}-${pkgver}
  sed -i '' -e 's/do2=yes/do2=no/' RunTest
  gmake -j1 check 
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
