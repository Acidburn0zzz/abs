# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbasename=icu
pkgname=lib32-icu
pkgver=52.1
pkgrel=1
pkgdesc="International Components for Unicode library (32 bit)"
arch=('x86_64')
url="http://www.icu-project.org/"
license=('custom:icu')
depends=('lib32-gcc-libs' 'icu')
source=(#http://download.icu-project.org/files/${_pkgbasename}4c/${pkgver}/${_pkgbasename}4c-${pkgver/./_}-src.tgz
        http://download.icu-project.org/files/${_pkgbasename}4c/${pkgver}/${_pkgbasename}4c-${pkgver//./_}-src.tgz
	patch-config__mh-bsd-gcc
	patch-install)
md5sums=('9e96ed4c1d99c0d14ac03c140f9f346c'
         'f7d1f090364f4fae01626e087abccb78'
         '2717f21ede04b55edae471bdf3b32ca0')

prepare() {
  cd icu/source

  for i in patch-config__mh-bsd-gcc patch-install; do
    msg "patch $i"
    patch -p0 -i "${srcdir}/$i"
  done
}

build() {
  export CC="$CC -m32"
  export CXX="$CXX -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd icu/source

  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --sysconfdir=/etc \
              --mandir=/usr/share/man \
              --disable-sample \
              --with-data-packaging=library \
              --enable-release \
              --enable-shared \
              --enable-static

  export LD_32_LIBRARY_PATH="$srcdir/icu/source/lib:$srcdir/icu/source/stubdata"
  gmake
}

check() {
  cd "$srcdir/icu/source"
  gmake -k check # passes all
}

package() {
  cd icu/source

  export LD_32_LIBRARY_PATH="$srcdir/icu/source/lib:$srcdir/icu/source/stubdata"
  gmake -j1 DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr"/{include,sbin,share}

  # keep icu-config-32
  find "${pkgdir}/usr/bin" -type f -not -name icu-config -delete
  mv "${pkgdir}/usr/bin"/icu-config{,-32}

  install -d m644 "${pkgdir}/usr/share/licenses"
  ln -s icu "${pkgdir}/usr/share/licenses/${pkgname}"
}
