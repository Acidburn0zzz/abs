# Port: devel/icu

_pkgbasename=icu
pkgname=lib32-icu
pkgver=55.1
pkgrel=1
pkgdesc="International Components for Unicode library (32 bit)"
arch=('i386' 'amd64')
url="http://www.icu-project.org/"
license=('custom:icu')
depends=('freebsd-lib32' 'icu')
options=('!pathfix' 'pathfix32')
patchdir=("icu/source")
source=(#http://download.icu-project.org/files/${_pkgbasename}4c/${pkgver}/${_pkgbasename}4c-${pkgver/./_}-src.tgz
        http://download.icu-project.org/files/${_pkgbasename}4c/${pkgver}/${_pkgbasename}4c-${pkgver//./_}-src.tgz)
md5sums=('e2d523df79d6cb7855c2fbe284f4db29')

build() {
  export CC="$CC -m32"
  export CXX="$CXX -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd icu/source

  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --sysconfdir=/etc \
              --mandir=/usr/share/man \
              --disable-sample \
              --with-data-packaging=library \
              --enable-release \
              --enable-shared \
              --enable-static

  export LD_32_LIBRARY_PATH="$srcdir/icu/source/lib:$srcdir/icu/source/stubdata"
  gmake
}

## Not anymore it doesn't. just wrong ld paths.
#check() {
#  cd "$srcdir/icu/source"#
#
#  gmake -k check # passes all
#}

package() {
  cd icu/source

  export LD_32_LIBRARY_PATH="$srcdir/icu/source/lib:$srcdir/icu/source/stubdata"
  gmake -j1 DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr"/{include,sbin,share}

  # keep icu-config-32
  find "${pkgdir}/usr/bin" -type f -not -name icu-config -delete
  mv "${pkgdir}/usr/bin"/icu-config{,-32}

  install -d m644 "${pkgdir}/usr/share/licenses"
  ln -s icu "${pkgdir}/usr/share/licenses/${pkgname}"
}
