pkgname=lib32-libffi
pkgver=3.2.1
pkgrel=2
pkgdesc="A portable, high level programming interface to various calling conventions (32-bit)"
arch=('i386' 'amd64')
license=('MIT')
url="http://sourceware.org/libffi/"
depends=('freebsd-lib32' 'lib32-gcc-libs')
checkdepends=('dejagnu')
options=('!pathfix' 'pathfix32')
source=(ftp://sourceware.org/pub/libffi/libffi-${pkgver}.tar.gz)
sha1sums=('280c265b789e041c02e5c97815793dfc283fb1e6')

build() {
  cd "${srcdir}/libffi-${pkgver}"

  export CC="clang -m32"
  export CPP=clang-cpp
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"

  ./configure --prefix=/usr \
    --libdir=/usr/lib32 --libexecdir=/usr/libexec32 \
    --disable-static

  gmake
}

# dejagnu doesn't like lib32
#check() {
#  cd "${srcdir}/libffi-${pkgver}"
#
#  gmake check
#}

package() {
  cd "${srcdir}/libffi-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/"
  rm -r "${pkgdir}"/usr/share/{info,man}
}
