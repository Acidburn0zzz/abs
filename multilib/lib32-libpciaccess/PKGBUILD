# Port: devel/libpciaccess

_pkgbasename=libpciaccess
pkgname=lib32-${_pkgbasename}
pkgver=0.13.4
pkgrel=1
pkgdesc="X11 PCI access library (32-bit"
arch=('i386' 'amd64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('freebsd-lib32' ${_pkgbasename})
makedepends=('pkgconf' 'xorg-util-macros')
options=('!pathfix' 'pathfix32')
patchdir=("${_pkgbasename}-${pkgver}")
source=(${url}/releases/individual/lib/${_pkgbasename}-${pkgver}.tar.bz2)
md5sums=('ace78aec799b1cf6dfaea55d3879ed9f')


build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP='clang-cpp'
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --libdir=/usr/lib32 --with-pciids-path=/usr/share/hwdata \
    --enable-malloc0returnsnull
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/include
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
