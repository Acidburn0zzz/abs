# $Id: PKGBUILD 69230 2012-04-10 16:20:33Z lcarlier $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

_pkgbasename=libpciaccess
pkgname=lib32-${_pkgbasename}
pkgver=0.13.2
pkgrel=1
pkgdesc="X11 PCI access library (32-bit"
arch=(i686 x86_64)
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('freebsd-lib32' ${_pkgbasename})
makedepends=('lib32-devel' 'pkgconf' 'xorg-util-macros')
options=('!libtool')
source=(${url}/releases/individual/lib/${_pkgbasename}-${pkgver}.tar.bz2)
md5sums=('b7c0d3afce14eedca57312a3141ec13a')


build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --libdir=/usr/lib32 --with-pciids-path=/usr/share/hwdata
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/include
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
