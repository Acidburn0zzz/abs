# Port: x11-fonts/fontconfig
_pkgbasename=fontconfig
pkgname=lib32-fontconfig

pkgver=2.11.1
pkgrel=1
pkgdesc="A library for configuring and customizing font access (32-bit)"
arch=('amd64')
url="http://www.fontconfig.org/release/"
license=('custom')
depends=('lib32-expat' 'lib32-freetype2' $_pkgbasename)
options=('!pathfix' 'pathfix32')
patchdir=("${_pkgbasename}-${pkgver}")
source=(http://www.fontconfig.org/release/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('dc62447533bca844463a3c3fd4083b57c90f18a70506e7a9f4936b5a1e516a99')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  # make sure there's no rpath trouble and sane .so versioning - FC and Gentoo do this as well
  libtoolize -f
  autoreconf -fi

  # so the tests can run:
  gsed -i 's/shlibpath_var=.*/shlibpath_var=LD_32_LIBRARY_PATH/g' configure
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-templatedir=/etc/fonts/conf.avail \
    --with-xmldir=/etc/fonts \
    --with-expat-lib=/usr/lib32 \
    --with-expat-includes=/usr/include \
    --localstatedir=/var \
    --disable-static \
    --with-default-fonts=/usr/share/fonts \
    --with-add-fonts=/usr/share/fonts \
    --with-cache-dir=/var/db/fontconfig \
    --disable-docs \
    --libdir=/usr/lib32

  gmake
}

check() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  gmake -k check
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin} "$pkgdir"/{etc,var}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
