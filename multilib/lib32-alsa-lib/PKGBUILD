# $Id: PKGBUILD 91441 2013-05-22 15:08:25Z lcarlier $
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

_pkgbasename=alsa-lib
pkgname=lib32-${_pkgbasename}
pkgver=1.0.27.2
pkgrel=2
pkgdesc="An alternative implementation of Linux sound support (32 bit)"
arch=('amd64')
url="http://www.alsa-project.org"
depends=('freebsd-lib32' 'lib32-gcc-libs' $_pkgbasename)
license=('GPL')
options=(!libtool)
source=(ftp://ftp.alsa-project.org/pub/lib/${_pkgbasename}-$pkgver.tar.bz2
        patch-alsa-lib)
md5sums=('69129a7c37697f81ac092335e9fa452b'
         '2b3dff85ffcba8c7d156cb2e33f8c2e6')

prepare() {
  cd ${_pkgbasename}-$pkgver
# ports patch
  patch -p0 -i "${srcdir}/patch-alsa-lib"

# from ports
  sed -i '' -e '/LIBS/ { s/-ldl//g; s/-lrt//g; }' \
  	        -e '/-D_GNU_SOURCE/d' \
  	        -e '/lt_cv_dlopen/s/-ldl//g' \
  	  configure
  sed -i '' -e '/LIBADD/s/-ldl//g' \
  	modules/mixer/simple/Makefile.in
}

build() {
  cd ${_pkgbasename}-$pkgver

  export CC='clang -m32'
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  ./configure --prefix=/usr --libdir=/usr/lib32 \
              --disable-aload --disable-python \
              --with-pkgconfdir=/usr/lib32/pkgconfig
  gmake
}

package() {
  cd $srcdir/${_pkgbasename}-$pkgver

  gmake DESTDIR=$pkgdir install

  # Clean up lib32 package
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
