_pkgbasename=alsa-lib
pkgname=lib32-${_pkgbasename}
pkgver=1.0.29
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support (32 bit)"
arch=('amd64')
url="http://www.alsa-project.org"
depends=('freebsd-lib32' 'lib32-gcc-libs' $_pkgbasename)
license=('GPL')
options=(!pathfix pathfix32 libtoolfix)
patchdir=(${_pkgbasename}-$pkgver)
source=(ftp://ftp.alsa-project.org/pub/lib/${_pkgbasename}-$pkgver.tar.bz2)
md5sums=('de67e0eca72474d6b1121037dafe1024')

prepare() {
  cd ${_pkgbasename}-$pkgver

  CPPFLAGS+=" -I${startdir}/files"

# from ports
  sed -i '' -e '/LIBS/ { s/-ldl//g; s/-lrt//g; }' \
  	        -e '/-D_GNU_SOURCE/d' \
  	        -e '/lt_cv_dlopen/s/-ldl//g' \
  	  configure
  sed -i '' -e '/LIBADD/s/-ldl//g' \
  	modules/mixer/simple/Makefile.in
}

build() {
  cd ${_pkgbasename}-$pkgver

  export CC='clang -m32'
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  ./configure --prefix=/usr --libdir=/usr/lib32 \
              --disable-aload --disable-python \
              --with-pkgconfdir=/usr/libdata32/pkgconfig
  gmake
}

package() {
  cd $srcdir/${_pkgbasename}-$pkgver

  gmake DESTDIR=$pkgdir install

  # Clean up lib32 package
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
