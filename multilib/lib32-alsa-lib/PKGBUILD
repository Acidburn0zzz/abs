# Port: audio/alsa-lib

_pkgbasename='alsa-lib'
pkgname=lib32-alsa-lib
pkgver=1.1.2
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support (32 bit)"
arch=('i386' 'amd64')
url="http://www.alsa-project.org"
depends=('freebsd-lib32' "${_pkgbasename}")
license=('GPL')
options=(!pathfix pathfix32 libtoolfix)
patchdir=(${_pkgbasename}-$pkgver)
source=(ftp://ftp.alsa-project.org/pub/lib/${_pkgbasename}-$pkgver.tar.bz2)
sha256sums=('d38dacd9892b06b8bff04923c380b38fb2e379ee5538935ff37e45b395d861d6')

prepare() {
  cd ${_pkgbasename}-$pkgver

  CPPFLAGS+=" -I${startdir}/files"

# from ports
  sed -i '' -e '/LIBS/ { s/-ldl//g; s/-lrt//g; }' \
  	        -e '/-D_GNU_SOURCE/d' \
  	        -e '/lt_cv_dlopen/s/-ldl//g' \
  	  configure
  sed -i '' -e '/LIBADD/s/-ldl//g' \
  	modules/mixer/simple/Makefile.in
}

build() {
  cd ${_pkgbasename}-$pkgver

  export CC='clang -m32'
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  ./configure --prefix=/usr --libdir=/usr/lib32 \
              --disable-aload --disable-python \
              --with-pkgconfdir=/usr/libdata32/pkgconfig
  gmake
}

package() {
  cd $srcdir/${_pkgbasename}-$pkgver

  gmake DESTDIR=$pkgdir install

  # Clean up lib32 package
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
