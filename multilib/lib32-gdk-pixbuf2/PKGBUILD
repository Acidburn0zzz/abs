# Port: graphics/gdk-pixbuf2
_pkgbasename=gdk-pixbuf2
pkgname=lib32-gdk-pixbuf2
pkgver=2.32.3
pkgrel=1
pkgdesc="An image loading library (32-bit)"
arch=('i386' 'amd64')
url="http://www.gtk.org/"
license=('LGPL2')
depends=(lib32-glib2 lib32-libpng lib32-libtiff lib32-libjpeg lib32-libx11
         $_pkgbasename)
options=('!pathfix' 'pathfix32' '!docs' 'libtoolfix')
patchdir=("gdk-pixbuf-${pkgver}")
install=gdk-pixbuf2.install
source=(http://download.gnome.org/sources/gdk-pixbuf/${pkgver%.*}/gdk-pixbuf-$pkgver.tar.xz)
sha256sums=('2b6771f1ac72f687a8971e59810b8dc658e65e7d3086bd2e676e618fd541d031')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"
  export LDFLAGS="-L/usr/lib32 ${LDFLAGS}"

  cd "${srcdir}/gdk-pixbuf-${pkgver}"

  ./configure --prefix=/usr  --libdir=/usr/lib32 \
    --without-libjasper \
    --with-x11 \
    --with-included-loaders=png

  export LD_32_LIBRARY_PATH="$srcdir/gdk-pixbuf-${pkgver}/gdk-pixbuf/.libs:${LD_32_LIBRARY_PATH}"
  gmake V=1
}

package() {
  cd "${srcdir}/gdk-pixbuf-${pkgver}"

  gmake DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/etc
  rm -rf "${pkgdir}"/usr/{include,share}

  cd "${pkgdir}"/usr/bin
  mv gdk-pixbuf-query-loaders gdk-pixbuf-query-loaders-32
  rm gdk-pixbuf-pixdata
  rm gdk-pixbuf-csource
}

# vim:set ts=2 sw=2 et:
