# $Id: PKGBUILD 93101 2013-06-24 11:17:27Z bluewind $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
_pkgbasename=gdk-pixbuf2
pkgname=lib32-$_pkgbasename
pkgver=2.28.2
pkgrel=4
pkgdesc="An image loading library (32-bit)"
arch=('x86_64')
url="http://www.gtk.org/"
license=('LGPL2')
depends=(lib32-glib2 lib32-libpng lib32-libtiff lib32-libjpeg lib32-libx11
         $_pkgbasename)
makedepends=(lib32-devel)
options=('!libtool' '!docs')
install=gdk-pixbuf2.install
source=(http://download.gnome.org/sources/gdk-pixbuf/${pkgver%.*}/gdk-pixbuf-$pkgver.tar.xz)
sha256sums=('183113c2eb2232963e88864a6a54fd963dbfeb1a3679fb0d3456f9e0b79e4617')

prepare() {
  cd "$srcdir/gdk-pixbuf-$pkgver"

  sed -i '' \
      -e "/^ltmain=/!s|\$$ac_aux_dir/ltmain.sh|/usr/share/libtool/config/ltmain.sh|g" \
      -e "/^LIBTOOL=/s|\$$\(top_builddir\)/libtool|/usr/bin/libtool|g" \
      aclocal.m4
  find . -name "Makefile.in" | \
    xargs sed -i '' -e 's|^LIBTOOL[ ]*=.*|LIBTOOL=/usr/bin/libtool|g'
}

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"
  export LDFLAGS="-L/usr/lib32 ${LDFLAGS}"

  cd "${srcdir}/gdk-pixbuf-${pkgver}"

  ./configure --prefix=/usr  --libdir=/usr/lib32 \
    --without-libjasper \
    --with-x11 \
    --with-included-loaders=png
# ltverhack:
  for i in ltmain.sh libtool; do
    sed -i '' -e '/freebsd-elf)/,/;;/ s|major="\.$current"|major=.0|' \
              -e '/freebsd-elf)/,/;;/ s|versuffix="\.$current"|versuffix="$major"|' \
        $i
  done
  export LD_32_LIBRARY_PATH="$srcdir/gdk-pixbuf-${pkgver}/gdk-pixbuf/.libs:${LD_32_LIBRARY_PATH}"
  gmake V=1
}

package() {
  cd "${srcdir}/gdk-pixbuf-${pkgver}"

  gmake DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/etc
  rm -rf "${pkgdir}"/usr/{include,share}

  cd "${pkgdir}"/usr/bin
  mv gdk-pixbuf-query-loaders gdk-pixbuf-query-loaders-32
  rm gdk-pixbuf-pixdata
  rm gdk-pixbuf-csource
}

# vim:set ts=2 sw=2 et:
