# Port: audio/libsndfile

_pkgbasename=libsndfile
pkgname=lib32-libsndfile
pkgver=1.0.26
pkgrel=1
pkgdesc="A C library for reading and writing files containing sampled sound (32-bit)"
arch=('i386' 'amd64')
url="http://www.mega-nerd.com/libsndfile"
license=('LGPL')
depends=('lib32-flac' 'lib32-libvorbis' $_pkgbasename)
makedepends=('lib32-alsa-lib')
options=('!pathfix' 'pathfix32')
source=(http://www.mega-nerd.com/libsndfile/files/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('ec810a0c60c08772a8a5552704b63393')
sha1sums=('c9092c06f6ed51034c9e809e150083ff1f55e79e')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  export GETCONF="getconf -v POSIX_V7_ILP32_OFFBIG"
  export GETCONF_DIR="/usr/lib32/getconf"

  ./configure --prefix=/usr --disable-sqlite --libdir=/usr/lib32
  make -C src
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make -C src DESTDIR="${pkgdir}" install
  make DESTDIR="$pkgdir" install-pkgconfigDATA

  rm -rf "$pkgdir/usr/include"
}
