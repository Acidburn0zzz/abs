# Port: graphics/giflib
_pkgbasename=giflib
pkgname=lib32-giflib
pkgver=5.1.3
pkgrel=1
pkgdesc="A library for reading and writing gif images (32-bit)"
url="http://sourceforge.net/projects/giflib/"
arch=('i386' 'amd64')
license=('MIT')
depends=('lib32-libx11' 'lib32-libsm' $_pkgbasename)
makedepends=('xmlto' 'docbook-xsl' 'docbook-xml' 'freebsd-lib32')
provides=("lib32-libungif=${pkgver}")
conflicts=('lib32-libungif')
replaces=('lib32-libungif')
options=('!pathfix' 'libtoolfix' 'pathfix32')
patchdir=("${_pkgbasename}-${pkgver}")
source=(http://downloads.sourceforge.net/sourceforge/giflib/${_pkgbasename}-${pkgver}.tar.bz2)
md5sums=('420ca1e0244a6fa247fa4eb4eeed2960')


build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd ${srcdir}/${_pkgbasename}-${pkgver}

  ./configure --prefix=/usr --libdir=/usr/lib32
  gmake
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  gmake DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  install -m755 -d ${pkgdir}/usr/share/licenses
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
