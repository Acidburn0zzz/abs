# Port: graphics/png
_pkgbasename=libpng
pkgname=lib32-libpng
pkgver=1.6.23
_apngver=1.6.23
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files (32-bit)"
arch=('i386' 'amd64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=($_pkgbasename 'freebsd-lib32')
options=('libtoolfix' '!pathfix' 'pathfix32')
source=("http://downloads.sourceforge.net/sourceforge/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-${_apngver}-apng.patch.gz")
md5sums=('9b320a05ed4db1f3f0865c8a951fd9aa'
         'bf44929a1c272f585a11baeca05af500')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=cpp
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  # Add animated PNG (apng) support
  # see http://sourceforge.net/projects/libpng-apng/
  patch -p1 -i ../libpng-$_apngver-apng.patch

  ./configure --prefix=/usr --libdir=/usr/lib32 --program-suffix=-32
  gmake
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  gmake DESTDIR="${pkgdir}" install

  cd contrib/pngminus
  gmake CC="clang -m32" CXX="clang++ -m32" PNGLIB="-L${pkgdir}/usr/lib32 -lpng" -f makefile.std png2pnm pnm2png

  rm -rf "${pkgdir}"/usr/{include,share}

  rm "$pkgdir/usr/bin/libpng-config"
  ln -s "libpng${_libversion}-config-32" "$pkgdir/usr/bin/libpng-config-32"

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
