# $Id: PKGBUILD 88206 2013-04-13 21:21:28Z bluewind $
# Maintainer:
# Contributor: Judd Vinet <jvinet@zeroflux.org>

_pkgbasename=libldap
pkgname=lib32-$_pkgbasename
pkgver=2.4.39
pkgrel=1
pkgdesc="Lightweight Directory Access Protocol (LDAP) client libraries (32-bit)"
arch=('x86_64')
license=('custom')
url="http://www.openldap.org/"
#openssl from lib32 world
depends=('freebsd-lib32' $_pkgbasename)
makedepends=(lib32-devel)
options=('!libtool')
patchdir=("openldap-${pkgver}")
source=("ftp://ftp.openldap.org/pub/OpenLDAP/openldap-release/openldap-${pkgver}.tgz"
        'ntlm.patch')
md5sums=('b0d5ee4b252c841dec6b332d679cf943'
         '4258ddbef923d1f29f2843bc050f8c56')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd ${srcdir}/openldap-${pkgver}

  patch -Np1 -i ${srcdir}/ntlm.patch

  ./configure --prefix=/usr \
              --libexecdir=/usr/sbin \
              --sysconfdir=/etc \
              --mandir=/usr/share/man \
              --localstatedir=/var/lib/openldap \
              --enable-crypt --enable-dynamic \
              --with-threads --disable-wrappers \
              --disable-spasswd --without-cyrus-sasl \
              --disable-bdb --disable-hdb --libdir=/usr/lib32

  cd include
  gmake

  cd ../libraries
  gmake depend
  gmake

}

package() {
  cd ${srcdir}/openldap-${pkgver}

  cd include
  gmake DESTDIR=${pkgdir} install

  cd ../libraries
  gmake DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}"/usr/{include,share,bin} "$pkgdir/etc"
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
