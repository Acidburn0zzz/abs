# $Id: PKGBUILD 91894 2013-05-28 20:22:04Z bluewind $
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

_pkgbasename=flac
pkgname=lib32-$_pkgbasename
pkgver=1.3.1
pkgrel=1
pkgdesc="Free Lossless Audio Codec (32-bit)"
arch=('i386' 'amd64')
url="http://flac.sourceforge.net/"
license=('custom:Xiph' 'LGPL' 'GPL' 'FDL')
depends=('lib32-libogg' 'lib32-devel' $_pkgbasename)
makedepends=('nasm' lib32-devel)
options=('libtoolfix' '!makeflags' '!pathfix' 'pathfix32')
source=(http://downloads.xiph.org/releases/flac/${_pkgbasename}-${pkgver}.tar.xz)
sha1sums=('38e17439d11be26207e4af0ff50973815694b26f')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man --enable-shared \
              --disable-rpath --libdir=/usr/lib32 --with-ogg-libraries=/usr/lib32 \
              --without-libiconv-prefix
  gmake
}

package() {
  cd "${_pkgbasename}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
