# Port: x11-toolkits/pango
_pkgbasename=pango
pkgname=lib32-$_pkgbasename
pkgver=1.38.0
pkgrel=1
pkgdesc="A library for layout and rendering of text (32-bit)"
arch=('i386' 'amd64')
license=('LGPL')
makedepends=('lib32-libthai' 'lib32-fontconfig' 'lib32-libgl' 'lib32-libxft' 'lib32-harfbuzz' $_pkgbasename 'lib32-cairo')
options=('libtoolfix' '!pathfix' 'pathfix32' '!emptydirs')
install=pango.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/${pkgver:0:4}/${_pkgbasename}-${pkgver}.tar.xz)
url="http://www.pango.org/"
sha256sums=('1d4e75974bad853ee9ac5fc5caee5e7ab235abbd945d51d01f3806e04e7c226c')

prepare() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  gsed -i 's#"pango.modules"#"pango.modules-32"#' pango/modules.c
  gsed -i '/SUBDIRS = /s@ tests@@' Makefile.in
}

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  ./configure --prefix=/usr --libdir=/usr/lib32 --sysconfdir=/etc \
      --localstatedir=/var --with-included-modules=basic-fc \
      --with-dynamic-modules=arabic-fc,arabic-lang,basic-fc,basic-win32,basic-x,basic-atsui,hangul-fc,hebrew-fc,indic-fc,indic-lang,khmer-fc,syriac-fc,tibetan-fc \
      --disable-introspection

  export LD_32_LIBRARY_PATH="$srcdir/${_pkgbasename}-${pkgver}/pango/.libs:${LD_32_LIBRARY_PATH}"
  gmake
}

package() {
  depends=('lib32-libthai' 'lib32-libxft' 'lib32-harfbuzz' $_pkgbasename 'lib32-cairo')
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  gmake DESTDIR="${pkgdir}" install
  rm -rf "$pkgdir"/etc
  rm -rf "$pkgdir"/usr/{bin/pango-view,share,include}
}
