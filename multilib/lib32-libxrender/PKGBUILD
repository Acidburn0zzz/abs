# Port: x11/libXrender

_pkgbasename=libxrender
pkgname=lib32-$_pkgbasename
pkgver=0.9.9
pkgrel=1
pkgdesc="X Rendering Extension client library (32-bit)"
arch=('i386' 'amd64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libx11>=1.3.4' $_pkgbasename)
makedepends=('pkgconf' renderproto)
options=('!pathfix' 'pathfix32')
source=(${url}/releases/individual/lib/libXrender-${pkgver}.tar.bz2)
sha1sums=('31bf63dfb4fcb82a6db73abe98df87cb50c17176')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${srcdir}/libXrender-${pkgver}"
  ./configure --prefix=/usr --disable-static --libdir=/usr/lib32
  gmake
}

package() {
  cd "${srcdir}/libXrender-${pkgver}"

  gmake DESTDIR=${pkgdir} install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
