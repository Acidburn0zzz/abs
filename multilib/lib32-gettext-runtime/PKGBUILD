# Port: devel/gettext-runtime

_pkgbasename=gettext-runtime
_pkgname=gettext
pkgname=lib32-$_pkgbasename
pkgver=0.19.8.1
pkgrel=1
pkgdesc="GNU internationalization library (32-bit)"
arch=('i386' 'amd64')
url="http://www.gnu.org/software/gettext/"
license=('GPL')
depends=(freebsd-lib32 $_pkgbasename)
options=(!docs '!pathfix' 'pathfix32')
patchdir=("${_pkgname}-${pkgver}/gettext-runtime")
source=(ftp://ftp.gnu.org/pub/gnu/gettext/${_pkgname}-${pkgver}.tar.gz{,.sig})
md5sums=('97e034cf8ce5ba73a28ff6c3c0638092'
         'SKIP')

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}/${_pkgbasename}"

# Do not install csharp and java documentation.
	sed -i '' -E '/^SUBDIRS =/s/(intl-csharp|intl-java)//g' \
		Makefile.in
# Do not install html copies of manpages.
	sed -i '' \
		-e '/^all-local:/s/html-local//' \
		-e '/^install-data-local:/s/install-html//' \
		-e '/^installdirs-local:/s/installdirs-html//' \
		libasprintf/Makefile.in \
		man/Makefile.in

}

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig:/usr/lib32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  cd "${srcdir}/${_pkgname}-${pkgver}"

  EMACS="no" \
  ./configure --prefix=/usr --libdir=/usr/lib32 \
              --disable-csharp --disable-threads --disable-openmp \
              --with-included-libcroco \
              --without-git
  gmake
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
