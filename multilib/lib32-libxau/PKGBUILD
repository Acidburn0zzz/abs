# Port: x11/libXau

_pkgbasename=libxau
pkgname=lib32-$_pkgbasename
pkgver=1.0.8
pkgrel=1
pkgdesc="X11 authorisation library (32-bit)"
arch=('i386' 'amd64')
url="http://xorg.freedesktop.org/"
depends=('freebsd-lib32' $_pkgbasename)
makedepends=('pkgconf' 'xproto>=7.0.15')
license=('custom')
options=('!pathfix' 'pathfix32')
source=(${url}/releases/individual/lib/libXau-${pkgver}.tar.bz2)
sha1sums=('d9512d6869e022d4e9c9d33f6d6199eda4ad096b')

build() {
  cd "${srcdir}/libXau-${pkgver}"

  export CC="clang -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"

  ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib32
  gmake
}

package() {
  cd "${srcdir}/libXau-${pkgver}"

  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
