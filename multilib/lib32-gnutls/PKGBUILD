# Port: security/gnutls
_pkgbasename=gnutls
pkgname=lib32-gnutls
pkgver=3.4.14
pkgrel=1
pkgdesc="A library which provides a secure layer over a reliable transport layer (32-bit)"
arch=('i386' 'amd64')
license=('GPL3' 'LGPL2.1')
url="http://gnutls.org/"
depends=('lib32-nettle>=2.4' 'lib32-p11-kit>=0.12' 'lib32-libtasn1>=3.0' 'lib32-gettext-runtime' $_pkgbasename)
makedepends=('lib32-libidn')
patchdir=("${_pkgbasename}-${pkgver}")
options=('!pathfix' 'pathfix32' 'libtoolfix')
source=(ftp://ftp.gnutls.org/gcrypt/gnutls/v3.4/${_pkgbasename}-${pkgver}.tar.xz{,.sig})
md5sums=('ad3e269a6793424d5d21c9626e1c9ef1'
         'SKIP')

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"
  export CFLAGS="-m32 ${CFLAGS}"
  export CXXFLAGS="-m32 ${XXCFLAGS}"
  export LDFLAGS="-m32 ${LDFLAGS}"

  cd ${srcdir}/${_pkgbasename}-${pkgver}

  # build fails without --disable-hardware-acceleration because of assembler errors
  ./configure --prefix=/usr --libdir=/usr/lib32 \
    --with-zlib \
    --disable-static \
    --disable-guile \
    --disable-valgrind-tests --disable-hardware-acceleration \
    ac_cv_type_max_align_t=yes
  gmake
}

check() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  #make -k check
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
  find $pkgdir

  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
