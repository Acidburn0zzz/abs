# Port: textproc/expat2

_pkgbasename=expat
# pkgname=lib32-${_pkgbasename}
pkgname=lib32-expat
pkgver=2.2.0
pkgrel=1
pkgdesc="An XML Parser library written in C (32 bit)"
arch=('i386' 'amd64')
url="http://expat.sourceforge.net/"
license=('custom')
depends=('freebsd-lib32' "${_pkgbasename}")
options=('!pathfix' 'pathfix32')
source=(http://downloads.sourceforge.net/project/${_pkgbasename}/${_pkgbasename}/${pkgver}/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('d9e50ff2d19b3538bd2127902a89987474e1a4db8e43a66a4d1a712ab9a504ff')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  export CC='clang -m32'
  export CPP=clang-cpp
  export PKG_CONFIG_PATH="/usr/libdata32/pkgconfig"
  export CFLAGS="${CFLAGS//-fstack-protector/}"
  export CPPFLAGS="${CPPFLAGS//-fstack-protector/}"
  export CXXFLAGS="${CXXFLAGS//-fstack-protector/}"
  ./configure --prefix=/usr --libdir=/usr/lib32 --mandir=/usr/share/man 
  gmake
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/licenses/"
  ln -s ${_pkgbasename} "${pkgdir}/usr/share/licenses/${pkgname}"

  # Clean up lib32 package
  rm -rf "${pkgdir}"/usr/{bin,include,share/man}
}
