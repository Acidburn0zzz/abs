# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=libpthread-stubs
pkgver=0.3
pkgrel=6
pkgdesc="This library provides weak aliases for pthread functions"
arch=(i686 x86_64)
groups=('base')
license=('GPL')
url="http://xcb.freedesktop.org/"
makedepends=('pkgconf')
port_patches=(
	patch-Makefile.in
	patch-configure-objformat
	patch-stubs.c
)
source=("$url/dist/${pkgname}-${pkgver}.tar.bz2"
	${port_patches[@]})
md5sums=('e8fa31b42e13f87e8f5a7a2b731db7ee'
         'edea71d916650d26bc3996693ac64634')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	for i in ${port_patches[@]}; do
		msg "Applying patch ${i}"
		patch -p0 -i "${srcdir}/${i}"
	done
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure --prefix=/usr
	gmake
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	gmake DESTDIR="${pkgdir}" install
}
