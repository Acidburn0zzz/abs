# Contributor: Wolfgang Bumiller <blub@speed.at>
# Port: devel/pth

pkgname=pth
pkgver=2.0.7
pkgrel=1
pkgdesc="GNU Portable Threads."
arch=('i386' 'amd64')
url="http://www.gnu.org/software/pth/"
license=('LGPL')
options=('!makeflags' 'libtoolfix' 'gnu_configure')
source=(ftp://ftp.gnu.org/gnu/pth/$pkgname-$pkgver.tar.gz{,.sig})
md5sums=('9cb4a25331a4c4db866a31cbe507c793'
         '2371608ef1866b310393ef262800609c'
         '278b91f55f2fe30707c1271c04142681')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --mandir=/usr/share/man \
              --enable-optimize \
              --enable-batch \
              --includedir=/usr/include/pth \
              --libdir=/usr/lib/pth \
              --enable-syscall-soft --enable-pthread \
              --localstatedir=/var \
              --with-pic

  make -j1
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  make -j1 check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make -j1 DESTDIR=${pkgdir} install

  rm ${pkgdir}/usr/share/man/man3/pthread.3

  install -dm755 "${pkgdir}/usr/libdata/ldconfig"
  echo "/usr/lib/pth" >> "${pkgdir}/usr/libdata/ldconfig/pth"

}
