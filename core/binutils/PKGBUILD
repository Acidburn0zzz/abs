# $Id: PKGBUILD 261104 2016-03-06 11:06:03Z allan $
# Port: devel/binutils

pkgname=binutils
pkgver=2.30
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('i686' 'amd64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
groups=('base-devel')
depends=('zlib' 'gmp' 'mpfr')
checkdepends=('dejagnu')
options=('staticlibs' '!distcc' '!ccache' '!pathfix')
install=binutils.install
source=("http://mirrors.kernel.org/sources.redhat.com/${pkgname}/releases/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('efeade848067e9a03f1918b1da0d37aaffa0b0127a06b5e9236229851d9d0c09')

prepare() {
  cd "${pkgname}-${pkgver}"

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  gsed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure


  if [[ ! -d ${srcdir}/binutils-build ]]; then
	mkdir ${srcdir}/binutils-build
  fi

  LDFLAGS+=" -lintl"

}

build() {
   cd "${pkgname}-build"

    ../"${pkgname}-${pkgver}"/configure --prefix=/usr/local \
    --with-lib-path=/lib:/usr/lib:/usr/local/lib --mandir=/usr/local/man \
    --with-bugurl=https://bugs.pacbsd.org/ \
    --enable-shared --with-pic \
    --enable-ld=default --enable-gold --enable-plugins \
    --with-sysroot=/ \
    --disable-werror --disable-gdb \
    --build=x86_64-pacbsd-freebsd${OSREL//-*/}

    gmake tooldir=/usr/local
}

package() {
  cd binutils-build
  gmake prefix=${pkgdir}/usr/local mandir=${pkgdir}/usr/local/man tooldir=${pkgdir}/usr/local install

  rm "${pkgdir}"/usr/local/share/info/dir
}
