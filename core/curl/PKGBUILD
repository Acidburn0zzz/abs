# Maintainer: Anthony Donnelly <amzo@archbsd.net>
# Contributor: Wolfgang Bumiller <archbsd at bumiller dot com>
# Port: ftp/curl
pkgname=curl
pkgver=7.47.1
pkgrel=1
pkgdesc="An URL retrieval utility and library"
arch=('i386' 'amd64')
url="http://curl.haxx.se"
license=('MIT')
depends=('ca_root_nss')
options=('libtoolfix')
source=("http://curl.haxx.se/download/$pkgname-$pkgver.tar.gz"{,.asc}
        curlbuild.h)
sha1sums=('09952f607522e39b82a72c604df276255cdb9918'
          'SKIP'
          '67da0e46ce02287ad1f4f93e73160835cbb54dc7')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i '' -e '/^SUBDIRS = / s|$$| docs|; /^DIST_SUBDIRS = / s| docs||; /cd docs &&/d' Makefile.in
  sed -i '' -e 's|\(flags_dbg_off=\)".*"|\1""|; s|\(flags_opt_off=\)".*"|\1""|; s|lib/pkgconfig|libdata/pkgconfig|g' configure
  sed -i '' -e 's|include <gssapi.h>|include <gssapi/gssapi.h>|' lib/curl_gssapi.h lib/urldata.h
  sed -i '' -e 's|gss_nt_service_name|GSS_C_NT_HOSTBASED_SERVICE|' lib/curl_sasl_gssapi.c

}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --sysconfdir=/etc \
      --disable-werror \
      --enable-imap \
      --enable-pop3 \
      --enable-rtsp \
      --enable-smtp \
      --without-axtls \
      --with-ca-bundle=/usr/share/certs/ca-root-nss.crt \
      --enable-cookies \
      --disable-curldebug \
      --disable-debug \
      --without-nghttp2 \
      --without-libidn \
      --enable-ipv6 \
      --disable-ldap \
      --disable-ldaps \
      --without-libssh2 \
      --without-libmetalink \
      --enable-proxy \
      --without-libpsl \
      --without-librtmp \
      --enable-tls-srp \
      --with-gssapi=/usr \
      LIBS="-lkrb5 -lgssapi -lgssapi_krb5 -lkrb5 -lgssapi -lgssapi_krb5" \
      KRB5CONFIG="/usr/bin/krb5-config" \
      --disable-ares \
      --enable-threaded-resolver \
      --without-gnutls \
      --without-nss \
      --with-ssl=/usr \
      --without-polarssl --without-cyassl \
      OPENSSL_CFLAGS="-DNO_IDEA" \
      ac_cv_func_SSLv2_client_method=no

  gmake
}

package() {
  cd "$pkgname-$pkgver"

  gmake DESTDIR="$pkgdir" install

  ln -s libcurl.so.4 ${pkgdir}/usr/lib/libcurl.so.7

  local ptrsize=$(cpp <<<'__SIZEOF_POINTER__' | sed '/^#/d')
  case $ptrsize in
    8) _curlbuild=curlbuild-64.h ;;
    4) _curlbuild=curlbuild-32.h ;;
    *) error "unknown pointer size for architecture: %s bytes" "$ptrsize"
      exit 1
      ;;
  esac

  # license
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  # devel
  install -dm755 "$pkgdir/usr/share/aclocal/"
  install -m644 docs/libcurl/libcurl.m4 "$pkgdir/usr/share/aclocal/libcurl.m4"

  mv "$pkgdir/usr/include/curl/curlbuild.h" "$pkgdir/usr/include/curl/$_curlbuild"
  install -m644 "$srcdir/curlbuild.h" "$pkgdir/usr/include/curl/curlbuild.h"
}
