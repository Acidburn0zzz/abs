# Port: devel/pkgconf

pkgname=pkgconf
pkgver=1.4.2
pkgrel=1
pkgdesc="Utility to help to configure compiler and linker flags"
conflicts=('pkg-config')
provides=('pkg-config' 'pkgconfig')
arch=('amd64')
url="https://github.com/pkgconf/pkgconf"
license=('BSD')
groups=('base-devel')
options=('libtoolfix' 'gnu_configure' 'pathfix')
source=("https://distfiles.dereferenced.org/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('bab39371d4ab972be1d539a8b10b6cc21f8eafc97f617102e667e82bd32eb234')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix=/usr \
                --mandir=/usr/share/man \
                --infodir=/usr/share/info \
                --with-pkg-config-dir=/usr/lib/pkgconfig:/usr/libdata/pkgconfig:/usr/local/libdata/pkgconfig:/usr/local/lib/pkgconfig \
                --with-system-libdir=/usr/lib \
                --with-system-includedir=/usr/include
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
    install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    ln -s /usr/bin/pkgconf ${pkgdir}/usr/bin/pkg-config
}
