pkgname=pkgconf
pkgver=0.9.11
pkgrel=6
pkgdesc="Utility to help to configure compiler and linker flags"
conflicts=('pkg-config')
provides=('pkg-config' 'pkgconfig')
arch=('i386' 'amd64')
url="https://github.com/pkgconf/pkgconf"
license=('BSD')
groups=('base-devel')
source=(http://rabbit.dereferenced.org/~nenolod/distfiles/${pkgname}-${pkgver}.tar.bz2)
md5sums=('e560dc63a5e65b45b7304680494cb642')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix=/usr \
                --mandir=/usr/share/man \
                --infodir=/usr/share/info \
                --with-pkg-config-dir=/usr/lib/pkgconfig:/usr/libdata/pkgconfig \
                --with-system-libdir=/usr/lib \
                --with-system-includedir=/usr/include
    make
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make check
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
    install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    ln -s /usr/bin/pkgconf ${pkgdir}/usr/bin/pkg-config
}
