# Port: converters/libiconv

pkgname=libiconv
pkgver=1.14
pkgrel=16
pkgdesc='Provides GNU libiconv.so and libcharset.so'
arch=('i386' 'amd64')
url='http://www.gnu.org/software/libiconv/'
license=('LGPL')
source=("http://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz"
	"http://distcache.FreeBSD.org/local-distfiles/kwm/libiconv-1.14-ja-1.patch.gz")
sha256sums=('72b24ded17d687193c3366d0ebe7cde1e6b18f0df8c55438ac95be39e8a30613'
            'c9474ee420b971029073dabf5e599b049945fbb040f353dce4db32191401bfee')

prepare() {
	cd $srcdir/${pkgname}-${pkgver}

	patch -p1 -i ${srcdir}/${pkgname}-${pkgver}-ja-1.patch 

	sed -i '' -e 's:ei_ksc5601:ei_euc_kr:g' lib/aliases.h
	sed -i ''  -e 's:gawk:awk:g' configure \
		libcharset/configure \
		preload/configure

	CFLAGS+=" -std=gnu89"
}

build() {
	cd $srcdir/${pkgname}-${pkgver}
	./configure --prefix=/usr/local \
		am_cv_func_iconv=no \
		--enable-extra-encodings

	gmake
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  gmake DESTDIR=${pkgdir} install

}
# vim:set ts=2 sw=2 et:
