# Port: lang/gawk
pkgname=gawk
pkgver=4.1.4
pkgrel=1
pkgdesc="GNU version of awk"
arch=('i386' 'amd64')
url="http://www.gnu.org/directory/GNU/gawk.html"
license=('GPL')
groups=('base-devel')
depends=('mpfr' 'gmp' 'gettext-runtime' 'libiconv' 'libsigsegv' 'readline')
provides=('awk')
install=gawk.install
source=(ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=('8c03080e2b5a56263e8783f6f1f306398d4591be18254041f3f547efef944d35'
            'SKIP')

prepare() {
	cd ${srcdir}/${pkgname}-${pkgver}

	sed -i '' -e 's/ -export-dynamic/ -Wl,--export-dynamic/' \
		configure
}

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	AWK=/usr/bin/awk ./configure --prefix=/usr \
	--libexecdir=/usr/libexec --with-libiconv-prefix=/usr/local \
	--with-libsigsegv

	make 
}

check() {
	cd ${srcdir}/${pkgname}-${pkgver}
	# one test failure calling mktime. we can ignore this
	make check || true
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
	rm -f "${pkgdir}/usr/bin/awk"
}
