# $Id: PKGBUILD 180833 2013-03-26 15:59:12Z bpiotrowski $
# Maintainer: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>

pkgbase=mariadb
pkgname=('libmariadbclient' 'mariadb-clients' 'mariadb' 'mytop')
pkgver=5.5.37
_pkgver=${pkgver/.a/a}
pkgrel=2
arch=('i686' 'x86_64')
license=('GPL')
url='http://mariadb.org/'
makedepends=('cmake' 'gcc-fortran')
options=('!libtool')
port_patches=(
	patch-mysys_default.c
	patch-scripts_mysqld_safe.sh
	patch-sql_sql_trigger.cc
	patch-sql_sys_vars.cc
	patch-support-files_CMakeLists.txt
)
source=(http://mariadb.kisiek.net/mariadb-$pkgver/source/mariadb-$pkgver.tar.gz
	mariadb-post.sh
	mariadb-tmpfile.conf
	mysql-server.in
	${port_patches[@]})
sha256sums=('a0faf492b3595d938684ed701812a4bd5aaab395b8402efe3322338a80fb3c9c'
            '368f9fd2454d80eb32abb8f29f703d1cf9553353fb9e1ae4529c4b851cb8c5dd'
            '2af318c52ae0fe5428e8a9245d1b0fc3bc5ce153842d1563329ceb1edfa83ddd'
            '7b9968696e67a4a097452aa189d1b7a2b843bd623cc850063d84736efaba19f3'
            '66f4969db48e8999c9bd8a56f14a97b901edaa319056b1f52c66188e7d80cd74'
            '4b1c2916bc1d6d2566b9bf046827b1bde4920eb0e55f8451ec70ea78aaf09ae5'
            '41152ed552ef098bf94fdb342255ad776629c3023018416a4111c79fdfd48660'
            'd734d194b1722e788f6dff34c8605e067654363268f796be4aadac1a5ced6e56'
            '49e9c13f17cc5af75355abefdeb6f2688829b18327929cf1b6c5be7d64a0cc87')

build() {
  cd $srcdir/$pkgbase-$pkgver

  for patch in  ${port_patches[@]}; do
   msg "Applying patch ${patch}"
   patch -p0 -i ${srcdir}/$patch
  done

  cd $srcdir
  if [ ! -d build ]; then
    mkdir build
  fi

  if [ ! -d "${srcdir}/bin" ]; then
    mkdir ${srcdir}/bin
    ln -s /usr/bin/gfortran ${srcdir}/bin/f77
  fi

  #just until we fix gcc-fortran missing f77
  export PATH="${srcdir}/bin:$PATH"
  cd build

  cmake ../$pkgbase-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc/mysql \
    -DMYSQL_DATADIR=/var/lib/mysql \
    -DMYSQL_UNIX_ADDR=/run/mysqld/mysqld.sock \
    -DDEFAULT_CHARSET=utf8 \
    -DDEFAULT_COLLATION=utf8_general_ci \
    -DENABLED_LOCAL_INFILE=ON \
    -DINSTALL_INFODIR=share/mysql/docs \
    -DINSTALL_MANDIR=share/man \
    -DINSTALL_PLUGINDIR=/usr/lib/mysql/plugin \
    -DINSTALL_SCRIPTDIR=bin \
    -DINSTALL_INCLUDEDIR=include/mysql \
    -DINSTALL_DOCREADMEDIR=share/mysql \
    -DINSTALL_SUPPORTFILESDIR=share/mysql \
    -DINSTALL_MYSQLSHAREDIR=share/mysql \
    -DINSTALL_DOCDIR=share/mysql/docs \
    -DINSTALL_SHAREDIR=share/mysql \
    -DWITH_READLINE=ON \
    -DWITH_ZLIB=system \
    -DWITH_SSL=system \
    -DWITH_LIBWRAP=OFF \
    -DWITH_EXTRA_CHARSETS=complex \
    -DWITH_EMBEDDED_SERVER=ON \
    -DWITH_ARCHIVE_STORAGE_ENGINE=1 \
    -DWITH_BLACKHOLE_STORAGE_ENGINE=1 \
    -DWITH_INNOBASE_STORAGE_ENGINE=1 \
    -DWITH_PARTITION_STORAGE_ENGINE=1 \
    -DWITHOUT_EXAMPLE_STORAGE_ENGINE=1 \
    -DWITHOUT_FEDERATED_STORAGE_ENGINE=1 \
    -DWITHOUT_PBXT_STORAGE_ENGINE=1 \
    -DCMAKE_C_FLAGS="-fPIC ${CFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -fomit-frame-pointer" \
    -DCMAKE_CXX_FLAGS="-fPIC ${CXXFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -felide-constructors -fno-rtti" \
    -DWITH_MYSQLD_LDFLAGS="${LDFLAGS}"


  make
}

package_libmariadbclient() {
  pkgdesc='MariaDB client libraries'
  conflicts=('libmysqlclient')
  provides=("libmysqlclient=$pkgver")
  
  cd "$srcdir"/build
  for dir in include libmysql libmysqld libservices; do
    make -C $dir DESTDIR="$pkgdir" install
  done

  install -d "$pkgdir"/usr/bin
  install -m755 scripts/mysql_config "$pkgdir"/usr/bin/
  install -d "$pkgdir"/usr/share/man/man1
  for man in mysql_config mysql_client_test_embedded mysqltest_embedded; do
    install -m644 "$srcdir"/$pkgbase-$_pkgver/man/$man.1 "$pkgdir"/usr/share/man/man1/$man.1
  done

}

package_mariadb-clients() {
  pkgdesc='MariaDB client tools'
  depends=('libmariadbclient')
  conflicts=('mysql-clients')
  provides=("mysql-clients=$pkgver")
  
  cd "$srcdir"/build
  make -C client DESTDIR="$pkgdir" install

  # install man pages
  install -d "$pkgdir"/usr/share/man/man1
  for man in mysql mysqladmin mysqlcheck mysqldump mysqlimport mysqlshow mysqlslap; do
    install -m644 "$srcdir"/$pkgbase-$pkgver/man/$man.1 "$pkgdir"/usr/share/man/man1/$man.1
  done

  # provided by mariadb
  rm "$pkgdir"/usr/bin/{mysql_{plugin,upgrade},mysqlbinlog,mysqltest}
}

package_mytop() {
  pkgdesc='Top clone for MariaDB'
  depends=('perl-dbd-mysql' 'perl-term-readkey')

  cd build
	install -dm755 "$pkgdir"/usr/bin
  install -m755 scripts/mytop "$pkgdir"/usr/bin/mytop
}

package_mariadb() {
  pkgdesc='Fast SQL database server, drop-in replacement for MySQL'
  backup=('etc/mysql/my.cnf')
  install=mariadb.install
  depends=('mariadb-clients')
  conflicts=('mysql' 'mytop')
  provides=("mysql=$pkgver" 'mytop')
  optdepends=('perl-dbd-mysql: mytop dependency'
              'perl-term-readkey: mytop dependency')
  options=('emptydirs')

  cd "$srcdir"/build
  make DESTDIR="$pkgdir" install
  
  install -dm755 "$pkgdir"/etc/mysql/
	install -dm755 "${pkgdir}"/etc/rc.d
	install -m775 "${srcdir}/mysql-server.in" ${pkgdir}/etc/rc.d/mysql
	chmod +x ${pkgdir}/etc/rc.d/mysql

  install -m644 "$pkgdir"/usr/share/mysql/my-medium.cnf "$pkgdir"/etc/mysql/my.cnf
  install -m755 "$srcdir"/mariadb-post.sh "$pkgdir"/usr/bin/mysqld-post

  cd "$pkgdir"
  rm usr/bin/{mysql_config,mysql_client_test_embedded,mysqltest_embedded}

  rm usr/lib/libmysql*
  rm -r ${pkgdir}/usr/include/
  rm ${pkgdir}/usr/share/man/man1/{mysql_config,mysql_client_test_embedded,mysqltest_embedded}.1

  # provided by mariadb-clients
  rm usr/bin/{mysql,mysqladmin,mysqlcheck,mysqldump,mysqlimport,mysqlshow,mysqlslap}
  rm usr/share/man/man1/{mysql,mysqladmin,mysqlcheck,mysqldump,mysqlimport,mysqlshow,mysqlslap}.1

  # provided by mytop
  rm usr/bin/mytop

  # not needed
  rm -r usr/{data,mysql-test,sql-bench}
  rm usr/share/man/man1/mysql-test-run.pl.1

  #provided in base
  rm -f ${pkgdir}/usr/bin/perror
  rm -f ${pkgdir}/usr/share/man/man1/perror.1
}

