pkgname=opentmpfiles
pkgver=0.1.3
pkgrel=2
pkgdesc='a standalone utility for handling systemd-style tmpfiles.d settings'
arch=('any')
url='https://github.com/openrc/opentmpfiles'
license=('BSD')
makedepends=("ginstall")
backup=('etc/conf.d/opentmpfiles-dev'
        'etc/conf.d/opentmpfiles-setup')
source=($pkgname-$pkgver::https://github.com/OpenRC/$pkgname/archive/$pkgver.tar.gz)
sha512sums=('fd0b10ebfeb621466edadede50ec6e8f415861ab3b14b833c75bd71f7296e9c091e59144d39e1f9efd342b6d97776cade5a692c941085e33e9651fd885da7121')

package() {
    cd "${pkgname}-${pkgver}"
    
    # install tmpfiles.sh script
    gmake bindir=/usr/bin DESTDIR="${pkgdir}" install

    # put service files in correct locations
    for f in opentmpfiles-dev opentmpfiles-setup; do
        ginstall -Dm755 openrc/$f.confd "$pkgdir"/etc/conf.d/$f
        ginstall -Dm755 openrc/$f.initd "$pkgdir"/etc/init.d/$f
    done
    
    # License is BSD 2 clause type and needs to be installed
    ginstall -Dm644 "${srcdir}/${pkgname}-${pkgver}/license" "${pkgdir}/usr/share/licenses/${pkgname}/license"
}
