# Port: databases/sqlite3
pkgbase="sqlite"
pkgname=('sqlite' 'sqlite-tcl' 'sqlite-doc' 'sqlite-analyzer')
_srcver=3200000
_docver=${_srcver}
pkgver=3.20.0
pkgrel=1
pkgdesc="A C library that implements an SQL database engine"
arch=('i386' 'amd64')
license=('custom:Public Domain')
url="http://www.sqlite.org/"
makedepends=('tcl' 'ginstall' 'readline')
source=(http://www.sqlite.org/2017/sqlite-src-${_srcver}.zip
        http://www.sqlite.org/2017/sqlite-doc-${_docver}.zip
        license.txt)
options=('!emptydirs' '!makeflags' 'pathfix' 'gnu_configure' 'libtoolfix')
sha256sums=('4b358e77a85d128651e504aaf548253ffe10d5d399aaef5a6e34f29262614bee'
            '5b7a4dc411937f33f17a8b0b7cb490d5e718fe37a751ee772d8989c23745f394'
            '4e57d9ac979f1c9872e69799c2597eeef4c6ce7224f3ede0bf9dc8d217b1e65d')

build() {
  export CPPFLAGS="$CPPFLAGS -DHAVE_ISNAN=1 \
		-DHAVE_MALLOC_USABLE_SIZE=1 \
		-DSQLITE_ENABLE_DBSTAT_VTAB=1 \
		-DSQLITE_ENABLE_FTS3_TOKENIZER=1 \
		-DSQLITE_ENABLE_FTS3_PARENTHESIS=1 \
		-DSQLITE_ENABLE_FTS4=1 \
		-DSQLITE_ENABLE_COLUMN_METADATA=1 \
		-DSQLITE_SECURE_DELETE=1 \
		-DSQLITE_ENABLE_UNLOCK_NOTIFY=1 \
		-DSQLITE_USE_URI=1 \
		-DSQLITE_TEMP_STORE=1 \
		-DSQLITE_ENABLE_RTREE=1"

  # build sqlite
  cd "$srcdir"/sqlite-src-$_srcver
  ./configure --prefix=/usr \
	--disable-static \
	--disable-amalgamation \
	--disable-editline \
	--enable-fts3 \
	--enable-fts4 \
	--enable-fts5 \
	--enable-rtree \
	--enable-json1 \
	TCLLIBDIR=/usr/lib/sqlite$pkgver
  gmake
  # build additional tools
  gmake showdb showjournal showstat4 showwal sqldiff sqlite3_analyzer
}

package_sqlite() {

 pkgdesc="A C library that implements an SQL database engine"
 depends=('readline')
 provides=("sqlite3=$pkgver")
 replaces=("sqlite3")

  cd sqlite-src-$_srcver
  make DESTDIR=${pkgdir} install

  ginstall -m755 showdb showjournal showstat4 showwal sqldiff ${pkgdir}/usr/bin/

  # install manpage
  ginstall -m755 -d ${pkgdir}/usr/share/man/man1
  ginstall -m644 sqlite3.1 ${pkgdir}/usr/share/man/man1/

  # license - no linking required because pkgbase=pkgname
  ginstall -D -m644 ${srcdir}/license.txt ${pkgdir}/usr/share/licenses/${pkgbase}/license.txt

  # split out tcl extension
  mkdir $srcdir/tcl
  mv $pkgdir/usr/lib/sqlite* $srcdir/tcl
}

package_sqlite-tcl() {

 pkgdesc="sqlite Tcl Extension Architecture (TEA)"
 depends=('sqlite')
 provides=("sqlite3-tcl=$pkgver")
 replaces=("sqlite3-tcl")

  ginstall -m755 -d ${pkgdir}/usr/lib
  mv $srcdir/tcl/* ${pkgdir}/usr/lib

  # install manpage
  ginstall -m755 -d ${pkgdir}/usr/share/man/mann
  ginstall -m644 ${srcdir}/sqlite-src-$_srcver/autoconf/tea/doc/sqlite3.n ${pkgdir}/usr/share/man/mann/

  # link license
  ginstall -m755 -d ${pkgdir}/usr/share/licenses
  ln -sf /usr/share/licenses/${pkgbase} "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_sqlite-analyzer() {

 pkgdesc="An analysis program for sqlite3 database files"
 depends=('sqlite' 'tcl')

  cd sqlite-src-$_srcver
  ginstall -m755 -d ${pkgdir}/usr/bin
  ginstall -m755 sqlite3_analyzer ${pkgdir}/usr/bin/
}

package_sqlite-doc() {

 pkgdesc="most of the static HTML files that comprise this website, including all of the SQL Syntax and the C/C++ interface specs and other miscellaneous documentation"
 #arch=('any') - not yet supported
 provides=("sqlite3-doc=$pkgver")
 replaces=("sqlite3-doc")

  cd sqlite-doc-${_docver}
  mkdir -p ${pkgdir}/usr/share/doc/${pkgbase}
  cp -R *  ${pkgdir}/usr/share/doc/${pkgbase}/
}

