# Maintainer:  Anthony Donnelly Amzo@archbsd.net>
# Port: shells/bash
pkgname=bash
_basever=4.4
_patchlevel=12 # prepare for some patches
pkgver=$_basever.$_patchlevel
pkgrel=1
pkgdesc="The GNU Bourne Again shell"
arch=('i386' 'amd64')
license=('GPL')
url="http://www.gnu.org/software/bash/bash.html"
backup=(etc/bash.bash{rc,_logout} usr/share/skel/.bash{rc,_profile,_logout})
depends=('gettext-runtime' 'libiconv' 'freebsd-world')
makedepends=('gmake')
install=bash.install
options=('iconv' 'pathfix' 'gnu_configure')
patchdir=("$pkgname-$_basever")
source=(http://ftp.gnu.org/gnu/bash/bash-$_basever.tar.gz{,.sig}
        dot.bashrc
        dot.bash_profile
        dot.bash_logout
        system.bashrc
        system.bash_logout)
sha256sums=('d86b3392c1202e8ff5a423b302e6284db7f8f435ea9f39b5b1b20fd3ac36dfcb'
            'SKIP'
            'c7b148dc9e0927fe8d8b400faf70da2b243581767a2db2ecba37b4a3e0e1af24'
            'e149407c2bee17779caec70a7edd3d0000d172e7e4347429b80cb4d55bcec9c2'
            '4330edf340394d0dae50afb04ac2a621f106fe67fb634ec81c4bfb98be2a1eb5'
            'afcea714e921e7f73d124462eb8f7ad5ef92350aa7704a96eb68ce0162921461'
            '025bccfb374a3edce0ff8154d990689f30976b78f7a932dc9a6fcef81821811e'
            '3e28d91531752df9a8cb167ad07cc542abaf944de9353fe8c6a535c9f1f17f0f'
            'SKIP'
            '7020a0183e17a7233e665b979c78c184ea369cfaf3e8b4b11f5547ecb7c13c53'
            'SKIP'
            '51df5a9192fdefe0ddca4bdf290932f74be03ffd0503a3d112e4199905e718b2'
            'SKIP'
            'ad080a30a4ac6c1273373617f29628cc320a35c8cd06913894794293dc52c8b3'
            'SKIP'
            '221e4b725b770ad0bb6924df3f8d04f89eeca4558f6e4c777dfa93e967090529'
            'SKIP'
            '6a8e2e2a6180d0f1ce39dcd651622fb6d2fd05db7c459f64ae42d667f1e344b8'
            'SKIP'
            'de1ccc07b7bfc9e25243ad854f3bbb5d3ebf9155b0477df16aaf00a7b0d5edaf'
            'SKIP'
            '86144700465933636d7b945e89b77df95d3620034725be161ca0ca5a42e239ba'
            'SKIP'
            '0b6bdd1a18a0d20e330cc3bc71e048864e4a13652e29dc0ebf3918bea729343c'
            'SKIP'
            '8465c6f2c56afe559402265b39d9e94368954930f9aa7f3dfa6d36dd66868e06'
            'SKIP'
            'dd56426ef7d7295e1107c0b3d06c192eb9298f4023c202ca2ba6266c613d170d'
            'SKIP'
            'fac271d2bf6372c9903e3b353cb9eda044d7fe36b5aab52f21f3f21cd6a2063e'
            'SKIP')

if [[ $((10#${_patchlevel})) -gt 0 ]]; then
  for (( _p=1; _p<=$((10#${_patchlevel})); _p++ )); do
    source=(${source[@]} http://ftp.gnu.org/gnu/bash/bash-$_basever-patches/bash${_basever//.}-$(printf "%03d" $_p){,.sig})
  done
fi
validpgpkeys=('7C0135FB088AAF6C66C650B9BB5869F064EA74AB') # Chet Ramey

prepare() {
  cd "$srcdir/$pkgname-$_basever"

  for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
    msg "applying patch bash${_basever//./}-$(printf "%03d" $p)"
    patch -p0 -i ../bash${_basever//./}-$(printf "%03d" $p)
  done
}

build() {
  cd "${srcdir}/${pkgname}-$_basever"

  _bashconfig=(-DDEFAULT_PATH_VALUE=\'\"/bin:/sbin:/usr/local/sbin:/usr/local/bin:/usr/bin\"\'
               -DSTANDARD_UTILS_PATH=\'\"/usr/bin\"\'
               -DSYS_BASHRC=\'\"/etc/bash.bashrc\"\'
               -DSYS_BASH_LOGOUT=\'\"/etc/bash.bash_logout\"\')
  export CFLAGS="${CFLAGS} ${_bashconfig[@]} -Wl,-export-dynamic"

  ./configure --prefix=/usr \
    --without-bash-malloc ${ICONV_CONFIGURE_ARG}

  gmake
}

check() {
  gmake -C $pkgname-$_basever check
}

package() {
  cd "${srcdir}/${pkgname}-$_basever"
  gmake DESTDIR=${pkgdir} install

  # system-wide configuration files
  install -dm755 ${pkgdir}/etc
  install -m644 ${srcdir}/system.bashrc ${pkgdir}/etc/bash.bashrc
  install -m644 ${srcdir}/system.bash_logout ${pkgdir}/etc/bash.bash_logout

  # user configuration file skeletons
  install -dm755 ${pkgdir}/usr/share/skel/
  install -m644 ${srcdir}/dot.bashrc ${pkgdir}/usr/share/skel/.bashrc
  install -m644 ${srcdir}/dot.bash_profile ${pkgdir}/usr/share/skel/.bash_profile
  install -m644 ${srcdir}/dot.bash_logout ${pkgdir}/usr/share/skel/.bash_logout

  install -dm755 "${pkgdir}/bin"
  ln -s "/usr/bin/bash" "${pkgdir}/bin/bash"

  # Remove reference to srcdir
  sed -i '' -e '/BUILD_DIR/d' ${pkgdir}/usr/lib/bash/Makefile.inc
}
