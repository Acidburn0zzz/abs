# $Id$
# Maintainer: Anthony Amzo@ArchBSD.net>
_branch=releng/10.1
pkgname=freebsd-configs
pkgver=2015.01
pkgrel=1
pkgdesc='System wide FreeBSD configs'
arch=('any')
license=('BSL')
url='http://www.archbsd.net'
groups=('base')
depends=("freebsd-world>=1:10.1")
patchdir=("${srcdir}")
backup=('etc/profile' 'etc/sysctl.conf' 'boot/loader.conf' 'boot/brand.4th' 
        'boot/beastie.4th' 'etc/devd/asus.conf' 'etc/devd/uath.conf' 
        'etc/devd/usb.conf' 'etc/csh.cshrc' 'etc/devd.conf' 'etc/devfs.conf'
        'etc/motd' 'etc/os-release')
source=("https://raw.github.com/freebsd/freebsd/${_branch}/etc/devd/asus.conf"
	"https://raw.github.com/freebsd/freebsd/${_branch}/etc/devd/apple.conf"
	"https://raw.github.com/freebsd/freebsd/${_branch}/etc/devd/uath.conf"
	"https://raw.github.com/freebsd/freebsd/${_branch}/etc/devd/usb.conf"
	"https://raw.github.com/freebsd/freebsd/${_branch}/etc/motd"
	"https://raw.github.com/freebsd/freebsd/${_branch}/etc/devd.conf"
	"https://raw.github.com/freebsd/freebsd/${_branch}/etc/devfs.conf"
	"https://raw.github.com/freebsd/freebsd/${_branch}/etc/defaults/rc.conf"
	"https://raw.githubusercontent.com/freebsd/freebsd/${_branch}/sys/boot/forth/beastie.4th"
	"https://raw.githubusercontent.com/freebsd/freebsd/${_branch}/sys/boot/forth/brand.4th"
	'os-release' 'csh.cshrc' 'profile' 'sysctl.conf' 'patch-logo'
	'loader.conf' 'disable_moused-devd')
md5sums=('3fab19520668156176f62be9e0af21c1'
         '6d8c1bf7220d24a7f426efd2ba0be7e2'
         '88ec33a1523b62b2e3f7488ff8ca7c48'
         '63ad4fd4a55647ad9be79698f372e222'
         'b38d686cc2299afb691a1cc6f15e3df1'
         '734954e4574d4ac5da80d78150149dea'
         'd8a21e59a96525f106ab750d5938af40'
         'c3b7948ebaa095b68d67743591de528a'
         'd6f0e2b3981d063e454d530e27692f1a'
         '70a1fcf535d0814f3d155e21a397a85a'
         '239dd0da7c1de745c4a823b0404f7ee5'
         'b516bd19fc7cfaa27463edfab9b2f1c0'
         'f3b6ae7db8adffaaa4bffc6099dcbd50'
         '461507865683b0d07a090df9bcef6e16'
         '9f43efd20c311deeb6d13b333c689d2f'
         '4a04b5ab418dbd367b5f17758135a247'
         '02ac55efc9c4d1a44d2cd4b148fe39a8')

build() {
	patch -p0 -i patch-logo
	patch -p0 -i disable_moused-devd

	sed -i '' -e 's|ldconfig_local_dirs="/usr/local/libdata/ldconfig"|ldconfig_local_dirs="/usr/lib/ldconfig"|g' \
	-e 's|ldconfig_local32_dirs="/usr/local/libdata/ldconfig32"|ldconfig_local32_dirs="/usr/lib/ldconfig32"|g' \
	-e 's|sendmail_submit_enable="YES"|sendmail_submit_enable="NO"|g' \
	-e 's|sendmail_outbound_enable="YES"|sendmail_outbound_enable="NO"|g' \
	-e 's|libexec|lib|g' \
	${srcdir}/rc.conf

	echo "Welcome To ArchBSD" > ${srcdir}/motd
	
}

package() {

#
# setup root filesystem
#


	for d in boot etc ; do
		install -o root -g wheel -d -m755 ${pkgdir}/${d}
	done

	# setup /etc
	install -o root -g wheel -dm755  ${pkgdir}/etc/devd

	for f in motd os-release profile sysctl.conf csh.cshrc devd.conf devfs.conf; do
		install -o root -g wheel -m644 ${srcdir}/${f} ${pkgdir}/etc/
	done

	for f in loader.conf brand.4th beastie.4th; do
		install -o root -g wheel -m644 ${srcdir}/${f} ${pkgdir}/boot/
	done

	for f in asus.conf uath.conf usb.conf; do
		install -o root -g wheel -m644 ${srcdir}/${f} ${pkgdir}/etc/devd/
	done

	install -o root -g wheel -dm755 "${pkgdir}/etc/defaults"
	install -o root -g wheel -m644 "${srcdir}/rc.conf" "${pkgdir}/etc/defaults/"
	install -o root -g wheel -dm755 "${pkgdir}/etc/local.d"

	touch "${srcdir}/archbsd-release"
	install -o root -g wheel -m644 "${srcdir}/archbsd-release" ${pkgdir}/etc/archbsd-release
}
