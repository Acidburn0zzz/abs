pkgname=gmp
pkgver=5.1.3
pkgrel=1
pkgdesc="A free library for arbitrary precision arithmetic"
arch=('i686' 'x86_64')
url="http://gmplib.org/"
depends=('bash')
license=('LGPL3')
options=('!libtool' 'libtoolfix')
install=gmp.install
source=(ftp://ftp.gmplib.org/pub/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.xz{,.sig})
md5sums=('e5fe367801ff067b923d1e6a126448aa'
         'SKIP')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i '' "s/\(athlon64\-\*\-\*\)/x86_64-*-* | \1/;s/\(amd64)\)/x86_64 | \1/" \
		configure
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
	if [ "${CARCH}" == "x86_64" ]; then
    conf_args=(ABI=64)

  else
    conf_args=(ABI=32)
  fi

  ./configure --build=${CHOST} \
    --prefix=/usr --enable-cxx \
    --infodir=/usr/share/info \
    ${conf_args[@]}
    
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
}
