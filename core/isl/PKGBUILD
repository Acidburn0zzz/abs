# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=isl
pkgver=0.13
pkgrel=10
pkgdesc="Library for manipulating sets and relations of integer points bounded by linear constraints"
arch=('i386' 'amd64')
url="http://freecode.com/projects/isl"
depends=('gmp' 'libc')
license=('MIT')
options=('!libtool' 'libtoolfix')
source=(http://isl.gforge.inria.fr/$pkgname-$pkgver.tar.bz2)
md5sums=('e4cf20c4c10d5d613457558b1f46b5e2')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr

  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make -j1 DESTDIR="$pkgdir/" install

  install -dm755 $pkgdir/usr/share/licenses/isl
  install -m644 LICENSE $pkgdir/usr/share/licenses/isl/LICENSE
}
