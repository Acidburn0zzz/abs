# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=isl
pkgver=0.12.2
pkgrel=1
pkgdesc="Library for manipulating sets and relations of integer points bounded by linear constraints"
arch=('i686' 'x86_64')
url="http://freecode.com/projects/isl"
depends=('gmp')
license=('MIT')
options=('!libtool')
source=(http://isl.gforge.inria.fr/$pkgname-$pkgver.tar.bz2)
md5sums=('e039bfcfb6c2ab039b8ee69bf883e824')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # prevent using gawk, causes a circular dep hell when gmp gets a lib bump
  AWK=/usr/bin/awk ./configure --prefix=/usr
  find . -type f | xargs sed -i '' -e 's/gawk/awk/g'
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make -j1 DESTDIR="$pkgdir/" install

  install -dm755 $pkgdir/usr/share/gdb/auto-load/usr/lib/
  mv $pkgdir/{,/usr/share/gdb/auto-load/}usr/lib/libisl.so.12-gdb.py
  
  install -dm755 $pkgdir/usr/share/licenses/isl
  install -m644 LICENSE $pkgdir/usr/share/licenses/isl/LICENSE
}
