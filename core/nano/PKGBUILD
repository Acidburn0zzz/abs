# $Id: PKGBUILD 142207 2011-11-06 08:47:48Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Judd <judd@archlinux.org>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=nano
pkgver=2.4.2
pkgrel=5
pkgdesc="Pico editor clone with enhancements"
arch=('i386' 'amd64')
license=('GPL')
url="http://www.nano-editor.org"
depends=('ncurses' 'gettext')
backup=('etc/nanorc')
install=nano.install
source=(http://www.nano-editor.org/dist/v2.4/${pkgname}-${pkgver}.tar.gz{,.asc})
md5sums=('ce6968992fec4283c17984b53554168b'
         'SKIP')
validpgpkeys=('8DA6FE7BFA7A418AB3CB2354BCB356DF91009FA7') # "Chris Allegretta <chrisa@asty.org>"

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --enable-color \
    --enable-nanorc \
    --enable-multibuffer \
    --disable-wrapping-as-root
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -dm755 ${pkgdir}/etc/
  install -m644 ${srcdir}/${pkgname}-${pkgver}/doc/nanorc.sample ${pkgdir}/etc/nanorc
}
