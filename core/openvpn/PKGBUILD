# Port: security/openvpn
pkgname=openvpn
pkgver=2.3.10
pkgrel=1
pkgdesc="An easy-to-use, robust, and highly configurable VPN (Virtual Private Network)"
arch=('i386' 'amd64')
url="http://openvpn.net/index.php/open-source.html"
depends=('lzo2' 'easy-rsa') # 'openssl' in world
license=('custom')
source=(http://swupdate.openvpn.net/community/releases/openvpn-${pkgver}.tar.gz{,.asc}
        http://build.openvpn.net/downloads/releases/easy-rsa-2.2.0_master.tar.gz{,.asc}
        openvpn.in)
md5sums=('275a640ccfc6a9f9a8faa4c84a74d294'
         'SKIP'
         'fbf818b6e1f212e77b9ce0e6d92584a1'
         'SKIP'
         '3226a9c218641ea2f525ac785a6399e1')
options=(libtoolfix)

build() {
  cd $srcdir/$pkgname-$pkgver
  
  # Build openvpn
  CFLAGS="$CFLAGS -DPLUGIN_LIBDIR=\\\"/usr/lib/openvpn\\\" -fPIC" ./configure \
    --prefix=/usr \
    --sbindir=/usr/sbin \
    --enable-password-save \
    --mandir=/usr/share/man \
    --with-crypto-library=openssl
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  # Install openvpn
  make DESTDIR=$pkgdir install
  install -dm755 $pkgdir/etc/openvpn
  # Install examples
  install -dm755 $pkgdir/usr/share/openvpn
  cp -r sample/sample-config-files $pkgdir/usr/share/openvpn/examples
  # Install license
  install -dm755 $pkgdir/usr/share/licenses/$pkgname
  install -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
  # Install contrib
  install -dm755 $pkgdir/usr/share/openvpn/contrib
  cp -r contrib $pkgdir/usr/share/openvpn

  # Install rc scripts
  install -dm755 $pkgdir/etc/rc.d
  install -m755 $srcdir/openvpn.in $pkgdir/etc/rc.d/openvpn
}
