# Maintainer: Anthony Donnelly <Amzo@archbsd.net>
# Port: security/ca_root_nss
_downloadname=nss
pkgname=ca_root_nss
pkgver=3.22.2
pkgrel=2
pkgdesc="The root certificate bundle from the Mozilla Project"
arch=('any')
license=('MPL')
url="http://www.mozilla.org/"
makedepends=('perl')
install=${pkgname}.install
source=(https://ftp.mozilla.org/pub/mozilla.org/security/nss/releases/NSS_${pkgver//\./_}_RTM/src/${_downloadname}-${pkgver}.tar.gz
	"MAca-bundle.pl")
md5sums=('c42c743b3e70b2912c77fb4129b0a12b'
         '3a39202d9085d11952bf5dba3189bfe9')

build() {
	cd ${srcdir}/${_downloadname}-${pkgver}

	cp ${srcdir}/${_downloadname}-${pkgver}/${_downloadname}/lib/ckfw/builtins/certdata.txt ${srcdir}

	perl ${srcdir}/MAca-bundle.pl \
	< ${srcdir}/certdata.txt > \
	${srcdir}/ca-root-nss.crt
}

package() {
	install -dm755 ${pkgdir}/usr/share/certs
	install -m644 ${srcdir}/ca-root-nss.crt ${pkgdir}/usr/share/certs
	install -dm755 ${pkgdir}/etc/ssl
	ln -s /usr/share/certs/ca-root-nss.crt "${pkgdir}/etc/ssl/cert.pem"
}
