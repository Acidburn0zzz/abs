# Maintainer: Anthony Donnelly <Amzo@archbsd.net>
# Contributor: Wolfgang Bumiller <blub@speed.at>
# Port: devel/gmake

_downloadname=make
pkgname=gmake
pkgver=4.2.1
pkgrel=1
pkgdesc="GNU make utility to maintain groups of programs"
arch=('i386' 'amd64')
url="http://www.gnu.org/software/make"
license=('GPL3')
groups=('base-devel')
depends=('gettext-runtime' 'libiconv')
makedepends=('gettext-tools')
options=('iconv')
install=$pkgname.install
patchdir=(${_downloadname}-${pkgver})
source=(ftp://ftp.gnu.org/gnu/${_downloadname}/${_downloadname}-${pkgver}.tar.bz2{,.sig}
	patch-test_driver)
validpgpkeys=('3D2554F0A15338AB9AF1BB9D96B047156338B6D4')   # Paul Smith
sha256sums=('d6e262bf3601b42d2b1e4ef8310029e1dcf20083c5446b4b7aa67081fdffc589'
            'SKIP'
            '6ec589a99fbf2c96ea21098ead45ef9a64e517f3caaa3edb5cd1030f69941559')

build() {
  cd ${srcdir}/${_downloadname}-${pkgver}


  ./configure --prefix=/usr \
              --infodir=/usr/share/info --mandir=/usr/share/man \
              --program-prefix=g --without-guile \
	      ${ICONV_CONFIGURE_ARG} MAKEINFO="makeinfo --no-split"
  make
}

check() {
  cd ${srcdir}/${_downloadname}-${pkgver}

  # Patch the test timeout
  # The test times out and fails before it hits the
  # max open files on a FreeBSD system, change the timeout
  # so the test is successful.

  patch -p0 -i "${srcdir}/patch-test_driver"
  make check || true ## missing tests
}

package() {
  cd ${srcdir}/${_downloadname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
