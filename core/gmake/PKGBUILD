# Maintainer: Anthony Donnelly <Amzo@archbsd.net>
# Contributor: Wolfgang Bumiller <blub@speed.at>

_downloadname=make
pkgname=gmake
pkgver=4.1
pkgrel=1
pkgdesc="GNU make utility to maintain groups of programs"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/make"
license=('GPL3')
groups=('base-devel')
depends=('gettext')
install=$pkgname.install
source=(ftp://ftp.gnu.org/gnu/${_downloadname}/${_downloadname}-${pkgver}.tar.bz2{,.sig})
md5sums=('57a7a224a822f94789a587ccbcedff69'
         'SKIP')

build() {
  cd ${srcdir}/${_downloadname}-${pkgver}


  ./configure --prefix=/usr \
              --infodir=/usr/share/info --mandir=/usr/share/man \
              --program-prefix=g
  make
}

check() {
  cd ${srcdir}/${_downloadname}-${pkgver}

  # Patch the test timeout
  # The test times out and fails before it hits the
  # max open files on a FreeBSD system, change the timeout
  # so the test is successful.

  patch -p0 -i "${srcdir}/patch-test_driver"
  make check
}

package() {
  cd ${srcdir}/${_downloadname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
