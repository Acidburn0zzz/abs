# Maintainer: Anthony Donnelly <Amzo@archbsd.net>
# Contributor: Wolfgang Bumiller <blub@speed.at>

_downloadname=make
pkgname=gmake
pkgver=4.1
pkgrel=5
pkgdesc="GNU make utility to maintain groups of programs"
arch=('i686' 'amd64')
url="http://www.gnu.org/software/make"
license=('GPL3')
groups=('base-devel')
depends=('gettext')
install=$pkgname.install
patchdir=(${_downloadname}-${pkgver})
source=(ftp://ftp.gnu.org/gnu/${_downloadname}/${_downloadname}-${pkgver}.tar.bz2{,.sig}
	patch-test_driver)
md5sums=('57a7a224a822f94789a587ccbcedff69'
         'SKIP'
         'c9aa782e8a077ebd3e1594b84b1f95fa')

build() {
  cd ${srcdir}/${_downloadname}-${pkgver}


  ./configure --prefix=/usr \
              --infodir=/usr/share/info --mandir=/usr/share/man \
              --program-prefix=g
  make
}

check() {
  cd ${srcdir}/${_downloadname}-${pkgver}

  # Patch the test timeout
  # The test times out and fails before it hits the
  # max open files on a FreeBSD system, change the timeout
  # so the test is successful.

  patch -p0 -i "${srcdir}/patch-test_driver"
  make check || true ## missing tests
}

package() {
  cd ${srcdir}/${_downloadname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
