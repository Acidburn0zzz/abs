# Port: security/cracklib
pkgname=cracklib
pkgver=2.9.6
pkgrel=1
pkgdesc="Password Checking Library"
arch=('i386' 'amd64')
license=('GPL')
url="http://sourceforge.net/projects/cracklib"
depends=('libc' 'zlib' 'gettext-runtime')
options=('libtoolfix')
source=(https://github.com/${pkgname}/${pkgname}/releases/download/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz
	cracklib.3)
md5sums=('c52f463585d85924b28cdc1e373ae06d'
         '14d1996c436869d3d9f6ac6db12dcb64')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --without-python
  make 
}

check() {
  cd $srcdir/$pkgname-$pkgver
  make check
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install

  install -dm755 ${pkgdir}/usr/share/man/man3
  ${BSD_INSTALL_MAN} ${srcdir}/${pkgname}.3 ${pkgdir}/usr/share/man/man3/
  ln -s ${pkgname}.3 ${pkgdir}/usr/share/man/man3/FascistCheck.3

  # symlink cracklib-small #34778
  mkdir -p $pkgdir/usr/share/dict
  ln -sf /usr/share/${pkgname}/${pkgname}-small $pkgdir/usr/share/dict/${pkgname}-small
  sh ./util/${pkgname}-format dicts/${pkgname}-small \
    | sh ./util/${pkgname}-packer $pkgdir/usr/share/${pkgname}/pw_dict
}
