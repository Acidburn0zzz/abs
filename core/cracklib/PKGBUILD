# $Id: PKGBUILD 191698 2013-07-29 09:44:29Z tpowa $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Federico Quagliata (quaqo) <quaqo@despammed.com>

pkgname=cracklib
pkgver=2.9.4
pkgrel=7
pkgdesc="Password Checking Library"
arch=('i386' 'amd64')
license=('GPL')
url="http://sourceforge.net/projects/cracklib"
depends=('libc' 'zlib' 'gettext-runtime')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('b31f7e3618cda7a2ac38588067275013')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --sbindir=/usr/bin --without-python
  make 
}

check() {
  cd $srcdir/$pkgname-$pkgver
  make check
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  # symlink cracklib-small #34778
  mkdir -p $pkgdir/usr/share/dict
  ln -sf /usr/share/cracklib/cracklib-small $pkgdir/usr/share/dict/cracklib-small
  sh ./util/cracklib-format dicts/cracklib-small \
    | sh ./util/cracklib-packer $pkgdir/usr/share/cracklib/pw_dict
}
