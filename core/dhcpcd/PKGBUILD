# Port: net/dhcpcd

pkgname=dhcpcd
pkgver=6.11.5
pkgrel=1
pkgdesc="RFC2131 compliant DHCP client daemon"
url="http://roy.marples.name/projects/dhcpcd/"
arch=('i386' 'amd64')
license=('BSD')
groups=('base')
depends=('libc')
provides=('dhcp-client')
backup=('etc/dhcpcd.conf')
options=('emptydirs')  # We Need the Empty /var/lib/dhcpcd Directory
source=("http://roy.marples.name/downloads/$pkgname/$pkgname-$pkgver.tar.xz"
	dhcpcd.in)
sha256sums=('6f9674dc7e27e936cc787175404a6171618675ecfb6903ab9887b1b66a87d69e'
            '9cbd991043310d7269b46c9fae565fb1a94e5823be93f3a1bee730dbc0d465f4')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # configure variables
  ./configure \
      --libexecdir=/usr/libexec/dhcpcd \
      --dbdir=/var/lib/dhcpcd \
      --rundir=/run

  # Build
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # Install License
  install -d $pkgdir/usr/share/licenses/$pkgname
  sed 26q "$srcdir/$pkgname-$pkgver/control.h" \
      >>"$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Set Options in /etc/dhcpcd.conf
  echo noipv4ll >> ${pkgdir}/etc/dhcpcd.conf # Disable ip4vall
  install -dm755 "${pkgdir}/etc/rc.d"
  install -m755 "${srcdir}/dhcpcd.in" "${pkgdir}/etc/rc.d/dhcpcd"
}
