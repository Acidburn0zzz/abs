# $Id: PKGBUILD 192483 2013-08-12 21:11:39Z dreisner $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Tom Killian <tom.archlinux.org>
# Contributor: Judd Vinet <jvinet.zeroflux.org>

pkgname=dhcpcd
pkgver=6.9.3
pkgrel=3
pkgdesc="RFC2131 compliant DHCP client daemon"
url="http://roy.marples.name/projects/dhcpcd/"
arch=('amd64')
license=('BSD')
groups=('base')
depends=('libc' 'bash')
provides=('dhcp-client')
backup=('etc/dhcpcd.conf')
options=('emptydirs')  # We Need the Empty /var/lib/dhcpcd Directory
source=("http://roy.marples.name/downloads/$pkgname/$pkgname-$pkgver.tar.xz"
	dhcpcd.in)
sha1sums=('f205894d3d2d505be08fc8b6b5445ac69319244d'
          'c4ef45e8b3c3da2dd8532656891dbf323feb95b0')
build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # configure variables
  ./configure \
      --sbindir=/usr/bin \
      --libexecdir=/usr/lib/dhcpcd \
      --dbdir=/var/lib/dhcpcd \
      --rundir=/run

  # Build
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # Install License
  install -d $pkgdir/usr/share/licenses/$pkgname
  sed 26q "$srcdir/$pkgname-$pkgver/control.h" \
      >>"$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Set Options in /etc/dhcpcd.conf
  echo noipv4ll >> ${pkgdir}/etc/dhcpcd.conf # Disable ip4vall
  install -dm755 "${pkgdir}/etc/rc.d"
  install -m755 "${srcdir}/dhcpcd.in" "${pkgdir}/etc/rc.d/dhcpcd"
}
