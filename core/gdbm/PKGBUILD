# Maintainer: Anthony Donnelly <Amzo@archbsd.net>
# Port: databases/gdbm

pkgname=gdbm
pkgver=1.13
pkgrel=1
pkgdesc="GNU database library"
url="http://www.gnu.org/software/gdbm/gdbm.html"
license=('GPL')
arch=('i386' 'amd64')
depends=('libiconv')
source=(ftp://ftp.gnu.org/gnu/gdbm/${pkgname}-${pkgver}.tar.gz)
options=('!makeflags' 'libtoolfix' 'gnu_configure')
sha256sums=('9d252cbd7d793f7b12bcceaddda98d257c14f4d1890d851c386c37207000a253')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i '' -e 's|LIBS = @LIBS@ -lc|LIBS = @LIBS@|' \
            -e 's|BINOWN = bin|BINOWN = root|' \
            -e 's|BINGRP = bin|BINGRP = wheel|' \
            Makefile.in
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-libgdbm-compat
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # create symlinks for compatibility
  install -dm755 "${pkgdir}"/usr/include/gdbm
  ln -sf ../gdbm.h "${pkgdir}"/usr/include/gdbm/gdbm.h
  ln -sf ../dbm.h  "${pkgdir}"/usr/include/gdbm/dbm.h
  mv -v "${pkgdir}"/usr/include/{,gdbm/}ndbm.h
}
