# Maintainer: Anthony Donnelly <Amzo@archbsd.net>
# Port: databases/gdbm
pkgname=gdbm
pkgver=1.12
pkgrel=1
pkgdesc="GNU database library"
url="http://www.gnu.org/software/gdbm/gdbm.html"
license=('GPL')
arch=('i386' 'amd64')
depends=('libiconv')
source=(ftp://ftp.gnu.org/gnu/gdbm/${pkgname}-${pkgver}.tar.gz
        gdbm-1.10-zeroheaders.patch)
options=('!makeflags')
install=gdbm.install
sha256sums=('d97b2166ee867fd6ca5c022efee80702d6f30dd66af0e03ed092285c3af9bcea'
            'ff33700f7919039037e5f56452c56ca00e968637f6f38485c9b8ae586770ee5a')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Prevent gdbm from storing uninitialized memory content
  # to database files. This patch improves security, as the
  # uninitialized memory might contain sensitive informations
  # from other applications.
  # https://bugzilla.redhat.com/show_bug.cgi?id=4457
  # http://bugs.debian.org/cgi-bin/bugreport.cgi?bug=208927
  patch -Np1 -i ../gdbm-1.10-zeroheaders.patch

  sed -i '' -e 's|LIBS = @LIBS@ -lc|LIBS = @LIBS@|' \
            -e 's|BINOWN = bin|BINOWN = root|' \
            -e 's|BINGRP = bin|BINGRP = wheel|' \
            Makefile.in
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-libgdbm-compat
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # create symlinks for compatibility
  install -dm755 "${pkgdir}"/usr/include/gdbm
  ln -sf ../gdbm.h "${pkgdir}"/usr/include/gdbm/gdbm.h
  ln -sf ../dbm.h  "${pkgdir}"/usr/include/gdbm/dbm.h
  mv -v "${pkgdir}"/usr/include/{,gdbm/}ndbm.h
}
