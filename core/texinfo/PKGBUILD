pkgname=texinfo
pkgver=5.2
pkgrel=4
pkgdesc="Utilities to work with and produce manuals, ASCII text, and on-line documentation from a single source file"
arch=('amd64')
url="http://www.gnu.org/software/texinfo/"
license=('GPL3')
groups=('base-devel')
install=texinfo.install
options=('!emptydirs')
depends=('gettext-runtime')
makedepends=('help2man')
source=(ftp://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
	texinfo-5.2-C-n-fix.patch)
md5sums=('cb489df8a7ee9d10a236197aefdb32c5'
         'SKIP'
         '89724bac1ecbd1a57b1ea81e87f96f44')

prepare() {
  cd $pkgname-$pkgver
  patch -p0 -i ../texinfo-5.2-C-n-fix.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  LDFLAGS="$LDFLAGS -lintl" \
  ./configure --prefix=/usr/local --without-included-gettext
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}/usr/local/lib/charset.alias"
  rm -rf "${pkgdir}/usr/local/share/info/dir"
}
