# Port: print/texinfo
pkgname=texinfo
pkgver=6.1.20160425
_pkgver=6.1
pkgrel=2
pkgdesc="Utilities to work with and produce manuals, ASCII text, and on-line documentation from a single source file"
arch=('i386' 'amd64')
url="http://www.gnu.org/software/texinfo/"
license=('GPL3')
groups=('base-devel')
install=texinfo.install
options=('!emptydirs')
depends=('perl' 'gettext-runtime')
makedepends=('help2man')
source=(ftp://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$_pkgver.tar.xz{,.sig})
validpgpkeys=('EAF669B31E31E1DECBD11513DDBC579DAB37FBA9') # Gavin Smith
sha256sums=('ac68394ce21b2420ba7ed7cec65d84aacf308cc88e9bf4716fcfff88286883d2'
            'SKIP')

build() {
  cd ${srcdir}/${pkgname}-${_pkgver}
  LDFLAGS="$LDFLAGS -lintl" \
  ./configure --prefix=/usr/local --without-included-gettext
  make
}

check() {
  make -C $pkgname-$_pkgver check
}

package() {
  cd ${srcdir}/${pkgname}-${_pkgver}
  make DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}/usr/local/lib/charset.alias"
  rm -rf "${pkgdir}/usr/local/share/info/dir"
}
