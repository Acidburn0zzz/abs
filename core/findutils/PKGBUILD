# $Id: PKGBUILD 216034 2014-07-01 12:13:13Z tpowa $
# Maintainer:

pkgname=findutils
pkgver=4.4.2
pkgrel=6
pkgdesc="GNU utilities to locate files"
arch=('i386' 'amd64')
license=('GPL3')
depends=('libc' 'sh')
url="http://www.gnu.org/software/findutils"
source=(ftp://ftp.gnu.org/pub/gnu/findutils/${pkgname}-${pkgver}.tar.gz{,.sig})
install=findutils.install
sha1sums=('e8dd88fa2cc58abffd0bfc1eddab9020231bb024'
          '77d9585d9feea0814752a31bf109fe287f528243')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Don't build or install locate because we use mlocate,
  # which is a secure version of locate.
  gsed -i '/^SUBDIRS/s/locate//' Makefile.in

  ./configure --prefix=/usr --program-prefix=g
  gmake
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR=$pkgdir install

  rm "${pkgdir}/usr/lib/charset.alias"
}
