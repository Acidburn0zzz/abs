# Port: misc/findutils

pkgname=findutils
pkgver=4.6.0
pkgrel=1
pkgdesc="GNU utilities to locate files"
arch=('amd64')
license=('GPL3')
depends=('libc' 'sh')
url="http://www.gnu.org/software/findutils"
options=('gnu_configure')
source=("ftp://ftp.gnu.org/pub/gnu/findutils/${pkgname}-${pkgver}.tar.gz"{,.sig})
install=findutils.install
sha256sums=('ded4c9f73731cd48fec3b6bdaccce896473b6d8e337e9612e16cf1431bb1169d'
            'SKIP')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Don't build or install locate because we use mlocate,
  # which is a secure version of locate.
  gsed -i '/^SUBDIRS/s/locate//' Makefile.in

  ./configure --prefix=/usr --program-prefix=g
  gmake -C locate dblocation.texi
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR=$pkgdir install

  rm "${pkgdir}/usr/lib/charset.alias"
}
