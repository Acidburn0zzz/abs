pkgname=mpfr
_pkgver=3.1.3
_patchlevel=p4
pkgver=3.1.3.p4
pkgrel=9
pkgdesc="Multiple-precision floating-point library"
arch=('amd64')
url="http://www.mpfr.org/"
license=('LGPL')
depends=('gmp>=5.0')
options=('libtoolfix')
install=mpfr.install
patchdir=("${pkgname}-${_pkgver}")
source=(http://www.mpfr.org/mpfr-current/mpfr-${_pkgver}.tar.xz{,.asc}
        mpfr-${_pkgver}-${_patchlevel}.patch)
md5sums=('6969398cd2fbc56a6af570b5273c56a9'
         'SKIP'
         'd462e1bb55399575ab0914f383683068')
validpgpkeys=('07F3DBBECC1A39605078094D980C197698C3739D')

prepare() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  patch -p1 -i $srcdir/mpfr-${_pkgver}-${_patchlevel}.patch
}

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  ./configure --prefix=/usr --infodir=/usr/share/info \
	      --enable-thread-safe --enable-shared
  make
}

check() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" install
}
