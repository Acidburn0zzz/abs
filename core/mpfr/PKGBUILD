pkgname=mpfr
pkgver=3.1.3
pkgrel=1
pkgdesc="Multiple-precision floating-point library"
arch=('i386' 'amd64')
url="http://www.mpfr.org/"
license=('LGPL')
depends=('gmp>=5.0')
options=('libtoolfix')
install=mpfr.install
source=(http://distcache.FreeBSD.org/ports-distfiles/mpfr-${pkgver}/mpfr-${pkgver}.tar.xz)
md5sums=('6969398cd2fbc56a6af570b5273c56a9')
validpgpkeys=('07F3DBBECC1A39605078094D980C197698C3739D')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --infodir=/usr/share/info \
	      --enable-thread-safe --enable-shared
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
