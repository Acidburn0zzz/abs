# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>
# Contributor: Claudiu Traistaru <claudiu2004@gmail.com>

pkgname=e2fsprogs
pkgver=1.42.13
pkgrel=5
pkgdesc="Ext2/3/4 filesystem utilities"
arch=('i386' 'amd64')
license=('GPL' 'LGPL' 'MIT')
url="http://e2fsprogs.sourceforge.net"
depends=('gettext-runtime')
#makedepends=('bc')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'MIT-LICENSE')
backup=('etc/mke2fs.conf')
install=${pkgname}.install

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Remove unnecessary init.d directory
  sed -i'' -e '/init\.d/s|^|#|' misc/Makefile.in
  sed -i '' -e 's/ install-doc-libs$$//' Makefile.in
  sed -i '' -E -e 's/md5sum ([^ ]*)/printf "%s  %s\\n" $$(md5 -q \1) \1/' \
  -e "s/ == 0/ = 0/" tests/[a-t]_*/script

  sed -i ''  -e 's/<malloc\.h>/<stdlib.h>/' ${srcdir}/${pkgname}-${pkgver}/*/*.c

 # disable f_mmp_garbage, fails on FreeBSD, and the resize*big_expand tests,
 # which are too unwieldy to run automatically (need too much free space).
 # f_extent_oobounds fails in Tinderbox and is fine outside, reason unclear.

 for i in f_mmp_garbage f_extent_oobounds r_64bit_big_expand r_bigalloc_big_expand r_ext4_big_expand
do
  mv tests/${i} tests/disabled_test-${i}
done


  ./configure  --prefix=/usr --sysconfdir=/etc \
	--disable-fsck --disable-e2initrd-helper CFLAGS="${CFLAGS} -fPIC"
  gmake pkgconfigdir='/usr/libdata/pkgconfig'
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" pkgconfigdir='/usr/libdata/pkgconfig' install install-libs
  rm  ${pkgdir}/usr/bin/compile_et
  rm  ${pkgdir}/usr/include/com_err.h
  rm  ${pkgdir}/usr/lib/libcom_err.a
  rm  ${pkgdir}/usr/share/man/man1/compile_et.1
  rm  ${pkgdir}/usr/share/man/man3/com_err.3
  rm  ${pkgdir}/usr/share/man/man3/uuid.3
  rm  ${pkgdir}/usr/share/man/man3/uuid_compare.3

  #sed -i'' -e 's/^AWK=.*/AWK=awk/' "${pkgdir}/usr/bin/compile_et"

  # remove references to build directory
  sed -i'' -e 's#^SS_DIR=.*#SS_DIR="/usr/share/ss"#' "${pkgdir}/usr/bin/mk_cmds"
  #sed -i'' -e 's#^ET_DIR=.*#ET_DIR="/usr/share/et"#' "${pkgdir}/usr/bin/compile_et"
 
  # install MIT license
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644 "${srcdir}/MIT-LICENSE" \
    "${pkgdir}/usr/share/licenses/${pkgname}/MIT-LICENSE"

  # keep libuuid seperate
  rm -f ${pkgdir}/usr/include/uuid/uuid.h
  rm -f ${pkgdir}/usr/lib/libuuid.a
  rm -f ${pkgdir}/usr/libdata/pkgconfig/uuid.pc
  rm -f ${pkgdir}/usr/share/man/man3/uuid*
  rm -f ${pkgdir}/usr/bin/mk_cmds-e

  mv ${pkgdir}/etc/mke2fs.conf.dist ${pkgdir}/etc/mke2fs.conf
}

md5sums=('bc759fc62666786f5436e2075beb3265'
         '035b7c69b7a2cecf996a4708c262245e')
