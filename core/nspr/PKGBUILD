# $Id: PKGBUILD 173931 2012-12-28 07:40:52Z andyrtr $
pkgname=nspr
pkgver=4.10.9
pkgrel=4
pkgdesc="Netscape Portable Runtime"
arch=(i686 amd64)
url="http://www.mozilla.org/projects/nspr/"
license=('MPL' 'GPL')
makedepends=('zip')
options=('!emptydirs')
patchdir=("${pkgname}-${pkgver}/${pkgname}")
source=(ftp://ftp.mozilla.org/pub/mozilla.org/nspr/releases/v${pkgver}/src/${pkgname}-${pkgver}.tar.gz)
md5sums=('86769a7fc3b4c30f7fdcb45ab284c452')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  if [ "$CARCH" = "amd64" ]; then
    confflags="--enable-64bit"
  else
    confflags=""
  fi

  ./nspr/configure \
      --prefix=/usr \
      --libdir=/usr/lib \
      --includedir=/usr/include/nspr \
      --enable-optimize \
      --disable-debug ${_confflags}
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/usr/libdata/pkgconfig"
  ln -s nspr.pc "$pkgdir/usr/libdata/pkgconfig/mozilla-nspr.pc"
  rm -r "$pkgdir"/usr/bin/{compile-et.pl,prerr.properties} \
         "$pkgdir/usr/include/nspr/md"

  for lib in libnspr4.so.1 libplc4.so.1 libplds4.so.1; do
    ln -s "${lib}" "${pkgdir}"/usr/lib/${lib%.*}
  done
}
