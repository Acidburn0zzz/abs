# Maintainer:  Anthony Donnelly <Amzo@archbsd.net>
# Port: security/fakeroot

pkgname=fakeroot
pkgver=1.22
pkgrel=1
pkgdesc="Gives a fake root environment, useful for building packages as a non-privileged user"
arch=('i386' 'amd64')
license=('GPL')
url="http://packages.debian.org/fakeroot"
groups=('base-devel')
install=fakeroot.install
makedepends=('gmake')
options=('libtoolfix')
source=(http://ftp.debian.org/debian/pool/main/f/${pkgname}/${pkgname}_${pkgver}.orig.tar.bz2 )
md5sums=('fae64c9aeb2c895ead8e1b99bf50c631')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i '' -e '/LIBS=.*-lpthread/s/-lpthread/-pthread/g' ${srcdir}/${pkgname}-${pkgver}/configure

}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --libdir=/usr/lib/libfakeroot \
              --disable-static --with-ipc=sysv
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR=${pkgdir} install

  # install README for sysv/tcp usage
  install -dm755 $pkgdir/usr/share/doc/$pkgname
  install -m644 $srcdir/$pkgname-$pkgver/README $pkgdir/usr/share/doc/$pkgname/README

  # ldconfig path
  install -dm755 "${pkgdir}/usr/libdata/ldconfig"
  echo "/usr/lib/libfakeroot" >> "${pkgdir}/usr/libdata/ldconfig/$pkgname"

  cd ${pkgdir}/usr/lib/libfakeroot

  mv libfakeroot-0.so libfakeroot.so.0
  ln -sf libfakeroot.so.0 libfakeroot-0.so

}
