# Port: textproc/expat2
pkgname=expat
pkgver=2.1.1
pkgrel=1
pkgdesc="An XML parser library"
arch=('i386' 'amd64')
url="http://expat.sourceforge.net/"
license=('custom')
source=(http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('7380a64a8e3a9d66a9887b01d0d7ea81')
options=('libtoolfix' 'pathfix')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  ln -s libexpat.so.1 ${pkgdir}/usr/lib/libexpat.so.6
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}/
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}

