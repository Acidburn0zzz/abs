#
# Maintainer:

pkgbase=openldap
pkgname=('libldap' 'openldap')
pkgver=2.4.39
pkgrel=4
pkgdesc="Open source LDAP client implementation"
arch=('i686' 'amd64')
url="http://www.openldap.org/"
license=('custom')
makedepends=('libltdl' 'db>=6' 'libsasl' 'chrpath')
options=('!makeflags')
install=openldap.install
_port_patches=(
        patch-build__top.mk
        patch-configure
        patch-libraries__libldap__tls_o.c
        patch-shlib-version
        patch-smbk5pwd-Makefile)
source=(ftp://ftp.openldap.org/pub/OpenLDAP/openldap-release/${pkgbase}-${pkgver}.tgz
        slapd 
	slapd.in
        "${_port_patches[@]}")
sha1sums=('2b8e8401214867c361f7212e7058f95118b5bd6c'
          '71e6be307473efaf076965d3f0064656a7e19354'
          '8bbb97314152b7f6f2c3ae35a977d9cf1c62bacd'
          '9ad64cee312b3fcf63a64f210335382f5611d1f0'
          '20290d81f46fc440d37c6e994e9f8882996889f8'
          '92f072d16cc1e2983422cac0457011b7990b0ea1'
          '26d75bafaa6f5db333fa767d71abd9962f2918aa'
          'f61228397cbbd16701a52652190adb8c5fd7571d')

prepare() {
  cd "${srcdir}"/${pkgbase}-${pkgver}

  for i in "${_port_patches[@]}"; do
    msg "patch $i"
    patch -p0 -i "${srcdir}/$i"
  done

  #  -e 's,ol_DB_LIB=$$,&-ldb,' \
  #  -e 's,(ol_cv_lib_db=)yes$$,\1-ldb,' \
  #
  sed -i '' -E \
    -e 's,-kthread,-pthread,g' -e 's,uuid/uuid.h,xxuuid/uuid.h,g' \
    configure

  gsed -i 's|-m 644 $(LIBRARY)|-m 755 $(LIBRARY)|' libraries/{liblber,libldap,libldap_r}/Makefile.in
  gsed -i 's|#define LDAPI_SOCK LDAP_RUNDIR LDAP_DIRSEP "run" LDAP_DIRSEP "ldapi"|#define LDAPI_SOCK LDAP_DIRSEP "run" LDAP_DIRSEP "openldap" LDAP_DIRSEP "ldapi"|' include/ldap_defaults.h
  gsed -i 's|%LOCALSTATEDIR%/run|/run/openldap|' servers/slapd/slapd.{conf,ldif}
  gsed -i 's|-$(MKDIR) $(DESTDIR)$(localstatedir)/run|-$(MKDIR) $(DESTDIR)/run/openldap|' servers/slapd/Makefile.in
}

build() {
  cd "${srcdir}"/${pkgbase}-${pkgver}


  export LDFLAGS="$LDFLAGS -L\"${pkgdir}/libldap/usr/lib\""

  # have to explicitly disable libmdb - it's not built anywhere in ports/archlinux
  # but it seems to still try anyway - but it requires O_DSYNC which does not exist on BSD
  ./configure --prefix=/usr --mandir=/usr/share/man --libexecdir=/usr/lib \
    --sysconfdir=/etc --localstatedir=/var/db \
    --enable-ipv6 --enable-syslog --enable-local \
    --enable-bdb --enable-hdb \
    --enable-crypt --enable-dynamic \
    --with-threads --disable-wrappers \
    --without-fetch \
    --enable-lmpasswd --with-cyrus-sasl \
    --enable-overlays=mod --enable-modules=yes \
    --enable-rewrite --enable-null=mod --enable-ldap=mod \
    --enable-meta=mod --enable-monitor=mod \
    --disable-mdb
  make
}

package_libldap() {
  pkgdesc="Lightweight Directory Access Protocol (LDAP) client libraries"
  depends=('libsasl')
  backup=('etc/openldap/ldap.conf')

  cd "${srcdir}"/${pkgbase}-${pkgver}
  for dir in include libraries doc/man/man3 ; do
    pushd ${dir}
    make DESTDIR="${pkgdir}" install
    popd
  done
  install -dm755 "${pkgdir}"/usr/share/man/man5
  install -m644 doc/man/man5/ldap.conf.5.tmp "${pkgdir}"/usr/share/man/man5/ldap.conf.5

# get rid of duplicate default conf files
  rm "${pkgdir}"/etc/openldap/*.default

  ln -sf liblber.so "${pkgdir}"/usr/lib/liblber.so.2
  ln -sf libldap.so "${pkgdir}"/usr/lib/libldap.so.2

  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_openldap() {
  pkgdesc="Lightweight Directory Access Protocol (LDAP) client and server"
  depends=("libldap>=${pkgver}" 'libltdl' 'db')
  # FIXME: backup=('etc/openldap/slapd.conf' 'etc/conf.d/slapd')
  backup=('etc/openldap/slapd.conf')
  options=('emptydirs')
  install=openldap.install

  cd "${srcdir}"/${pkgbase}-${pkgver}
  for dir in clients servers doc/man/man{1,5,8} ; do
    pushd ${dir}
    make DESTDIR="${pkgdir}" install
    popd
  done
  rm "${pkgdir}"/usr/share/man/man5/ldap.conf.5

# get rid of duplicate default conf files
  rm "${pkgdir}"/etc/openldap/*.default

  ln -s ../lib/slapd "${pkgdir}"/usr/sbin/slapd

  chmod 640 "${pkgdir}"/etc/openldap/{slapd.conf,DB_CONFIG.example}

# FIXME: init scripts
  install -dm755 "${pkgdir}"/etc/{rc.d,init.d}
  install -m755 "${srcdir}/slapd.in" "${pkgdir}"/etc/rc.d/slapd
  install -m755 "${srcdir}/slapd" "${pkgdir}"/etc/init.d/slapd
  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
