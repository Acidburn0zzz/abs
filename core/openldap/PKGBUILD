#
# Maintainer:

pkgbase=openldap
pkgname=('libldap' 'openldap')
pkgver=2.4.42
pkgrel=4
pkgdesc="Open source LDAP client implementation"
arch=('amd64')
url="http://www.openldap.org/"
license=('custom')
makedepends=('libltdl' 'libsasl' 'chrpath' 'unixodbc')
options=('!makeflags')
install=openldap.install
patchdir=(${pkgbase}-${pkgver})
source=(ftp://ftp.openldap.org/pub/OpenLDAP/openldap-release/${pkgbase}-${pkgver}.tgz
        slapd 
	slapd.in)
sha1sums=('ec03e061bfdb2e6a90827855cf77a72cb3f89cf4'
          '71e6be307473efaf076965d3f0064656a7e19354'
          '8bbb97314152b7f6f2c3ae35a977d9cf1c62bacd')

prepare() {
  cd "${srcdir}"/${pkgbase}-${pkgver}

  sed -i '' -E \
    -e 's,-kthread,-pthread,g' -e 's,uuid/uuid.h,xxuuid/uuid.h,g' \
    configure

  gsed -i 's|-m 644 $(LIBRARY)|-m 755 $(LIBRARY)|' libraries/{liblber,libldap,libldap_r}/Makefile.in
  gsed -i 's|#define LDAPI_SOCK LDAP_RUNDIR LDAP_DIRSEP "run" LDAP_DIRSEP "ldapi"|#define LDAPI_SOCK LDAP_DIRSEP "run" LDAP_DIRSEP "openldap" LDAP_DIRSEP "ldapi"|' include/ldap_defaults.h
  gsed -i 's|%LOCALSTATEDIR%/run|/run/openldap|' servers/slapd/slapd.{conf,ldif}
  gsed -i 's|-$(MKDIR) $(DESTDIR)$(localstatedir)/run|-$(MKDIR) $(DESTDIR)/run/openldap|' servers/slapd/Makefile.in
}

build() {
  cd "${srcdir}"/${pkgbase}-${pkgver}


  export LDFLAGS="$LDFLAGS -L\"${pkgdir}/libldap/usr/lib\""

  # have to explicitly disable libmdb - it's not built anywhere in ports/archlinux
  # but it seems to still try anyway - but it requires O_DSYNC which does not exist on BSD
  ./configure --prefix=/usr --mandir=/usr/share/man --libexecdir=/usr/libexec \
    --sysconfdir=/etc --localstatedir=/var/db \
    --enable-ipv6 --enable-syslog --enable-local \
    --disable-bdb --disable-hdb \
    --enable-crypt --enable-dynamic \
    --with-threads --disable-wrappers \
    --without-fetch \
    --enable-lmpasswd --with-cyrus-sasl \
    --enable-overlays=mod --enable-modules=yes \
    --enable-rewrite --enable-null=mod --enable-ldap=mod \
    --enable-meta=mod --enable-monitor=mod \
    --disable-mdb
  make
}

package_libldap() {
  pkgdesc="Lightweight Directory Access Protocol (LDAP) client libraries"
  depends=('libsasl')
  backup=('etc/openldap/ldap.conf')

  cd "${srcdir}"/${pkgbase}-${pkgver}
  for dir in include libraries doc/man/man3 ; do
    pushd ${dir}
    make DESTDIR="${pkgdir}" install
    popd
  done


  ln -sf liblber.so "${pkgdir}"/usr/lib/liblber.so.2
  ln -sf libldap.so "${pkgdir}"/usr/lib/libldap.so.2

  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_openldap() {
  pkgdesc="Lightweight Directory Access Protocol (LDAP) client and server"
  depends=("libldap>=${pkgver}" 'libltdl' 'db')
  # FIXME: backup=('etc/openldap/slapd.conf' 'etc/conf.d/slapd')
  backup=('etc/openldap/slapd.conf')
  options=('emptydirs')
  install=openldap.install

  cd "${srcdir}"/${pkgbase}-${pkgver}
  for dir in clients servers doc/man/man{1,5,8} ; do
    pushd ${dir}
    make DESTDIR="${pkgdir}" install
    popd
  done

  ln -s ../lib/slapd "${pkgdir}"/usr/sbin/slapd

# FIXME: init scripts
  install -dm755 "${pkgdir}"/etc/{rc.d,init.d}
  install -m755 "${srcdir}/slapd.in" "${pkgdir}"/etc/rc.d/slapd
  install -m755 "${srcdir}/slapd" "${pkgdir}"/etc/init.d/slapd
  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
