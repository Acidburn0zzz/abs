# $Id: PKGBUILD 172792 2012-12-06 01:48:03Z tomegun $
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>
pkgbase=dbus
pkgname=('dbus' 'libdbus')
pkgver=1.8.18
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=(i686 amd64)
license=('GPL' 'custom')
makedepends=('gmake' 'libx11' 'dbus')
optdepends=('libx11: dbus-launch support')
options=('libtoolfix')
source=(http://dbus.freedesktop.org/releases/${pkgname}/${pkgname}-$pkgver.tar.gz #{,.asc}
        dbus.initd
        dbus.in
	30-dbus)
md5sums=('83e607e9ccb1c921d5b6bbea2376a36c'
         '6d6884189633b03233cb04e401d08f56'
         '69a06eaece20386d14a4f44f5dceed18'
         '771530365fb03326b8ea1035064ad1ed')

build() {
  cd dbus-$pkgver

  sed -i '' -e '/^SUBDIRS/,/^$/ s|test||g' Makefile.in
  sed -i '' -e 's|THREAD_LIBS -lrt|THREAD_LIBS|g ; s|dummy man2html|dummy man2html_disable|g' configure

  rm -rf "${srcdir}/test"
  mkdir "${srcdir}/test"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var \
              --with-test-socket-dir="${srcdir}/test" \
              --with-system-pid-file=/var/run/dbus.pid \
              --libexecdir=/usr/libexec \
              --with-system-socket=/var/run/dbus/system_bus_socket \
              --with-session-socket-dir=/tmp \
              --disable-doxygen-docs \
              --disable-xml-docs \
              --with-dbus-user=messagebus \
              --disable-systemd
  gmake
}

package_dbus(){
  depends=('libdbus' 'expat' 'gnome_subr')
  install=dbus.install
  optdepends=('libx11: dbus-launch support')
  provides=('dbus-core')
  conflicts=('dbus-core')
  replaces=('dbus-core')

  cd dbus-$pkgver

  # Disable installation of libdbus
  sed -i -e 's/^SUBDIRS = dbus/SUBDIRS =/' Makefile

  gmake DESTDIR="$pkgdir" install

  rm -r "${pkgdir}/var/run"
  rm -r "${pkgdir}/usr/libdata/pkgconfig"

  install -dm755 "$pkgdir/etc/X11/xinit/xinitrc.d"
  install -dm755 "$pkgdir/usr/share/licenses/dbus/"
  install -m755 ../30-dbus "$pkgdir/etc/X11/xinit/xinitrc.d/30-dbus"
  install -m644 COPYING "$pkgdir/usr/share/licenses/dbus/COPYING"

  install -m755 -d "$pkgdir"/etc/{init.d,rc.d}
  install -m755 ../dbus.initd "$pkgdir/etc/init.d/dbus"
  install -m755 ../dbus.in "$pkgdir/etc/rc.d/dbus"
}

package_libdbus(){
  pkgdesc="DBus library"
  depends=('libc')

  cd dbus-$pkgver
  gmake DESTDIR="$pkgdir" -C dbus install
  gmake DESTDIR="$pkgdir" install-data-am
  install -dm755 "$pkgdir/usr/share/licenses/libdbus/"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/libdbus/COPYING"
}
