#
# Maintainer:  Anthony Donnelly <Amzo@archbsd.net>

pkgname=readline
_basever=6.3
_patchlevel=008 #prepare for some patches
pkgver=$_basever.$_patchlevel
pkgrel=15
pkgdesc="GNU readline library"
arch=('i386' 'amd64')
url="http://tiswww.case.edu/php/chet/readline/rltop.html"
license=('GPL')
backup=('usr/local/etc/inputrc')
groups=(base)
options=('!emptydirs')
depends=('libc' 'ncurses')
install=readline.install
patchdir=($pkgname-$_basever)
source=(http://ftp.gnu.org/gnu/readline/readline-$_basever.tar.gz{,.sig}
        inputrc)
if [ $_patchlevel -gt 00 ]; then
    for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
        source=(${source[@]} http://ftp.gnu.org/gnu/readline/readline-$_basever-patches/readline${_basever//./}-$(printf "%03d" $p){,.sig})
    done
fi

prepare() {
  cd $pkgname-$_basever
  for (( _p=1; _p <= $((10#${_patchlevel})); _p++ )); do
    msg "applying patch readline${_basever//.}-$(printf "%03d" $_p)"
    patch -p0 -i ../readline${_basever//.}-$(printf "%03d" $_p)
  done

  # remove RPATH from shared objects (FS#14366)
  gsed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf
}

build() {
  cd $pkgname-$_basever

  # build with -fPIC for x86_64 (FS#15634)
  [ $CARCH == "amd64" ] && CFLAGS="$CFLAGS -fPIC"

  ./configure --prefix=/usr/local
  gmake SHLIB_LIBS=-lncurses
}

package() {
  gmake -C $pkgname-$_basever DESTDIR="$pkgdir" install
  install -dm755 ${pkgdir}/usr/local/etc/
  install -m644 inputrc ${pkgdir}/usr/local/etc/inputrc
  rm ${pkgdir}/usr/local/share/info/dir
}

md5sums=('33c8fb279e981274f485fd91da77e94a'
         'SKIP'
         '58d54966c1191db45973cb3191ac621a'
         '4343f5ea9b0f42447f102fb61576b398'
         'SKIP'
         '700295212f7e2978577feaee584afddb'
         'SKIP'
         'af4963862f5156fbf9111c2c6fa86ed7'
         'SKIP'
         '11f9def89803a5052db3ba72394ce14f'
         'SKIP'
         '93721c31cd225393f80cb3aadb165544'
         'SKIP'
         '71dc6ecce66d1489b96595f55d142a52'
         'SKIP'
         '062a08ed60679d3c4878710b3d595b65'
         'SKIP'
         'ee1c04072154826870848d8b218d7b04'
         'SKIP')

