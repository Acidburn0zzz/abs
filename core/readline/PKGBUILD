#
# Maintainer:  Anthony Donnelly <Amzo@archbsd.net>
# Port: devel/readline

pkgname=readline
_basever=7.0
_patchlevel=003 #prepare for some patches
pkgver=$_basever.${_patchlevel#00}
pkgrel=1
pkgdesc="GNU readline library"
arch=('i386' 'amd64')
url="http://tiswww.case.edu/php/chet/readline/rltop.html"
license=('GPL')
backup=('etc/inputrc')
groups=(base)
options=('!emptydirs' 'gnu_configure')
patchdir=($pkgname-$_basever)
depends=('libc')
source=(http://ftp.gnu.org/gnu/readline/readline-$_basever.tar.gz{,.sig}
        inputrc)
if [ $_patchlevel -gt 00 ]; then
    for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
        source=(${source[@]} http://ftp.gnu.org/gnu/readline/readline-$_basever-patches/readline${_basever//./}-$(printf "%03d" $p){,.sig})
    done
fi

prepare() {
  cd $pkgname-$_basever
  for (( _p=1; _p <= $((10#${_patchlevel})); _p++ )); do
    msg "applying patch readline${_basever//.}-$(printf "%03d" $_p)"
    patch -p0 -i ../readline${_basever//.}-$(printf "%03d" $_p)
  done

  # remove RPATH from shared objects (FS#14366)
  gsed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf
}

build() {
  cd $pkgname-$_basever

  # build with -fPIC for x86_64 (FS#15634)
  [ $CARCH == "amd64" ] && CFLAGS="$CFLAGS -fPIC"

  ./configure --prefix=/usr
  gmake SHLIB_LIBS=-lncurses
}

package() {
  gmake -C $pkgname-$_basever DESTDIR="$pkgdir" install
  install -dm755 ${pkgdir}/etc/
  install -m644 inputrc ${pkgdir}/etc/inputrc

  rm ${pkgdir}/usr/share/info/dir
  rm ${pkgdir}/usr/share/man/man3/history.3
}

md5sums=('205b03a87fc83dab653b628c59b9fc91'
         'SKIP'
         '58d54966c1191db45973cb3191ac621a'
         'e299384458a4cbefaaac3f30e9cc2bba'
         'SKIP'
         'f9071a353e2fd52a91d32667b23715d6'
         'SKIP'
         '03595464cf0283286a6e07f4f01c4a70'
         'SKIP')

