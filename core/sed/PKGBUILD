# Port: textproc/gsed

pkgbase=sed
pkgname=gsed
pkgver=4.2.2
pkgrel=2
pkgdesc="GNU stream editor"
groups=('base-devel')
arch=('i386' 'amd64')
url="http://www.gnu.org/software/sed"
license=('GPL3')
depends=('gettext-runtime' 'libiconv')
makedepends=('gmake')
install=gsed.install
source=(ftp://ftp.gnu.org/pub/gnu/sed/${pkgbase}-${pkgver}.tar.gz{,.sig})
md5sums=('4111de4faa3b9848a0686b2f260c5056'
         '86a5ab72f414d4cb38126e8e27cf0101')

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  ./configure --prefix=/usr --program-prefix=g
  gmake
}

check() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  gmake check
}

package() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  gmake DESTDIR=${pkgdir} install

  # remove stale charset.alias file
  rm -rf "${pkgdir}/usr/lib"
}
