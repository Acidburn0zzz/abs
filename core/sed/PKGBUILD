# $Id: PKGBUILD 173887 2012-12-27 05:05:41Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgbase=sed
pkgname=gsed
pkgver=4.2.2
pkgrel=11
pkgdesc="GNU stream editor"
groups=('base-devel')
arch=('i686' 'amd64')
url="http://www.gnu.org/software/sed"
license=('GPL3')
depends=('gettext-runtime')
install=gsed.install
source=(ftp://ftp.gnu.org/pub/gnu/sed/${pkgbase}-${pkgver}.tar.gz{,.sig})
md5sums=('4111de4faa3b9848a0686b2f260c5056'
         '86a5ab72f414d4cb38126e8e27cf0101')

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  ./configure --prefix=/usr --program-prefix=g
  make
}

check() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install

  # remove stale charset.alias file
  rm -rf "${pkgdir}/usr/lib"
}
