# Port: archivers/gtar
pkgname=gnutar
_realname=tar
pkgver=1.28
pkgrel=5
pkgdesc="GNU version of tar"
arch=('i386' 'amd64')
url="http://www.gnu.org/software/tar/tar.html"
license=('GPL3')
depends=('libc' 'gettext-runtime' 'libiconv') # 'sh')
options=('!emptydirs')
provides=('gtar' 'gnutar')
patchdir=("$_realname-$pkgver")
source=(ftp://ftp.gnu.org/gnu/$_realname/$_realname-$pkgver.tar.xz{,.sig}
	gtar.1)
md5sums=('49b6306167724fe48f419a33a5beb857'
         'SKIP'
         'e97e7343f4e23724b4c48bca2f033e38')

prepare() {
  cd ${srcdir}/$_realname-$pkgver

  # fix build failure with glibc-2.16
  sed -i '' -e '/gets is a/d' gnu/stdio.in.h

}

build() {
  cd ${srcdir}/$_realname-$pkgver
  ./configure --program-prefix=g --prefix=/usr --libexecdir=/usr/libexec/gtar
  make
  
}

package() {
  cd ${srcdir}/$_realname-$pkgver
  make DESTDIR=${pkgdir} install

  rm -rf ${pkgdir}/usr/share/info
  rm -rf ${pkgdir}/usr/share/locale
  rm ${pkgdir}/usr/lib/charset.alias
 
  install -dm755 "${pkgdir}/usr/share/man/man1"
  install -m644 ${srcdir}/gtar.1 ${pkgdir}/usr/share/man/man1/gtar.1
  
  # fix broken man-page
  sed -i '' -e '/^$/d' ${pkgdir}/usr/share/man/man1/gtar.1

}
