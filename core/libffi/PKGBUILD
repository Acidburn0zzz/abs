# $Id: PKGBUILD 157383 2012-04-28 09:25:57Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libffi
pkgver=3.1
pkgrel=1
pkgdesc="A portable, high level programming interface to various calling conventions"
arch=('i686' 'amd64')
url="http://sourceware.org/libffi"
license=('MIT')
checkdepends=('dejagnu' 'gcc')
install=libffi.install
source=(ftp://sourceware.org/pub/libffi/libffi-${pkgver}.tar.gz)
md5sums=('f5898b29bbfd70502831a212d9249d10')
options=('libtoolfix' '!clang')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  [[ "$CARCH" == "x86_64" ]] && CFLAGS="${CFLAGS} -fPIC"

  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -dm755 "$pkgdir"/usr/include
  ln -sf ../lib/libffi-${pkgver}/include/ffi.h "$pkgdir"/usr/include/
  ln -sf ../lib/libffi-${pkgver}/include/ffitarget.h "$pkgdir"/usr/include/

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install    -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

