_src_uri="http://dev.gentoo.org/~williamh/dist"
_net_uri="http://dev.gentoo.org/~robbat2/distfiles"
_net="netifrc"
_nver=0.2.4

pkgname=openrc
pkgver=0.13.9
pkgrel=4
pkgdesc='Dependency based init system that works with sysvinit.'
arch=('i686' 'x86_64')
depend=('bash')
provides=('init')
conflicts=('init')
url='http://www.gentoo.org/proj/en/base/openrc/'
license=('BSD')
backup=(etc/inittab
        etc/rc.conf
        etc/conf.d/{bootmisc,consolefont,dmesg,fsck,hostname,hwclock,keymaps}
        etc/conf.d/{killprocs,localmount,modules,netmount,network,staticroute}
        etc/conf.d/{tmpfiles,urandom,ntp-client,sshd})
source=("${_src_uri}/${pkgname}-${pkgver}.tar.bz2"
	"${_net_uri}/${_net}-${_nver}.tar.bz2"
	{init,conf}.d-sshd
	{init,conf}.d-modules
	{init,conf}.d-ntp-client
	{init,conf}.d-ldconfig
	init.d-cron
	init.d-pflog
        init.d-devfs
	init.d-{zfs,zvol}
)

_makeargs=(BRANDING='Arch BSD' OS=FreeBSD LIBEXECDIR=/lib/rc MKNET=no)
_net_args=( "${_base_args[@]}" LIBEXECDIR=/lib/${_net})

prepare() {
  cd "$srcdir"

  find . -type f | xargs sed -i '' -e 's|/libexec/|/lib/|g'
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  
#  find "$srcdir"/$pkgname-$pkgver -name *.sh | xargs sed -i '' -e 's|/bin/sh|/usr/bin/bash|g'
  gmake "${_makeargs}"

  cd "${srcdir}/${_net}-${_nver}"
  gmake "${_net_args[@]}"

}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" "${_makeargs[@]}" install

  rm -f ${pkgdir}/usr/share/man/man8/rc.8
  rm -f ${pkgdir}/usr/share/man/man8/service.8

  install -dm755 "${pkgdir}/etc/init.d"
  install -dm755 "${pkgdir}/etc/conf.d"

  # Services with both init.d and conf.d files
  for i in sshd modules ntp-client ldconfig
  do
	install -m755 "${srcdir}/init.d-$i" "${pkgdir}/etc/init.d/$i"
	install -m644 "${srcdir}/conf.d-$i" "${pkgdir}/etc/conf.d/$i"
  done

  # For which there is no conf.d
  for i in cron pflog devfs zfs zvol
  do
  	install -m755 "${srcdir}/init.d-$i" "${pkgdir}/etc/init.d/$i"
  done
  for i in ldconfig devd devfs modules; do
        ln -sf /etc/init.d/${i} ${pkgdir}/etc/runlevels/boot/${i}
  done

  #urandom seed fix
  install -d -m755 "${pkgdir}/var/lib/misc"

  install -d -m755 ${pkgdir}/var/log

  for i in messages security auth.log maillog lpd-errs xferlog cron debug.log ppp.log; do
	touch ${pkgdir}/var/log/${i}
  done

# netifrc
cd "${srcdir}/${_net}-${_nver}"
gmake DESTDIR="${pkgdir}" "${_net_args[@]}" install
install -m644 "${srcdir}/${_net}-${_nver}/doc/net.example" "${pkgdir}/etc/conf.d/net"
}
md5sums=('8f676fc51208a63dd789a8e78ae6e0d2'
         '4d4939ad612e2272a73537d6fca48b6b'
         'a116b4dd1504f38aad78c682ef446410'
         'b35e9f3829f4cfca07168fcba98749c7'
         '67a68234849d68f18c0f6e05777eb2db'
         '0e7438caba45a532c4470a507451158e'
         '120a8d041c92838a1bd1c1db805f3346'
         '5527c30b1f101bfe27b061d2a8d4dacb'
         '636e05421a33eb018afb55787063e38c'
         'facbbe4ec0a4888cd2b71529eaddb900'
         'd0e6c4d30a5b789fe36ab20f50517698'
         '6855b684a7cc9363d6694a790d7c80f2'
         '41597df204caf6cdb4310be28027f7e5'
         '79d403e2245f039f901966c06efe1007'
         '96d00582f3551fca71ec4a22fb488b93')
