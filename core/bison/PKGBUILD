# Maintainer: Anthony Donnelly <Amzo@archbsd.net>
# Contributor: Wolfgang Bumiller <blub@speed.at>
# Port: devel/bison

pkgname=bison
pkgver=3.0.4
pkgrel=1
pkgdesc="The GNU general-purpose parser generator"
arch=('i386' 'amd64')
license=('GPL3')
url="http://www.gnu.org/software/bison/bison.html"
depends=('gettext' 'gm4' 'libc' 'sh')
options=('!emptydirs')
groups=('base-devel')
install=bison.install
source=(ftp://ftp.gnu.org/gnu/bison/${pkgname}-${pkgver}.tar.xz{,.sig})
validpgpkeys=('7DF84374B1EE1F9764BBE25D0DDCAA3278D5264E')  # Akim Demaille
sha256sums=('a72428c7917bdf9fa93cb8181c971b6e22834125848cf1d03ce10b1bb0716fe1'
            'SKIP')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  rm -f doc/bison.info*
  sed -i '' -e 's|$$(docdir)\(/examples\)|$$(datarootdir)\1/bison|' Makefile.in
  sed -i '' -e 's/\(.*\) _Noreturn void/_Noreturn \1 void/' data/*.c lib/*.[ch]
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  export M4=/usr/bin/gm4
  ./configure --prefix=/usr --datadir=/usr/share \
              --sysconfdir=/etc \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
	      --disable-examples \
	      --disable-yacc # yacc is provided by world 
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  rm "${pkgdir}/usr/lib/charset.alias"
}
