# Maintainer: Anthony Donnelly <Amzo@archbsd.net>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=bison
pkgver=3.0.4
pkgrel=4
pkgdesc="The GNU general-purpose parser generator"
arch=('i686' 'amd64')
license=('GPL3')
url="http://www.gnu.org/software/bison/bison.html"
depends=('gettext' 'gm4' 'libc')
makedepends=('gmake')
options=('!emptydirs')
groups=('base-devel')
install=bison.install
source=(ftp://ftp.gnu.org/gnu/bison/${pkgname}-${pkgver}.tar.xz{,.sig})
md5sums=('c342201de104cc9ce0a21e0ad10d4021'
         'SKIP')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  rm doc/bison.info*

  export M4=/usr/bin/gm4
  ./configure --prefix=/usr --datadir=/usr/share \
              --sysconfdir=/etc \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
	      --disable-examples \
	      --disable-yacc # yacc is provided by world 
  gmake
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  gmake check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  gmake DESTDIR=${pkgdir} install

  rm "${pkgdir}/usr/lib/charset.alias"
}
