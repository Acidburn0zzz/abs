# Port: security/libgpg-error

pkgname=libgpg-error
pkgver=1.26
pkgrel=1
pkgdesc="Support library for libgcrypt"
arch=('i386' 'amd64')
url="http://www.gnupg.org"
license=('LGPL')
depends=('gettext' 'bash')
makedepends=('libtool' 'libiconv')
install=libgpg-error.install
options=('libtoolfix' 'iconv')
source=(ftp://ftp.gnupg.org/gcrypt/libgpg-error/${pkgname}-${pkgver}.tar.bz2)

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  ./configure --prefix=/usr \
              --with-libiconv-prefix=/usr/local \
              --with-libintl-prefix=/usr/local
  gmake
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  gmake check
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  gmake DESTDIR="${pkgdir}/" install
}
md5sums=('97456709dbbcbb69354317ffe3e347cd')
sha256sums=('4c4bcbc90116932e3acd37b37812d8653b1b189c1904985898e860af818aee69')
