# $Id: PKGBUILD 156350 2012-04-17 15:57:56Z andyrtr $
# Maintainer: Anthony Donnelly <Amzo@archbsd.net>

pkgname=libgpg-error
pkgver=1.18
pkgrel=1
pkgdesc="Support library for libgcrypt"
arch=(i686 x86_64)
url="http://www.gnupg.org"
license=('LGPL')
depends=('gettext' 'bash')
makedepends=('libtool')
install=libgpg-error.install
options=('libtoolfix')
source=(ftp://ftp.gnupg.org/gcrypt/libgpg-error/${pkgname}-${pkgver}.tar.bz2)
md5sums=('12312802d2065774b787cbfc22cc04e9')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  ./configure --prefix=/usr \
              --with-libiconv-prefix=/usr \
              --with-libintl-prefix=/usr
  gmake
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  gmake check
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  gmake DESTDIR="${pkgdir}/" install

  #temporary libfix
  install -dm755 ${pkgdir}/usr/lib/unfixed
  ln -s /usr/lib/libgpg-error.so.0 "$pkgdir"/usr/lib/unfixed/libgpg-error.so.11
  install -dm755 "$pkgdir"/usr/lib/ldconfig
  echo '/usr/lib/unfixed' > "$pkgdir"/usr/lib/ldconfig/libgpg-error-unfixed
}
