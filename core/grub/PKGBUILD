# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Keshav P R <(the.ridikulus.rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>

_grub_lua_ver=24
_grub_ntldr_ver=21
_grub_915_ver=9

pkgname=('grub-common' 'grub-bios' 'grub-efi-i386')
pkgbase=grub
pkgver=2.02
pkgrel=1
url="https://www.gnu.org/software/grub/"
arch=('i686' 'x86_64')
license=('GPL3')
makedepends=('bdf-unifont' 'ttf-dejavu' 'python' 'autogen'
             'texinfo' 'help2man' 'gettext' 'fusefs-libs')
source=("http://www.pcbsd.org/~kris/software/${pkgbase}-${pkgver}_2.tar.xz"
        'grub.default'
        'grub_bzr_export.sh'
         00_header.in
         10_archbsd.in
)
sha1sums=('c5bdec5a9075f0c23c0808bfa16779d04ef4d2e7'
          'dbf493dec4722feb11f0b5c71ad453a18daf0fc5'
          '0cfd4e51cdb14a92f06cfd3c607f2aa21f3e55fc'
          'e6679e8a52c36af800ee3a8901452659676b72cb'
          'f19dc238ea211d0f65683f7e63c3e09247e858ea')

patchdir="${pkgbase}-${pkgver}/"
_build_grub-common_and_bios() {

	## copy the source for building the common/bios package
	cp -r "${srcdir}/grub-${pkgver}" "${srcdir}/grub_bios-${pkgver}"
	cd "${srcdir}/grub_bios-${pkgver}"

	## fix unifont.bdf location so that grub-mkfont can create *.pf2 files
	gsed 's|/usr/share/fonts/unifont|/usr/share/fonts/unifont /usr/share/fonts/misc|g' -i "${srcdir}/grub_bios-${pkgver}/configure.ac"

	## fix DejaVuSans.ttf location so that grub-mkfont can create *.pf2 files for starfield theme
	gsed 's|/usr/share/fonts/dejavu|/usr/share/fonts/dejavu /usr/share/fonts/TTF|g' -i "${srcdir}/grub_bios-${pkgver}/configure.ac"

	## Requires python2
	# sed 's|python |python2 |g' -i "${srcdir}/grub_bios-${pkgver}/autogen.sh"

	## start the actual build process
	cd "${srcdir}/grub_bios-${pkgver}"
	./autogen.sh
	echo

	CFLAGS="" ./configure \
		--with-platform="pc" \
		--target="i386" \
		"${_EFIEMU}" \
		--enable-mm-debug \
		--enable-nls \
		--enable-cache-stats \
		--enable-grub-mkfont \
		--enable-grub-mount \
		--prefix="/usr" \
		--bindir="/usr/bin" \
		--sbindir="/usr/sbin" \
		--mandir="/usr/share/man" \
		--infodir="/usr/share/info" \
		--datarootdir="/usr/share" \
		--sysconfdir="/etc" \
		--program-prefix="" \
		--with-bootdir="/boot" \
		--with-grubdir="grub" \
		--disable-werror
	echo

	ln -svf /usr/include/{machine,sys,x86} grub-core
	CFLAGS="" gmake
	echo

}

_build_grub-efi-i386() {

	## copy the source for building the efi package
	cp -r "${srcdir}/grub-${pkgver}" "${srcdir}/grub_efi-${pkgver}"
	cd "${srcdir}/grub_efi-${pkgver}"

	cd "${srcdir}/grub_efi-${pkgver}"
	./autogen.sh
	echo

	LIBS=-lgeom CFLAGS="" ./configure \
		--with-platform="efi" \
		--target="i386" \
		--host="${CHOST}" \
		--disable-efiemu \
		--enable-mm-debug \
		--enable-nls \
		--enable-device-mapper \
		--enable-cache-stats \
		--enable-grub-mkfont \
		--enable-grub-mount \
		--prefix="/usr" \
		--bindir="/usr/bin" \
		--sbindir="/usr/sbin" \
		--mandir="/usr/share/man" \
		--infodir="/usr/share/info" \
		--datarootdir="/usr/share" \
		--sysconfdir="/etc" \
		--program-prefix="" \
		--with-bootdir="/boot" \
		--with-grubdir="grub" \
		--disable-werror
	echo

	gsed -i -e 's/^LIBS .*=\(.*\)/LIBS = \1 -lgeom/' Makefile
	ln -svf /usr/include/{machine,sys,x86} grub-core
	CFLAGS="" gmake
	echo

}

build() {

	## set architecture dependent variables
	if [[ "${CARCH}" == 'x86_64' ]]; then
		_EFIEMU="--enable-efiemu"
	else
		_EFIEMU="--disable-efiemu"
	fi

	_HOST="${CARCH}"

	cd "${srcdir}/grub-${pkgver}"
	# _get_locale_files

	_build_grub-common_and_bios
	echo

	_build_grub-efi-i386
	echo

}

package_grub-common() {

	pkgdesc="GNU GRand Unified Bootloader - Utilities and Common Files"
	depends=('freetype2' 'gettext' 'fusefs-libs')
	conflicts=('grub-legacy' 'grub')
	replaces=('grub2-common')
	provides=('grub2-common')
	backup=('boot/grub/grub.cfg' 'etc/default/grub' 'etc/grub.d/40_custom')
	optdepends=('libisoburn: provides xorriso for generating grub rescue iso using grub-mkrescue'
	            'os-prober: to detect other OSes when generating grub.cfg in BIOS systems'
	            'mtools: for grub-mkrescue FAT FS support')
	install="grub.install"
	options=('strip' 'purge' 'docs' 'zipman' '!emptydirs')

	cd "${srcdir}/grub_bios-${pkgver}"
	gmake DESTDIR="${pkgdir}/" bashcompletiondir="/usr/share/bash-completion/completions" install
	echo

	## install extra /etc/grub.d/ files
	install -dm0755 "${pkgdir}/etc/grub.d"

  install -m0644 "${srcdir}/00_header.in" "${pkgdir}/etc/grub.d/00_header"
  install -m0644 "${srcdir}/10_archbsd.in" "${pkgdir}/etc/grub.d/10_archbsd"

	## install /etc/default/grub (used by grub-mkconfig)
	install -dm0755 "${pkgdir}/etc/default"
	install -m0644 "${srcdir}/grub.default" "${pkgdir}/etc/default/grub"

	## install grub.cfg (needed so it doesn't get removed on upgrading because it was previously here)
	install -dm0755 "${pkgdir}/boot/grub"
	touch "${pkgdir}/boot/grub/grub.cfg"

	# remove platform specific files
	rm -rf "${pkgdir}/usr/lib/grub/i386-pc/"

}

package_grub-bios() {

	pkgdesc="GNU GRand Unified Bootloader - i386 PC BIOS Modules"
	depends=("grub-common=${pkgver}")
	options=('!strip' '!emptydirs')
	replaces=('grub2-bios')
	provides=('grub2-bios')

	cd "${srcdir}/grub_bios-${pkgver}"
	gmake DESTDIR="${pkgdir}/" install
	echo

	## remove non platform-specific files
	rm -rf "${pkgdir}"/{boot,etc,usr/{share,bin,sbin}}

	## remove gdb debugging related files
	rm -f "${pkgdir}/usr/lib/grub/i386-pc"/*.module || true
	rm -f "${pkgdir}/usr/lib/grub/i386-pc"/*.image || true
	rm -f "${pkgdir}/usr/lib/grub/i386-pc"/{kernel.exec,gdb_grub,gmodule.pl} || true

}

package_grub-efi-i386() {

	pkgdesc="GNU GRand Unified Bootloader - i386 UEFI Modules"
	depends=("grub-common=${pkgver}" 'dosfstools' 'efibootmgr')
	options=('!strip' '!emptydirs')
	replaces=('grub2-efi-i386')
	provides=('grub2-efi-i386')

	cd "${srcdir}/grub_efi-${pkgver}"
	gmake DESTDIR="${pkgdir}/" install
	echo

	## remove non platform-specific files
	rm -rf "${pkgdir}"/{boot,etc,usr/{share,bin,sbin}}

	## remove gdb debugging related files
	rm -f "${pkgdir}/usr/lib/grub/i386-efi"/*.module || true
	rm -f "${pkgdir}/usr/lib/grub/i386-efi"/*.image || true
	rm -f "${pkgdir}/usr/lib/grub/i386-efi"/{kernel.exec,gdb_grub,gmodule.pl} || true
	rm -f "${pkgdir}/usr/lib/charset.alias"
}
