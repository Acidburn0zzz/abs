# $Id: PKGBUILD 168333 2012-10-08 22:50:22Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>
# Contributor: Wolfgang Bumiller <blub@speed.at>

# Arch PKGBUILD:
# # NOTE: requires rebuild with each new gcc version

pkgname=('libtool')
pkgver=2.4.6
_gccver=5.1.0
pkgrel=3
pkgdesc="A generic library support script"
arch=('i686' 'amd64')
url="http://www.gnu.org/software/libtool"
license=('GPL')
provides=("libltdl=$pkgver" "libtool-multilib=$pkgver")
conflicts=('libltdl' 'libtool-multilib')
replaces=('libltdl' 'libtool-multilib')
makedepends=("gcc=$_gccver")
groups=(base-devel)
source=(ftp://ftp.gnu.org/pub/gnu/libtool/${pkgname}-${pkgver}.tar.xz{,.sig})
md5sums=('1bfb9b923f2c1339b4d2ce1807064aa5'
         'SKIP')
validpgpkeys=('CFE2BE707B538E8B26757D84151308092983D606')   # Gary Vaughan

prepare() {
  cd ${pkgbase}-${pkgver}

  sed -i "" -e 's|sed gsed|sed|g' \
            -e 's|CC=cc|CC=${CC}|g' \
      configure libltdl/configure

}

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
	      lt_cv_sys_lib_dlsearch_path_spec="/usr/lib /usr/lib32"

  find . -type f | xargs touch -f

  make
}

package() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install
}

