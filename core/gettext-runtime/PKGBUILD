# $Id: PKGBUILD 174268 2013-01-03 13:58:09Z allan $
# Maintainer:
# Contributor Wolfgang Bumiller <blub@speed.at>

pkgname=gettext-runtime
_pkgname=gettext
pkgver=0.19.3
pkgrel=12
pkgdesc="GNU internationalization library"
arch=('i386' 'amd64')
url="http://www.gnu.org/software/gettext/"
license=('GPL')
groups=('base-devel')
depends=('libc' 'gcc-libs')
makedepends=('libtool')
replaces=('gettext')
provides=('gettext')
conflicts=('gettext')
options=(!docs)
patchdir=("${srcdir}/${_pkgname}-${pkgver}/${pkgname}")
source=(ftp://ftp.gnu.org/pub/gnu/gettext/${_pkgname}-${pkgver}.tar.gz{,.sig})

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}/${pkgname}"

# Do not install csharp and java documentation.
	sed -i '' -E '/^SUBDIRS =/s/(intl-csharp|intl-java)//g' \
		Makefile.in
# Do not install html copies of manpages.
	sed -i '' \
		-e '/^all-local:/s/html-local//' \
		-e '/^install-data-local:/s/install-html//' \
		-e '/^installdirs-local:/s/installdirs-html//' \
		libasprintf/Makefile.in \
		man/Makefile.in

}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}/${pkgname}"

	./configure --prefix=/usr \
		--disable-csharp --disable-java --with-included-gettext \
		ac_cv_lib_rt_sched_yield=no

  gmake
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}/${pkgname}"
  gmake check
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/${pkgname}"
  gmake DESTDIR="${pkgdir}" install
  install -dm755 ${pkgdir}/usr/lib/gettext
}

md5sums=('c365029ffc866fc4e485d9e5ca60b260'
         'SKIP')

