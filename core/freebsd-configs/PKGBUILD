# $Id$
# Maintainer: Anthony Amzo@ArchBSD.net>
_branch=releng/11.1
pkgname=freebsd-configs
pkgver=20180122
pkgrel=1
pkgdesc='System wide FreeBSD configs'
arch=('any')
license=('BSL')
url='http://www.pacbsd.org'
groups=('base')
depends=("freebsd-world")
patchdir=("${srcdir}")
backup=('etc/profile' 'etc/sysctl.conf' 'boot/loader.conf' 'boot/brand.4th' 
        'boot/beastie.4th' 'etc/devd/asus.conf' 'etc/devd/uath.conf' 
        'etc/devd/usb.conf' 'etc/csh.cshrc' 'etc/devd.conf' 'etc/devfs.conf'
        'etc/motd' 'etc/os-release' 'etc/fstab')
source=("https://raw.github.com/freebsd/freebsd/${_branch}/etc/devd/asus.conf"
	"https://raw.github.com/freebsd/freebsd/${_branch}/etc/devd/apple.conf"
	"https://raw.github.com/freebsd/freebsd/${_branch}/etc/devd/uath.conf"
	"https://raw.github.com/freebsd/freebsd/${_branch}/etc/devd/usb.conf"
	"https://raw.github.com/freebsd/freebsd/${_branch}/etc/motd"
	"https://raw.github.com/freebsd/freebsd/${_branch}/etc/devd.conf"
	"https://raw.github.com/freebsd/freebsd/${_branch}/etc/devfs.conf"
	"https://raw.github.com/freebsd/freebsd/${_branch}/etc/defaults/rc.conf"
	"https://raw.githubusercontent.com/freebsd/freebsd/${_branch}/sys/boot/forth/beastie.4th"
	"https://raw.githubusercontent.com/freebsd/freebsd/${_branch}/sys/boot/forth/brand.4th"
	'os-release' 'csh.cshrc' 'profile' 'sysctl.conf'
	'loader.conf' 'fstab' 'patch-add-tmpfiles' 'patch-logo')
sha256sums=('ffaa8b49c1c3f998f4d1733379ee5bde18eb392a94ae8a4f4f13e76e17a1fbf6'
            '2081538aa78cb399ddb1588abff51edfe2e7ea54c0a8129274650ea1f90f34d9'
            '15d9c79ca89de97de881c94d2efe158852f8321241fe3c9d3fe3560dd42f15ad'
            'e4eaf7c6b8f03248d9f6e46000b5778b612b5eb7e3dbb48e6186b4e79da946dd'
            'c76d9a02e764e77686f9bf4a9192311b6a0387a088cc414dd312ef6ba069ad7e'
            '795281409e9eda07bccb8b65ce3c29dee5d683e8065b95995742c7205cce7b48'
            'ec52e0121103c31b18817d3edd8c16ecf6964c9b2534a5a64ff4ba8a94c4757a'
            'b6e21e64fc947041e51ea12f7c9bae0b1e5fbf1872ab0ceedce27c2fcf9c22bb'
            '3fccbcc3b27451157d9165ae10c2897821a5a31c0cd34e09c29997cf90816f57'
            '33a11d58caeff0f9d9c2c6227587eb7f4906bee14e9f4c752d6cf23b99624518'
            '09dbdb85cf8e3e5311f7da1458fef167d79015deb0bf52826ee60756b8e1dc00'
            '5f30790d8fcbe90b6e4707ae5e1279c0ad2c314989248ecf43c481ce64534ec4'
            '91243d1a06e93ead2f263eaf9dd3ed10d0c664a9a7f922e2015ac4ae1219f4ac'
            '93552a860110e961d254c9ccce414413fa4f27916aaa1a214aedb52e1310c4d1'
            '48c20139ac044f64058f36a88385fa651645753cd628fa9a4f0181b4c993476c'
            '788e4a42ad14f92e2348f6ec709569467912b0158d06af9a69ec4d0a7a820506'
            '55245d677baaaeebed7fcc5f588cd92e4b5939c4f1f2c830c37a00cbd01495f3'
            '91750eea3cd89c6b8f263ab2ad9ae89c9ebcf5856299f565af42de975f39496a')

pkgver() {
  date '+%Y%m%d'
}

build() {
	patch -p0 -i patch-add-tmpfiles

	sed -i '' -e 's|sendmail_submit_enable="YES"|sendmail_submit_enable="NO"|g' \
	-e 's|sendmail_outbound_enable="YES"|sendmail_outbound_enable="NO"|g' \
	-e 's|/usr/local/libdata/ldconfig32|/usr/libdata/ldconfig32|g' \
	-e 's|/usr/local/libdata/ldconfig|/usr/libdata/ldconfig|g' \
	${srcdir}/rc.conf

	echo "Welcome To PacBSD" > ${srcdir}/motd
	
}

package() {

#
# setup root filesystem
#


	for d in boot etc ; do
		install -o root -g wheel -d -m755 ${pkgdir}/${d}
	done

	# setup /etc
	install -o root -g wheel -dm755  ${pkgdir}/etc/devd

	for f in motd os-release profile sysctl.conf csh.cshrc devd.conf devfs.conf fstab; do
		install -o root -g wheel -m644 ${srcdir}/${f} ${pkgdir}/etc/
	done

	for f in loader.conf brand.4th beastie.4th; do
		install -o root -g wheel -m644 ${srcdir}/${f} ${pkgdir}/boot/
	done

	for f in asus.conf uath.conf usb.conf; do
		install -o root -g wheel -m644 ${srcdir}/${f} ${pkgdir}/etc/devd/
	done

	install -o root -g wheel -dm755 "${pkgdir}/etc/defaults"
	install -o root -g wheel -m644 "${srcdir}/rc.conf" "${pkgdir}/etc/defaults/"
	install -o root -g wheel -dm755 "${pkgdir}/etc/local.d"
 
	touch "${srcdir}/pacbsd-release"
	install -o root -g wheel -m644 "${srcdir}/pacbsd-release" ${pkgdir}/etc/pacbsd-release
}
