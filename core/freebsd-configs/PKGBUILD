# $Id$
# Maintainer: Anthony Amzo@ArchBSD.net>
_branch=master
pkgname=freebsd-configs
pkgver=20151022
pkgrel=1
pkgdesc='System wide FreeBSD configs'
arch=('any')
license=('BSL')
url='http://www.pacbsd.org'
groups=('base')
depends=("freebsd-world")
patchdir=("${srcdir}")
backup=('etc/profile' 'etc/sysctl.conf' 'boot/loader.conf' 'boot/brand.4th' 
        'boot/beastie.4th' 'etc/devd/asus.conf' 'etc/devd/uath.conf' 
        'etc/devd/usb.conf' 'etc/csh.cshrc' 'etc/devd.conf' 'etc/devfs.conf'
        'etc/motd' 'etc/os-release' 'etc/fstab')
source=("https://raw.github.com/freebsd/freebsd/${_branch}/etc/devd/asus.conf"
	"https://raw.github.com/freebsd/freebsd/${_branch}/etc/devd/apple.conf"
	"https://raw.github.com/freebsd/freebsd/${_branch}/etc/devd/uath.conf"
	"https://raw.github.com/freebsd/freebsd/${_branch}/etc/devd/usb.conf"
	"https://raw.github.com/freebsd/freebsd/${_branch}/etc/motd"
	"https://raw.github.com/freebsd/freebsd/${_branch}/etc/devd.conf"
	"https://raw.github.com/freebsd/freebsd/${_branch}/etc/devfs.conf"
	"https://raw.github.com/freebsd/freebsd/${_branch}/etc/defaults/rc.conf"
	"https://raw.githubusercontent.com/freebsd/freebsd/${_branch}/sys/boot/forth/beastie.4th"
	"https://raw.githubusercontent.com/freebsd/freebsd/${_branch}/sys/boot/forth/brand.4th"
	'os-release' 'csh.cshrc' 'profile' 'sysctl.conf'
	'loader.conf' 'disable_moused-devd' 'fstab' 'patch-add-tmpfiles' 'patch-logo')

pkgver() {
  date '+%Y%m%d'
}

build() {
	patch -p0 -i disable_moused-devd
	patch -p0 -i patch-add-tmpfiles 

	sed -i '' -e 's|sendmail_submit_enable="YES"|sendmail_submit_enable="NO"|g' \
	-e 's|sendmail_outbound_enable="YES"|sendmail_outbound_enable="NO"|g' \
	-e 's|/usr/local/libdata/ldconfig32|/usr/libdata/ldconfig32|g' \
	-e 's|/usr/local/libdata/ldconfig|/usr/libdata/ldconfig|g' \
	${srcdir}/rc.conf

	echo "Welcome To PacBSD" > ${srcdir}/motd
	
}

package() {

#
# setup root filesystem
#


	for d in boot etc ; do
		install -o root -g wheel -d -m755 ${pkgdir}/${d}
	done

	# setup /etc
	install -o root -g wheel -dm755  ${pkgdir}/etc/devd

	for f in motd os-release profile sysctl.conf csh.cshrc devd.conf devfs.conf fstab; do
		install -o root -g wheel -m644 ${srcdir}/${f} ${pkgdir}/etc/
	done

	for f in loader.conf brand.4th beastie.4th; do
		install -o root -g wheel -m644 ${srcdir}/${f} ${pkgdir}/boot/
	done

	for f in asus.conf uath.conf usb.conf; do
		install -o root -g wheel -m644 ${srcdir}/${f} ${pkgdir}/etc/devd/
	done

	install -o root -g wheel -dm755 "${pkgdir}/etc/defaults"
	install -o root -g wheel -m644 "${srcdir}/rc.conf" "${pkgdir}/etc/defaults/"
	install -o root -g wheel -dm755 "${pkgdir}/etc/local.d"
 
	touch "${srcdir}/pacbsd-release"
	install -o root -g wheel -m644 "${srcdir}/pacbsd-release" ${pkgdir}/etc/pacbsd-release
}
md5sums=('3fab19520668156176f62be9e0af21c1'
         '966b3ec749130f693d6a8fdddd884663'
         '88ec33a1523b62b2e3f7488ff8ca7c48'
         'e9aaabc66c31596122b6c8c12a8be007'
         'b38d686cc2299afb691a1cc6f15e3df1'
         '11b707a72aae7f34b566b59ae4c514a3'
         'd8a21e59a96525f106ab750d5938af40'
         '20b08ea8160722dcd980c4a0145e7b12'
         'b640c390c4ab1015937dd88880d2e4d8'
         '6c535306f334efd68d8c0469299f1929'
         '6fcfbe48c6888c126aa2cf8229f98d59'
         'b516bd19fc7cfaa27463edfab9b2f1c0'
         'f3b6ae7db8adffaaa4bffc6099dcbd50'
         'dd38ec50ec134cc70cba89f44838eb5e'
         'c98d3a6ca938ccb3085ef14098eb6312'
         '02ac55efc9c4d1a44d2cd4b148fe39a8'
         'd9805d13aa906fa95d3c4ab57df7beca'
         '4fd321a3516d090ab08e1a2ccaecad06'
         '2ed47e14ab7e02cd7b089dc984d16298')
