# Port: security/gnupg

pkgname=gnupg
pkgver=2.2.5
pkgrel=1
pkgdesc='Complete and free implementation of the OpenPGP standard'
url='http://www.gnupg.org/'
license=('GPL')
arch=('amd64')
optdepends=('curl: gpg2keys_curl')
makedepends=('gettext' 'libassuan' 'libgcrypt' 'libgpg-error' 'libksba' 'npth' 'gmake')
depends=('npth' 'libgpg-error' 'libgcrypt' 'libksba' 'libassuan'
         'pinentry' 'readline' 'gnutls' 'sqlite')
provides=("gnupg2=${pkgver}")
conflicts=('gnupg2')
replaces=('gnupg2')
options=('gnu_configure' 'iconv')
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6'
              '46CC730865BB5C78EBABADCF04376F3EE0856959'
              '031EC2536E580D8EA286A9F22071B08A33BD3F06'
              'D238EA65D64C67ED4C3073F28A861B1C7EFD60D9')
source=("ftp://ftp.gnupg.org/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2"{,.sig})
sha256sums=('3fa189a32d4fb62147874eb1389047c267d9ba088f57ab521cb0df46f08aef57'
            'SKIP')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure --prefix=/usr \
		    --sysconfdir=/etc \
		    --sbindir=/usr/bin \
        --disable-ntbtls \
        --disable-wks-tools \
        --enable-gpg-is-gpg2 \
        --enable-symcryptrun \
        --mandir=/usr/share/man \
        --infodir=/usr/share/info \
		    --with-readline=/usr \
        --enable-symcryptrun \
	      --with-libcurl=/usr \
        --libexecdir=/usr/libexec/gnupg \
        --disable-ldap \
		    --enable-maintainer-mode
	gmake
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
	# make check || [[ $CARCH = i686 ]]
	# https://lists.gnupg.org/pipermail/gnupg-devel/2016-December/032364.html
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	gmake DESTDIR="${pkgdir}" install
	ln -s gpg2 "${pkgdir}"/usr/bin/gpg
	ln -s gpgv2 "${pkgdir}"/usr/bin/gpgv
	ln -s gpg2.1.gz "${pkgdir}"/usr/share/man/man1/gpg.1.gz
}
