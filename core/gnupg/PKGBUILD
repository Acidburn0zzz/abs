# Port: security/gnupg

pkgname=gnupg
pkgver=2.1.21
pkgrel=2
pkgdesc='Complete and free implementation of the OpenPGP standard'
url='http://www.gnupg.org/'
license=('GPL')
arch=('i386' 'amd64')
optdepends=('curl: gpg2keys_curl')
makedepends=('gettext' 'libassuan' 'libgcrypt' 'libgpg-error' 'libksba' 'npth' 'gmake')
depends=('npth' 'libgpg-error' 'libgcrypt' 'libksba' 'libassuan'
         'pinentry' 'readline' 'gnutls' 'sqlite')
provides=("gnupg2=${pkgver}")
conflicts=('gnupg2')
replaces=('gnupg2')
options=('gnu_configure')
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6'
              '46CC730865BB5C78EBABADCF04376F3EE0856959'
              '031EC2536E580D8EA286A9F22071B08A33BD3F06'
              'D238EA65D64C67ED4C3073F28A861B1C7EFD60D9')
source=("ftp://ftp.gnupg.org/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2"{,.sig})
sha256sums=('7aead8a8ba75b69866f583b6c747d91414d523bfdfbe9a8e0fe026b16ba427dd'
            'SKIP')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	gsed '/noinst_SCRIPTS = gpg-zip/c sbin_SCRIPTS += gpg-zip' -i tools/Makefile.in
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure --prefix=/usr \
		    --sysconfdir=/etc \
		    --sbindir=/usr/bin \
	            --mandir=/usr/share/man \
	            --infodir=/usr/share/info \
		    --with-readline=/usr \
	            --enable-symcryptrun \
	            --disable-scdaemon \
	            --with-libcurl=/usr \
                    --libexecdir=/usr/libexec/gnupg \
                    --disable-ldap \
		    --enable-maintainer-mode
	gmake
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
	# make check || [[ $CARCH = i686 ]]
	# https://lists.gnupg.org/pipermail/gnupg-devel/2016-December/032364.html
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	gmake DESTDIR="${pkgdir}" install
	ln -s gpg2 "${pkgdir}"/usr/bin/gpg
	ln -s gpgv2 "${pkgdir}"/usr/bin/gpgv
	ln -s gpg2.1.gz "${pkgdir}"/usr/share/man/man1/gpg.1.gz
}
