# $Id: PKGBUILD 172477 2012-12-03 21:23:28Z allan $
# Maintainer: Anthony Donnelly <Amzo@archbsd.net>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=pcre
pkgver=8.36
pkgrel=1
pkgdesc="A library that implements Perl 5-style regular expressions"
arch=('i686' 'x86_64')
url="http://www.pcre.org/"
license=('BSD')
depends=('gcc-libs')
options=('!libtool' 'libtoolfix')
source=(ftp://ftp.csx.cam.ac.uk/pub/software/programming/pcre/${pkgname}-${pkgver}.tar.bz2{,.sig}
        '001-fix-heap-overflow.patch')
md5sums=('b767bc9af0c20bc9c1fe403b0d41ad97'
         'SKIP'
         '44a7e4fca07eff04d07bef65c982de9a')

prepare() {
  patch -d $pkgname-$pkgver < 001-fix-heap-overflow.patch
}

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  # the ports pkg adds this flag for amd64/ia64
  if [[ "${CARCH}" == "x86_64" ]]; then
    CFLAGS="${CFLAGS} -fPIC"
  fi

  # ports comment on this:
  # work around for a autoconf assumption, to fix c++ linking with clang
  sed -i '' -e 's|GREP "\\-L"|GREP "conftest\\."|g' configure

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --enable-pcre8 --enable-pcre16 --enable-pcre32 --enable-jit \
    --enable-utf --enable-unicode-properties
  make
}

check() {
  cd "${srcdir}"/${pkgname}-${pkgver}
# disable the stack test
  sed -i '' -e 's/do2=yes/do2=no/' RunTest
  make check
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 LICENCE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
