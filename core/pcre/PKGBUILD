# $Id: PKGBUILD 172477 2012-12-03 21:23:28Z allan $
# Maintainer: Anthony Donnelly <Amzo@archbsd.net>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=pcre
pkgver=8.37
pkgrel=8
pkgdesc="A library that implements Perl 5-style regular expressions"
arch=('i386' 'amd64')
url="http://www.pcre.org/"
license=('BSD')
depends=('gcc-libs')
options=('libtoolfix')
validpgpkeys=('45F68D54BBE23FB3039B46E59766E084FB0F43D8') # Philip Hazel
source=("ftp://ftp.csx.cam.ac.uk/pub/software/programming/pcre/$pkgname-$pkgver.tar.bz2"{,.sig}
        '01-svn-snapshot.patch')
md5sums=('ed91be292cb01d21bc7e526816c26981'
         'SKIP'
         '0e10946bb151c081c2bd0495fb77ccf4')

prepare() {
  # bump to latest snapshot. Too many security issues
  gpatch -p0 -d $pkgname-$pkgver < 01-svn-snapshot.patch
}

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  # the ports pkg adds this flag for amd64/ia64
  if [[ "${CARCH}" == "amd64" ]]; then
    CFLAGS="${CFLAGS} -fPIC"
  fi

  # ports comment on this:
  # work around for a autoconf assumption, to fix c++ linking with clang
  sed -i '' -e 's|GREP "\\-L"|GREP "conftest\\."|g' configure

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --enable-pcre8 --enable-pcre16 --enable-pcre32 --enable-jit \
    --enable-utf --enable-unicode-properties
  make
}

check() {
  cd "${srcdir}"/${pkgname}-${pkgver}
# disable the stack test
  sed -i '' -e 's/do2=yes/do2=no/' RunTest
  make check
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 LICENCE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
