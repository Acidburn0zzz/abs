# Port: devel/scons
pkgname=scons
pkgver=2.5.1
pkgrel=1
pkgdesc="Extensible Python-based build utility"
arch=('i386' 'amd64')
url="http://scons.org"
license=('MIT')
depends=('python2')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('0b25218ae7b46a967db42f2a53721645b3d42874a65f9552ad16ce26d30f51f2')

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --standard-lib \
                           --prefix=/usr \
                           --install-data=/usr/share \
                           --optimize=1 \
                           --root="$pkgdir"

  # fix for python 2.7
  sed -i '' 's:^#!.*bin/env python:#!/usr/bin/env python2:' \
    "$pkgdir"/usr/bin/*

  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 LICENSE.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
