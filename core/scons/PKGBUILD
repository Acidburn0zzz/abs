# Port: devel/scons

pkgname=scons
pkgver=3.0.1
pkgrel=1
pkgdesc="Extensible Python-based build utility"
arch=('amd64')
url="http://scons.org"
license=('MIT')
depends=('python2')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('24475e38d39c19683bc88054524df018fe6949d70fbd4c69e298d39a0269f173')

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --standard-lib \
                           --prefix=/usr \
                           --install-data=/usr/share \
                           --optimize=1 \
                           --root="$pkgdir"

  # fix for python 2.7
  sed -i '' 's:^#!.*bin/env python:#!/usr/bin/env python2:' \
    "$pkgdir"/usr/bin/*

  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 LICENSE.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
