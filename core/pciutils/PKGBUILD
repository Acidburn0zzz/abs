# $Id: PKGBUILD 184187 2013-05-03 10:22:45Z tpowa $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=pciutils
pkgver=3.3.1
pkgrel=3
pkgdesc="PCI bus configuration space access library and tools"
arch=(i386 amd64)
license=('GPL2')
groups=('base')
depends=('hwids')
url="http://mj.ucw.cz/sw/pciutils/"
source=(#ftp://ftp.kernel.org/pub/software/utils/${pkgname}/${pkgname}-${pkgver}.tar.bz2
       ftp://atrey.karlin.mff.cuni.cz/pub/linux/pci/${pkgname}-${pkgver}.tar.gz)
md5sums=('d76ba41d0ec0e16f12a4723cbd1477e9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake CC="$CC" OPT="${CFLAGS} -fPIC -DPIC" ZLIB=no SHARED=no PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man lib/libpci.a 
  cp lib/libpci.a "${srcdir}/"
  gmake clean
  gmake CC="$CC" OPT="${CFLAGS}" ZLIB=no SHARED=yes PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake CC="$CC" SHARED=yes PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man DESTDIR="${pkgdir}" install install-lib
  install -m644 "${srcdir}/libpci.a" "${pkgdir}/usr/lib/"
  # this is now supplied by the hwids package
  rm -rf $pkgdir/usr/{sbin/update-pciids,share/{man/man8/update-pciids.8,hwdata}}
}
