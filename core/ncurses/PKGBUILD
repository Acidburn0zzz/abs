# $Id: PKGBUILD 150103 2012-02-12 13:47:59Z allan $
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgname=ncurses
pkgver=5.9.20150214
_pkgver=5.9-20150214
pkgrel=1
pkgdesc="System V Release 4.0 curses emulation library"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/ncurses/"
license=('MIT')
depends=('libc' 'gcc-libs')
source=(http://distcache.freebsd.org/ports-distfiles/${pkgname}/${pkgname}-${_pkgver}.tgz)
md5sums=('ba499fe0df88170f87ab96f75bef6505')

prepare() {
  mkdir ncurses{,w}-build

}

build() {
  cd ncursesw-build
  ../$pkgname-${_pkgver/_/-}/configure --prefix=/usr --mandir=/usr/share/man \
     --with-shared --with-normal --without-debug --without-ada --with-pkg-config=yes \
     --with-pkg-config-libdir=/usr/libdata/pkgconfig --enable-widec --enable-pc-files --with-cxx-binding --with-cxx-shared
  # add --enable-ext-colors and --enable-ext-mouse with next soname bump
  gmake

  # libraries for external binary support
  cd ${srcdir}/ncurses-build
  [[ $CARCH = "x86_64" ]] && CONFIGFLAG="--with-chtype=long"
  ../$pkgname-${_pkgver/_/-}/configure --prefix=/usr \
    --with-shared --with-normal --without-debug --without-ada $CONFIGFLAG --with-pkg-config=yes \
    --with-cxx-binding --with-cxx-shared --with-pkg-config-libdir=/usr/libdata/pkgconfig
  gmake
}

package() {
  cd ncursesw-build
  gmake DESTDIR="$pkgdir" install

  # fool packages looking to link to non-wide-character ncurses libraries
  for lib in ncurses ncurses++ form panel menu; do
    echo "INPUT(-l${lib}w)" > "$pkgdir"/usr/lib/lib${lib}.so
  done

  for lib in ncurses ncurses++ form panel menu; do
    ln -s ${lib}w.pc "$pkgdir"/usr/libdata/pkgconfig/${lib}.pc
  done

  # some packages look for -lcurses during build
  echo "INPUT(-lncursesw)" > "$pkgdir"/usr/lib/libcursesw.so
  ln -s libncurses.so "$pkgdir"/usr/lib/libcurses.so

  # non-widec compatibility libraries
  cd "$srcdir"/ncurses-build
  for lib in ncurses form panel menu; do
    install -Dm755 lib/lib${lib}.so.${pkgver%.*} "$pkgdir"/usr/lib/lib${lib}.so.${pkgver%.*}
    ln -s lib${lib}.so.${pkgver%.*} "$pkgdir"/usr/lib/lib${lib}.so.5
  done

  # install license, rip it from the readme
  cd "$srcdir"/$pkgname-${_pkgver/_/-}
  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  grep -B 100 '$Id' README > "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

