# Port: security/p11-kit
pkgname=p11-kit
pkgver=0.23.3
pkgrel=1
pkgdesc="Library to work with PKCS#11 modules"
arch=(i386 amd64)
url="http://p11-glue.freedesktop.org"
license=('BSD')
depends=('libtasn1' 'libffi' 'gettext-runtime' 'ca_root_nss' 'libiconv')
options=('libtoolfix')
source=("https://github.com/p11-glue/p11-kit/releases/download/${pkgver}/p11-kit-${pkgver}.tar.gz"{,.sig}
	libnssckbi-compat.patch)
sha256sums=('d487f04dba3f9e8256f53034c59c944ca45fd7b8434c095da6a74079644dcd82'
            'SKIP'
            '8f763cdbc6c0ca6c5a7898f9fd6f3018b7ac5b1aca36f67c6c813343c2962962')

prepare() {
  cd $pkgname-$pkgver

  # Build and install an additional library (libnssckbi-p11-kit.so) which
  # is a copy of p11-kit-trust.so but uses the same label for root certs as
  # libnssckbi.so ("Builtin Object Token" instead of "Default Trust")
  # https://bugs.freedesktop.org/show_bug.cgi?id=66161
  patch -Np1 -i ../libnssckbi-compat.patch

  autoreconf -vi

  find ${srcdir}/${pkgname}-${pkgver} -name "configure" -type f | xargs sed -i '' -e 's|-lpthread|-pthread|g'
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc \
    --with-module-path=/usr/lib/pkcs11 \
    --with-trust-paths=/usr/share/certs/ca-root-nss.crt \
    LDFLAGS="-pthread"
  gmake
}

check() {
  cd $pkgname-$pkgver
  gmake check
}

package() {
  cd  $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
  install -dm755 $pkgdir/usr/share/licenses/$pkgname/
  install -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}

# vim:set ts=2 sw=2 et:
