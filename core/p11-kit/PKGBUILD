# $Id: PKGBUILD 183529 2013-04-22 13:10:36Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=p11-kit
pkgver=0.23.1
pkgrel=6
pkgdesc="Library to work with PKCS#11 modules"
arch=(i386 amd64)
url="http://p11-glue.freedesktop.org"
license=('BSD')
depends=('libtasn1' 'libffi' 'gettext-runtime')
options=('libtoolfix')
source=($url/releases/$pkgname-$pkgver.tar.gz{,.sig}
	libnssckbi-compat.patch)
md5sums=('96f073270c489c9a594e1c9413f42db8'
         'SKIP'
         '8c3f119005908cf4a3e0ef0a0a310f14')

prepare() {
  cd $pkgname-$pkgver

  # Build and install an additional library (libnssckbi-p11-kit.so) which
  # is a copy of p11-kit-trust.so but uses the same label for root certs as
  # libnssckbi.so ("Builtin Object Token" instead of "Default Trust")
  # https://bugs.freedesktop.org/show_bug.cgi?id=66161
  patch -Np1 -i ../libnssckbi-compat.patch

  autoreconf -vi

  find ${srcdir}/${pkgname}-${pkgver} -name "configure" -type f | xargs sed -i '' -e 's|-lpthread|-pthread|g'
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc \
    --with-module-path=/usr/lib/pkcs11 \
    --with-trust-paths=/usr/share/certs/ca-root-nss.crt \
    LDFLAGS="-pthread"
  gmake
}

check() {
  cd $pkgname-$pkgver
  gmake check
}

package() {
  cd  $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
  install -dm755 $pkgdir/usr/share/licenses/$pkgname/
  install -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}

# vim:set ts=2 sw=2 et:
