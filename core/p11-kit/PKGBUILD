# Port: security/p11-kit

pkgname=p11-kit
pkgver=0.23.8
pkgrel=1
pkgdesc="Library to work with PKCS#11 modules"
arch=(i386 amd64)
url="http://p11-glue.freedesktop.org"
license=('BSD')
depends=('libtasn1' 'libffi' 'gettext-runtime' 'ca_root_nss' 'libiconv')
options=('libtoolfix' 'gnu_configure' 'pathfix')
source=("https://github.com/p11-glue/p11-kit/releases/download/${pkgver}/p11-kit-${pkgver}.tar.gz"{,.sig})
sha256sums=('4ba134e5fe4b62bcaf7a2d66841767d9d23e875b977bba6939367a9f400b133f'
            'SKIP')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc \
    --disable-nls --enable-debug=no \
    --with-module-path=/usr/lib/pkcs11 \
    --with-trust-paths=/usr/share/certs/ca-root-nss.crt

  gmake
}

check() {
  cd $pkgname-$pkgver
  gmake check || true # one failure
}

package() {
  cd  $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
  install -dm755 $pkgdir/usr/share/licenses/$pkgname/
  install -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}

# vim:set ts=2 sw=2 et:
