# Port: devel/cmake
pkgname=cmake
pkgver=3.9.2
pkgrel=1
arch=('amd64')
url="http://www.cmake.org/"
license=('custom')
depends=('curl' 'freebsd-world' 'jsoncpp' 'expat' 'libuv' 'rhash')
makedepends=('python-sphinx')
options=('!docs')
source=("http://www.cmake.org/files/v${pkgver%.*}/${pkgname}-${pkgver}.tar.gz"
  'InitialCache.cmake')
sha256sums=('954a5801a456ee48e76f01107c9a4961677dd0f3e115275bbd18410dc22ba3c1'
            'd3a0e66122ac11f841c008df48a24cf5f2351eb024d528f6329d86e9d5f8d7c2')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  ./bootstrap --prefix=/usr \
    --mandir=/share/man \
    --docdir=/share/doc/cmake \
    --sphinx-man \
    --system-libs \
    --parallel=$(/usr/bin/getconf NPROCESSORS_ONLN) \
    --init=InitialCache.cmake

  gmake
}

package() {
  pkgname="cmake"
  pkgdesc="A cross-platform open-source make system"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  vimpath="${pkgdir}/usr/share/vim/vimfiles"
  install -d "${vimpath}"/{help,indent,syntax}
  ln -s /usr/share/cmake-${pkgver%.*}/editors/vim/cmake-help.vim "${vimpath}"/help/
  ln -s /usr/share/cmake-${pkgver%.*}/editors/vim/cmake-indent.vim "${vimpath}"/indent/
  ln -s /usr/share/cmake-${pkgver%.*}/editors/vim/cmake-syntax.vim "${vimpath}"/syntax/

  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}-${pkgver}
  install -m644 Copyright.txt "${pkgdir}"/usr/share/licenses/${pkgname}-${pkgver}/LICENSE
}
