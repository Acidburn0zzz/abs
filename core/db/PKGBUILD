# Contributor: Wolfgang Bumiller <blub@speed.at>
# Port: databases/db6

pkgname=db
pkgver=6.1.26
pkgrel=1
pkgdesc="The Berkeley DB embedded database system"
arch=('i386' 'amd64')
url="http://www.oracle.com/technology/software/products/berkeley-db/index.html"
depends=('freebsd-world')
makedepends=('gmake')
patchoptions=("-p1")
license=('custom')
install=db.install
source=(http://download.oracle.com/berkeley-db/db-${pkgver}.tar.gz)
md5sums=('9525aa57a282d49e5d1bf5e48ffa8a56')

prepare() {
  cd "${srcdir}"/$pkgname-${pkgver}/build_unix

  sed -i '' -e '/^DOCLIST/{s/csharp//;}' ../dist/Makefile.in
  sed -i '' -Ee 's/[[:<:]]atomic_init[[:>:]]/db_atomic_init/g' \
    ../src/mp/mp* ../src/mutex/mut_* \
    ../src/dbinc/atomic.h

}

build() {
  cd "${srcdir}"/$pkgname-${pkgver}/build_unix

  ../dist/configure --enable-cxx --enable-stl \
     --enable-compat185 --enable-dump185 \
     --disable-tcl \
     --includedir=/usr/local/include/db6 \
     --libdir=/usr/local/lib/db6 \
     --bindir=/usr/local/bin/db6 \
     --with-cryptography=yes --prefix=/usr/local

  gmake LIBSO_LIBS=-lpthread
}

package() {
  cd "${srcdir}"/$pkgname-${pkgver}/build_unix
  sed -i '' -e '/^library_install:/s/install_docs//p' Makefile
  unset MAKEFLAGS
  gmake DESTDIR="${pkgdir}" install -j1

  for i in libdb libdb_cxx libdb_stl;do
    ln -s -f db6/${i}-6.1.so ${pkgdir}/usr/local/lib
  done

  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 "${srcdir}"/${pkgname}-${pkgver}/LICENSE \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  cd ${pkgdir}/usr/local/bin/db6
  for i in *; do 
    ln -s -f db6/$i ../$i-6.1
  done

}
