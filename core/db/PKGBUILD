# $Id: PKGBUILD 160636 2012-06-02 22:25:49Z allan $
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=db
pkgver=6.0.20
pkgrel=1
pkgdesc="The Berkeley DB embedded database system"
arch=('i686' 'x86_64')
url="http://www.oracle.com/technology/software/products/berkeley-db/index.html"
license=('custom')
install=db.install
source=(http://download.oracle.com/berkeley-db/db-${pkgver}.tar.gz)
md5sums=('f73afcb308aefde7e6ece4caa87b22a9')

build() {
  cd "${srcdir}"/$pkgname-${pkgver}/build_unix

  ../dist/configure --prefix=/usr --enable-compat185 \
    --mandir=/usr/share/man --infodir=/usr/share/info \
    --sysconfdir=/etc \
    --enable-shared --enable-static --enable-cxx --enable-dbm \
    --enable-stl --disable-tcl

  gmake LIBSO_LIBS=-lpthread
}

package() {
  cd "${srcdir}"/$pkgname-${pkgver}/build_unix
  sed -i '' -e '/^library_install:/s/install_docs//p' Makefile
  unset MAKEFLAGS
  gmake DESTDIR="${pkgdir}" install -j1
  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 "${srcdir}"/${pkgname}-${pkgver}/LICENSE \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
