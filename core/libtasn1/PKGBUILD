# Port: security/libtasn1

pkgname=libtasn1
pkgver=4.10
pkgrel=1
pkgdesc="The ASN.1 library used in GNUTLS"
arch=('i386' 'amd64')
license=('GPL3' 'LGPL')
url="http://www.gnu.org/software/libtasn1/"
options=('libtoolfix')
install=libtasn1.install
source=(http://ftp.gnu.org/gnu/libtasn1/${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=('681a4d9a0d259f2125713f2e5766c5809f151b3a1392fd91390f780b4b8f5a02'
            'SKIP')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
      --with-packager=PacBSD \
      --with-packager-bug-reports="http://bugs.pacbsd.org/" \
      --with-packager-version=${pkgver}-${pkgrel} \
      --disable-gcc-warnings

  gmake
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
}
