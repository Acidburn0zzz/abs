pkgbase=glib2
pkgname=(glib2 glib2-docs)
pkgver=2.46.1
pkgrel=4
pkgdesc="Common C routines used by GTK+ and other libs"
url="http://www.gtk.org/"
arch=(i686 amd64)
license=('LGPL')
makedepends=('pkg-config' 'python2' 'libxslt' 'docbook-xml' 'pcre' 'libffi' 'icu')
patchdir="$srcdir/glib-$pkgver"
options=('!docs' '!emptydirs' 'libtoolfix' 'iconv')
replaces=('gio-fam')
provides=('gio-fam')
install=glib2.install
source=(http://ftp.gnome.org/pub/GNOME/sources/glib/${pkgver:0:4}/glib-$pkgver.tar.xz
        revert-warn-glib-compile-schemas.patch)
sha256sums=('5a1f03b952ebc3a7e9f612b8724f70898183e31503db329b4f15d07163c8fdfb'
            '049240975cd2f1c88fbe7deb28af14d4ec7d2640495f7ca8980d873bb710cc97')

prepare() {
  cd glib-$pkgver
  patch -Rp1 -i ../revert-warn-glib-compile-schemas.patch
}

prepare() {
  cd "$srcdir/glib-$pkgver"


  sed -i '' -e 's|inotify_support=yes|inotify_support=no|' \
            -e 's|-Werror||' \
            -e 's|#define HAVE_SYS_INOTIFY_H 1||' configure

}

build() {
  cd "$srcdir/glib-$pkgver"

# fix FS#34630 https://bugs.archlinux.org/task/34630
  export CFLAGS+=" -Wall"

  sed -i '' -e 's|-liconv||g' configure
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --libdir=/usr/lib \
      --sysconfdir=/etc \
      --enable-shared \
      --with-libiconv=native \
      --disable-gtk-doc --with-html-dir=/usr/share/doc \
      --disable-dtrace \
      --with-pcre=system \
      --target="x86_64-pacbsd-freebsd" \
      --libexecdir=/usr/libexec \
      --disable-fam \
      --enable-debug \
      PTHREAD_CFLAGS="" \
      PTHREAD_LIBS="-pthread" \
      ac_cv_header_sys_inotify_h=

  gmake
}

#check() {
#  cd glib-$pkgver
#  gmake -k check : true
#}

package_glib2() {
  depends=('pcre' 'libffi' 'icu')
  optdepends=('python2: for gdbus-codegen and gtester-report')
  options=('!docs' '!emptydirs')
  license=('LGPL')

  cd glib-$pkgver
  gmake completiondir=/usr/share/bash-completion/completions DESTDIR="$pkgdir" install

  for _i in "$pkgdir/usr/share/bash-completion/completions/"*; do
      chmod -x "$_i"
  done

  # Our gdb does not ship the required python modules, so remove it
  rm -rf "$pkgdir/usr/share/gdb/"

  if [[ -f ${pkgdir}/usr/lib/charset.alias ]]; then
    rm ${pkgdir}/usr/lib/charset.alias
  fi

}

package_glib2-docs() {
  pkgdesc="Documentation for glib2"
  conflicts=('gobject2-docs')
  replaces=('gobject2-docs')
  license=('custom')
  options=('docs' '!emptydirs')
  
  cd glib-$pkgver/docs
  gmake DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/glib2-docs"
  install -m644 reference/COPYING "${pkgdir}/usr/share/licenses/glib2-docs/"

  rm -rf "${pkgdir}/usr/share/man"
}

