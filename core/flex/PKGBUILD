# Maintainer: Anthony Donnelly <amzo@archbsd.net>
# Port: textproc/flex
pkgname=flex
pkgver=2.6.1
pkgrel=2
pkgdesc="A tool for generating text-scanning programs"
arch=('i386' 'amd64')
url="http://flex.sourceforge.net"
license=('custom')
groups=('base-devel')
depends=('gettext-runtime' 'gm4' 'libiconv')
makedepends=('help2man' 'gmake')
source=(https://github.com/westes/flex/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.xz)
md5sums=('cd3c86290fc2676a641aefafeb10848a')

build() {
  cd $srcdir/$pkgname-$pkgver

  rm -f doc/*.info*

  ./configure --prefix=/usr/local \
    --includedir=/usr/local/include/flex \
    M4=/usr/bin/gm4 MAKEINFO="makeinfo --no-split"
  gmake
}

check() {
  cd $srcdir/$pkgname-$pkgver
  
  # these tests used features removed in bison-2.6
  sed -i '' -e '/test-bison-yylloc/d' -e '/test-bison-yylval/d' tests/Makefile.in

  gmake check
}

package() {
  cd $srcdir/$pkgname-$pkgver

  gmake PREFIX=/usr/local DESTDIR=$pkgdir install
  install -dm755 \
  	$pkgdir/usr/share/licenses/$pkgname/
  install -m644 COPYING \
  	$pkgdir/usr/share/licenses/$pkgname/license.txt

  # man page and info is provided by world flex
  rm -r ${pkgdir}/usr/local/share/{info,man}
}

