# Port: security/gnutls
pkgname=gnutls
pkgver=3.3.17.1
pkgrel=1
pkgdesc="A library which provides a secure layer over a reliable transport layer"
arch=('i386' 'amd64')
license=('GPL3' 'LGPL')
url="http://www.gnutls.org/"
install=gnutls.install
options=('!zipman' 'libtoolfix')
depends=('libtasn1' 'nettle' 'p11-kit' 'libiconv')
source=(http://artfiles.org/gnupg.org/gnutls/v3.3/${pkgname}-${pkgver}.tar.xz{,.sig})
md5sums=('8d01c7e7f2cbc5871fdca832d2260b6b'
         'SKIP')
validpgpkeys=('0424D4EE81A0E3D119C6F835EDA21E94B565716F'
              '1F42418905D8206AA754CCDC29EE58B996865171')
               # "Simon Josefsson <simon@josefsson.org>"
               # "Nikos Mavrogiannopoulos <nmav@gnutls.org>

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
    --with-zlib \
    --disable-static \
    --disable-guile \
    --disable-valgrind-tests \
    --with-default-trust-store-pkcs11="pkcs11:model=p11-kit-trust;manufacturer=PKCS%2311%20Kit" \
    ac_cv_type_max_align_t=yes


  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  # lots of .png files are put into infodir and are gzipped by makepkg! this may need to be fixed by using !zipman
  # gzip -9 all files in infodir and manpages manually
  find "$pkgdir/usr/share/info" -name '*.info*' -exec gzip -9 {} \;
  find "$pkgdir/usr/share/man" -exec gzip -9 {} \;
}
