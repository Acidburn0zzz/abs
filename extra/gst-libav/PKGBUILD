pkgname=gst-libav
pkgver=1.4.5
pkgrel=1
pkgdesc="Gstreamer libav Plugin"
arch=('i686' 'amd64')
license=('GPL')
url="http://gstreamer.freedesktop.org/"
depends=('gst-plugins-base-libs')
makedepends=('python' 'yasm')
options=(!emptydirs)
provides=("gst-ffmpeg=$pkgver-$pkgrel")
source=(${url}/src/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('605c62624604f3bb5c870844cc1f2711779cc533b004c2aa1d8c0d58557afbbc')

prepare() {
  cd $pkgname-$pkgver
  export LDFLAGS+=" -Wl,-Bsymbolic"
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-pic --with-libav-extra-configure="--enable-runtime-cpudetect" \
    --with-package-name="GStreamer libav Plugin (Pac BSD)" \
    --with-package-origin="http://www.pacbsd.org/"


  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  gmake
}

check() {
  cd $pkgname-$pkgver
  gmake check
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="${pkgdir}" install
}
