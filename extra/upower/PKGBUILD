# $Id: PKGBUILD 180239 2013-03-18 19:28:47Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=upower
pkgver=0.99.0
pkgrel=5
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
arch=('i686' 'x86_64')
url="http://upower.freedesktop.org"
license=('GPL')
depends=('polkit' 'dbus-glib' 'gio-fam')
makedepends=('intltool' 'docbook-xsl' 'gobject-introspection')
options=('!libtool')
source=($url/releases/$pkgname-$pkgver.tar.xz)
md5sums=('14f43bc13353e23e7280863f33ac50d2')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname \
    --disable-static \
    --with-backend=freebsd \
    --enable-deprecated
  gmake
}

package() {
  cd "$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install
}
