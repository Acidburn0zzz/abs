# Port: sysutils/libcdio-paranoia
pkgname=libcdio-paranoia
pkgver=10.2+0.94+1
pkgrel=1
pkgdesc="CD paranoia libraries from libcdio"
arch=('i386' 'amd64')
license=('GPL' 'LGPL')
url="http://www.gnu.org/software/libcdio/"
depends=('libcdio')
makedepends=('gmake')
source=(http://ftp.gnu.org/gnu/libcdio/${pkgname}-${pkgver}.tar.bz2{,.sig})
md5sums=('1e8f35be7fff7e0658063ae019ce3b6e'
         'SKIP')

prepare() {
  sed -i '' -e 's|/jp/man1|/ja/man1|' \
    ${pkgname}-${pkgver}/doc/ja/Makefile.in

}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  LDFLAGS+=" -L/usr/local/lib"
  MAKE=gmake \
  ./configure --prefix=/usr --enable-cpp-progs --disable-example-progs \
		--without-versioned-libs --disable-static
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake -j1 DESTDIR="${pkgdir}" install
  
  #sed -i "/LIBCDIO_SOURCE_PATH/s|.*|/* #undef LIBCDIO_SOURCE_PATH */|" \
  #  $pkgdir/usr/include/cdio/cdio_config.h
}
