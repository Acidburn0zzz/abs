# $Id: PKGBUILD 189300 2013-07-02 19:56:50Z lcarlier $
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-video-intel
pkgver=2.99.917+478+gdf72bc5
pkgrel=1
epoch=1
arch=('i386' 'amd64')
url="http://xorg.freedesktop.org/"
license=('custom')
install=$pkgname.install
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
depends=('intel-dri' 'libxvmc' 'xcb-util>=0.3.9' 'pixman')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=19' 'git')
replaces=('xf86-video-intel-uxa' 'xf86-video-intel-sna')
provides=('xf86-video-intel-uxa' 'xf86-video-intel-sna')
conflicts=('xorg-server<1.16' 'X-ABI-VIDEODRV_VERSION<19' 'X-ABI-VIDEODRV_VERSION>=20'
           'xf86-video-intel-sna' 'xf86-video-intel-uxa' 'xf86-video-i810' 'xf86-video-intel-legacy')
groups=('xorg-drivers' 'xorg')
patchdir=($pkgname)
source=('git://anongit.freedesktop.org/xorg/driver/xf86-video-intel#commit=df72bc5')
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --long | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${srcdir}/${pkgname}"

  ./configure --prefix=/usr \
    --enable-dri --enable-sna \
    --disable-glamor  \
    --disable-dri3 \
    xorg_cv_cc_flag__Wno_maybe_uninitialized=no

  make
}

package() {
  cd "${srcdir}/${pkgname}"

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
