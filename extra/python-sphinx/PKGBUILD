# Port: textproc/py-sphinx

pkgbase=python-sphinx
pkgname=('python-sphinx' 'python2-sphinx')
pkgver=1.4.8
pkgrel=1
arch=('amd64')
url='http://sphinx.pocoo.org/'
license=('GPL')
makedepends=(
  'python-setuptools' 'python2-setuptools'
  'python-docutils' 'python2-docutils'
  'python-jinja' 'python2-jinja'
  'python-pygments' 'python2-pygments'
  'python-six' 'python2-six'
  'python-imagesize' 'python2-imagesize'
  'findutils'
)
#checkdepends=(
#  'python-nose' 'python2-nose'
#  'texlive-latexextra'
#  'python-snowballstemmer' 'python2-snowballstemmer'
#  'python-babel' 'python2-babel'
#  'python-sphinx-alabaster-theme' 'python2-sphinx-alabaster-theme'
#  'python-sphinx_rtd_theme' 'python2-sphinx_rtd_theme'
#  'python2-mock'
#)
source=("https://files.pythonhosted.org/packages/source/S/Sphinx/Sphinx-${pkgver}.tar.gz")
sha256sums=('41af978f653ef862eb4bb3776dabd8ff13afed17e431907310fe990a3947707f')

prepare() {
  # souce duplication is required because makefile modify source code
  # setyp.py --build tricks don't works well
  cp -a Sphinx-$pkgver Sphinx-${pkgver}2
  # change python2 interpreter
  gfind Sphinx-${pkgver}2 -type f -exec \
    gsed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \;
  # change sphinx-binaries name in source code
  gfind Sphinx-${pkgver}2 -type f -name '*.py' -exec \
    gsed -ri 's,(sphinx-(:?build|apidoc|autogen|quickstart)),\12,' {} \;
}

build() {
  msg2 'Python 3 version'
  cd "$srcdir"/Sphinx-$pkgver
  gmake PYTHON=python3 build
  msg2 'Python 2 version'
  cd "$srcdir"/Sphinx-${pkgver}2
  gmake PYTHON=python2 build
}

#check() {
#  msg2 'Python 3 version'
#  cd "$srcdir"/Sphinx-$pkgver
##  make PYTHON=python3 test
#  rm -r tests
#
#  msg2 'Python 2 version'
#  cd "$srcdir"/Sphinx-${pkgver}2
##  make PYTHON=python2 test
#  rm -r tests
#}

package_python-sphinx() {
  pkgdesc='Python3 documentation generator'
  depends=(
    'python-jinja'
    'python-pygments'
    'python-docutils'
    'python-sphinx_rtd_theme'
    'python-sphinx-alabaster-theme'
    'python-babel'
    'python-snowballstemmer'
    'python-six'
    'python-imagesize')
  optdepends=('texlive-latexextra: for generation of PDF documentation')

  cd Sphinx-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -dm755 "$pkgdir"/usr/share/licenses/python-sphinx
  install -m644 "$srcdir"/Sphinx-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/python-sphinx/LICENSE
}

package_python2-sphinx() {
  pkgdesc='Python2 documentation generator'
  depends=(
  'python2-jinja'
  'python2-pygments'
  'python2-docutils'
  'python2-sphinx_rtd_theme'
  'python2-sphinx-alabaster-theme'
  'python2-babel' 
  'python2-snowballstemmer'
  'python2-six'
  'python2-imagesize')
  optdepends=('texlive-latexextra: for generation of PDF documentation')

  cd Sphinx-${pkgver}2
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -dm755 "$pkgdir"/usr/share/licenses/python2-sphinx
  install -m644 "$srcdir"/Sphinx-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/python2-sphinx/LICENSE
}
