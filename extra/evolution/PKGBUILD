# $Id: PKGBUILD 201348 2013-12-09 08:51:13Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=evolution
pkgver=3.12.5
pkgrel=3
pkgdesc="Manage your email, contacts and schedule"
arch=(i686 x86_64)
license=(GPL)
depends=(gnome-desktop evolution-data-server gtkhtml4 libcanberra libpst libytnef psmisc
         desktop-file-utils hicolor-icon-theme dconf libldap gnome-keyring)
makedepends=(intltool itstool docbook-xsl bogofilter)
optdepends=('bogofilter: possible junk filter plugin')
groups=('gnome-extra')
options=('!emptydirs')
install=evolution.install
url=http://www.gnome.org/projects/evolution/
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('524fb582db58fe22d79aa51e673e5fc85bcdd7d389e29c117664aed57cc4c123')

build() {
  cd $pkgname-$pkgver

  sed -i ''  -e '/^plugins_standard_always/s/audio-inline//' \
	-e 's|-Wl,--no-undefined||g' \
	-e 's|-Wmissing-include-dirs||g' \
	configure

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib \
      --enable-nss=yes \
      --with-openldap=yes \
      --enable-smime=yes \
      --with-krb5=/usr \
      --disable-schemas-compile \
      --disable-spamassassin \
      --disable-text-highlight \
      LIBS="-lintl"
  
  gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool

  gmake
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
}
