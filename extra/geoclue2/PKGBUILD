# $Id: PKGBUILD 197983 2013-10-30 12:04:51Z allan $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=geoclue2
pkgver=2.1.8
pkgrel=1
pkgdesc="Modular geoinformation service built on the D-Bus messaging system."
arch=(i686 x86_64)
url="http://geoclue.freedesktop.org/"
license=(LGPL)
depends=(libsoup json-glib)
makedepends=(intltool geoip glib2)
optdepends=('geoip: geoip-lookup server binary')
install=geoclue.install
source=(http://freedesktop.org/software/geoclue/releases/2.1/geoclue-$pkgver.tar.xz)
md5sums=('49096b9c3c2458b5e8e36b886983d9aa')

build() {
  cd geoclue-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-static \
    --disable-3g-source \
    --disable-wifi-source \
    --disable-modem-gps-source
  gmake
}

package() {
  cd geoclue-$pkgver
  gmake DESTDIR="$pkgdir" install
}
