# $Id: PKGBUILD 183194 2013-04-18 17:14:21Z andyrtr $
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Low Kian Seong <fastmail_low@speedymail.org>

pkgname=bogofilter
pkgver=1.2.4
pkgrel=10
pkgdesc="A fast Bayesian spam filtering tool"
arch=('i386' 'amd64')
license=('GPL3')
url="http://bogofilter.sourceforge.net"
depends=('db' 'perl' 'gsl')
backup=('etc/bogofilter/bogofilter.cf')
source=(http://sourceforge.net/projects/${pkgname}/files/${pkgname}-current/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.bz2
	patch-contrib__trainbogo.sh)
md5sums=('d0a5eebb3274b23ceabe766a6443a1c5'
         'c71501f2546b5d0a9d2aab48f8ead3d2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p0 -i ${srcdir}/patch-contrib__trainbogo.sh

  #gsl complaisn of undefined symbols to cblas when using --as-needed, so unset it to fix linking
  LDFLAGS="${LDFLAGS/--as-needed/}"

  ./configure --prefix=/usr \
              --sysconfdir=/etc/bogofilter  \
              --localstatedir=/var \
              --enable-transactions
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  mv "${pkgdir}/etc/bogofilter/bogofilter.cf.example" "${pkgdir}/etc/bogofilter/bogofilter.cf"

  install -dm755 "${pkgdir}/usr/share/${pkgname}/contrib"
  install -m644 contrib/* "${pkgdir}/usr/share/${pkgname}/contrib/"
}
