# $Id: PKGBUILD 184245 2013-05-04 21:03:39Z giovanni $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: dorphell <dorphell@gmx.net>

pkgbase=unrar
pkgname=('unrar' 'libunrar')
pkgver=4.2.4
pkgrel=5
epoch=1
arch=('i686' 'amd64')
url="http://www.rarlab.com/rar_add.htm"
license=('custom')
source=("http://www.rarlab.com/rar/unrarsrc-${pkgver}.tar.gz"
        'unrar-4.2.1-soname.patch')
md5sums=('8ea9d1b4139474b282d76e627a2de3e4'
         'b9ec767da9a36f5ac38034c974e77758')

build() {
  cd "${srcdir}/${pkgbase}"

  patch -Np0 -i "${srcdir}/unrar-4.2.1-soname.patch"

  cp -a ${srcdir}/unrar ${srcdir}/libunrar
  gmake -C "${srcdir}"/libunrar -f makefile.unix lib libversion=${pkgver} CXXFLAGS="-fPIC ${CXXFLAGS}"
  gmake -f makefile.unix CXXFLAGS="-fPIC ${CXXFLAGS}" STRIP="true"
}

package_unrar() {
  pkgdesc="The RAR uncompression program"
  depends=('gcc-libs')
  cd "${srcdir}/${pkgbase}"

  install -dm755 "${pkgdir}/usr/bin"
  install -m755 unrar "${pkgdir}/usr/bin/unrar"
  # install license
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_libunrar() {
  pkgdesc="Library and header file for applications that use libunrar"
  depends=('gcc-libs')
  cd "${srcdir}/libunrar"

  install -dm755 "${pkgdir}/usr/lib"
  install -m755 libunrar.so.${pkgver} "${pkgdir}/usr/lib/libunrar.so.${pkgver}"
  install -dm755 "${pkgdir}/usr/include/unrar"
  install -m644 dll.hpp "${pkgdir}/usr/include/unrar/dll.hpp"
  ln -s libunrar.so.${pkgver} "${pkgdir}/usr/lib/libunrar.so.4"
  ln -s libunrar.so.${pkgver} "${pkgdir}/usr/lib/libunrar.so"
  # install license
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
