# Port: archivers/unrar
pkgbase=unrar
pkgname=('unrar' 'libunrar')
pkgver=5.31
_pkgver=5.3.11
pkgrel=1
arch=('i386' 'amd64')
url="http://www.rarlab.com/rar_add.htm"
license=('custom')
patchdir=("$pkgbase")
source=("http://www.rarlab.com/rar/unrarsrc-${_pkgver}.tar.gz")
md5sums=('4a29cc1b47ced82db11e06090a95ed51')

prepare() {
  cd "${srcdir}/${pkgbase}"

  sed -i '' -e 's|CXX=g++|CXX=clang++|g' makefile

  LDFLAGS+=" -lpthread"
}

build() {
  cd "${srcdir}/${pkgbase}"
  cp -a ${srcdir}/unrar ${srcdir}/libunrar
  gmake -C "${srcdir}"/libunrar lib libversion=${pkgver} CXXFLAGS="-fPIC ${CXXFLAGS}"
  gmake CXXFLAGS="-fPIC ${CXXFLAGS}" STRIP="true"
}

package_unrar() {
  pkgdesc="The RAR uncompression program"
  cd "${srcdir}/${pkgbase}"

  install -dm755 "${pkgdir}/usr/bin"
  install -m755 unrar "${pkgdir}/usr/bin/unrar"
  # install license
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_libunrar() {
  pkgdesc="Library and header file for applications that use libunrar"
  cd "${srcdir}/libunrar"

  install -dm755 "${pkgdir}/usr/lib"
  install -m755 libunrar.so.${pkgver} "${pkgdir}/usr/lib/libunrar.so.${pkgver}"
  install -dm755 "${pkgdir}/usr/include/unrar"
  install -m644 dll.hpp "${pkgdir}/usr/include/unrar/dll.hpp"
  ln -s libunrar.so.${pkgver} "${pkgdir}/usr/lib/libunrar.so.5"
  ln -s libunrar.so.${pkgver} "${pkgdir}/usr/lib/libunrar.so"
  # install license
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
