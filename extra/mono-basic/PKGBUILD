# $Id: PKGBUILD 177067 2013-02-05 18:04:09Z eric $
# Maintainer: Daniel Isenmann <daniel@archlinux.org>

pkgname=mono-basic
pkgver=2.10
pkgrel=4
pkgdesc="Mono Visual Basic.NET compiler"
arch=('i686' 'amd64')
license=('GPL')
url="http://www.mono-project.com/"
depends=('mono>=2.10.1')
makedepends=('pkgconf')
#source=(http://ftp.novell.com/pub/mono/sources/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
port_patches=(
	patch-vbnc_vbnc_Makefile
	patch-vbnc_vbnc_setversion.sh
)
source=(http://download.mono-project.com/sources/${pkgname}/${pkgname}-${pkgver}.tar.bz2
	${port_patches[@]})
md5sums=('b459890e5447419ab1a5ea43d9b8afe8'
         '2adf6a58fb9e82137a6393b5ee5775f7'
         '1f8251f067baed642c15e658a523402a')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  for i in ${port_patches[@]}; do
    patch -p0 -i "${srcdir}/${i}"
  done
}
build() {
  # get rid of that .wapi errors; thanks to brice
  export MONO_SHARED_DIR="${srcdir}/weird"
  mkdir -p "${MONO_SHARED_DIR}"

  # build mono
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
}


