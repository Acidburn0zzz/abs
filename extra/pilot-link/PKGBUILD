# $Id: PKGBUILD 187867 2013-06-07 09:25:06Z tomegun $
# Maintainer: dale <dale@archlinux.org>

# todo: add perl support, DESTDIR has no affect on perl
# and it seems that moving the files manually doesn't work

pkgname=pilot-link
pkgver=0.12.5
pkgrel=7
pkgdesc="A suite of tools for connecting to PalmOS handheld devices"
arch=('i386' 'amd64')
license=('GPL' 'LGPL')
depends=('readline>=6.0' 'libpng>=1.5.0' 'popt')
options=('!libtool')
url="http://www.pilot-link.org/"
source=(http://downloads.pilot-link.org/${pkgname}-${pkgver}.7z{,.asc}
        pilot-link-png14.patch)
md5sums=('cd409c09b93654f0fc45015a7fc17b2c'
         'SKIP'
         'a21a2eeb01544a8a261a75e64a3120c3')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -Np1 -i "${srcdir}/pilot-link-png14.patch"

  sed -i '' -e 's|png_voidp_NULL|NULL|g' \
	-e 's|png_error_ptr_NULL|NULL|g' \
	src/pilot-read-notepad.c \
	src/pilot-read-palmpix.c \
	src/pilot-read-screenshot.c \
	src/pilot-read-veo.c

  sed -i '' -e 's|Libs: |Libs: -pthread |' pilot-link.pc.in pilot-link-pp.pc.in
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-conduits --enable-libusb \
    --with-libiconv --with-libpng --disable-static LIBS="-lusb -pthread"
  gmake
}

package() {
  cd ${pkgname}-${pkgver}
  gmake DESTDIR="${pkgdir}" install
}
