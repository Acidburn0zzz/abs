# $Id: PKGBUILD 191908 2013-08-01 06:53:41Z bpiotrowski $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=sox
pkgver=14.4.2
pkgrel=2
pkgdesc="The Swiss Army knife of sound processing tools"
arch=('i386' 'amd64')
url="http://sox.sourceforge.net/"
license=('GPL' 'LGPL')
depends=('libltdl' 'file' 'libsndfile' 'libpng' 'lame' 'opencore-amr' 'gsm')
depends=('libltdl'        'libsndfile' 'libpng' 'lame' 'opencore-amr' 'gsm')
makedepends=('libao' 'libmad' 'libid3tag' 'wavpack' 'libpulse')
#checkdepends=('time')
optdepends=('libao: for ao plugin'
            'libmad: for mp3 plugin'
            'libid3tag: for mp3 plugin'
            'wavpack: for wavpack plugin'
            'libpulse: for pulse plugin')
options=('!libtool')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('dc9668256b9d81ef25d672f14f12ec026b0b4087')

prepare() {
  cd ${pkgname}-${pkgver}
  gsed -i 's|man1/sox.1 soxeffect.7|man1/sox.1.gz soxeffect.7.gz|' Makefile.in
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
    --with-dyn-default --with-distro="Arch BSD"
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make bindir=. installcheck
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
