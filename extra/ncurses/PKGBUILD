# $Id: PKGBUILD 150103 2012-02-12 13:47:59Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=ncurses
pkgver=5.9
pkgrel=6
pkgdesc="System V Release 4.0 curses emulation library"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/ncurses/"
license=('MIT')
source=(ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig}
        patch-c++-clang
        configure-mk_shared_lib.diff
)
md5sums=('8cb9c412e5f2d96bc6f459aa8c6282a1'
         'SKIP'
         'e22107a919e707e6ca82163109c12031'
         'dd9f9e281798f066851b75b38e0e5f3a')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 -i "${srcdir}/patch-c++-clang"
  patch -p0 -i "${srcdir}/configure-mk_shared_lib.diff"
}
build() {
  cd ${srcdir}/
  mkdir ncurses{,w}-build

  #cd ${srcdir}/${pkgname}-${pkgver}
  #sed -i '' -e 's,\$(INSTALL_PROG) ncurses-config,@INSTALL_SCRIPT@ ncurses-config,' misc/Makefile.in


  cd ${srcdir}/ncursesw-build
  ../${pkgname}-${pkgver}/configure --prefix=/usr/local --mandir=/usr/local/share/man \
     --with-shared --with-normal --without-debug --without-ada \
     --enable-widec --enable-pc-files
  # add --enable-ext-colors and --enable-ext-mouse with next soname bump
  gmake

  # libraries for external binary support 
  cd ${srcdir}/ncurses-build
  [ $CARCH = "x86_64" ] && CONFIGFLAG="--with-chtype=long"
  ../${pkgname}-${pkgver}/configure --prefix=/usr/local \
    --with-shared --with-normal --without-debug --without-ada $CONFIGFLAG
  gmake
}

# use AS_NEEDED(-ltinfo) and AS_NEEDED(-ltinfow) (ports does it too, seems to fix some other package builds)
package() {
  cd ${srcdir}/ncursesw-build
  gmake DESTDIR=${pkgdir} install

  # Fool packages looking to link to non-wide-character ncurses libraries
  for lib in ncurses form panel menu; do
    echo "INPUT(-l${lib}w AS_NEEDED(-ltinfow))" >${pkgdir}/usr/local/lib/lib${lib}.so
    ln -s lib${lib}w.a ${pkgdir}/usr/local/lib/lib${lib}.a
  done
  ln -s libncurses++w.a ${pkgdir}/usr/local/lib/libncurses++.a

  install -dm755 "${pkgdir}/usr/lib/pkgconfig"
  for lib in ncurses ncurses++ form panel menu; do
    ln -s ${lib}w.pc ${pkgdir}/usr/lib/pkgconfig/${lib}.pc
  done

  # Some packages look for -lcurses during build
  echo "INPUT(-lncursesw)" >${pkgdir}/usr/local/lib/libcursesw.so
  ln -s libncurses.so ${pkgdir}/usr/local/lib/libcurses.so
  ln -s libncursesw.a ${pkgdir}/usr/local/lib/libcursesw.a
  ln -s libncurses.a ${pkgdir}/usr/local/lib/libcurses.a

  # non-widec compatibility library
  cd ${srcdir}/ncurses-build
  install -dm755 ${pkgdir}/usr/local/lib
  for lib in ncurses form panel menu; do
    install -m755 lib/lib${lib}.so.${pkgver} ${pkgdir}/usr/local/lib/lib${lib}.so.${pkgver}
    ln -s lib${lib}.so.${pkgver} ${pkgdir}/usr/local/lib/lib${lib}.so.5
  done

  # install license, rip it from the readme
  cd ${srcdir}/${pkgname}-${pkgver}
  install -dm755 ${pkgdir}/usr/share/licenses/$pkgname
  grep -B 100 '$Id' README > ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
