# $Id: PKGBUILD 120438 2014-10-09 11:11:45Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Fabian Schoelzel <myfirstname.mylastname@googlemail.com>
# Contributor: funkyou <spamopfer@nickname.berlin.de>
# Contributor: tardo <tardo@nagi-fanboi.net>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgbase=mlt
pkgname=('mlt' 'mlt-python-bindings')
pkgver=0.9.8
pkgrel=1
pkgdesc="An open source multimedia framework"
arch=('i386' 'amd64')
url="http://www.mltframework.org"
license=('GPL')
makedepends=('sdl_image' 'libsamplerate' 'libdv' 'qt4' 'sox' 'libxml2' 'gtk2' 'ffmpeg'
	     'swig' 'python2' "jack" "ladspa" 'gnu-libiconv' 'gcc')
source=("https://github.com/mltframework/mlt/archive/v$pkgver.tar.gz")
md5sums=('57397f2f0fa0545c248c5e9b59c140d5')

prepare() {
  cd "$srcdir/mlt-$pkgver"
  gsed -i 's|freetype/|freetype2/|' src/modules/gtk2/producer_pango.c
}

build() {
  # mlt
  cd "$srcdir/mlt-$pkgver"

  msg "SSE2=$SSE2"
  export LDFLAGS+=" -L/usr/local/lib"
  ./configure --prefix=/usr \
	--avformat-swscale \
	--enable-gpl --enable-gpl3 \
	--target-os=FreeBSD
  gmake

  # mlt python bindings
  cd "$srcdir/mlt-$pkgver/src/swig/python"
  gsed -i 's_path=`which python_path=`which python2_' build
  gsed -i 's_`python -c_`python2 -c_' build
  gsed -i 's#python-config#python2-config#' build
  ./build
}

package_mlt() {
  depends=('sdl_image' 'libsamplerate' 'libdv' 'sox' 'libxml2' 'ffmpeg')
  optdepends=('jack' 'libexif' 'qt4' 'gtk2' "ladspa")
  conflicts=('mlt++<=0.3.8')

  cd "$srcdir/mlt-$pkgver"
  gmake DESTDIR="$pkgdir" install
}

package_mlt-python-bindings() {
  depends=('python2' 'mlt')

  cd "$srcdir/mlt-$pkgver/src/swig/python"
  mkdir -p "$pkgdir/usr/lib/python2.7/"
  install -m755 mlt.py "$pkgdir/usr/lib/python2.7/"
  install -m755 _mlt.so "$pkgdir/usr/lib/python2.7/"
  install -m755 mlt_wrap.o "$pkgdir/usr/lib/python2.7/"
}
