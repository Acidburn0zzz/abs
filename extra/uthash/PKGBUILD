# Port: devel/uthash
pkgname=uthash
pkgver=2.0.1
pkgrel=1
pkgdesc="C preprocessor implementations of a hash table and a linked list"
arch=('any')
url="https://troydhanson.github.io/uthash/index.htm"
license=('BSD')
source=("https://github.com/troydhanson/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('613b95fcc368b7d015ad2d0802313277012f50c4ac290c3dfc142d42ebea3337')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
 
  # create directory for header files 
  install -dm755 "${pkgdir}"/usr/include/

  # install header files in /usr/include
  for h in *.h; do
      install -m 644 ${h} "${pkgdir}"/usr/include/
  done

  # install licence file
  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 ../LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

