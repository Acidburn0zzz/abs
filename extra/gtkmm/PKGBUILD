# $Id: PKGBUILD 189082 2013-06-28 13:55:25Z lcarlier $
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gtkmm
pkgname=('gtkmm')
pkgver=2.24.4
pkgrel=15
arch=('i386' 'amd64')
makedepends=('gtk2' 'pangomm' 'atkmm' 'glibmm-docs')
license=('LGPL')
options=('libtoolfix' '!emptydirs')
url="http://www.gtkmm.org/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgbase}/2.24/${pkgbase}-${pkgver}.tar.xz)
sha256sums=('443a2ff3fcb42a915609f1779000390c640a6d7fd19ad8816e6161053696f5ee')

prepare() {
  sed -i '' -e '/^SUBDIRS =/s/tests//' \
    "${srcdir}/${pkgbase}-${pkgver}"/Makefile.in
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  export MAKE=gmake
  export CXXFLAGS+=" -std=c++11"
  ./configure --prefix=/usr --disable-documentation 
  gmake -j1
}

package_gtkmm() {
  pkgdesc="C++ bindings for gtk2"
  depends=('gtk2' 'pangomm' 'atkmm')
  cd "${srcdir}/${pkgbase}-${pkgver}"

  gsed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile

  export PATH="${srcdir}/bin:${PATH}"
  gmake -j1 DESTDIR="${pkgdir}" install
}
