# $Id: PKGBUILD 183321 2013-04-21 22:06:40Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgbase=pyatspi
pkgname=(python-atspi python2-atspi)
pkgver=2.18.0
pkgrel=1
arch=('i386' 'amd64')
url="http://www.linuxfoundation.org/en/AT-SPI_on_D-Bus"
license=('GPL2')
makedepends=(python-gobject python2-gobject at-spi2-core)
source=(http://download.gnome.org/sources/${pkgbase}/${pkgver:0:4}/${pkgbase}-${pkgver}.tar.xz)
sha256sums=('f86f03ad05d0502cd60fe8b98f746f74ce3bc208e9f3798f6a2130b3c5f4ab46')

build() {
  mkdir python2 python3

  cd $pkgbase-$pkgver
  gsed '/AC_PATH_XTRA/d' -i configure.ac
  autoreconf -fi

  cd ../python2
  ../$pkgbase-$pkgver/configure --prefix=/usr --with-python=/usr/bin/python2
  gmake

  cd ../python3
  ../$pkgbase-$pkgver/configure --prefix=/usr --with-python=/usr/bin/python
  gmake
}

package_python-atspi() {
  pkgdesc="Python 3 bindings for at-spi"
  depends=(python-gobject at-spi2-core)

  cd python3
  gmake DESTDIR="$pkgdir" install
}

package_python2-atspi() {
  pkgdesc="Python 2 bindings for at-spi"
  depends=(python2-gobject at-spi2-core)
  provides=("pyatspi=$pkgver")
  replaces=(pyatspi)
  conflicts=(pyatspi)

  cd python2
  gmake DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
