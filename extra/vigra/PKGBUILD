# $Id: PKGBUILD 184444 2013-05-06 19:40:04Z foutrelis $
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgbase=vigra
pkgname=('vigra' 'vigra-doc')
pkgver=1.10.0
pkgrel=5
arch=('i386' 'amd64')
url="http://hci.iwr.uni-heidelberg.de/vigra/"
license=('custom:MIT')
makedepends=(# runtime deps
             'libpng' 'libtiff' 'openexr' 'gcc-libs' 'bash' 'hdf5' 'fftw'
	     # additional makedeps
             'cmake' 'python2-nose' 'doxygen' 'python2-sphinx' 'boost' 'python2-numpy')
options=('!libtool')
source=("http://github.com/ukoethe/vigra/releases/download/Version-${pkgver//./-}/${pkgname}-${pkgver}-src-with-docu.tar.gz"
        patch-include__vigra__mathutil.hxx)
md5sums=('b6155afe1ea967917d2be16d98a85404'
         'fb3a0b97c7cf241303312770a3612df5')

prepare() {
  cd "${srcdir}"/${pkgbase}-${pkgver}
  patch -p0 -i "${srcdir}/patch-include__vigra__mathutil.hxx"
}

build() {
  cd "${srcdir}"/${pkgbase}-${pkgver}
  export CXXFLAGS="${CXXFLAGS} -std=c++11 -stdlib=libc++"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
      -DPYTHON_EXECUTABLE=/usr/bin/python2 \
      -DWITH_VIGRANUMPY=0 \
      -DDOCINSTALL=share/doc
  make
}

check() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make -k check || true # i386 fails
}

package_vigra() {
  pkgdesc="Computer vision library"
  depends=('libpng>=1.5.7' 'libtiff>=4.0.0'  'bash' 'hdf5>=1.8.7' 'fftw')
  optdepends=('boost-libs: for python bindings')

  cd "${srcdir}"/${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  # license
  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  
  # remove doc
  rm -rf "${pkgdir}"/usr/share/doc
  
  #fix shebang for python2
  gsed -i 's|python$|python2|' "$pkgdir"/usr/bin/vigra-config
}

package_vigra-doc() {
  pkgdesc="Computer vision library - documentation and examples"
  #depends=('vigra')
  #arch=('any')

  cd "${srcdir}"/${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  # cleanup
  rm -rf "${pkgdir}"/usr/{bin,include,lib}

  # license
  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -D -m644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
md5sums=('85e2968e4ee5f9541b5dd8b3fb9cc433'
         'fb3a0b97c7cf241303312770a3612df5')
