pkgbase=glibmm
pkgname=('glibmm' 'glibmm-docs')
pkgver=2.46.1
pkgrel=2
arch=('i386' 'amd64')
license=('LGPL')
makedepends=('glib2' 'libsigc++' 'pkgconf')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
options=('libtoolfix')
url="http://www.gtkmm.org/"
sha256sums=('9647e596c1081d2ea202bd3da2824ec2ea359498fa86eb59a55b1b307dd8c4aa')

build() {
  cd "$pkgname-$pkgver"

  export MAKE=gmake
  export PATH=${srcdir}/bin:$PATH
  ./configure --prefix=/usr
  gmake
}

package_glibmm() {
  pkgdesc="Glib-- (glibmm) is a C++ interface for glib"
  depends=('glib2' 'libsigc++2.0')
  cd "$pkgbase-$pkgver"
  gsed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  gmake DESTDIR="$pkgdir" install
}

package_glibmm-docs() {
  pkgdesc="Developer documentation for glibmm"
  cd "$pkgbase-$pkgver/docs"
  gmake DESTDIR="$pkgdir" install
}
