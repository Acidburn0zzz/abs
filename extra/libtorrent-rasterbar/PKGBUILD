# $Id: PKGBUILD 201262 2013-12-07 10:35:01Z bpiotrowski $
# Maintainer : Anthony Donnelly <Amzo@PacBSD.org>

pkgname=libtorrent-rasterbar
pkgver=1.0.6
pkgrel=1
epoch=1
pkgdesc="A C++ library that aims to be a good alternative to all the other bittorrent implementations around"
url="http://www.rasterbar.com/products/libtorrent/"
arch=('amd64')
license=('BSD')
depends=('boost-libs' 'geoip' 'python2')
makedepends=('boost')
options=('!emptydirs')
source=(https://github.com/arvidn/libtorrent/releases/download/libtorrent-${pkgver//./_}/$pkgname-$pkgver.tar.gz)
sha1sums=('160e7cde6aafdb3dff1abf5ae384676367d04f2b')

build() {
  cd $pkgname-$pkgver

  autoreconf -vfi 
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
	--enable-python-binding \
	--with-libgeoip=system \
	--with-boost-system=boost_system \
	--with-iconv --with-libiconv-prefix=/usr/local
  gmake
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
