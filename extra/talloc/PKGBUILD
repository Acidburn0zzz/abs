# $Id: PKGBUILD 172901 2012-12-06 14:08:40Z stephane $
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgname=talloc
pkgver=2.1.9
pkgrel=1
pkgdesc="A hierarchical pool based memory allocator with destructors"
arch=('i386' 'amd64')
license=('GPL3')
url="http://talloc.samba.org/"
depends=('libc')
makedepends=('python2' 'docbook-xsl')
optdepends=('python2: for python bindings')
source=(http://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.{gz,asc}
        'fix_test_path.patch')
validpgpkeys=(9147A339719518EE9011BCB54793916113084025) # samba-bugs@samba.org
sha256sums=('f0aad4cb88a3322207c82136ddc07bed48a37c2c21f82962d6c5ccb422711062'
            'SKIP'
            '4d2e9ed069e2c395e46ed520cb27c6ff604a005bf1f3110400c2bd34b1bb222e')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < '../fix_test_path.patch'
  gsed -i -e 's#python#python2#g' buildtools/bin/waf
}

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   
   ./configure --prefix=/usr \
     --sysconfdir=/etc/samba \
     --localstatedir=/var \
     --bundled-libraries=NONE \
     --builtin-libraries=replace \
     --enable-talloc-compat1
   make
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   make DESTDIR="${pkgdir}" install
}
