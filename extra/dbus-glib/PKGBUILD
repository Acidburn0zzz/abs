# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=dbus-glib
pkgver=0.102
pkgrel=2
pkgdesc="GLib bindings for DBUS"
arch=(i686 x86_64)
license=('GPL')
url="http://www.freedesktop.org/wiki/Software/DBusBindings"
depends=('dbus-core' 'glib2>=2.34.3')
makedepends=('pkgconf')
options=('!emptydirs')
source=(http://dbus.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('f76b8558fd575d0106c3a556eaa49184')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  rm -rf "${srcdir}/test"
  mkdir "${srcdir}/test"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --libexecdir=/usr/lib \
              --with-test-socket-dir="${srcdir}/test" \
              --disable-gtk-doc \
              --with-html-dir=/usr/share/doc
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

}
