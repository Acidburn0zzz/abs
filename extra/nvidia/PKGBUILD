# Contributor: Wolfgang Bumiller <blub at speed dot at>
# Contributor: Kim Carlb√§cker <kim dot carlbacker at gmail dot com>

pkgbase=nvidia
pkgname=('nvidia' 'nvidia-utils' 'linux-nvidia-utils')
pkgver=331.49
pkgrel=1
arch=('i686' 'x86_64')
license=('NVIDIA')
makedepends=('freebsd-source')
url="http://www.nvidia.com/"
[[ "$CARCH" = "x86_64" ]] && _arch=x86_64
[[ "$CARCH" = "i686"   ]] && _arch=x86
source=("ftp://download.nvidia.com/XFree86/FreeBSD-${_arch}/${pkgver}/NVIDIA-FreeBSD-${_arch}-${pkgver}.tar.gz"
        nvidia-settings.in freebsd10.patch
       )
md5sums=('3f8c55deb4d746089bb4ca258e8a4736'
         '86da362e820aafb226affa8dec13db3d'
         'b0039076d3dd1e05ea7b9e600e95fb46')

build() {
  cd "${srcdir}/NVIDIA-FreeBSD-${_arch}-${pkgver}/"

  unset CFLAGS LDFLAGS CPPFLAGS CXXFLAGS
  export CFLAGS="-O"
  patch -p0 -i ${srcdir}/freebsd10.patch

  sed -i '' -E '/#include "machine\/\.\.\/linux(32)?\/linux.h"/{ x ; s/.*/#include "machine\/..\/..\/compat\/linux\/linux_ioctl.h"/ ; H ; x ; }' \
		src/nvidia_linux.c

  sed -i '' -e '/#include <vm\/vm_object.h>/{ x ; s/.*/#include <sys\/rwlock.h>/ ; G ; }' \
			src/nv-freebsd.h

  sed -i '' -E 's/(VM_OBJECT_)(UN)?(LOCK)/\1W\2\3/' \
		src/nvidia_subr.c

  sed -i '' -e '/kmem_/s/kernel_map/kernel_arena/' \
		src/nvidia_subr.c

  sed -i '' -e 's/u_long cmd;/& cap_rights_t rights;/ ; s/CAP_IOCTL/cap_rights_init(\&rights, &)/' \
			src/nvidia_linux.c

  sed -i '' -e 's/virtual_address, size,/& 0,/' \
		src/nvidia_subr.c

  
  make LINUX=YES
}

package_nvidia() {
 pkgdesc="NVidia graphics card binary drivers"
 install=nvidia.install
  cd "${srcdir}/NVIDIA-FreeBSD-${_arch}-${pkgver}/"

# Kernel module
  install -dm755 "${pkgdir}/usr/modules"
  install -m555 src/nvidia.ko "${pkgdir}/usr/modules/nvidia.ko"

# X11 driver
  install -dm755 "${pkgdir}/usr/lib/xorg/modules/drivers"
  install -m755 "obj/nvidia_drv.so" "${pkgdir}/usr/lib/xorg/modules/drivers/nvidia_drv.so"

}

package_nvidia-utils() {
 pkgdesc="NVidia binary OpenGL libraries"
 depends=("nvidia=${pkgver}" 'gtk2' 'pango')
 conflicts=('libgl')
 provides=('libgl')
  cd "${srcdir}/NVIDIA-FreeBSD-${_arch}-${pkgver}/"

# OpenGL Libraries
  install -dm755 "${pkgdir}/usr/lib"
  for i in libGL.so.1 \
           libnvidia-cfg.so.1 \
           libnvidia-glcore.so.1 \
           libnvidia-tls.so.1
  do
    install -C -o root -g wheel -m 555 "obj/$i" "${pkgdir}/usr/lib/$i"
    ln -sv "$i" "${pkgdir}/usr/lib/${i%%.1}"
  done

# vdpau
  install -dm755 "${pkgdir}/usr/lib/vdpau"
  # libvdpau_trace.so.1 is provided by libvdpau here as well
  for i in libvdpau_nvidia.so.1
  do
    install -C -o root -g wheel -m 555 "obj/$i" "${pkgdir}/usr/lib/vdpau/$i"
    ln -sv "$i" "${pkgdir}/usr/lib/vdpau/${i%%.1}"
  done

# tools
  install -dm755 "${pkgdir}/usr/bin"
  install -m755 "obj/nvidia-bug-report.sh" "${pkgdir}/usr/bin/"
  install -m755 "obj/nvidia-xconfig"       "${pkgdir}/usr/bin/"

# X11 GLX module
  install -dm755 "${pkgdir}/usr/lib/xorg/modules/extensions"
  install -C -o root -g wheel -m 555 "obj/libglx.so.1" "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so.1"
  ln -s 'libglx.so.1' "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"

# work around nvidia relying on ports-hacks
  install -dm755 "${pkgdir}/usr/lib/nvidia/tools"
  install -m755 "obj/nvidia-settings"          "${pkgdir}/usr/lib/nvidia/tools/nvidia-settings"
  install -m755 "${srcdir}/nvidia-settings.in" "${pkgdir}/usr/bin/nvidia-settings"
  install -dm755 "${pkgdir}/usr/lib/nvidia/tools/lib"
  for i in libgtk-x11-2.0.so libgdk-x11-2.0.so \
           libpangox-1.0.so libpango-1.0.so \
           libgobject-2.0.so libgmodule-2.0.so \
           libglib-2.0.so
  do
    ln -s "/usr/lib/$i" "${pkgdir}/usr/lib/nvidia/tools/lib/${i}.0"
  done
}

package_linux-nvidia-utils() {
 pkgdesc="Linux - NVidia binary OpenGL libraries"
 depends=("nvidia-utils=${pkgver}")
 conflicts=('linux-mesa-libgl')
 provides=('linux-libgl')

 cd "${srcdir}/NVIDIA-FreeBSD-${_arch}-${pkgver}/"

	install -dm755 "${pkgdir}/compat/linux/usr/lib"

	for p in libEGL.so.${pkgver} libGLESv1_CM.so.${pkgver} libcuda.so.${pkgver} libnvidia-eglcore.so.${pkgver}  libnvidia-glsi.so.${pkgver} \
		libvdpau.so.${pkgver} libvdpau_trace.so.${pkgver} libGL.so.${pkgver} libGLESv2.so.${pkgver} libnvidia-cfg.so.${pkgver} libnvidia-glcore.so.${pkgver} \
		libnvidia-tls.so.${pkgver} libvdpau_nvidia.so.${pkgver}; do
			install -m 555 -o root -g wheel obj/linux/${p} ${pkgdir}/compat/linux/usr/lib/
			ln -s ${pkgdir}/compat/linux/usr/lib/${p} ${pkgdir}/compat/linux/usr/lib/${p%.*.*}.so.1
	done
}

