# Maintainer: Kim "BKC" Carlb√§cker <kim.carlbacker@gmail.com>

pkgbase='python-sysctl'
pkgname=('python2-sysctl' 'python-sysctl')
_pipname='sysctl'
pkgver=0.1b9
pkgrel=2
pkgdesc="Python wrapper for the sysctl system functions"
arch=('amd64')
url="https://github.com/williambr/py-sysctl"
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=(https://pypi.python.org/packages/source/${_pipname::1}/${_pipname}/${_pipname}-${pkgver}.tar.gz)
sha1sums=('6f79abdd03cc1bdcd62b1303cccafffcb84d7f7f')

prepare() {
	cd "${srcdir}"

	cp -r "${_pipname}-${pkgver}" "python-${_pipname}-${pkgver}"
	cp -r "${_pipname}-${pkgver}" "python2-${_pipname}-${pkgver}"

}

build() {
	cd "${srcdir}/python-${_pipname}-${pkgver}"
	python setup.py build
	
	cd "${srcdir}/python2-${_pipname}-${pkgver}"
	python2 setup.py build
}

package_python-sysctl() {
	pkgdesc="Python wrapper for the sysctl system functions"
	depends=('python')
	cd "${srcdir}/python-${_pipname}-${pkgver}"

	python setup.py install --root="${pkgdir}"
}

package_python2-sysctl() {
	pkgdesc="Python2 wrapper for the sysctl system functions"
	depends=('python2')
	cd "${srcdir}/python2-${_pipname}-${pkgver}"
	python2 setup.py install --root="${pkgdir}"
}
