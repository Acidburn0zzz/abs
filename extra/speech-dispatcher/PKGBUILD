# Port: accessibility/speech-dispatcher

pkgname=speech-dispatcher
pkgver=0.8.3
pkgrel=1
arch=('i386' 'amd64')
pkgdesc="High-level device independent layer for speech synthesis interface"
url="http://www.freebsoft.org/speechd"
license=('GPL2' 'FDL')
depends=('glib2' 'libltdl' 'python-xdg' 'dotconf' 'libpulse')
makedepends=('intltool' 'espeak')
optdepends=('festival: Speech output using Festival'
        'espeak: Speech output using ESpeak'
        'pulseaudio: PulseAudio support')
provides=("speechd=${pkgver}")
options=('libtoolfix') 
backup=(etc/speech-dispatcher/clients/emacs.conf
	etc/speech-dispatcher/modules/ibmtts.conf
	etc/speech-dispatcher/modules/espeak-generic.conf
	etc/speech-dispatcher/modules/espeak.conf
	etc/speech-dispatcher/modules/swift-generic.conf
	etc/speech-dispatcher/modules/festival.conf
	etc/speech-dispatcher/modules/cicero.conf
	etc/speech-dispatcher/modules/espeak-mbrola-generic.conf
	etc/speech-dispatcher/modules/dtk-generic.conf
	etc/speech-dispatcher/modules/llia_phon-generic.conf
	etc/speech-dispatcher/modules/ivona.conf
	etc/speech-dispatcher/modules/epos-generic.conf
	etc/speech-dispatcher/modules/flite.conf
	etc/speech-dispatcher/modules/pico-generic.conf
	etc/speech-dispatcher/speechd.conf)
install="${pkgname}.install"
source=("http://www.freebsoft.org/pub/projects/speechd/$pkgname-$pkgver.tar.gz")
md5sums=('d17b041fa3c87cb1b73ac6e95b80d276')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Disabled due to bug: https://bugs.archlinux.org/task/35700
  gsed -i "s/cicero //g" configure.ac
  gsed -i "s/sd_cicero//g" src/modules/Makefile.am

  autoreconf -i
  ./configure --prefix=/usr \
    --enable-shared \
    --sysconfdir=/etc \
    --without-flite

  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  install -d "${pkgdir}/var/log/speech-dispatcher"
}
