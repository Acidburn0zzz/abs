# $Id: PKGBUILD 207712 2014-03-13 18:44:20Z schiv $
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Tobias Kieslich tobias [funnychar] archlinux org
# Contributor: see .contrib

pkgname=aubio
pkgver=0.4.2
pkgrel=4
pkgdesc="A tool for extracting annotations from audio signals"
arch=('i386' 'amd64')
url="http://aubio.org/"
license=('GPL')
depends=('fftw' 'jack' 'ffmpeg')
makedepends=('python2-numpy' 'python2-setuptools')
optdepends=('python2-numpy: Python 2.x interface')
source=("http://aubio.org/pub/$pkgname-$pkgver.tar.bz2")
md5sums=('c0f6d8355e92669722501c3a762ba548')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf configure --prefix=/usr --enable-fftw3f
  python2 waf build $MAKEFLAGS
  cd python && LD_PRELOAD=/usr/lib/gcc/libgcc_s.so.1 python2 setup.py build # python interface
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

# Tries to use the libgcc_s.so.1 provide by world and ignores the one 
  # provides by gcc libs.
  #
  # /lib/libgcc_s.so.1: version GCC_4.6.0 required by /usr/lib/libgfortran.so.3 not found

  LD_PRELOAD=/usr/lib/gcc/libgcc_s.so.1 python2 waf --destdir="$pkgdir" install

  # python interface
  cd python || return 1

  LD_PRELOAD=/usr/lib/gcc/libgcc_s.so.1 python2 setup.py install --root="$pkgdir" --optimize=1

  # python2 compat
  gsed -i 's:bin/env python:bin/env python2:' \
      "$pkgdir/usr/lib/python2.7/site-packages/aubio/__init__.py"
}
