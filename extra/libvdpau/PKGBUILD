# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=libvdpau
pkgver=0.8
pkgrel=2
pkgdesc="Nvidia VDPAU library"
arch=(i686 x86_64)
url='http://cgit.freedesktop.org/~aplattner/libvdpau'
conflicts=('nvidia-utils<190.42-2')
license=('custom')
source=(http://people.freedesktop.org/~aplattner/vdpau/${pkgname}-${pkgver}.tar.gz)
depends=('libxext')
makedepends=('dri2proto')
sha1sums=('7eefd24a49e96aa87c576c8ac46d04cc5a368a4f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i '' -e '/^export /d' doc/Makefile.am

  mv src/vdpau_wrapper.cfg src/vdpau_wrapper.cfg.sample
# ports does this on Makefile.am instead of .in and reruns autotools
# but this is nicer - autotools as makedepnds is nasty
  sed -i '' -e 's/vdpau_wrapper.cfg/&.sample/' src/Makefile.in

  ./configure --prefix=/usr --sysconfdir=/etc --disable-documentation
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
