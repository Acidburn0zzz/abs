# $Id: PKGBUILD 189864 2013-07-10 07:26:55Z bpiotrowski $
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgname=libgphoto2
pkgver=2.5.7
pkgrel=1
pkgdesc="The core library of gphoto2, designed to allow access to digital camera by external programs."
arch=(i686 amd64)
url="http://www.gphoto.org"
license=(LGPL)
depends=('libexif' 'libjpeg' 'gd' 'libltdl')
install=libgphoto2.install
options=('libtoolfix')
source=(http://downloads.sourceforge.net/gphoto/${pkgname}-${pkgver}.tar.gz)
md5sums=('6db5ee9b147b48b7b193ee4de348152b')

prepare(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  for i in configure libgphoto2_port/configure; do
    sed -i '' -e \
	's|{libdir}/pkgconfig|{prefix}/libdata/pkgconfig| ; /FLAGS/s|-g|| ; /grep -i linux/s|; fi ; then|; else false; fi ; then|' ${i}
  done

  sed -i '' -e \
	's|-lusb-1.0|-lusb|' \
	libgphoto2_port/configure
  sed -i '' -e \
	'/^SUBDIRS/s|linux-hotplug||' \
	packaging/Makefile.in

}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
	--prefix=/usr \
	--disable-rpath \
	ac_cv_path_DOT=false \
	ac_cv_path_DOXYGEN=false \
	CXXFLAGS="${CXXFLAGS} -std=gnu89" \
	CFLAGS="${CFLAGS} -std=gnu89" \
	LTDLINCL="-I/usr/include" \
	LIBLTDL="-L/usr/lib -lltdl" \

  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  # Remove unused udev helper
  rm -rf "${pkgdir}/usr/lib/udev"

  # Remove recursive symlink
  rm -f "${pkgdir}/usr/include/gphoto2/gphoto2"
}
