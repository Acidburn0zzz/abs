# Maintainer:
# Contributor: damir <damir@archlinux.org>

pkgname=libcdio
pkgver=0.92
pkgrel=9
pkgdesc="GNU Compact Disc Input and Control Library"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/libcdio/"
depends=('libcddb' 'ncurses' 'popt' 'cdparanoia' 'gcc-libs')
makedepends=('gmake')
install=libcdio.install
source=(http://ftp.gnu.org/gnu/libcdio/${pkgname}-${pkgver}.tar.gz{,.sig}
        libcdio-0.83-linking.patch)
md5sums=('86f3f2869c1d34e4c6e52db77992b918'
         'SKIP'
         '5a7f50209c03d5919d5b932f07871af7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ../libcdio-0.83-linking.patch
  sed -i '' -e 's,freebsd5\.\*,freebsd*,' configure

  MAKE=gmake \
  ./configure --prefix=/usr \
              --disable-vcd-info --enable-cpp-progs --disable-static
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake -j1 DESTDIR="${pkgdir}" install
  
  sed -i '' -e "/LIBCDIO_SOURCE_PATH/s|.*|/* #undef LIBCDIO_SOURCE_PATH */|" \
    $pkgdir/usr/include/cdio/cdio_config.h
}
