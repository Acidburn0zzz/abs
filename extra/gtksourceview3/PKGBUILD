# $Id: PKGBUILD 204834 2014-01-28 09:00:12Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gtksourceview3
_pkgbasename=gtksourceview
pkgver=3.16.1
pkgrel=1
pkgdesc="A text widget adding syntax highlighting and more to GNOME"
arch=('amd64')
license=('GPL')
depends=('gtk3' 'libxml2')
makedepends=('intltool' 'gobject-introspection' 'glade')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$_pkgbasename/${pkgver%.*}/$_pkgbasename-$pkgver.tar.xz)
sha256sums=('669e9846ca744ec4d04ce61377e85df7ca909ad476329922a6abf4da423f1b0c')

build() {
  cd "$_pkgbasename-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-glade-catalog LIBS="-lintl"
  gmake
}

package() {
  cd "$_pkgbasename-$pkgver"
  gmake DESTDIR="$pkgdir" install
}
