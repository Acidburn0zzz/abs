# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=atk
pkgver=2.10.0
pkgrel=3
pkgdesc="A library providing a set of interfaces for accessibility"
arch=(i686 x86_64)
license=('LGPL')
depends=('glib2>=2.34.3')
makedepends=('gobject-introspection>=1.38.0')
options=('!docs')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
url='http://www.gtk.org/'
sha256sums=('636917a5036bc851d8491194645d284798ec118919a828be5e713b6ecc5b50b0')

prepare() {
  cd "${srcdir}/$pkgname-$pkgver"
  find . -name libtool -exec cp /usr/bin/libtool '{}' \;
  find . -name ltmain.sh -exec cp /usr/share/libtool/config/ltmain.sh '{}' \;
}

build() {
  cd "${srcdir}/$pkgname-$pkgver"

  export CFLAGS="${CFLAGS} -fPIC"

  ./configure --prefix=/usr --enable-static \
              --with-html-dir=/usr/share/share/doc
              # the glib-test uses variables from the glib-library but doesn -lglib

  gmake
}

package() {
  cd "$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install
}
