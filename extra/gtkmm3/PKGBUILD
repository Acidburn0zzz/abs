pkgbase=gtkmm3
_pkgbasename=gtkmm
pkgname=('gtkmm3' 'gtkmm3-docs')
pkgver=3.18.0
pkgrel=1
arch=('i386' 'amd64')
makedepends=('gtk3' 'pangomm' 'atkmm' 'glibmm-docs' 'mm-common' 'doxygen' 'libxslt')
license=('LGPL')
options=('!emptydirs')
url="http://www.gtkmm.org/"
source=(http://ftp.gnome.org/pub/GNOME/sources/$_pkgbasename/${pkgver:0:4}/$_pkgbasename-$pkgver.tar.xz)
sha256sums=('829fa113daed74398c49c3f2b7672807f58ba85d0fa463f5bc726e1b0138b86b')

build() {
  cd "$_pkgbasename-$pkgver"
  MAKE=gmake ./configure --prefix=/usr
  gmake
}

package_gtkmm3() {
  pkgdesc="C++ bindings for gtk3"
  depends=('gtk3' 'pangomm' 'atkmm')
  cd "$_pkgbasename-$pkgver"

  gsed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile

  gmake DESTDIR="$pkgdir" install
}

package_gtkmm3-docs() {
  pkgdesc="Developer documentation for gtkmm v3"

  cd "$_pkgbasename-$pkgver"
  gmake -C docs DESTDIR="$pkgdir" install
}

