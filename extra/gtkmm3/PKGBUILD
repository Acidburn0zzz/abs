# Port: x11-toolkits/gtkmm30

pkgbase=gtkmm3
_pkgbasename=gtkmm
pkgname=('gtkmm3' 'gtkmm3-docs')
pkgver=3.16.0
pkgrel=1
arch=('i386' 'amd64')
makedepends=('gtk3' 'pangomm' 'atkmm' 'glibmm-docs' 'mm-common' 'doxygen' 'libxslt')
license=('LGPL')
options=('!emptydirs')
url="http://www.gtkmm.org/"
source=(http://ftp.gnome.org/pub/GNOME/sources/$_pkgbasename/${pkgver:0:4}/$_pkgbasename-$pkgver.tar.xz)
sha256sums=('9b8d4af5e1bb64e52b53bc8ef471ef43e1b9d11a829f16ef54c3a92985b0dd0c')

build() {
  cd "$_pkgbasename-$pkgver"
  export CXXFLAGS+=" -std=c++11"
  MAKE=gmake ./configure --prefix=/usr
  gmake
}

package_gtkmm3() {
  pkgdesc="C++ bindings for gtk3"
  depends=('gtk3' 'pangomm' 'atkmm')
  cd "$_pkgbasename-$pkgver"

  gsed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile

  gmake DESTDIR="$pkgdir" install
}

package_gtkmm3-docs() {
  pkgdesc="Developer documentation for gtkmm v3"

  cd "$_pkgbasename-$pkgver"
  gmake -C docs DESTDIR="$pkgdir" install
}

