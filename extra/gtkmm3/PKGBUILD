# $Id: PKGBUILD 204983 2014-02-02 18:51:29Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgbase=gtkmm3
_pkgbasename=gtkmm
pkgname=('gtkmm3' 'gtkmm3-docs')
pkgver=3.12.0
pkgrel=1
arch=('i686' 'amd64')
makedepends=('gtk3' 'pangomm' 'atkmm' 'glibmm-docs' 'mm-common' 'doxygen' 'libxslt')
license=('LGPL')
options=('!emptydirs')
url="http://www.gtkmm.org/"
source=(http://ftp.gnome.org/pub/GNOME/sources/$_pkgbasename/${pkgver:0:4}/$_pkgbasename-$pkgver.tar.xz)
sha256sums=('86c526ceec15d889996822128d566748bb36f70cf5a2c270530dfc546a2574e1')

build() {
  cd "$_pkgbasename-$pkgver"
  export MAKE=gmake
  ./configure --prefix=/usr
  gmake
}

package_gtkmm3() {
  pkgdesc="C++ bindings for gtk3"
  depends=('gtk3' 'pangomm' 'atkmm')
  cd "$_pkgbasename-$pkgver"

  gsed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile

  gmake DESTDIR="$pkgdir" install
}

package_gtkmm3-docs() {
  pkgdesc="Developer documentation for gtkmm v3"

  cd "$_pkgbasename-$pkgver"
  gmake -C docs DESTDIR="$pkgdir" install
}
