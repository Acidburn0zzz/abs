# $Id: PKGBUILD 203497 2014-01-11 20:07:13Z eric $
# Maintainer: Anthony Donnelly <amzo@archbsd.net>

pkgname=wxgtk
pkgver=3.0.2
pkgrel=3
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
arch=('i686' 'amd64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'gstreamer0.10-base' 'gnu-libiconv')
makedepends=('gstreamer0.10-base-plugins' 'gconf' 'webkitgtk2' 'glu')
optdepends=('webkitgtk2: for webview support')
options=('!emptydirs' 'iconv')
patchdir=("wxWidgets-${pkgver}/")
source=(http://downloads.sourceforge.net/wxwindows/wxWidgets-${pkgver}.tar.bz2)
sha1sums=('6461eab4428c0a8b9e41781b8787510484dea800')

build() {
  cd wxWidgets-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx --enable-mediactrl --enable-webview --enable-stl --with-regex=builtin \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --disable-precomp-headers

  gmake
  gmake -C locale allmo
}

package() {
  cd wxWidgets-${pkgver}
  gmake DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
