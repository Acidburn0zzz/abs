# $Id: PKGBUILD 203497 2014-01-11 20:07:13Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=wxgtk
pkgver=3.0.1
pkgrel=1
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'gstreamer0.10-base')
makedepends=('gstreamer0.10-base-plugins' 'gconf' 'webkitgtk2' 'glu')
optdepends=('webkitgtk2: for webview support')
options=('!emptydirs')
patchdir=("wxWidgets-${pkgver}/")
source=(http://downloads.sourceforge.net/wxwindows/wxWidgets-${pkgver}.tar.bz2)
sha1sums=('73e58521d6871c9f4d1e7974c6e3a81629fddcf8')

build() {
  cd wxWidgets-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx --enable-mediactrl --enable-webview --enable-stl --with-regex=builtin \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --disable-precomp-headers 

  gmake
  gmake -C locale allmo
}

package() {
  cd wxWidgets-${pkgver}
  gmake DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
