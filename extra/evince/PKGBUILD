# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Wolfgang Bumiller <archbsd at bumiller dot com>

pkgname=evince
pkgver=3.18.0
pkgrel=4
pkgdesc="Simply a document viewer"
url="http://projects.gnome.org/evince/"
arch=('amd64')
license=(GPL)
depends=(gtk3 libgxps libspectre gsfonts poppler-glib djvulibre t1lib libsecret desktop-file-utils dconf gsettings-desktop-schemas gnome-desktop)
makedepends=(itstool libnautilus-extension texlive-bin intltool gobject-introspection docbook-xsl python)
optdepends=('texlive-bin: DVI support')
#	    'gvfs: bookmark support and session saving')
groups=(gnome)
install=evince.install
options=('!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('96e8351f6a6fc5823bb8f51178cde1182bd66481af6fb09bf58a18b673cafa70')

build() {
  cd $pkgname-$pkgver

  sed -i '' -e 's|execinfo.h|#|g' \
	-e 's|" == "|" = "|g' \
	-e 's|-Werror||g' \
	configure

  # Every halfway decent dev uses -Werror, and they these guys do!
  # - but clang has a useful warning they chose to ignore:
  export CFLAGS="${CFLAGS} -Wno-format-nonliteral"

  ./configure --prefix=/usr   --sysconfdir=/etc \
      --localstatedir=/var    --libexecdir=/usr/lib/evince \
      --disable-static        --disable-nautilus \
      --enable-pdf            --enable-tiff \
      --enable-djvu           --enable-dvi \
      --enable-t1lib          --enable-comics \
      --disable-scrollkeeper  --disable-schemas-compile \
      --enable-introspection LIBS="-lintl"
  gmake
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
}
