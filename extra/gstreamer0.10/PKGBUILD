# $Id: PKGBUILD 211698 2014-04-23 15:31:03Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gstreamer0.10
pkgver=0.10.36
pkgrel=7
pkgdesc="GStreamer Multimedia Framework"
arch=('i386' 'amd64')
license=('LGPL')
url="http://gstreamer.com"
depends=('libxml2' 'glib2')
optdepends=('sh: feedback script')
makedepends=('intltool' 'pkgconfig' 'gobject-introspection' 'git')
patchdir=("gstreamer")
source=("git://anongit.freedesktop.org/gstreamer-sdk/gstreamer#commit=3ddc31eaa18c3be1613e43430eca78a3e445639e"
        tests-remove-silly-test_fail_abstract_new-check.patch
        bison3.patch)
sha256sums=('SKIP'
            'd3d3f4f04453831fdb4244bfe174a38c4e6f9f4da5c8c9050dcfa1a6097aad52'
            'ed154e280abf59b24d98a8ab0fe868b449b26aa61f7ae3813fab8ac615fcaefa')

prepare() {
  cd gstreamer

  patch -Np1 -i ../tests-remove-silly-test_fail_abstract_new-check.patch
  patch -Np1 -i ../bison3.patch
  gsed -e 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' -i configure.ac
}

build() {
  cd gstreamer
  NOCONFIGURE=1 ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
    --with-package-name="GStreamer (ArchBSD)" \
    --with-package-origin="http://www.archbsd.net/" \
    --disable-static \
    --disable-tests \
    --disable-examples \
    --disable-failing-tests \
    --disable-gtk-doc \
    ac_cv_func_register_printf_function="no"
  gmake
}

check() {
  cd gstreamer
  gmake check
}

package() {
  cd gstreamer
  gmake DESTDIR="${pkgdir}" install

  #Remove unversioned gst-* binaries to get rid of conflicts
  cd "${pkgdir}/usr/bin"
  for bins in `ls *-0.10`; do
    rm -f ${bins/-0.10/}
  done
}
