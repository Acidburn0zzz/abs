# Port: devel/swig30
pkgname=swig
pkgver=3.0.12
pkgrel=1
pkgdesc="Generate scripting interfaces to C/C++ code"
arch=('i386' 'amd64')
url="http://www.swig.org/"
license=('custom')
depends=('pcre' 'gcc-libs')
options=('gnu_configure')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz)
sha1sums=('5cc1af41d041e4cc609580b99bb3dcf720effa25')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  gmake
}

check() {
  cd ${pkgname}-${pkgver}
  gmake check || warning "Tests failed"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}/
  install -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -m644 LICENSE-UNIVERSITIES "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE-UNIVERSITIES
}
