# $Id: PKGBUILD 173268 2012-12-17 16:30:15Z stephane $
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=swig
pkgver=2.0.12
pkgrel=1
pkgdesc="Generate scripting interfaces to C/C++ code"
arch=('i686' 'amd64')
url="http://www.swig.org/"
license=('custom')
depends=('pcre' 'gcc-libs')
port_patches=(
	patch-Lib_swigrun.swg
)
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz
	${port_patches[@]})
sha1sums=('4203c68f79012a2951f542018ff4358d838b5035'
          'a4594289ebcea507e9b8ab8acf230359b6a049b5')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  for i in ${port_patches[@]}; do
    patch -p0 -i "${srcdir}/${i}"
  done
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}/
  install -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -m644 LICENSE-UNIVERSITIES "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE-UNIVERSITIES
}
