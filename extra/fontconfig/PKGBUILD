#
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=fontconfig
pkgver=2.11.1
pkgrel=7
pkgdesc="A library for configuring and customizing font access"
arch=('i386' 'amd64')
url="http://www.fontconfig.org/release/"
license=('custom')
depends=('expat' 'freetype2')
install=fontconfig.install
source=(http://www.fontconfig.org/release/$pkgname-$pkgver.tar.bz2)
sha256sums=('dc62447533bca844463a3c3fd4083b57c90f18a70506e7a9f4936b5a1e516a99')
options=('libtoolfix')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # make sure there's no rpath trouble and sane .so versioning - FC and Gentoo do this as well
  libtoolize -f
  autoreconf -fi

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-templatedir=/etc/fonts/conf.avail \
    --with-xmldir=/etc/fonts \
    --localstatedir=/var \
    --disable-static \
    --with-default-fonts=/usr/share/fonts \
    --with-add-fonts=/usr/share/fonts

  gmake
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
#  one test is failign for some reason, but works manually
  make -k check || true
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install

  #Install license
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname"
}
