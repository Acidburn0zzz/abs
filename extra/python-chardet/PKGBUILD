# Port: textproc/py-chardet

pkgbase=python-chardet
pkgname=('python-chardet' 'python2-chardet')
pkgver=2.3.0
pkgrel=1
arch=('i386' 'amd64')
url="https://github.com/chardet/chardet"
license=('LGPL')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/c/chardet/chardet-${pkgver}.tar.gz")
sha256sums=('e53e38b3a4afe6d1132de62b7400a4ac363452dc5dfcf8d88e8e0cce663c68aa')

prepare() {
   cp -r chardet-${pkgver} python2-chardet-${pkgver}
}

package_python-chardet() {
   depends=('python>=3.3')
   pkgdesc="Python3 module for character encoding auto-detection"
   cd chardet-${pkgver}
   python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-chardet() {
   depends=('python2>=2.7.1')
   pkgdesc="Python2 module for character encoding auto-detection"
   cd python2-chardet-${pkgver}
   python2 setup.py install --root="${pkgdir}" --optimize=1

   # To avoid file conflict with the python3 version
   mv "$pkgdir/usr/bin/chardetect" "$pkgdir/usr/bin/chardetect-py2"
}
