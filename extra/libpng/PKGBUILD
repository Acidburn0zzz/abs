# Port: graphics/png

pkgname=libpng
pkgver=1.6.31
_apngver=1.6.31
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('amd64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-${_apngver}-apng.patch.gz")
sha256sums=('232a602de04916b2b5ce6f901829caf419519e6a16cc9cd7c1c91187d3ee8b41'
            '8ae6147298248bf4bcd64c94878e95956d74c6bc2052bdef26a148540c8c2038')

prepare() {
  cd "${pkgname}-${pkgver}"

  # Add animated PNG (apng) support
  # see http://sourceforge.net/projects/libpng-apng/
  patch -Np1 -i "../libpng-${_apngver}-apng.patch"
}

build() {
  cd "${pkgname}-${pkgver}"

  ./configure --prefix=/usr --disable-static \
    -with-pkgconfigdir=/usr/libdata/pkgconfig
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  cd contrib/pngminus
  make CC=clang CXX=clang++ PNGLIB="-L${pkgdir}/usr/lib -lpng" -f makefile.std png2pnm pnm2png
  install -m755 png2pnm pnm2png "${pkgdir}/usr/bin/"
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 ../../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
