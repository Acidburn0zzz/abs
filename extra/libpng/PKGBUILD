# $Id: PKGBUILD 169446 2012-10-21 10:49:41Z ibiru $

pkgname=libpng
pkgver=1.6.17
_apngver=1.6.17
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('i686' 'amd64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
makedepends=('gcc')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-$_apngver-apng.patch.gz")
md5sums=('430a9b76b78533235cd4b9b26ce75c7e'
         'f68d34f10bcc7f43b8c3dce5708e1933')

prepare() {
  cd $pkgname-$pkgver
  sed -i '' \
    -e 's|LIBDIR}/pkgconfig|LIBDIR}data/pkgconfig|' \
    CMakeLists.txt
}

build() {
  cd $pkgname-$pkgver

  # Add animated PNG (apng) support
  # see http://sourceforge.net/projects/libpng-apng/
  patch -p1 -i ../libpng-$_apngver-apng.patch

  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cd contrib/pngminus
  make PNGLIB="-L$pkgdir/usr/lib -lpng" -f makefile.std png2pnm pnm2png
  install -m755 png2pnm pnm2png "$pkgdir/usr/bin/"
  install -d -m755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 ../../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
