pkgname=libpng
pkgver=1.6.18
_apngver=1.6.18
pkgrel=2
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('i386' 'amd64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
makedepends=('gcc')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-$_apngver-apng.patch.gz")
md5sums=('6a57c8e0f5469b9c9949a4b43d57b3a1'
         'a9c85fe3cf0347ea94356f1e70d07973')

prepare() {
  cd $pkgname-$pkgver
  sed -i '' \
    -e 's|LIBDIR}/pkgconfig|LIBDIR}data/pkgconfig|' \
    CMakeLists.txt
}

build() {
  cd $pkgname-$pkgver

  # Add animated PNG (apng) support
  # see http://sourceforge.net/projects/libpng-apng/
  patch -p1 -i ../libpng-$_apngver-apng.patch

  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cd contrib/pngminus
  make PNGLIB="-L$pkgdir/usr/lib -lpng" -f makefile.std png2pnm pnm2png
  install -m755 png2pnm pnm2png "$pkgdir/usr/bin/"
  install -d -m755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 ../../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
