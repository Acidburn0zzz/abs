# Port: graphics/png
pkgname=libpng
pkgver=1.6.23
_apngver=1.6.23
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('i386' 'amd64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-$_apngver-apng.patch.gz")
md5sums=('9b320a05ed4db1f3f0865c8a951fd9aa'
         'bf44929a1c272f585a11baeca05af500')

build() {
  cd $pkgname-$pkgver

  # Add animated PNG (apng) support
  # see http://sourceforge.net/projects/libpng-apng/
  patch -p1 -i ../libpng-$_apngver-apng.patch

  ./configure --prefix=/usr --disable-static \
    -with-pkgconfigdir=/usr/libdata/pkgconfig
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cd contrib/pngminus
  make CC=clang CXX=clang++ PNGLIB="-L$pkgdir/usr/lib -lpng" -f makefile.std png2pnm pnm2png
  install -m755 png2pnm pnm2png "$pkgdir/usr/bin/"
  install -d -m755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 ../../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
