pkgbase=('gstreamer0.10-base')
pkgname=('gstreamer0.10-base' 'gstreamer0.10-base-plugins')
pkgver=0.10.36
pkgrel=12
arch=('i386' 'amd64')
license=('LGPL')
makedepends=('pkgconf' 'gstreamer0.10>=0.10.35' 'orc' 'libxv' 'alsa-lib' 'cdparanoia' 'libvisual' 'libvorbis' 'libtheora' 'pango' 'gobject-introspection')
options=(!libtool !emptydirs)
url="http://gstreamer.freedesktop.org/"
patchdir=("gst-plugins-base-${pkgver}")
source=(${url}/src/gst-plugins-base/gst-plugins-base-${pkgver}.tar.xz
        fix-crash-0-byte-ogg.patch
        colorbalance-fix-abi.patch
        videoscale-fix-negotiation.patch
        ayuv64-lanczos.patch
        gstaudio-symbols.patch
	revert-decodebin-playbin-removal.patch)
sha256sums=('1fe45c3894903001d4d008b0713dab089f53726dcb5842d5b40c2595a984e64a'
            'a6a01035ea9627737f9c17f72919857ed43ccc7c2cb08b645b43ed89f78d0f4f'
            '7442c5c68068428b8c7ac1d3825ce29f1bb152b75b77047b9e806c7d322b780c'
            'ae27f7be58997217f67898b37b138a485c203389e56b65e6b31c23f769ef39ca'
            '3792dfe80c69f51c0db98533e8fb16707b5dd2ee6933ea6098583af873ceb44a'
            '56e7a988df39d2ec4befa265536ad8c30d3c8d18d136cebef64e8d6baac1abae'
            'ba20659fafea73db016ddaecd128f12087e0957ce35cf2c3ce29f72c51551ef3')

prepare() {
  cd "${srcdir}/gst-plugins-base-${pkgver}"

  sed -i '' -e '/AC_PATH_XTRA/d' -e 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' configure.ac
  patch -Np1 -i ../fix-crash-0-byte-ogg.patch
  patch -Np1 -i ../colorbalance-fix-abi.patch
  patch -Np1 -i ../ayuv64-lanczos.patch
  patch -Np1 -i ../videoscale-fix-negotiation.patch
  patch -Np1 -i ../gstaudio-symbols.patch
}

build() {
  cd "${srcdir}/gst-plugins-base-${pkgver}"
  NOCONFIGURE=1 ./autogen.sh
  sed -i '' -e 's|-Wmissing-include-dirs||g' \
    configure

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental --disable-gnome_vfs \
    --with-package-name="GStreamer Base Plugins (PacBSD)" \
    --with-package-origin="http://www.pacbsd.org/" \
    --enable-pic --enable-shared ac_cv_func_register_printf_function="no"
  gmake
  gsed -e 's/^SUBDIRS_EXT =.*/SUBDIRS_EXT =/' -i Makefile
}

check() {
  cd "${srcdir}/gst-plugins-base-${pkgver}"
  # some orc failures, which apaprently aren't failures
  # https://bugzilla.gnome.org/show_bug.cgi?id=625008
  gmake check || true
}

package_gstreamer0.10-base() {
  pkgdesc="GStreamer Multimedia Framework Base plugin libraries"
  depends=('gstreamer0.10>=0.10.35' 'orc' 'libxv')

  cd "${srcdir}/gst-plugins-base-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  #files provided 
}

package_gstreamer0.10-base-plugins() {
  pkgdesc="GStreamer Multimedia Framework Base Plugins (gst-plugins-base)"
  depends=("gstreamer0.10-base=${pkgver}" 'alsa-lib' 'cdparanoia' 'libvisual' 'libvorbis' 'libtheora' 'pango')
  replaces=('gstreamer0.10-alsa' 'gstreamer0.10-theora' 'gstreamer0.10-libvisual' 'gstreamer0.10-pango' 'gstreamer0.10-cdparanoia' 'gstreamer0.10-vorbis' 'gstreamer0.10-ogg')
  conflicts=('gstreamer0.10-alsa' 'gstreamer0.10-theora' 'gstreamer0.10-libvisual' 'gstreamer0.10-pango' 'gstreamer0.10-cdparanoia' 'gstreamer0.10-vorbis' 'gstreamer0.10-ogg')
  groups=('gstreamer0.10-plugins')

  cd "${srcdir}/gst-plugins-base-${pkgver}"
  gmake -C gst-libs DESTDIR="${pkgdir}" install
  gmake -C ext DESTDIR="${pkgdir}" install
  gmake -C gst-libs DESTDIR="${pkgdir}" uninstall

}
