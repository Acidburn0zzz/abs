# $Id: PKGBUILD 162749 2012-06-29 12:15:20Z pierre $
# Maintainer:
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=lynx
pkgver=2.8.8
_relver=${pkgver}rel.2
pkgrel=8
pkgdesc="A text browser for the World Wide Web"
url="http://lynx.isc.org/"
arch=('i386' 'amd64')
license=('GPL')
depends=('gettext-runtime')
backup=('etc/lynx.cfg')
patchdir=(${pkgname}${pkgver//./-})
source=("http://invisible-mirror.net/archives/lynx/tarballs//${pkgname}${_relver}.tar.bz2")
md5sums=('b231c2aa34dfe7ca25681ef4e55ee7e8')
validpgpkeys=('0AFD1FFEEA2EA063B959ACDA5DDF8FB7688E31A6')

build() {
  unset ac_cv_build
  unset ac_cv_host

  case $CARCH in
    i386)
      build="$(uname -m)-pacbsd-freebsd${OSREL/-*/}"
      ;;
    amd64)
      build="x86_64-pacbsd-freebsd${OSREL/-*/}"
      ;;
  esac

  cd ${srcdir}/${pkgname}${pkgver//./-}
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-zlib \
    --with-ssl \
    --enable-nls \
    --enable-ipv6 \
    --mandir=/usr/share/man \
    --build="${build}"
  gmake -j1
}

package() {
  cd ${srcdir}/${pkgname}${pkgver//./-}
  gmake -j1 DESTDIR=${pkgdir} install

  # FS#20404 - points to local help
  sed -i -e "s|^HELPFILE.*$|HELPFILE:file:///usr/share/doc/lynx/lynx_help/lynx_help_main.html|" ${pkgdir}/etc/lynx.cfg

  install -d ${pkgdir}/usr/share/doc/lynx
  cp -rf lynx_help ${pkgdir}/usr/share/doc/lynx
}
