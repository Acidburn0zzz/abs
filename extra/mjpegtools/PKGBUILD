# Port: multimedia/mjpegtools

pkgname=mjpegtools
pkgver=2.1.0
pkgrel=1
pkgdesc="Set of tools that can do recording of videos and playback, simple cut-and-paste editing and the MPEG compression of audio and video"
arch=('i386' 'amd64')
license=('GPL')
url="http://mjpeg.sourceforge.net/"
depends=('libjpeg' 'libpng' 'sdl' 'gcc-libs' 'libdv')
makedepends=('gtk2' 'v4l-compat')
optdepends=('gtk2: glav GUI')
options=('!makeflags')
install=mjpegtools.install
source=(http://downloads.sourceforge.net/sourceforge/mjpeg/${pkgname}-${pkgver}.tar.gz)
md5sums=('57bf5dd78976ca9bac972a6511b236f3')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i '' \
	-e 's| \*-\*-linux\*)| *-*-*)|' \
	-e '/ARCHFLAGS=/d' configure
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-largefile --build=x86_64-pacbsd-freebsd11.0

  gmake
}

package() {
  cd ${pkgname}-${pkgver}
  gmake DESTDIR="${pkgdir}" install
  install -m644 mpeg2enc/mpeg2syntaxcodes.h "${pkgdir}/usr/include/mjpegtools/mpeg2enc/"
}
