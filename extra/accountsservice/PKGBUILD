# $Id: PKGBUILD 193628 2013-08-26 09:59:39Z jgc $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=accountsservice
pkgver=0.6.37
pkgrel=1
pkgdesc="D-Bus interface for user account query and manipulation"
arch=(i686 x86_64)
url="http://www.freedesktop.org/software/accountsservice/"
license=('GPL3')
depends=('glib2' 'polkit')
makedepends=('intltool' 'gobject-introspection' 'vala')
options=('!libtool')
source=($url/$pkgname-$pkgver.tar.xz
	patch-src_user.c
	patch-src_daemon.c)
md5sums=('aa5f4da715b8ec19024e39def88831a0'
         'a1bdbfb2d981e077243459becac98fac'
         'fd2385ae9c0c63f984152d41d662c6de')

prepare() {
  cd $pkgname-$pkgver
  patch -p0 -i ../patch-src_daemon.c
  patch -p0 -i ../patch-src_user.c
}
build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/libexec/accountsservice \
    --localstatedir=/var --disable-static
  gmake
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
