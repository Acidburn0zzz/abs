pkgname=babl
pkgver=0.1.14
pkgrel=1
pkgdesc="Dynamic, any to any, pixel format conversion library"
arch=('i386' 'amd64')
url="http://gegl.org/babl/"
license=('LGPL3')
depends=('libc')
options=('libtoolfix' '!makeflags')
source=(http://ftp.gimp.org/pub/babl/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('e6dcb112c8f8f75471823fdcc5a6a65f753b4d0e96e377979ea01a5d6fad7d4f')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i '' -e 's|x86_64|amd64|g' \
    configure

}
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  gmake
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
}
