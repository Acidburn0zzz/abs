pkgname=babl
pkgver=0.1.12
pkgrel=1
pkgdesc="Dynamic, any to any, pixel format conversion library"
arch=('amd64')
url="http://gegl.org/babl/"
license=('LGPL3')
depends=('libc')
options=('libtoolfix' '!makeflags')
source=(http://ftp.gimp.org/pub/babl/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('b9a811d9d05717d66bc107a18447fbd74cff7eea')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i '' -e 's|x86_64|amd64|g' \
    configure

}
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  gmake
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
}
