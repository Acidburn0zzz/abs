# Port: audio/faac
pkgname=faac
pkgver=1.28
pkgrel=1
pkgdesc="An AAC audio encoder"
arch=('i386' 'amd64')
url="http://www.audiocoding.com/"
license=('GPL' 'custom')
depends=('libmp4v2' 'libc')
options=('libtoolfix' '!gnu_configure')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")

prepare() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  echo "" >> libfaac/psych.h
  sed -i '' -e 's|<mp4\.h>|<mp4v2/mp4v2.h>|g' \
    frontend/main.c
}

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  find . -type f -print0 | xargs -0 gsed -i 's/\r//g'

  ./bootstrap

  gnu_configure
  ./configure --prefix=/usr
  gmake
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  gmake DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}"/usr/share/licenses/faac
  install -m644 "${srcdir}"/${pkgname}-${pkgver}/libfaac/kiss_fft/COPYING \
    "${pkgdir}"/usr/share/licenses/faac/LICENSE
}
md5sums=('80763728d392c7d789cde25614c878f6')
