# Port: graphics/glew

pkgname=glew
pkgver=1.13.0
pkgrel=2
pkgdesc="The OpenGL Extension Wrangler Library"
arch=('i386' 'amd64')
url="http://glew.sourceforge.net"
license=('BSD' 'MIT' 'GPL')
depends=('libxmu' 'libxi' 'glu')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tgz)
sha1sums=('d5b1c499f429aa91c466193b4e8ea94a84019e37')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i '' -e 's|lib64|lib|' config/Makefile.freebsd
  sed -i '' -e '/^.PHONY: .*\.pc$/d' Makefile
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -dm755 ${pkgdir}/usr/{bin,lib,include/GL,libdata/pkgconfig}
  ${BSD_INSTALL_PROGRAM} bin/glewinfo ${pkgdir}/usr/bin
  ${BSD_INSTALL_PROGRAM} bin/visualinfo ${pkgdir}/usr/bin

  ${BSD_INSTALL_PROGRAM} lib/libGLEW.so.${pkgver} \
    ${pkgdir}/usr/lib/libGLEW.so.1
  ${BSD_INSTALL_PROGRAM} lib/libGLEWmx.so.${pkgver} \
		${pkgdir}/usr/lib/libGLEWmx.so.1
  ln -sf /usr/lib/libGLEW.so.1 ${pkgdir}/usr/lib/libGLEW.so
  ln -sf /usr/lib/libGLEWmx.so.1 ${pkgdir}/usr/lib/libGLEWmx.so
  ${BSD_INSTALL_DATA} lib/libGLEW.a ${pkgdir}/usr/lib
  ${BSD_INSTALL_DATA} lib/libGLEWmx.a ${pkgdir}/usr/lib
  mkdir -p ${pkgdir}/usr/include/GL
  ${BSD_INSTALL_DATA} include/GL/*.h ${pkgdir}/usr/include/GL
  ${BSD_INSTALL_DATA} glew.pc ${pkgdir}/usr/libdata/pkgconfig
  ${BSD_INSTALL_DATA} glewmx.pc ${pkgdir}/usr/libdata/pkgconfig
  mkdir -p ${pkgdir}/usr/share/docs

  for i in css html jpg png; do
    ${BSD_INSTALL_DATA} doc/*.${i} ${pkgdir}/usr/share/docs
  done


  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
