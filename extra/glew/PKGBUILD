# $Id$
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgname=glew
pkgver=1.12.0
pkgrel=1
pkgdesc="The OpenGL Extension Wrangler Library"
arch=('i686' 'x86_64')
url="http://glew.sourceforge.net"
license=('BSD' 'MIT' 'GPL')
depends=('libxmu' 'libxi' 'glu')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tgz)
sha1sums=('070dfb61dbb7cd0915517decf467264756469a94')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i '' -e 's|lib64|lib|' config/Makefile.linux
  sed -i '' -e '/^.PHONY: .*\.pc$/d' Makefile
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake GLEW_DEST="${pkgdir}/usr" install.all

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
