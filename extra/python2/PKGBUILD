#
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>
# ArchBSD: Wolfgang Bumiller <archbsd at bumiller com>

pkgname=python2
pkgver=2.7.10
pkgrel=5
_pybasever=2.7
pkgdesc="A high-level scripting language"
arch=('i386' 'amd64')
license=('PSF')
url="http://www.python.org/"
depends=('gettext' 'gdbm' 'readline')
conflicts=('python<3')
options=('!makeflags')
patchdir=("${srcdir}/Python-${pkgver}")
source=(http://www.python.org/ftp/python/${pkgver%rc?}/Python-${pkgver}.tar.xz)
sha1sums=('ee5a50c5562e7448f037d35fdedc18d95c748b9e')

prepare() {
  cd "${srcdir}/Python-${pkgver}"

  # Temporary workaround for FS#22322
  # See http://bugs.python.org/issue10835 for upstream report
  gsed -i "/progname =/s/python/python${_pybasever}/" Python/pythonrun.c

  # Enable built-in SQLite module to load extensions (fix FS#22122)
  gsed -i "/SQLITE_OMIT_LOAD_EXTENSION/d" setup.py

  # FS#23997
  gsed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python2|" Lib/cgi.py

  gsed -i "s/python2.3/python2/g" Lib/distutils/tests/test_build_scripts.py \
     Lib/distutils/tests/test_install_scripts.py Tools/scripts/gprof2html.py

  # Ensure that we are using the system copy of various libraries (expat, zlib and libffi),
  # rather than copies shipped in the tarball
  rm -r Modules/zlib
  rm -r Modules/_ctypes/darwin*

  # clean up #!s
  find . -name '*.py' | \
    xargs gsed -i "s|#[ ]*![ ]*/usr/bin/env python$|#!/usr/bin/env python2|"

  # Workaround asdl_c.py error
  touch Include/Python-ast.h Python/Python-ast.c

  # bsd
  cp -r Lib/plat-freebsd{8,9}
  cp -r Lib/plat-freebsd{8,10}
  mkdir -p Modules

  for i in 2to3 idle pydoc smtpd.py
  do
    sed -e '1s,^.*$,#!/usr/bin/python2.7,' \
        Tools/scripts/${script} \
        > `echo -en "$i" | sed -E \
                               -e 's,smtpd,smtpd-2.7,' \
                               -e 's,2to3,2to3-2.7,' \
                               -e 's,(idle|pydoc|python-shared|python),\1-2.7,'`
  done

  sed -e 's|^#!.*|#!/usr/bin/python2.7|' \
    -e 's|libs = getvar|libs = getvar("LDFLAGS").split() + getvar|' \
    Misc/python-config.in > python2.7-config
  sed -e 's|^#!.*|#!/usr/bin/python2.7|' \
    -e 's|libs = getvar|libs = getvar("LDFLAGS").split() + getvar|' \
    Misc/python-config.in > python-shared2.7-config
}

build() {
  cd "${srcdir}/Python-${pkgver}"

  export LDFLAGS="${LDFLAGS} -L${srcdir}/Python-${pkgver}"
  export OPT="${CFLAGS}"
  ./configure --prefix=/usr --with-threads --enable-ipv6 \
              --enable-unicode=ucs4 --enable-shared \
	      ac_cv_opt_olimit_ok=no OPT="" ac_cv_posix_semaphores_enabled=yes \
	      --with-pymalloc

  gmake LIBPC="/usr/libdata/pkgconfig" OPENSSL_CFLAGS="-DNO_IDEA"
}

package() {
  cd "${srcdir}/Python-${pkgver}"
  gmake DESTDIR="${pkgdir}" LIBPC="/usr/libdata/pkgconfig" OPENSSL_CFLAGS="-DNO_IDEA" altinstall maninstall

  ln -sf python${_pybasever}        "${pkgdir}"/usr/bin/python2
  ln -sf python${_pybasever}-config "${pkgdir}"/usr/bin/python2-config
  ln -sf python${_pybasever}.1      "${pkgdir}"/usr/share/man/man1/python2.1
  rm "${pkgdir}"/usr/share/man/man1/python.1

  # FS#33954
  ln -sf python-${_pybasever}.pc    "${pkgdir}"/usr/libdata/pkgconfig/python2.pc

  ln -sf ../../libpython${_pybasever}.so "${pkgdir}"/usr/lib/python${_pybasever}/config/libpython${_pybasever}.so

  # some useful "stuff"
  install -dm755 "${pkgdir}"/usr/lib/python${_pybasever}/Tools/{i18n,scripts}
  install -m755 Tools/i18n/{msgfmt,pygettext}.py "${pkgdir}"/usr/lib/python${_pybasever}/Tools/i18n/
  install -m755 Tools/scripts/{README,*py} "${pkgdir}"/usr/lib/python${_pybasever}/Tools/scripts/

  # fix conflicts with python (< did this the ports way)
  #mv "${pkgdir}"/usr/bin/idle{,2}
  #mv "${pkgdir}"/usr/bin/pydoc{,2}
  #mv "${pkgdir}"/usr/bin/2to3{,-2.7}
  # additional stuff from ports
  for i in 2to3 idle pydoc smtpd.py
  do
    install -m744 `echo -en "$i" | sed -E \
                               -e 's,smtpd,smtpd-2.7,' \
                               -e 's,2to3,2to3-2.7,' \
                               -e 's,(idle|pydoc|python-shared|python),\1-2.7,'` \
                  "$pkgdir"/usr/bin/
  done
  # clean-up reference to build directory
  gsed -i "s#${srcdir}/Python-${pkgver}:##" "${pkgdir}"/usr/lib/python${_pybasever}/config/Makefile

  # post-install from ports:
  install -m755 python2.7-config "$pkgdir"/usr/bin
  install -m755 python-shared2.7-config "$pkgdir"/usr/bin

  # license
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
