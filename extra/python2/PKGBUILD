#
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributer: Allan McRae <allan@archlinux.org>
# Contributer: Jason Chu <jason@archlinux.org>
# ArchBSD: Wolfgang Bumiller <archbsd at bumiller com>

pkgname=python2
pkgver=2.7.6
pkgrel=8
_pybasever=2.7
pkgdesc="A high-level scripting language"
arch=('i686' 'x86_64')
license=('PSF')
url="http://www.python.org/"
depends=('gettext' 'libffi' 'expat')
conflicts=('python<3')
options=('!makeflags')
_port_patches=(
        patch-Doc-library-fcntl.rst
        patch-Lib__distutils__unixccompiler.py
        patch-Modules-fcntlmodule.c
        patch-issue20374
        patch-setup.py)
source=(http://www.python.org/ftp/python/${pkgver%rc?}/Python-${pkgver}.tar.xz
        CVE-2014-1912.patch::http://hg.python.org/cpython/raw-rev/87673659d8f7
        "${_port_patches[@]}")
sha1sums=('8321636af2acbeaa68fc635d7dda7369ed446a80'
          '1d0527f7b8483e1e0e12867675fdff86f22cd297'
          '6cf6087b109c40af5cbfe273a9cd28ea06b97708'
          'd930ecfe67f8219ed3fc0abc377e21495d5e435a'
          '814883ca26f0695e5bd9771018afc5156c6cff2d'
          '8609ba7069314435c6960c5e2b5484b22675f881'
          'ccfb481f867888a3be81529fcde4080b54b77845')

prepare() {
  cd "${srcdir}/Python-${pkgver}"

  # FS#39040 / CVE-2014-1912, upstream report: http://bugs.python.org/issue20246
  # Remove the NEWS file update from upstream patch which will cause a conflict
  gsed -i "40,51d" "$srcdir/CVE-2014-1912.patch"
  patch -p1 -i "$srcdir/CVE-2014-1912.patch"

  # Temporary workaround for FS#22322
  # See http://bugs.python.org/issue10835 for upstream report
  gsed -i "/progname =/s/python/python${_pybasever}/" Python/pythonrun.c

  # Enable built-in SQLite module to load extensions (fix FS#22122)
  gsed -i "/SQLITE_OMIT_LOAD_EXTENSION/d" setup.py

  # FS#23997
  gsed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python2|" Lib/cgi.py

  gsed -i "s/python2.3/python2/g" Lib/distutils/tests/test_build_scripts.py \
     Lib/distutils/tests/test_install_scripts.py Tools/scripts/gprof2html.py

  # Ensure that we are using the system copy of various libraries (expat, zlib and libffi),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*

  # clean up #!s
  find . -name '*.py' | \
    xargs gsed -i "s|#[ ]*![ ]*/usr/bin/env python$|#!/usr/bin/env python2|"

  # Workaround asdl_c.py error
  touch Include/Python-ast.h Python/Python-ast.c

  # bsd
  for i in "${_port_patches[@]}"; do
    msg "patch $i"
    patch -p0 -i "${srcdir}/$i"
  done
  cp -r Lib/plat-freebsd{8,9}
  cp -r Lib/plat-freebsd{8,10}
  mkdir -p Modules

  for i in 2to3 idle pydoc smtpd.py
  do
    sed -e '1s,^.*$,#!/usr/bin/python2.7,' \
        Tools/scripts/${script} \
        > `echo -en "$i" | sed -E \
                               -e 's,smtpd,smtpd-2.7,' \
                               -e 's,2to3,2to3-2.7,' \
                               -e 's,(idle|pydoc|python-shared|python),\1-2.7,'`
  done

  #sed -i '' -e \
  #  's|^\( *prefixes = .*\)\]$|\1, "/usr"]|g' \
  #  Lib/site.py

  #sed -i '' \
  #  -e 's|^  \(..ASDLGEN.*\)$|  true|g' \
  #  -e 's|*\(..INSTALL_SCRIPT.*\)python-config$|#port \1|' \
  #  Makefile.pre.in

  sed -e 's|^#!.*|#!/usr/bin/python2.7|' \
    -e 's|libs = getvar|libs = getvar("LDFLAGS").split() + getvar|' \
    Misc/python-config.in > python2.7-config
  sed -e 's|^#!.*|#!/usr/bin/python2.7|' \
    -e 's|libs = getvar|libs = getvar("LDFLAGS").split() + getvar|' \
    Misc/python-config.in > python-shared2.7-config
}

build() {
  cd "${srcdir}/Python-${pkgver}"

  export LDFLAGS="${LDFLAGS} -L${srcdir}/Python-${pkgver}"
  export OPT="${CFLAGS}"
  ./configure --prefix=/usr --with-threads --enable-ipv6 \
              --enable-unicode=ucs4 --with-system-ffi \
		--with-system-expat

  gmake
}

package() {
  cd "${srcdir}/Python-${pkgver}"
  gmake DESTDIR="${pkgdir}" altinstall maninstall

  ln -sf python${_pybasever}        "${pkgdir}"/usr/bin/python2
  ln -sf python${_pybasever}-config "${pkgdir}"/usr/bin/python2-config
  ln -sf python${_pybasever}.1      "${pkgdir}"/usr/share/man/man1/python2.1
  rm "${pkgdir}"/usr/share/man/man1/python.1

  # FS#33954
  ln -sf python-${_pybasever}.pc    "${pkgdir}"/usr/lib/pkgconfig/python2.pc

  ln -sf ../../libpython${_pybasever}.so "${pkgdir}"/usr/lib/python${_pybasever}/config/libpython${_pybasever}.so

  # some useful "stuff"
  install -dm755 "${pkgdir}"/usr/lib/python${_pybasever}/Tools/{i18n,scripts}
  install -m755 Tools/i18n/{msgfmt,pygettext}.py "${pkgdir}"/usr/lib/python${_pybasever}/Tools/i18n/
  install -m755 Tools/scripts/{README,*py} "${pkgdir}"/usr/lib/python${_pybasever}/Tools/scripts/

  # fix conflicts with python (< did this the ports way)
  #mv "${pkgdir}"/usr/bin/idle{,2}
  #mv "${pkgdir}"/usr/bin/pydoc{,2}
  #mv "${pkgdir}"/usr/bin/2to3{,-2.7}
  # additional stuff from ports
  for i in 2to3 idle pydoc smtpd.py
  do
    install -m744 `echo -en "$i" | sed -E \
                               -e 's,smtpd,smtpd-2.7,' \
                               -e 's,2to3,2to3-2.7,' \
                               -e 's,(idle|pydoc|python-shared|python),\1-2.7,'` \
                  "$pkgdir"/usr/bin/
  done
  # clean-up reference to build directory
  gsed -i "s#${srcdir}/Python-${pkgver}:##" "${pkgdir}"/usr/lib/python${_pybasever}/config/Makefile

  # post-install from ports:
  install -m755 python2.7-config "$pkgdir"/usr/bin
  install -m755 python-shared2.7-config "$pkgdir"/usr/bin

  # license
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
