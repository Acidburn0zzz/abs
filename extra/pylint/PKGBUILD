# $Id: PKGBUILD 193615 2013-08-26 07:35:25Z angvp $
# Maintainer: Angel Velasquez <angvp@archlinux.org>  
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgbase=pylint
pkgname=('python2-pylint' 'python-pylint')
pkgver=1.4.4.20151005
pkgrel=1
pkgdesc="Analyzes Python code looking for bugs and signs of poor quality"
arch=('i386' 'amd64')
url="http://www.logilab.org/project/pylint"
license=('GPL')
makedepends=('python2-astroid' 'python-astroid') 
optdepends=('tk: Pylint GUI')
source=("hg+https://felixonmars@bitbucket.org/logilab/pylint#revision=$_commit")
sha1sums=('SKIP')

build() {
  cd "${srcdir}"
  cp -a ${pkgbase}-${pkgver}{,-py3}

  cd ${pkgbase}-${pkgver}

  sed -i '' -e "s|/usr/bin/env python|/usr/bin/env python2|" epylint.py
  python2 setup.py build

  cd ../${pkgbase}-${pkgver}-py3

  sed -i '' -e "s|/usr/bin/env python|/usr/bin/env python3|" epylint.py
  python3 setup.py build
}

package_python2-pylint() {
  depends=('python2-astroid' 'python2-setuptools')

  cd "${srcdir}"/${pkgbase}-${pkgver}

  python2 setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1

  install -d "${pkgdir}"/usr/share/man/man1
  install -m644 man/* "${pkgdir}/usr/share/man/man1"

  for i in epylint pylint pylint-gui pyreverse symilar; do
     mv "${pkgdir}"/usr/bin/${i}{,2}
     mv "${pkgdir}"/usr/share/man/man1/${i}{,2}.1
  done
}

package_python-pylint() {
  depends=('python-astroid' 'python-setuptools')

  cd "${srcdir}"/${pkgbase}-${pkgver}-py3

  python3 setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1

  install -d "${pkgdir}/usr/share/man/man1"
  install -m644 man/* "${pkgdir}/usr/share/man/man1"
}
