# $Id: PKGBUILD 196900 2013-10-21 03:40:08Z td123 $
# Maintainer: Thomas Dziedzic <gostrc@gmail.com>
# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Special note for devs looking to upgrade this package:
#   ghc places a unique hash for each library when it is built.
#   Libraries depend on versions specified by those hashes.
#   This implies that all libraries need to be rebuilt when ghc is rebuilt.

# use the following command to print out packages that need to get rebuilt
# grep -r ghc /var/abs/ | awk -F '/' '{ print $5; }' | sort -u

# Contributor: Wolfgang Bumiller <archbsd at bumiller dot com>
#   Note that I just added the freebsd bootstrapping
#   The comments in check() are still from ArchLinux

# 2: build the bootstrap wrapper libs, unpack and install the bootstrap ghc
# 1: use the existing bootstrap ghc - useful for makepkg -eR retries
# 0: use the system-installed ghc
_ghc_bootstrap=0
pkgname=ghc
pkgver=7.10.2
_pkgver=7.10.2
pkgrel=3
pkgdesc='The Glasgow Haskell Compiler'
arch=('i686' 'amd64')
url='http://www.haskell.org/ghc/'
license=('custom')
depends=('perl' 'gmp' 'gcc' 'libffi' 'gnu-libiconv' 'gnu-binutils')
makedepends=('perl' 'libxslt' 'docbook-xsl' 'pacman>=4.1.2-22')
(( _ghc_bootstrap )) || makedepends=('ghc' "${makedepends[@]}")
checkdepends=('python2')
install='ghc.install'
options=('staticlibs' '!clang')
options=('staticlibs')
provides=('haskell-array=0.5.1.0'
          'haskell-base=4.8.1.0'
          'haskell-bin-package-db=0.0.0.0'
          'haskell-binary=0.7.5.0'
          'haskell-bytestring=0.10.6.0'
          'haskell-containers=0.5.6.2'
          'haskell-deepseq=1.4.1.1'
          'haskell-directory=1.2.2.0'
          'haskell-filepath=1.4.0.0'
          'haskell-ghc-prim=0.4.0.0'
          'haskell-hoopl=3.10.0.2'
          'haskell-hpc=0.6.0.2'
          'haskell-integer-gmp=0.5.1.0'
          'haskell-integer-gmp2=1.0.0.0'
          'haskell-pretty=1.1.2.0'
          'haskell-process=1.2.3.0'
          'haskell-template-haskell=2.10.0.0'
          'haskell-time=1.5.0.1'
          'haskell-transformers=0.4.2.0'
          'haskell-unix=2.7.1.0'
          'haskell-cabal=1.22.4.0'
        )
replaces=('haskell-array'
          'haskell-base'
          'haskell-bin-package-db'
          'haskell-binary'
          'haskell-bytestring'
          'haskell-containers'
          'haskell-deepseq'
          'haskell-directory'
          'haskell-filepath'
          'haskell-ghc-prim'
          'haskell-hoopl'
          'haskell-hpc'
          'haskell-integer-gmp'
          'haskell-integer-gmp2'
          'haskell-pretty'
          'haskell-process'
          'haskell-template-haskell'
          'haskell-time'
          'haskell-transformers'
          'haskell-unix'
          'haskell-cabal'
        )
source=("http://www.haskell.org/ghc/dist/${pkgver}/ghc-${pkgver}-src.tar.xz"
        "http://www.haskell.org/ghc/dist/${pkgver}/ghc-${pkgver}-testsuite.tar.xz"
        'build.mk'
        'ghc')
md5sums=('fc1269fbc52513fa6b2ae6051c365bc3'
         '59cf83c92186502a15dcea4dbcc64c26'
         '9fd78f8e704445f5cf07a512b0dfb26e'
         'c1d46d86752c50dd85e7143b029fa07f')

prepare() {
  cd ghc-${_pkgver}
  autoreconf

  sed -i '' -e "s|%%CC%%|${CC}|g" \
    -e 's|%%AR%%|/usr/bin/ar|g' \
    -e 's|%%LD%%|/usr/bin/ld|g' \
    libraries/Cabal/Cabal/Distribution/Simple/Program/Builtin.hs

  cp ${srcdir}/build.mk mk/build.mk

  echo >> mk/build.mk "SRC_CC_OPTS = ${CFLAGS}"
  echo >> mk/build.mk "SRC_LD_OPTS = -L/usr/local/lib"
  echo >> mk/build.mk "EXTRA_CABAL_CONFIGURE_FLAGS = --extra-include-dirs=/usr/local/include"
  echo >> mk/build.mk "EXTRA_CABAL_CONFIGURE_FLAGS = --extra-lib-dirs=/usr/local/lib"

}

_bootstrap_build() {
  msg "BOOTSTRAP BUILD..."
  sleep 1

  cd "${srcdir}"
  rm -rf bootstrap lib_compat usr
  mkdir bootstrap
  mkdir lib_compat
  mkdir usr

  msg "Unpacking bootstrap archive..."
  bootstrapfile="ghc-${_pkgver}-x86_64-portbld-freebsd.tar.xz"
  if ! [ -f "../${bootstrapfile}" ]; then
    (cd .. && fetch "http://www.haskell.org/ghc/dist/${_pkgver}/${bootstrapfile}")
  fi
  tar -C bootstrap -xf "../${bootstrapfile}"

  unset LDFLAGS
  cd bootstrap/ghc-${_pkgver}
    ./configure --prefix="${srcdir}/usr" \
    --with-iconv-includes=/usr/local/include \
    --with-iconv-libraries=/usr/local/lib \
    --with-system-libffi \
    --with-ffi-includes=/usr/lib/libffi-3.2.1/include

  gmake install
}

build() {
  if (( _ghc_bootstrap )); then
    (( _ghc_bootstrap > 1 )) && _bootstrap_build
    export PATH="${srcdir}/usr/bin:${PATH}"
  fi

  cd ghc-${_pkgver}
  export LDFLAGS+=" -B/usr/local/bin -L/usr/local/lib"
  ./configure \
    --prefix=/usr \
    --with-system-libffi \
    --with-ffi-includes=/usr/lib/libffi-3.2.1/include \
    --with-iconv-includes=/usr/local/include \
    --with-iconv-libraries=/usr/local/lib \


   LC_ALL=en_US.UTF-8 gmake V=1  LD="/usr/local/bin/ld"
}

check() {
  # keep this otherwise a lot of tests will show as unexpected failures... need to investigate why using -jn for n > 1 does this...
  unset MAKEFLAGS

  cd ghc-${_pkgver}

  # python2 rename
  #gsed -e 's/PYTHON = python/&2/' -i testsuite/mk/boilerplate.mk
  gsed -e 's_#!/usr/bin/env python_&2_' -i testsuite/timeout/calibrate testsuite/timeout/timeout.py

  # upstream known failures on both i686 and x86_64:
  #    ghci.debugger/scripts     break011 [bad stderr] (ghci)
  #    perf/compiler             parsing001 [stat not good enough] (normal)
  #    simplCore/should_compile  spec-inline [stderr mismatch] (optasm)
  # upstream known failures on i686 only:
  #    numrun012 test fails on i686 works on x86_64 - http://hackage.haskell.org/trac/ghc/ticket/5856
  LC_ALL=en_US.UTF-8 gmake test

  # enabling threads to speed up
  # causes more failures than make test, need to investigate
  # upstream known failures on both i686 and x86_64:
  #    perf/compiler             parsing001 [stat not good enough] (normal)
  #    simplCore/should_compile  spec-inline [stderr mismatch] (optasm)
  # upstream known failures on i686 only:
  #    numrun012 test fails on i686 works on x86_64 - http://hackage.haskell.org/trac/ghc/ticket/5856
  #gmake THREADS=9 test

  # will most likely cause lots of failures
  # enable this when upstream has 0 test failures from a simple 'make test'
  # zero unexpected failures on all tier 1 platforms - http://hackage.haskell.org/trac/ghc/ticket/5757
  #gmake fulltest
}

package() {
  cd ghc-${_pkgver}

  gmake DESTDIR=${pkgdir} install

  install -d ${pkgdir}/usr/share/licenses/ghc
  install -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/ghc

  install -d ${pkgdir}/usr/share/bash-completion/completions
  install -m644 ${srcdir}/ghc \
    ${pkgdir}/usr/share/bash-completion/completions

  if (( _ghc_bootstrap )); then
    msg "YOU COMPILED A BOOTSTRAP GHC"
    msg "Delete the previously created /usr/lib/libiconv.so symlinks"
    msg "Install this package and rebuild..."
  fi
}

