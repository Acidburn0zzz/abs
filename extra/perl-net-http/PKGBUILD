# Port: net/p5-Net-HTTP

pkgname=perl-net-http
pkgver=6.17
pkgrel=1
pkgdesc="Low-level HTTP connection (client)"
arch=('any')
url='http://search.cpan.org/dist/Net-HTTP'
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl')
checkdepends=('perl-uri')
conflicts=('perl-libwww<6')
source=("http://cpan.metacpan.org/modules/by-module/Net/Net-HTTP-${pkgver}.tar.gz")
sha256sums=('1e8624b1618dc6f7f605f5545643ebb9b833930f4d7485d4124aa2f2f26d1611')

build() {
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd Net-HTTP-${pkgver}
    /usr/bin/perl Makefile.PL
    make
}

check() {
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd Net-HTTP-${pkgver}
    make test
}

package() {
    cd Net-HTTP-${pkgver}
    make DESTDIR="$pkgdir" install
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
