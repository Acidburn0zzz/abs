# $Id: PKGBUILD 146400 2012-01-10 20:33:06Z andrea $
# Maintainer:
# Contributor: François Charette <firmicus ατ gmx δοτ net> 
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Michal Hybner <dta081@gmail.com>

pkgname=wavpack
pkgver=4.60.1
pkgrel=8
pkgdesc="Audio compression format with lossless, lossy, and hybrid compression modes"
arch=('i686' 'x86_64')
url="http://www.wavpack.com/"
license=('custom')
depends=('libc') # glibc in world
options=('!libtool')
port_patches=(patch-src__wavpack_local.h)
source=("http://www.wavpack.com/${pkgname}-${pkgver}.tar.bz2"
	${port_patches[@]})
md5sums=('7bb1528f910e4d0003426c02db856063'
         'aca65401458fe7ba546eca2a527d0cbc')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  for _p in ${port_patches[@]}; do
	msg "Applying patch $_p"
	patch -p0 -i "${srcdir}/$_p"
  done
}
build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --enable-mmx \
    --disable-static
  gmake
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  gmake DESTDIR=${pkgdir} install

  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -Dm644 license.txt \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
