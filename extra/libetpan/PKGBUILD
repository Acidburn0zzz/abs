# $Id: PKGBUILD 212058 2014-05-05 19:13:40Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libetpan
pkgver=1.5
pkgrel=8
pkgdesc="A portable middleware for email access"
arch=('i686' 'x86_64')
url="http://www.etpan.org/libetpan.html"
license=("custom:etpan")
depends=('libsasl' 'curl' 'expat' 'gcc-libs')
source=(https://github.com/dinhviethoa/${pkgname}/archive/${pkgver}.tar.gz)
md5sums=('f5a0ae8071937b31d0facc1f2d874ead')

build() {
  cd ${pkgname}-${pkgver}
  
  # generate automake files that are not included in the tarball
  libtoolize --force --copy
  aclocal -I m4
  autoheader
  autoconf
  automake --add-missing --foreign --force --copy
  #autoreconf -vfi

  ./configure --prefix=/usr \
    --disable-static \
    --disable-db
  gmake
}

check() {
  cd ${pkgname}-${pkgver}
  gmake check
  cd tests
  gmake imap-sample
}

package() {
  cd ${pkgname}-${pkgver}
  gmake DESTDIR=${pkgdir} install
  install -dm755 ${pkgdir}/usr/share/licenses/$pkgname/
  install -m644 COPYRIGHT ${pkgdir}/usr/share/licenses/$pkgname/license.txt
}
