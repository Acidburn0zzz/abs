pkgname=yajl
pkgver=2.1.0
pkgrel=2
pkgdesc='Yet Another JSON Library.'
arch=('i386' 'amd64')
url='http://lloyd.github.com/yajl/'
license=('ISC')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/lloyd/$pkgname/archive/$pkgver.tar.gz")
md5sums=('6887e0ed7479d2549761a4d284d3ecb0')

build() {
  cd "$pkgname-$pkgver"

  cmake -DCMAKE_INSTALL_PREFIX=/usr .

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/usr/share/licenses/${pkgname}"
  install -m644 COPYING "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"

  # Could just sed the CMakeList to correct this, but I'm lazy
  install -dm755 "${pkgdir}/usr/libexec"
  mv "${pkgdir}/usr/share/pkgconfig" "${pkgdir}/usr/libexec/pkgconfig"
}

