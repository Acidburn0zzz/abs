pkgname=screen
pkgver=4.3.1
pkgrel=2
_ptygroup=4 #the UID of our PTY/TTY group
pkgdesc="Full-screen window manager that multiplexes a physical terminal"
arch=('amd64')
url="http://www.gnu.org/software/screen/"
license=('GPL')
depends=('ncurses') # 'pam')
backup=('etc/screenrc' 'etc/pam.d/screen')
options=('!makeflags')
install=screen.install
source=(http://ftp.gnu.org/gnu/screen/screen-${pkgver}.tar.gz
        screen.pam)
md5sums=('5bb3b0ff2674e29378c31ad3411170ad'
         '74f7919d7b65436cc2ea5341fa8ca374')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  ./configure --prefix=/usr --enable-pam \
        --with-sys-screenrc=/etc/screenrc --enable-colors256 \
       --with-pty-group=$_ptygroup \
       --mandir=/usr/share/man --infodir=/usr/share/info \
       --with-socket-dir=/run/screens
  gmake
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  gmake DESTDIR=${pkgdir} install

  install -dm755 ${pkgdir}/etc/pam.d
  install -m644 ${srcdir}/screen.pam ${pkgdir}/etc/pam.d/screen

  install -dm755 ${pkgdir}/usr/share/zsh/site-functions
  install -dm755 ${pkgdir}/etc
  install -dm755 ${pkgdir}/etc/skel
  install -m644 etc/completer.zsh ${pkgdir}/usr/share/zsh/site-functions/screen
  install -m644 etc/etcscreenrc ${pkgdir}/etc/screenrc
  install -m644 etc/screenrc ${pkgdir}/etc/skel/.screenrc
}
