# Port: sysutils/polkit
pkgname=polkit
pkgver=0.113
pkgrel=3
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=(i386 amd64)
license=('LGPL')
url="http://www.freedesktop.org/wiki/Software/polkit"
depends=('glib2' 'expat' 'js185' 'dbus')
makedepends=('intltool' 'gtk-doc' 'gobject-introspection')
options=('libtoolfix')
install=polkit.install
source=("http://www.freedesktop.org/software/polkit/releases/$pkgname-$pkgver.tar.gz")
md5sums=('4b77776c9e4f897dcfe03b2c34198edf')

build() {
  cd $pkgname-$pkgver
   
   export CPPFLAGS="${CPPFLAGS} -I/usr/local/include"

  ./configure --prefix=/usr --disable-gtk-doc-html \
		--without-html-dir \
		--sysconfdir=/etc \
		--with-authfw=pam \
		--with-pam-include=system \
		--with-os-type=freebsd \
		--with-polkitd-user=polkitd \
		--disable-static \
		--disable-examples \
		--with-mozjs=mozjs185 \
		--disable-libsystemd-login \
		--disable-test \
		--enable-introspection=yes \
		--libexecdir=/usr/libexec/polkit-1 \
		--localstatedir=/var --disable-gtk-doc --disable-man-pages
  gmake
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install

  chown -R 565 "$pkgdir/etc/polkit-1"
  chown -R 565 "$pkgdir/usr/share/polkit-1"

  install -dm755 "${pkgdir}/etc/dbus-1/system.d/"   
  install "${srcdir}/${pkgname}-${pkgver}/data/org.freedesktop.PolicyKit1.conf" \
		"${pkgdir}"/etc/dbus-1/system.d/org.freedesktop.PolicyKit1.conf.dist

  for i in 10-vendor.d 20-org.d 30-site.d 50-local.d 90-mandatory.d; do
    mkdir -p "${pkgdir}/etc/polkit-1/localauthority/${i}"
    mkdir -p "${pkgdir}/var/lib/polkit-1/localauthority/${i}"
  done

}
