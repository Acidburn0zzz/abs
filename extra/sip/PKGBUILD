# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: riai <riai@bigfoot.com>, Ben <ben@benmazer.net>

pkgbase=sip
pkgname=('sip' 'python-sip' 'python2-sip')
pkgver=4.16.9
pkgrel=2
arch=('i386' 'amd64')
url='http://www.riverbankcomputing.com/software/sip/intro'
license=('custom:"sip"')
makedepends=('python' 'python2')
source=("http://downloads.sourceforge.net/pyqt/${pkgbase}-${pkgver}.tar.gz")
md5sums=('7a1dfff4e6fade0e4adee2c4e3d3aa9a')

build() {
  cp -r ${pkgbase}-${pkgver} python2-${pkgbase}-${pkgver}

  cd ${pkgbase}-${pkgver}
  sed -e 's/gcc/'"$CC"'/g' \
      -e 's/g\+\+/'"$CXX"'/g' \
      specs/freebsd-g++ > specs/freebsd-clang
  python configure.py -p freebsd-clang CFLAGS="${CFLAGS}" LFLAGS="${LDFLAGS}" CC="$CC" CXX="$CXX"
  gmake

  ### Python2 version ###
  cd ../python2-${pkgbase}-${pkgver}
  sed -e 's/gcc/'"$CC"'/g' \
      -e 's/g\+\+/'"$CXX"'/g' \
      specs/freebsd-g++ > specs/freebsd-clang
  python2 configure.py -p freebsd-clang CFLAGS="${CFLAGS}" LFLAGS="${LDFLAGS}" CC="$CC" CXX="$CXX"
  gmake
}

package_sip() {
  pkgdesc="A tool that makes it easy to create Python bindings for C and C++ libraries"
  depends=('libc')

  cd ${pkgbase}-${pkgver}
  gmake DESTDIR="${pkgdir}" install -C sipgen

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python-sip() {
  pkgdesc="Python 3.x SIP bindings for C and C++ libraries"
  depends=('sip' 'python')

  cd ${pkgbase}-${pkgver}
  gmake DESTDIR="${pkgdir}" install -C siplib

  install -Dm644 sipconfig.py "${pkgdir}"/usr/lib/python3.5/site-packages/sipconfig.py
  install -Dm644 sipdistutils.py "${pkgdir}"/usr/lib/python3.5/site-packages/sipdistutils.py

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-sip() {
  pkgdesc="Python 2.x SIP bindings for C and C++ libraries"
  depends=('sip' 'python2')

  cd python2-${pkgbase}-${pkgver}
  gmake DESTDIR="${pkgdir}" install -C siplib

  install -Dm644 sipconfig.py "${pkgdir}"/usr/lib/python2.7/site-packages/sipconfig.py
  install -Dm644 sipdistutils.py "${pkgdir}"/usr/lib/python2.7/site-packages/sipdistutils.py

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

