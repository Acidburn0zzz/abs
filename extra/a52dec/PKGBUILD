# $Id: PKGBUILD 149328 2012-02-06 20:28:08Z stephane $
# Maintainer:
# Contributor: Thomas Baechler <thomas@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=a52dec
pkgver=0.7.4
pkgrel=1
pkgdesc="A free library for decoding ATSC A/52 streams"
url="http://liba52.sourceforge.net/"
arch=('i386' 'amd64')
license=('GPL2')
depends=('libc')
options=(!libtool 'libtoolfix')
source=("http://liba52.sourceforge.net/files/${pkgname}-${pkgver}.tar.gz"
        'a52dec-0.7.4-build.patch')
sha256sums=('a21d724ab3b3933330194353687df82c475b5dfb997513eef4c25de6c865ec33'
            '00a094d6178cd21bfc2303ff2ac91ce656810e6fef50e77ab30e18d0f4d4a650')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/a52dec-0.7.4-build.patch"

  sed -i '' -e 's|machine/soundcard.h|sys/soundcard.h|g' libao/audio_out_oss.c

  ./configure --prefix=/usr \
	--mandir=/usr/share/man \
	--enable-shared
  gmake CFLAGS="${CFLAGS} -fPIC"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m644 liba52/a52_internal.h "${pkgdir}/usr/include/a52dec/"
}
