# $Id: PKGBUILD 211973 2014-05-02 12:02:18Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgbase=phonon-gstreamer
pkgname=('phonon-qt4-gstreamer' 'phonon-qt5-gstreamer')
pkgver=4.8.2
pkgrel=1
arch=('i386' 'amd64')
url='http://phonon.kde.org/'
license=('LGPL')
makedepends=('gst-plugins-base' 'cmake' 'automoc4' 'phonon-qt4' 'phonon-qt5')
source=("http://download.kde.org/stable/phonon/phonon-backend-gstreamer/${pkgver}/src/phonon-backend-gstreamer-${pkgver}.tar.xz")
md5sums=('ce441035dc5a00ffaac9a64518ab5c62')

prepare() {
  mkdir build-qt4
  mkdir build-qt5
}

build() {
  cd build-qt4
  cmake ../phonon-backend-gstreamer-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_LIBDIR=lib
  make

  cd ../build-qt5
  cmake ../phonon-backend-gstreamer-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DPHONON_BUILD_PHONON4QT5=ON
  gmake
}

package_phonon-qt4-gstreamer() {
  pkgdesc="Phonon GStreamer backend for Qt4"
  depends=('gstreamer0.10-base-plugins' 'qt4' 'phonon-qt4')
  provides=('phonon-backend' 'phonon-qt4-backend')
  replaces=('phonon-gstreamer')
  conflicts=('phonon-gstreamer')
  install="phonon-qt4-gstreamer.install"

  cd build-qt4
  gmake DESTDIR="${pkgdir}" install
}

package_phonon-qt5-gstreamer() {
  pkgdesc="Phonon GStreamer backend for Qt5"
  depends=('gstreamer0.10-base-plugins' 'qt5-base' 'phonon-qt5')
  provides=('phonon-qt5-backend')
  replaces=('phonon-gstreamer')
  conflicts=('phonon-gstreamer')

  cd build-qt5
  gmake DESTDIR="${pkgdir}" install

  # Those are not needed
  rm -rf "${pkgdir}"/usr/share
}
