pkgname=tk
pkgver=8.6.4
pkgrel=1
pkgdesc="A windowing toolkit for use with tcl"
arch=('i386' 'amd64')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=("tcl=${pkgver}" 'libxss' 'libxft')
source=(http://downloads.sourceforge.net/sourceforge/tcl/tk${pkgver}-src.tar.gz)
md5sums=('261754d7dc2a582f00e35547777e1fea')

build() {
  cd ${srcdir}/tk${pkgver}/unix

  [[ $CARCH == "amd64" ]] && BIT="--enable-64bit"
  ./configure --prefix=/usr --mandir=/usr/share/man \
              --enable-threads \
              --enable-shared \
              --enable-xft \
              $BIT
  gmake
}

#check() {
#  cd ${srcdir}/tk${pkgver}/unix
#  make test
#}

package() {
  cd ${srcdir}/tk${pkgver}/unix

  gmake -j1 INSTALL_ROOT=${pkgdir} install install-private-headers
  ln -sf wish${pkgver%.*} "${pkgdir}/usr/bin/wish"
  ln -sf libtk${pkgver%.*}.so "${pkgdir}/usr/lib/libtk.so"

  # install license
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 license.terms ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # remove buildroot traces
  gsed -e "s#${srcdir}/tk${pkgver}/unix#/usr/lib#" \
      -e "s#${srcdir}/tk${pkgver}#/usr/include#" \
      -i "${pkgdir}/usr/lib/tkConfig.sh"
}
