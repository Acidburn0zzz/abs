# Port: x11-toolkits/tk86
pkgname=tk
pkgver=8.6.5
pkgrel=2
pkgdesc="A windowing toolkit for use with tcl"
arch=('i386' 'amd64')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=("tcl=${pkgver}" 'libxss' 'libxft')
patchdir="tk${pkgver}/unix"
source=(http://downloads.sourceforge.net/sourceforge/tcl/tk${pkgver}-src.tar.gz)
md5sums=('11dbbd425c3e0201f20d6a51482ce6c4')

prepare() {
  cd ${srcdir}/tk${pkgver}/unix

  sed -i '' -e \
    's|@TK_BUILD_LIB_SPEC@|@TK_LIB_SPEC@|' \
    -e 's|@TK_BUILD_STUB_LIB_SPEC@|@TK_STUB_LIB_SPEC@|' \
    -e 's|@TK_BUILD_STUB_LIB_PATH@|@TK_STUB_LIB_PATH@|' \
    -e 's|@TK_SRC_DIR@|${PREFIX}/include/tk8.6|' \
     tkConfig.sh.in
}
build() {
  cd ${srcdir}/tk${pkgver}/unix
  unset LDFLAGS

  [[ $CARCH == "amd64" ]] && BIT="--enable-64bit"
  ./configure --prefix=/usr --mandir=/usr/share/man \
              --enable-threads \
              --enable-shared \
              --enable-xft \
              $BIT
  gmake SHORT_TK_VER=86
}

#check() {
#  cd ${srcdir}/tk${pkgver}/unix
#  make test
#}

package() {
  cd ${srcdir}/tk${pkgver}/unix

  gmake -j1 INSTALL_ROOT=${pkgdir} install install-private-headers
  ln -sf wish${pkgver%.*} "${pkgdir}/usr/bin/wish"
  ln -sf libtk86.so.1 "${pkgdir}/usr/lib/libtk86.so"
  # install license
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 license.terms ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # remove buildroot traces
  gsed -e "s#${srcdir}/tk${pkgver}/unix#/usr/lib#" \
      -e "s#${srcdir}/tk${pkgver}#/usr/include#" \
      -i "${pkgdir}/usr/lib/tk8.6/tkConfig.sh"
}
