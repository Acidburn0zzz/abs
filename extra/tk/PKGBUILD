# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=tk
pkgver=8.6.3
_includever=8.6
pkgrel=1
pkgdesc="A windowing toolkit for use with tcl"
arch=('i686' 'amd64')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=("tcl=${pkgver}" 'libxss' 'libxft')
patchdir=(tk${pkgver}/unix)
source=(http://downloads.sourceforge.net/sourceforge/tcl/tk${pkgver}-src.tar.gz)
md5sums=('85ca4dbf4dcc19777fd456f6ee5d0221')


build() {
  cd ${srcdir}/tk${pkgver}/unix

  [[ $CARCH == "x86_64" ]] && BIT="--enable-64bit"
  ./configure --prefix=/usr --mandir=/usr/share/man \
              --enable-threads \
              --enable-shared \
              --enable-xft \
              $BIT
  gmake SHORT_TK_VER=86
}

#check() {
#  cd ${srcdir}/tk${pkgver}/unix
#  make test
#}

package() {
  cd ${srcdir}/tk${pkgver}/unix

  gmake -j1 SHORT_TK_VER=86 INSTALL_ROOT=${pkgdir} install install-private-headers
  ln -sf wish8.6 ${pkgdir}/usr/bin/wish

  # install license
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 license.terms ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # remove buildroot traces
  sed -i '' -e "s#${srcdir}#/usr/src#" ${pkgdir}/usr/lib/tk${_includever}/tkConfig.sh
}
