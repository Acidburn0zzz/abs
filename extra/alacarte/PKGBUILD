# $Id: PKGBUILD 197851 2013-10-30 11:15:28Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: pressh <pressh@gmail.com>

pkgname=alacarte
pkgver=3.11.91+12+g1bb265f
pkgrel=1
pkgdesc="Menu editor for gnome"
arch=('any')
license=('LGPL')
url="http://www.gnome.org"
depends=('gnome-menus' 'hicolor-icon-theme' 'python2-gobject' 'gtk3')
makedepends=('intltool' 'libxslt' 'docbook-xsl' 'git' 'gnome-common')
install=alacarte.install
source=('git://git.gnome.org/alacarte#commit=1bb265f7')
sha256sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe --long | sed 's/-/+/g'
}

build() {
  cd "${pkgname}"

  NOCONFIGURE=1 ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var PYTHON=/usr/bin/python2
  gmake
}

package() {
  cd "${pkgname}"
  gmake DESTDIR="$pkgdir" install
  python2 -m compileall "${pkgdir}/usr/lib/python2.7/site-packages/Alacarte/"
}
