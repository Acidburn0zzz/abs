# port: devel/apache-ant

pkgbase=apache-ant
pkgname=('apache-ant' 'apache-ant-doc')
pkgver=1.9.6
pkgrel=1
arch=('i386' 'amd64')
url='http://ant.apache.org/'
license=('APACHE')
makedepends=('java-environment' 'junit')
source=(http://archive.apache.org/dist/ant/source/${pkgbase}-${pkgver}-src.tar.bz2
	http://archive.apache.org/dist/ant/binaries/apache-ant-1.9.4-bin.zip
        apache-ant.sh
        apache-ant.csh
        apache-ant.install
        bin_ant
        ant.conf)
sha256sums=('2ad98dd3a4805452e546e0f5b682360370ae11692a2157fff8ec6d177c957123'
            '973d97d656e65561e2753823de6cf6db35570aaf93eeec6e7cb3d68e1ec4d0e8'
            '182b9212610790966d3a018d3cbab2e5327dd9fd11dc15dd1f377853216f5718'
            '919a3ab1acae1d0d190fe6e0034b00975caab40e55359a720bfccd098fe2d995'
            '3de451d0f963ba96e83f4e068a8325e3d4fc42b1e5c4818e9d7962ed4f5393b3'
            '29b443ae3af7e246b4ff0b5ec9f023ae95992db1a010a362e70ca7a65a6df461'
            'b86ce60f61cbd85a9faa61d698b0fc4607f1ff375cd15673aee57f053012eacb')

_ant_home=/usr/share/${pkgbase}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  # ant needs HOME to download libs to $HOME/.m2/repository
  export ANT_OPTS="-Duser.home=${srcdir}"
  ${srcdir}/apache-ant-1.9.4/bin/ant -Ddest=optional -f fetch.xml

  rm lib/optional/junit-4.11.jar
  ln -s /usr/share/java/junit.jar lib/optional/junit-4.11.jar
  rm lib/optional/hamcrest-core-1.3.jar
  ln -s /usr/share/java/hamcrest-core.jar lib/optional/hamcrest-core-1.3.jar

  ${srcdir}/apache-ant-1.9.4/bin/ant dist
}

package_apache-ant() {
  pkgdesc='A java-based build tool'
  depends=('java-environment')
  optdepends=('junit: to use the junit tasks')
  install=apache-ant.install

  cd "${srcdir}/${pkgbase}-${pkgver}/${pkgbase}-${pkgver}"

  install -dm755 "${pkgdir}${_ant_home}"

  install -dm755 "${pkgdir}/etc/${pkgbase}"
  cp -R etc/* "${pkgdir}/etc/${pkgbase}"
  rm "${pkgdir}/etc/${pkgbase}/ant-bootstrap.jar"
  ln -s /etc/${pkgbase} "${pkgdir}${_ant_home}/etc"

  install -dm755 "${pkgdir}${_ant_home}/bin"
  find bin -type f -a ! -name \*.bat -a ! -name \*.cmd \
    -exec install -m755 {} "${pkgdir}${_ant_home}/bin" \;

  install -dm755 "${pkgdir}/usr/bin"
  install -m755 "${srcdir}/bin_ant"  "${pkgdir}/usr/bin/ant"
  install -m644 "${srcdir}/ant.conf" "${pkgdir}/etc/apache-ant/ant.conf"

  install -dm755 "${pkgdir}/usr/share/java/${pkgbase}"
  install -m644 lib/*.jar "${pkgdir}/usr/share/java/${pkgbase}"
  ln -s /usr/share/java/${pkgbase} "${pkgdir}${_ant_home}/lib"

  # fix python2 path
  gsed -i 's|/usr/bin/python|/usr/bin/python2|' "${pkgdir}${_ant_home}/bin/runant.py"

  # install profile.d script
  install -dm755 "${pkgdir}/etc/profile.d"
  install -m755 "${srcdir}"/apache-ant.{csh,sh} "${pkgdir}/etc/profile.d/"

  # License says NOTICE file should be redistributed for derivative works
  cd "${srcdir}/${pkgbase}-${pkgver}"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgbase}"
  install -m644 LICENSE NOTICE "${pkgdir}/usr/share/licenses/${pkgbase}"
}

package_apache-ant-doc() {
  pkgdesc='Apache Ant build tool documentation'

  install -dm755 "${pkgdir}/usr/share/doc/${pkgbase}"
  cp -r "${srcdir}"/${pkgbase}-${pkgver}/${pkgbase}-${pkgver}/manual/* \
    "${pkgdir}/usr/share/doc/${pkgbase}/"
}

