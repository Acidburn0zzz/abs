# $Id: PKGBUILD 183529 2013-04-22 13:10:36Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=p11-kit
pkgver=0.22.1
pkgrel=2
pkgdesc="Library to work with PKCS#11 modules"
arch=(i686 x86_64)
url="http://p11-glue.freedesktop.org"
license=('BSD')
depends=('libtasn1' 'libffi')
options=('libtoolfix')
source=($url/releases/$pkgname-$pkgver.tar.gz{,.sig})
md5sums=('4e9bea1106628ffb820bdad24a819fac'
         'SKIP')

prepare() {
  cd $pkgname-$pkgver

  libtoolize --force --copy
  aclocal -I m4
  autoheader
  autoconf
  automake --add-missing --foreign --force --copy

  find ${srcdir}/${pkgname}-${pkgver} -name "configure" -type f | xargs sed -i '' -e 's|-lpthread|-pthread|g'
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc \
    --with-module-path=/usr/lib/pkcs11 \
    --with-trust-paths=/usr/share/certs/ca-root-nss.crt \
    LDFLAGS="-pthread"
  gmake
}

check() {
  cd $pkgname-$pkgver
  gmake check
}

package() {
  cd  $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
  install -dm755 $pkgdir/usr/share/licenses/$pkgname/
  install -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}

# vim:set ts=2 sw=2 et:
