# $Id: PKGBUILD 183529 2013-04-22 13:10:36Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=p11-kit
pkgver=0.20.2
pkgrel=1
pkgdesc="Library to work with PKCS#11 modules"
arch=(i686 x86_64)
url="http://p11-glue.freedesktop.org"
license=('BSD')
depends=('libtasn1' 'libffi')
options=('!libtool' 'libtoolfix')
port_patches=(
	patch-common-tests-Makefile.in
)
source=($url/releases/$pkgname-$pkgver.tar.gz{,.sig}
	${port_patches[@]})
md5sums=('757b97ee4ac0ce598661a90cd784c4f1'
         'SKIP'
         '8569cef99c354d5cc99f39d3951fac2e')

prepare() {
  cd $pkgname-$pkgver

  for p in ${port_patches[@]}; do
    msg "Applying patch ${p}"
    patch -p0 -i "${srcdir}/${p}"
  done

  find ${srcdir}/${pkgname}-${pkgver} -name "configure" -type f | xargs sed -i '' -e 's|-lpthread|-pthread|g'

  libtoolize --force --copy
  aclocal -I m4
  autoheader
  autoconf
  automake --add-missing --foreign --force --copy

}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc \
    --with-module-path=/usr/libexec/pkcs11 \
    --with-trust-paths=/usr/share/certs/ca-root-nss.crt \
    LDFLAGS="-pthread"
  gmake
}

check() {
  cd $pkgname-$pkgver
  gmake check
}

package() {
  cd  $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
  install -dm755 $pkgdir/usr/share/licenses/$pkgname/
  install -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}

# vim:set ts=2 sw=2 et:
