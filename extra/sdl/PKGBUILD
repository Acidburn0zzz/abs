# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=sdl
pkgver=1.2.15
pkgrel=9
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard"
arch=('i686' 'x86_64')
url="http://www.libsdl.org"
license=('LGPL')
depends=('libxext' 'libxrender' 'libx11')
makedepends=('nasm' 'mesa' 'glu' 'libpulse')
#optdepends=('libpulse: PulseAudio audio driver')
makedepends=('nasm' 'mesa' 'glu')
options=('!libtool')
source=(http://www.libsdl.org/release/SDL-${pkgver}.tar.gz
	patch-src_cdrom_freebsd_SDL_syscdrom.c
	patch-src_joystick_bsd_SDL_sysjoystick.c
	patch-src_video_x11_SDL_x11sym.h
)
md5sums=('9d96df8417572a2afb781a7c4c811a85'
         '95daaf7b18a2b197c66e92f8ce81d8f6'
         '6e3aba616d04afd4901e3a97be8430da'
         '57e3ad336e7a49562874644c1cf28310')

build() {
  cd SDL-$pkgver
  for i in patch-src_video_x11_SDL_x11sym.h \
           patch-src_cdrom_freebsd_SDL_syscdrom.c \
           patch-src_joystick_bsd_SDL_sysjoystick.c
  do
    patch -p0 -i "${srcdir}/$i"
  done
  ac_cv_header_libusbhid_h=no \
  ./configure --prefix=/usr --disable-alsa --disable-alsatest \
              --with-x --disable-video-directfb \
              --enable-video-opengl \
              --enable-oss

  gmake
}

package() {
  cd SDL-$pkgver
  gmake DESTDIR="$pkgdir" install

	# Remove -Wl and -rpath, and duplicate -pthread from sdl.pc
  sed -i '' -e 's|-Wl,-rpath,${libdir} ||g' \
		-e 's|-lSDL  -pthread|-lSDL|g' ${pkgdir}/usr/lib/pkgconfig/sdl.pc
}
