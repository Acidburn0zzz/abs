# $Id: $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Adam Walters <abwalters@abwalters.us>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=libatomic_ops
pkgver=7.4.0
pkgrel=1
pkgdesc="Provides semi-portable access to hardware provided atomic memory operations"
arch=('i686' 'x86_64')
url="https://github.com/ivmai/libatomic_ops"
depends=('libc')
license=('GPL2' 'MIT')
source=(http://www.hpl.hp.com/research/linux/atomic_ops/download/libatomic_ops-${pkgver}.tar.gz)
md5sums=('59f9a7cc845dcc775e7b7901eb582766')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname"
  install -m644 doc/LICENSING.txt "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
