#
# This package is mostly there to build llvm parts which need it,
# and to provide c++filt

pkgname=gnu-binutils
pkgver=2.24
pkgrel=4
pkgdesc="Provides additional binutils like the gold linker."
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
#checkdepends=('dejagnu' 'bc')
options=('!libtool' '!distcc' '!ccache')
install=binutils.install
provides=('c++filt')
_port_patches=(
	patch-gold_Makefile.in
	patch-gold_config.in
	patch-gold_configure
	patch-gold_options.h
	patch-gold_script.cc
	patch-gold_stringpool.cc
	patch-gold_system.h
	patch-include__safe-ctype.h
)
source=(ftp://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.bz2{,.sig}
        "${_port_patches[@]}")
md5sums=('e0f71a7b2ddab0f8612336ac81d9636b'
         'SKIP'
         '34a666c39b1058217ed5ec3eafd02178'
         '271bc023b48bf871d5b1cefe8c23d6c8'
         '07dfa0f588bc6680b5eb01a9025341de'
         '15fe0b4936738bf7a3772e31892c0306'
         '57dda4c012f4b8908d720281e755cc69'
         'd304de358234f0f3068d245cd0599f75'
         'fbc7da78b8eb04504e6cc0b5f1907d6e'
         'fda8607043bbdd5cbfdae27879171b5c')

prepare() {
  cd "${srcdir}/binutils-${pkgver}"

  for i in "${_port_patches[@]}"; do
    msg "patch $i"
    patch -p0 -i "${srcdir}/$i"
  done

  find . -name Makefile.in \
    | xargs sed -i '' -e '/SUBDIRS =/s/ doc//'
}

build() {
  cd ${srcdir}

  mkdir binutils-build && cd binutils-build

  ${srcdir}/binutils-${pkgver}/configure --prefix=/usr/local \
    --with-lib-path=/usr/lib:/usr/local/lib \
    --with-bugurl=https://bugs.archbsd.net/ \
    --enable-gold --enable-plugins \
    --with-pic --enable-shared \
    --disable-werror --enable-multilib \
    --disable-nls

  cd ${srcdir}/binutils-build
  # check the host environment and makes sure all the necessary tools are available
  gmake configure-host
  gmake tooldir=/usr/local
}

check() {
  cd ${srcdir}/binutils-build

  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  gmake LDFLAGS="" -k check || true
}

package() {
  cd ${srcdir}/binutils-build
  gmake prefix=${pkgdir}/usr/local tooldir=${pkgdir}/usr/local install

  # Add some useful headers
  install -m644 ${srcdir}/binutils-${pkgver}/include/libiberty.h ${pkgdir}/usr/local/include
  install -m644 ${srcdir}/binutils-${pkgver}/include/demangle.h ${pkgdir}/usr/local/include

  # Install PIC libiberty.a
  install -m644 libiberty/pic/libiberty.a ${pkgdir}/usr/local/lib

  #Install Plugin.api
  install -m644 ${srcdir}/binutils-${pkgver}/include/plugin-api.h ${pkgdir}/usr/local/include

  # Remove unwanted files
  rm ${pkgdir}/usr/local/share/info/{configure,standards}.info

  # No shared linking to these files outside binutils
  rm ${pkgdir}/usr/local/lib/lib{bfd,opcodes}.so

  rm -rf "${pkgdir}/usr/local/share/info"
  # screw those
  rm -rf "${pkgdir}/usr/local/share/man"
}
