# $Id: PKGBUILD 128452 2011-06-24 01:23:15Z eric $
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Michael Baehr <usemike@spamblocked.com>

pkgname=ffcall
pkgver=1.10
pkgrel=9
pkgdesc="C library for implementing foreign function calls in embedded interpreters"
arch=('i386' 'amd64')
url="http://www.gnu.org/software/libffcall/"
license=('GPL2')
options=('!makeflags' 'staticlibs')
source=(http://www.haible.de/bruno/gnu/${pkgname}-${pkgver}.tar.gz)
md5sums=('2db95007e901f3bc2ae7e5a9fe9ebea4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  [ "$CARCH" = "amd64" ] && unset ac_cv_build ac_cv_host && CONFIGFLAG="--with-pic --build=x86_64-pacbsd-freebsd11"
  ./configure --prefix=/usr --mandir=/usr/share/man $CONFIGFLAG
  gmake
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake check || true
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -d "${pkgdir}"/usr/share/{man,doc/ffcall}
  gmake DESTDIR="${pkgdir}" htmldir=/usr/share/doc/ffcall install
}
