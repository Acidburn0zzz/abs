# Port: graphics/webp

pkgname=libwebp
pkgver=0.5.0
pkgrel=1
pkgdesc="WebP library and conversion tools"
arch=('i386' 'amd64')
url="https://developers.google.com/speed/webp/"
license=(BSD)
depends=(libpng libjpeg libtiff giflib)
optdepends=('freeglut: for vwebp')
makedepends=('freeglut')
source=("http://downloads.webmproject.org/releases/webp/$pkgname-$pkgver.tar.gz")
sha256sums=('5cd3bb7b623aff1f4e70bd611dc8dbabbf7688fd5eb225b32e02e09e37dfb274')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
     --enable-swap-16bit-csp \
     --enable-experimental \
     --enable-libwebpmux \
     --enable-libwebpdemux \
     --enable-libwebpdecoder \
     --disable-static \
     --with-pkgconfigdir=/usr/libdata
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
