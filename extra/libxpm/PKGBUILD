# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=libxpm
pkgver=3.5.11
pkgrel=1
pkgdesc="X11 pixmap library"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxt' 'libxext')
makedepends=('xorg-util-macros' 'pkg-config')
source=("${url}/releases/individual/lib/libXpm-${pkgver}.tar.bz2")
sha1sums=('77b95dd1c8cd9bc00b3b834b53d824409202acbb')

build() {
  cd "${srcdir}/libXpm-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/libXpm-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
