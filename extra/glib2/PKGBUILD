# $Id: PKGBUILD 172097 2012-11-28 10:31:12Z jgc $
# Maintainer: Anthony Donnelly <Amzo@archsbd.net>

pkgname=glib2
pkgver=2.44.1
pkgrel=1
pkgdesc="Common C routines used by GTK+ and other libs"
url="http://www.gtk.org/"
arch=(i686 x86_64)
license=('LGPL')
depends=('python2' 'libxslt' 'pcre' 'libffi' 'icu' 'gnu-libiconv' 'gettext-tools')
makedepends=('pkgconf' 'docbook-xml' 'gsed')
patchdir="$srcdir/glib-$pkgver"
options=('!docs' '!emptydirs' 'libtoolfix')
replaces=('gio-fam')
provides=('gio-fam')
install=glib2.install
source=(http://ftp.gnome.org/pub/GNOME/sources/glib/${pkgver%.*}/glib-$pkgver.tar.xz)
md5sums=('83efba4722a9674b97437d1d99af79db')

prepare() {
  cd "$srcdir/glib-$pkgver"


  CPPFLAGS+=" -DLIBICONV_PLUG"
  CFLAGS+=" -L/usr/local/lib -DLIBICONV_PLUG"
  CXXFLAGS+=" -L/usr/local/lib -DLIBICONV_PLUG"

  sed -i '' -e 's|inotify_support=yes|inotify_support=no|' \
            -e 's|-Werror||' \
            -e 's|#define HAVE_SYS_INOTIFY_H 1||' configure

}

build() {
  cd "$srcdir/glib-$pkgver"

# fix FS#34630 https://bugs.archlinux.org/task/34630
  export CFLAGS+=" -Wall"

  sed -i '' -e 's|-liconv||g' configure
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --libdir=/usr/lib \
      --sysconfdir=/etc \
      --enable-shared \
      --disable-gtk-doc --with-html-dir=/usr/share/doc \
      --disable-dtrace \
      --with-pcre=system \
      --target=$CHOST \
      --libexecdir=/usr/libexec \
      --disable-fam \
      PTHREAD_CFLAGS="" \
      PTHREAD_LIBS="-pthread" \
      ICONV_LIBS="-L/usr/local/lib -liconv" ac_cv_header_sys_inotify_h=

  gmake
}

#check() {
#  cd glib-$pkgver
#  gmake -k check : true
#}

package_glib2() {
  depends=('pcre' 'libffi')
  optdepends=('python2: for gdbus-codegen and gtester-report'
              'elfutils: gresource inspection tool')
  options=('!docs' '!emptydirs')
  license=('LGPL')

  cd glib-$pkgver
  make completiondir=/usr/share/bash-completion/completions DESTDIR="$pkgdir" install

  for _i in "$pkgdir/usr/share/bash-completion/completions/"*; do
      chmod -x "$_i"
  done

  # Our gdb does not ship the required python modules, so remove it
  rm -rf "$pkgdir/usr/share/gdb/"

  if [[ -f ${pkgdir}/usr/lib/charset.alias ]]; then
    rm ${pkgdir}/usr/lib/charset.alias
  fi

}

package_glib2-docs() {
  pkgdesc="Documentation for glib2"
  conflicts=('gobject2-docs')
  replaces=('gobject2-docs')
  license=('custom')
  options=('docs' '!emptydirs')
  
  cd glib-$pkgver/docs
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/glib2-docs"
  install -m644 reference/COPYING "${pkgdir}/usr/share/licenses/glib2-docs/"

  rm -rf "${pkgdir}/usr/share/man"
}

