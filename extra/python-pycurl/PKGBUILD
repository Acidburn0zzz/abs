_pypiname=pycurl
pkgbase=python-$_pypiname
pkgname=('python-pycurl' 'python2-pycurl')
pkgver=7.19.5.1
pkgrel=1
arch=('i686' 'amd64')
url="http://pycurl.sourceforge.net/"
license=('LGPL' 'MIT')
makedepends=('python' 'python2' 'curl' 'git')
source=("git+https://github.com/pycurl/pycurl.git#tag=REL_${pkgver//./_}")
sha1sums=('SKIP')

prepare() {
  cp -a $_pypiname{,-py2}

  cd "$srcdir/$_pypiname"
  find . -name '*.py' -exec sed -i -e "s|#! /usr/bin/env python|#!/usr/bin/env python3|" {} +
    gsed -i 's/"share", "doc", PACKAGE/"share", "doc", "python-pycurl"/' setup.py
  gsed -i "s/nosetests/nosetests3/" Makefile

  cd "$srcdir/$_pypiname-py2"
  find . -name '*.py' -exec sed -i -e "s|#! /usr/bin/env python|#!/usr/bin/env python2|" {} +
    gsed -i 's/"share", "doc", PACKAGE/"share", "doc", "python2-pycurl"/' setup.py
  gsed -i "s/nosetests/nosetests2/" Makefile
}

build() {
  cd "$srcdir/$_pypiname"
  make
  python3 setup.py build --with-ssl

  cd "$srcdir/$_pypiname-py2"
  make
  python2 setup.py build --with-ssl
}

package_python-pycurl() {
  pkgdesc="A Python 3.x interface to libcurl"
  depends=('python' 'curl')

  cd $_pypiname
  python setup.py install -O1 --root="${pkgdir}"
  install -D -m644 COPYING-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pycurl() {
  pkgdesc="A Python 2.x interface to libcurl"
  depends=('python2' 'curl')

  cd $_pypiname-py2
  python2 setup.py install -O1 --root="${pkgdir}"
  install -D -m644 COPYING-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

