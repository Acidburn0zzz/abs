pkgbase=lapack
pkgname=('lapack' 'blas' 'cblas')
pkgver=3.6.0
pkgrel=9
url="http://www.netlib.org/lapack"
pkgdesc="Linear Algebra PACKage"
makedepends=('gcc-fortran' 'cmake')
arch=('i386' 'amd64')
license=("custom")
source=(http://www.netlib.org/${pkgbase}/${pkgbase}-${pkgver}.tgz
        LICENSE.blas)
sha1sums=('7e993de16d80d52b22b6093465eeb90c93c7a2e7'
          '2491a151a37f0162b25fc4e4e9a8ac444b574a76')

prepare() {
  cd $pkgname-$pkgver
  gsed -e 's|/CMAKE/|/cmake/|' -i CBLAS/CMakeLists.txt
}

build() {
  export LDFLAGS="-L/usr/lib/gcc -Wl,-rpath,/usr/lib/gcc/ $LDFLAGS"
  install -d build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_Fortran_COMPILER=gfortran \
    -DLAPACKE=ON \
    -DCBLAS=ON \
    -DBUILD_DEPRECATED=ON
  make
}

package_lapack() {
  depends=("blas=${pkgver}")
  
  cd build
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/lib/{libblas.*,libcblas.*}
  rm -r "$pkgdir"/usr/lib/pkgconfig/{blas.*,cblas.*}
  rm -r "$pkgdir"/usr/lib/cmake/cblas*
  rm -r "$pkgdir"/usr/include/cblas*
}

package_blas() {
  pkgdesc="Basic Linear Algebra Subprograms"
  depends=('gcc-libs')

  cd build/BLAS
  make DESTDIR="$pkgdir" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/blas"
  install -m644 "${srcdir}/LICENSE.blas" \
    "${pkgdir}/usr/share/licenses/blas/"
}

package_cblas() {
  pkgdesc=("C interface to BLAS")
  depends=("blas=$pkgver")

  cd build/CBLAS
  make DESTDIR="$pkgdir" install
}

