# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>

pkgname=xvidcore
pkgver=1.3.3
pkgrel=4
pkgdesc="XviD is an open source MPEG-4 video codec"
arch=('i686' 'x86_64')
url="http://www.xvid.org/"
license=('GPL')
makedepends=('nasm')
options=('libtoolfix')
source=(http://downloads.xvid.org/downloads/${pkgname}-${pkgver}.tar.bz2)
md5sums=('f0a77572ac4694038e8519726b2883d9')
sha1sums=('aead171974e2916bafce60ac9e0ca033641346f5')

build() {
  cd "${srcdir}/${pkgname}/build/generic"
  ac_cv_prog_ac_yasm=no \
  ./configure --prefix=/usr
  gmake
}

package() {
  cd "${srcdir}/${pkgname}/build/generic"
  gmake DESTDIR="${pkgdir}" install
}
