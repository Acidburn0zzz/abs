# $Id: PKGBUILD 183388 2013-04-21 22:10:05Z heftig $
# Maintainer: Jan de Groot <jan@archlinux.org>

pkgname=gnome-menus
pkgver=3.10.1
pkgrel=3
pkgdesc="GNOME menu specifications"
arch=('i686' 'x86_64')
depends=('glib2')
makedepends=('intltool' 'gobject-introspection')
options=('!libtool')
license=('GPL' 'LGPL')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        'git-fixes.patch'	'patch-util_Makefile.in')
sha256sums=('46950aba274c1ad58234374fa9b235258650737307f3bc396af48eb983668a71'
            'b738c2ea37784e531a2f4517d6bb5f2d1a00e488c89a5bf0dc73fdbca70374c1'
            '21a96148104df6a448e68ea538487752acf9c2dce0c22a680e6e67b7cbb9190a')

prepare() {
  cd "$pkgname-$pkgver"

  patch -Np1 -i ../git-fixes.patch
  patch -p0 -i ../patch-util_Makefile.in

  sed -i '' -e 's|%%PTHREAD_LIBS%%|-pthread|' \
                util/Makefile.in

}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
      --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib
  gmake
}

package(){
  cd "$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install
}
