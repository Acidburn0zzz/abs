# $Id: PKGBUILD 198563 2013-10-30 15:37:43Z allan $
# Maintainer: Anthony Donnelly <Amzo@Archbsd.net>

pkgname=xfce4-power-manager
pkgver=1.5.2
pkgrel=1
pkgdesc="Power manager for Xfce desktop"
arch=('i386' 'amd64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'upower' 'libnotify' 'hicolor-icon-theme')
optdepends=('xfce4-panel: for the Xfce panel plugin'
            'lxpanel: for the LXDE panel plugin')
makedepends=('intltool' 'xfce4-panel' 'lxpanel')
install=$pkgname.install
options=('libtoolfix')
source=(http://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('8bdc640f227ffccbdcfbbcca8ba36d1ce054236c0a32dbf87a4a751f3a835415')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --libexecdir=/usr/libexec \
    --localstatedir=/var \
    --disable-network-manager \
    --enable-polkit \
    --enable-dpms \
    --disable-debug
  gmake
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
