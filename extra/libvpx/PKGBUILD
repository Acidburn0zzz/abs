# Port: multimedia/libvpx

pkgname=libvpx
pkgver=1.6.1
pkgrel=1
pkgdesc="The VP8 and VP9 Codec"
arch=('amd64')
url="http://www.webmproject.org/"
license=('BSD')
makedepends=('yasm' 'git')
patchdir=$pkgname-${pkgver}
provides=('libvpx.so')
source=("libvpx-${pkgver}.tar.gz::https://github.com/webmproject/libvpx/archive/v${pkgver}.tar.gz")
sha256sums=('cda8bb6f0e4848c018177d3a576fa83ed96d762554d7010fe4cfb9d70c22e588')

build() {
    cd "$pkgname-$pkgver"

    if [ -n "$CC" ]; then
        sed -i '' -e 's|\(link_with_cc\)=gcc|\1='${CC}'|' \
            build/make/configure.sh
    fi
    grep -Rl '^#!/bin/bash' . | xargs sed -i '' \
        -e 's,#!/bin/bash,#!/usr/bin/env bash,g'

    grep -Rl -- '-l\?pthread' . | xargs sed -i ''  -E \
		-e 's/-l?pthread/-pthread/g'

    if [ ${CARCH} == "amd64" ]; then
        target="--target=x86_64-linux-gcc"
    else
        target="--target=x86-linux-gcc"
    fi

    ./configure --prefix=/usr --enable-vp8 \
	--enable-vp9 \
        --enable-runtime-cpu-detect \
        --enable-shared \
        --enable-postproc \
        --enable-pic \
        --disable-install-docs \
        --disable-install-srcs \
	--enable-vp9-highbitdepth \
	--enable-experimental \
	--enable-spatial-svc \
        "${target}"
    gmake V=1
}

package() {
    cd "$pkgname-$pkgver"
    gmake DIST_DIR="$pkgdir/usr" install
    install -dm755 "$pkgdir/usr/share/licenses/$pkgname/"
    install -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
