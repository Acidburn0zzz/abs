# Port: multimedia/libvpx
pkgname=libvpx
pkgver=1.5.0
pkgrel=1
pkgdesc="The VP8 and VP9 Codec"
arch=('i386' 'amd64')
url="http://www.webmproject.org/"
license=('BSD')
makedepends=('yasm')
patchdir=$pkgname-${pkgver}
source=("https://codeload.github.com/webmproject/libvpx/tar.gz/v1.5.0?dummy=/webmproject-libvpx-v${pkgver}_GH0.tar.gz")
sha1sums=('82426a59ad34d72c762f9e4079e8e5197afc842d')

build() {
    cd "$pkgname-$pkgver"

    if [ -n "$CC" ]; then
        sed -i '' -e 's|\(link_with_cc\)=gcc|\1='${CC}'|' \
            build/make/configure.sh
    fi
    grep -Rl '^#!/bin/bash' . | xargs sed -i '' \
        -e 's,#!/bin/bash,#!/usr/bin/env bash,g'

    grep -Rl -- '-l\?pthread' . | xargs sed -i ''  -E \
		-e 's/-l?pthread/-pthread/g'

    if [ ${CARCH} == "amd64" ]; then
        target="--target=x86_64-linux-gcc"
    else
        target="--target=x86-linux-gcc"
    fi

    ./configure --prefix=/usr --enable-vp8 \
	--enable-vp9 \
        --enable-runtime-cpu-detect \
        --enable-shared \
        --enable-postproc \
        --enable-pic \
        --disable-install-docs \
        --disable-install-srcs \
        "${target}"
    gmake V=1
}

package() {
    cd "$pkgname-$pkgver"
    gmake DIST_DIR="$pkgdir/usr" install
    install -dm755 "$pkgdir/usr/share/licenses/$pkgname/"
    install -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
