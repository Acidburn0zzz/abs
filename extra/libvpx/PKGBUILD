# Maintainer: Anthony Donnelly <amzo@archbsd.net>
pkgname=libvpx
pkgver=1.3.0
pkgrel=2
pkgdesc="The VP8 Codec SDK"
arch=('i686' 'x86_64')
url="http://www.webmproject.org/"
license=('BSD')
makedepends=('yasm')
source=(http://webm.googlecode.com/files/$pkgname-v$pkgver.tar.bz2)
sha1sums=('191b95817aede8c136cc3f3745fb1b8c50e6d5dc')

build() {
    cd "$pkgname-v$pkgver"

    if [ -n "$CC" ]; then
        sed -i '' -e 's|\(link_with_cc\)=gcc|\1='${CC}'|' \
            build/make/configure.sh
    fi
    grep -Rl '^#!/bin/bash' . | xargs sed -i '' \
        -e 's,#!/bin/bash,#!/usr/bin/env bash,g'

    grep -Rl -- '-l\?pthread' . | xargs sed -i ''  -E \
		-e 's/-l?pthread/-pthread/g'

    if [ ${CARCH} == "x86_64" ]; then
        target="--target=x86_64-linux-gcc"
    else
        target="--target=x86-linux-gcc"
    fi

    ./configure --prefix=/usr --enable-vp8 \
	--enable-vp9 \
        --enable-runtime-cpu-detect \
        --enable-shared \
        --enable-postproc \
        --enable-pic \
        --disable-install-docs \
        --disable-install-srcs \
        "${target}"
    gmake
}

package() {
    cd "$pkgname-v$pkgver"
    gmake DIST_DIR="$pkgdir/usr" install
    install -dm755 "$pkgdir/usr/share/licenses/$pkgname/"
    install -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
