# $Id: PKGBUILD 188613 2013-06-17 00:09:58Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=geoip-database
pkgver=20150804
pkgrel=3
pkgdesc="GeoLite country geolocation database compiled by MaxMind"
arch=('i386' 'amd64')
url="http://www.maxmind.com/app/ip-location"
license=('custom:OPEN DATA LICENSE')
source=(GeoIP-$pkgver.dat.gz::http://geolite.maxmind.com/download/geoip/database/GeoLiteCountry/GeoIP.dat.gz
        GeoIPv6-$pkgver.dat.gz::http://geolite.maxmind.com/download/geoip/database/GeoIPv6.dat.gz
        http://geolite.maxmind.com/download/geoip/database/LICENSE.txt)
noextract=(GeoIP-$pkgver.dat.gz
           GeoIPv6-$pkgver.dat.gz)
sha256sums=('b418d2b412054526595a548e7b5258ec56bba26f9f44771afca3046a7c5fdca2'
            'c492fc32b9a3e8da2462bad8b641e095afa47994e07ce2d36efc6ed4f5c6a836'
            '83a4cb82ef8953c0107886b49bc39fcf77590a222d30e778d48f5d92f5b5e383')

prepare() {
  cd "$srcdir"

  for _database_name in GeoIP GeoIPv6; do
    gunzip -c $_database_name-$pkgver.dat.gz >$_database_name.dat
  done
}

check() {
  cd "$srcdir"

  for _database in {GeoIP,GeoIPv6}.dat; do
    if ! grep -q 'MaxMind Inc All Rights Reserved' $_database; then
      error "Copyright notice is missing from $_database; something is fishy!"
      return 1
    fi
  done
}

package() {
  cd "$srcdir"

  install -dm755 "$pkgdir/usr/share/GeoIP"
  install -m644 GeoIP.dat "$pkgdir/usr/share/GeoIP/GeoIP.dat"
  install -m644 GeoIPv6.dat "$pkgdir/usr/share/GeoIP/GeoIPv6.dat"

  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

# vim:set ts=2 sw=2 et:
