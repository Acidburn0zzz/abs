pkgname=libtar
pkgver=1.2.20
pkgrel=1
pkgdesc="C library for manipulating POSIX tar files"
arch=('i686' 'amd64')
url="http://www.feep.net/libtar/"
license=('BSD')
depends=('zlib')
source=("http://pkgbuild.com/~giovanni/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('c8507795b3d461ed10566a61fb4cad9b')

prepare() {
  cd "${srcdir}/${pkgname}"

  gsed -i \
      -e '/INSTALL_PROGRAM/s:-s::' {doc,lib{,tar}}/Makefile.in
}

build() {
  cd "${srcdir}/${pkgname}"

  export CFLAGS+=" -fPIC"

  autoreconf -fi
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --disable-static \
              --disable-encap \
              --disable-epkg-install

  gmake
}

package() {
  cd "${srcdir}/${pkgname}"

  gmake DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
