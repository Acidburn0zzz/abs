# $Id: PKGBUILD 197450 2013-10-26 06:44:55Z allan $
# Maintainer : Anthony Donnelly <Amzo@PacBSD.org>

pkgname=libimobiledevice
pkgver=1.2.0
pkgrel=2
pkgdesc="Is a software library that talks the protocols to support iPhone and iPod Touch devices on Linux"
url="http://libimobiledevice.org/"
arch=('amd64')
license=('GPL2' 'LGPL2.1')
depends=('libusbmuxd' 'gnutls' 'libgcrypt')
source=(http://libimobiledevice.org/downloads/$pkgname-$pkgver.tar.bz2)
md5sums=('8757900ba7bbe2ef5f54342415d0223e')

prepare() {
  cd "$pkgname-$pkgver"

  #fix syntax error with
  sed -i '' -e 's|/bin/sh|/usr/bin/bash|g' configure
}

build() {
  cd "$pkgname-$pkgver"

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
    openssl_LIBS="-L/usr/lib -lssl -lcrypto"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
