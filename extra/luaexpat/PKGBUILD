pkgbase=luaexpat
pkgname=(lua-expat lua51-expat lua52-expat)
pkgver=1.3.0
pkgrel=1
pkgdesc='SAX XML parser based on the Expat library'
arch=('i686' 'amd64')
url='http://matthewwild.co.uk/projects/luaexpat/'
license=('MIT')
makedepends=('lua' 'lua51' 'lua52' 'expat' 'ginstall')
source=("http://matthewwild.co.uk/projects/luaexpat/luaexpat-$pkgver.tar.gz")
md5sums=('3c20b5795e7107f847f8da844fbfe2da')

prepare() {
  cp -a $pkgbase-$pkgver $pkgbase-$pkgver-51
  cp -a $pkgbase-$pkgver $pkgbase-$pkgver-52
}

build() {
  # 5.1
  msg2 "Building $pkgbase with lua 5.1"
  cd "$srcdir"/$pkgbase-$pkgver-51
  gmake LUA_V=5.1

  # 5.2
  msg2 "Building $pkgbase with lua 5.2"
  cd "$srcdir"/$pkgbase-$pkgver-52
  gmake LUA_V=5.2 CFLAGS='-DLUA_32BITS'

  # 5.3
  msg2 "Building $pkgbase with lua 5.3"
  cd "$srcdir/$pkgbase-$pkgver"
  gmake LUA_V=5.3 CFLAGS='-DLUA_32BITS'
}

package_lua-expat() {
  depends=('lua' 'expat')

  cd $pkgbase-$pkgver
  gmake INSTALL_PROGRAM=ginstall LUA_V=5.3 DESTDIR="$pkgdir" install
  ginstall -Dm0644 doc/us/license.html "$pkgdir/usr/share/licenses/$pkgname/license.html"
}

package_lua51-expat() {
  depends=('lua51' 'expat')
  replaces=('luaexpat')
  conflicts=('luaexpat')

  cd $pkgbase-$pkgver-51
  gmake INSTALL_PROGRAM=ginstall LUA_V=5.1 DESTDIR="$pkgdir" install
  ginstall -Dm0644 doc/us/license.html "$pkgdir/usr/share/licenses/$pkgname/license.html"
}

package_lua52-expat() {
  depends=('lua52' 'expat')

  cd $pkgbase-$pkgver-52
  gmake INSTALL_PROGRAM=ginstall LUA_V=5.2 DESTDIR="$pkgdir" install
  ginstall -Dm0644 doc/us/license.html "$pkgdir/usr/share/licenses/$pkgname/license.html"
}
