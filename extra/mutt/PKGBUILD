pkgname=mutt
pkgver=1.5.24
pkgrel=2
pkgdesc='Small but very powerful text-based mail client'
url='http://www.mutt.org/'
license=('GPL')
backup=('etc/Muttrc')
arch=('i386' 'amd64')
optdepends=('smtp-forwarder: to send mail')
depends=('gpgme' 'ncurses' 'libsasl' 'gdbm' 'libidn' 'mime-types' ) # krb5 & openssl in world
source=("http://ftp.mutt.org/pub/mutt/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha1sums=('38a2da5eb01ff83a90a2caee28fa2e95dbfe6898' 'SKIP')
validpgpkeys=('8975A9B33AA37910385C5308ADEF768480316BDA')
install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	autoreconf -vfi 
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--enable-gpgme \
		--enable-pop \
		--enable-imap \
		--enable-smtp \
		--enable-hcache \
		--with-curses=/usr \
		--with-regex \
		--with-gss=/usr \
		--with-ssl=/usr \
		--with-sasl \
		--with-idn

	# to use freebsd-world's krb5 and make --with-gss work:
	echo "#define HAVE_HEIMDAL" >> config.h.in
	sed -i '' -e 's|<gssapi/gssapi\.h>|<gssapi.h>|' imap/auth_gss.c

	gmake
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	gmake DESTDIR="${pkgdir}" install

	rm "${pkgdir}"/usr/bin/{flea,muttbug}
	rm "${pkgdir}"/usr/share/man/man1/{flea,muttbug}.1
	install -dm755 "${pkgdir}"/etc
	install -m644 contrib/gpg.rc "${pkgdir}"/etc/Muttrc.gpg.dist
}
