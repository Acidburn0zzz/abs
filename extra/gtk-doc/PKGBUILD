# $Id: PKGBUILD 139346 2011-10-01 18:59:04Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gtk-doc
pkgver=1.20
pkgrel=1
pkgdesc="Documentation tool for public library API"
arch=('any')
license=('GPL' 'FDL')
depends=('docbook-xsl' 'gnome-doc-utils' 'perl')
makedepends=('pkgconf' 'jade' 'itstool') 
optdepends=('jade: SGML support')
url="http://www.gtk.org/gtk-doc/"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('3e6ecf134dbf92a74c24d79848fea3a48e59ab95408a38c6405905d95a293011')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/lib"
  mv "${pkgdir}/usr/"{share,lib}"/pkgconfig"
}
