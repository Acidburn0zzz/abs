# $Id: PKGBUILD 135033 2011-08-09 23:31:20Z eric $
# Maintainer: Anthony Donnelly <amzo@archbsd.net>

pkgbase=flac
pkgname=('flac' 'flac-doc')
pkgver=1.3.0
pkgrel=13
arch=('i686' 'x86_64')
url="http://flac.sourceforge.net/"
license=('BSD' 'GPL')
depends=('libogg' 'gcc-libs')
makedepends=('nasm' 'doxygen')
options=('!makeflags' 'libtoolfix')
source=(http://downloads.xiph.org/releases/flac/${pkgbase}-${pkgver}.tar.xz)
sha1sums=('a136e5748f8fb1e6c524c75000a765fc63bb7b1b')

prepare() {
  cd "${pkgbase}-${pkgver}"

  for i in "${_port_patches[@]}"; do
    patch -p0 -i ${srcdir}/$i
  done
}

build() {
  cd "${pkgbase}-${pkgver}"

  ./configure --prefix=/usr --disable-sse 
  gmake
}

#check() {
#  cd "${pkgbase}-${pkgver}"
#  gmake check
#}

package_flac() {
  pkgdesc="Free Lossless Audio Codec"
  optdepends=('xmms: for using the xmms plugin')
  options=('!docs')

  cd "${pkgbase}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
  install -dm755  "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644 COPYING.Xiph "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_flac-doc() {
  pkgdesc="Developer documentation for the Free Lossless Audio Codec"
  depends=('flac')

  cd "${pkgbase}-${pkgver}"
  gmake DESTDIR="${pkgdir}" -C doc install
  gsed -i "s|$srcdir/${pkgbase}-${pkgver}|/usr|" "${pkgdir}/usr/share/doc/${pkgbase}-${pkgver}/FLAC.tag"
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}/
  install -m644 COPYING.Xiph "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
