# $Id: PKGBUILD 135033 2011-08-09 23:31:20Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=flac
pkgname=('flac' 'flac-doc')
pkgver=1.3.0
pkgrel=5
arch=('i686' 'x86_64')
url="http://flac.sourceforge.net/"
license=('BSD' 'GPL')
depends=('libogg')
makedepends=('nasm' 'xmms')
options=('!libtool' '!makeflags')
_port_patches=(
        patch-configure
        patch-man_flac.1
        patch-src_libFLAC_stream__encoder.c
)
source=(http://downloads.xiph.org/releases/flac/${pkgbase}-${pkgver}.tar.xz
        "${_port_patches[@]}")
sha1sums=('a136e5748f8fb1e6c524c75000a765fc63bb7b1b'
          'c5fa238a955b3d7809ce8bb9be89f60c6fa52fd1'
          '77d6d37a761bd41d13ea0d07eac7efbb6b217a89'
          '3776c7be9d480193f58ca56074cf6f91a6b6a69e')

prepare() {
  cd "${pkgbase}-${pkgver}"

  for i in "${_port_patches[@]}"; do
    patch -p0 -i ${srcdir}/$i
  done

  sed -i '' -e "/^ltmain=/!s|\$$ac_aux_dir/ltmain.sh|/usr/share/libtool/config/ltmain.sh|g" \
  	  -e "/^LIBTOOL=/s|\$$\(top_builddir\)/libtool|/usr/bin/libtool|g" \
	  ${srcdir}/${pkgname}-${pkgver}/aclocal.m4
  find ${srcdir}/${pkgname}-${pkgver} -name "Makefile.in" | xargs sed -i '' -e 's|^LIBTOOL[ ]*=.*|LIBTOOL=/usr/bin/libtool|g'
}

build() {
  cd "${pkgbase}-${pkgver}"

  ./configure --prefix=/usr --mandir=/usr/share/man --enable-shared \
    --disable-sse --disable-rpath --with-pic --without-libiconv-prefix
  gmake
}

check() {
  cd "${pkgbase}-${pkgver}"
  gmake check
}

package_flac() {
  pkgdesc="Free Lossless Audio Codec"
  optdepends=('xmms: for using the xmms plugin')
  options=('!libtool' '!docs')

  cd "${pkgbase}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
  install -dm755  "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -D -m644 COPYING.Xiph "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_flac-doc() {
  pkgdesc="Developer documentation for the Free Lossless Audio Codec"
  depends=('flac')

  cd "${pkgbase}-${pkgver}"
  gmake DESTDIR="${pkgdir}" -C doc install
  gsed -i "s|$srcdir/${pkgbase}-${pkgver}|/usr|" "${pkgdir}/usr/share/doc/${pkgbase}-${pkgver}/FLAC.tag"
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}/
  install -m644 COPYING.Xiph "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
