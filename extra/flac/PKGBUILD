# $Id: PKGBUILD 135033 2011-08-09 23:31:20Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=flac
pkgname=('flac' 'flac-doc')
pkgver=1.3.0
pkgrel=3
arch=('i686' 'x86_64')
url="http://flac.sourceforge.net/"
license=('BSD' 'GPL')
depends=('libogg')
makedepends=('nasm' 'doxygen')
options=('!makeflags' 'libtoolfix')
_port_patches=(
        patch-configure
        patch-man_flac.1
        patch-src_libFLAC_stream__encoder.c
)
source=(http://downloads.xiph.org/releases/flac/${pkgbase}-${pkgver}.tar.xz
        "${_port_patches[@]}")
sha1sums=('a136e5748f8fb1e6c524c75000a765fc63bb7b1b'
          'c5fa238a955b3d7809ce8bb9be89f60c6fa52fd1'
          '77d6d37a761bd41d13ea0d07eac7efbb6b217a89'
          '3776c7be9d480193f58ca56074cf6f91a6b6a69e')

prepare() {
  cd "${pkgbase}-${pkgver}"

  for i in "${_port_patches[@]}"; do
    patch -p0 -i ${srcdir}/$i
  done
}

build() {
  cd "${pkgbase}-${pkgver}"

  ./configure --prefix=/usr --disable-sse 
  gmake
}

#check() {
#  cd "${pkgbase}-${pkgver}"
#  gmake check
#}

package_flac() {
  pkgdesc="Free Lossless Audio Codec"
  optdepends=('xmms: for using the xmms plugin')
  options=('!libtool' '!docs')

  cd "${pkgbase}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
  install -dm755  "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644 COPYING.Xiph "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_flac-doc() {
  pkgdesc="Developer documentation for the Free Lossless Audio Codec"
  depends=('flac')

  cd "${pkgbase}-${pkgver}"
  gmake DESTDIR="${pkgdir}" -C doc install
  gsed -i "s|$srcdir/${pkgbase}-${pkgver}|/usr|" "${pkgdir}/usr/share/doc/${pkgbase}-${pkgver}/FLAC.tag"
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}/
  install -m644 COPYING.Xiph "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
