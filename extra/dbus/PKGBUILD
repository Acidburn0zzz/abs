# $Id: PKGBUILD 172792 2012-12-06 01:48:03Z tomegun $
# Maintainer: Tom Gundersen <teg@jklm.no>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>
#
pkgname=dbus
pkgver=1.6.16
pkgrel=6
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=(i686 x86_64)
license=('GPL' 'custom')
depends=('expat' 'gnome_subr')
makedepends=('gmake' 'libx11')
optdepends=('libx11: dbus-launch support')
provides=('dbus-core')
conflicts=('dbus-core')
replaces=('dbus-core')
install=dbus.install
_port_patches=(
        patch-bus__session.conf.in
        patch-bus__system.conf.in
        patch-dbus-1.pc.in
        patch-dbus_Makefile.in
        patch-dbus_dbus-connection.c
        patch-dbus_dbus-sysdeps-util-unix.c
        patch-doc_Makefile.in
        )
source=(http://dbus.freedesktop.org/releases/${pkgname}/${pkgname}-$pkgver.tar.gz #{,.asc}
        dbus
        dbus.in
        ${_port_patches[@]})

prepare() {
  cd ${pkgname}-${pkgver}
  for _p in ${_port_patches[@]}; do
    patch -p0 -i ../${_p}
  done
}

build() {
  cd ${pkgname}-${pkgver}

  sed -i '' -e '/^SUBDIRS/,/^$/ s|test||g' Makefile.in
  sed -i '' -e 's|THREAD_LIBS -lrt|THREAD_LIBS|g ; s|dummy man2html|dummy man2html_disable|g' configure

  rm -rf "${srcdir}/test"
  mkdir "${srcdir}/test"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var \
              --with-test-socket-dir="${srcdir}/test" \
              --with-system-pid-file=/var/run/dbus/pid \
              --libexecdir=/usr/libexec \
              --with-system-socket=/var/run/dbus/system_bus_socket \
              --with-session-socket-dir=/tmp \
              --disable-doxygen-docs \
              --disable-xml-docs \
              --with-dbus-user=messagebus \
              --disable-systemd
  gmake
}

package(){
  cd ${pkgname}-${pkgver}
  gmake DESTDIR="$pkgdir" install

  chmod o+x ${pkgdir}/usr/libexec/dbus-daemon-launch-helper
  rm -rf "$pkgdir/var/run"

  install -m755 -d "$pkgdir"/etc/{init.d,rc.d}
  install -m755 ../dbus "$pkgdir/etc/init.d/dbus"
  install -m755 ../dbus.in "$pkgdir/etc/rc.d/dbus"

  install -dm755 "$pkgdir/usr/share/licenses/dbus/"
  install -m644 COPYING "$pkgdir/usr/share/licenses/dbus/COPYING"
}
sha1sums=('b6b40c193a83f46fcbd9432c52206dd045fd13e2'
          '1838ab9afab18549dd74ec9547d543c58686b277'
          'b2af11333802829e76e7cb2a9464da2beb8f879d'
          'aa4a02776e5684b4bc97f2e844c777278c647a43'
          '7f5f6a6d9441e1d6c0207f137034799ca67a4dda'
          'd1c31640098f5ba0af60032fc05756d3901ed32a'
          '5ee309c26dc45911a32b6bd1c84df91c95b85a88'
          '902ec248d4a6e2cefd1f4a35a963357d7ea0c4e3'
          '9591750e863498d1a6d65d26b67a584367d05d11'
          '8d036631a27cea938a42fc0d8c80a780e3e37ff2')
