# $Id: PKGBUILD 172792 2012-12-06 01:48:03Z tomegun $
# Maintainer: Tom Gundersen <teg@jklm.no>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>
#
pkgbase=dbus
pkgname=('dbus' 'libdbus')
pkgver=1.8.6
pkgrel=2
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=(i686 x86_64)
license=('GPL' 'custom')
depends=('expat' 'gnome_subr')
makedepends=('gmake' 'libx11')
optdepends=('libx11: dbus-launch support')
provides=('dbus-core')
conflicts=('dbus-core')
replaces=('dbus-core')
install=dbus.install
_port_patches=(
	patch-bus__session.conf.in
	patch-bus__system.conf.in
	patch-dbus-1.pc.in
	patch-dbus_Makefile.in
	patch-dbus_dbus-connection.c
	patch-dbus_dbus-sysdeps-util-unix.c
	patch-doc_Makefile.in
)
source=(http://dbus.freedesktop.org/releases/${pkgname}/${pkgname}-$pkgver.tar.gz #{,.asc}
        dbus
        dbus.in
	30-dbus
        ${_port_patches[@]})
md5sums=('6a08ba555d340e9dfe2d623b83c0eea8'
         '659d3de2e72991542f7dc153706aad24'
         'a788b41f50459b9e26391fa4c4c136f2'
         '3314d727fa57fc443fce25b5cbeebbcc'
         'ecb16fff9a0deec0a1d0fabef6160609'
         'bd423a571ef529d9e97a26a6db572951'
         'e6c55c477e59d7222d6a0e5f69aaf6fd'
         '0697a4474d7748e93e7f0f5a66497735'
         '169de96d0d328f45b58fe2113295f487'
         'f21785dbfd848b4b0d9185085e95acc3'
         'f07e395a955fe1bda6b25854076f771c')

prepare() {
  cd dbus-${pkgver}
  for _p in ${_port_patches[@]}; do
    patch -p0 -i ../${_p}
  done
}

build() {
  cd dbus-$pkgver

  sed -i '' -e '/^SUBDIRS/,/^$/ s|test||g' Makefile.in
  sed -i '' -e 's|THREAD_LIBS -lrt|THREAD_LIBS|g ; s|dummy man2html|dummy man2html_disable|g' configure

  rm -rf "${srcdir}/test"
  mkdir "${srcdir}/test"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var \
              --with-test-socket-dir="${srcdir}/test" \
              --with-system-pid-file=/var/run/dbus/pid \
              --libexecdir=/usr/lib \
              --with-system-socket=/var/run/dbus/system_bus_socket \
              --with-session-socket-dir=/tmp \
              --disable-doxygen-docs \
              --disable-xml-docs \
              --with-dbus-user=messagebus \
              --disable-systemd
  gmake
}

package_dbus(){
  depends=('libdbus' 'expat')
  optdepends=('libx11: dbus-launch support')
  provides=('dbus-core')
  conflicts=('dbus-core')
  replaces=('dbus-core')

  cd dbus-$pkgver

  # Disable installation of libdbus
  sed -i -e 's/^SUBDIRS = dbus/SUBDIRS =/' Makefile

  make DESTDIR="$pkgdir" install

  rm -rf "${pkgdir}/var/run"
  rm -rf "${pkgdir}/usr/lib/pkgconfig"

  install -dm755 "$pkgdir/etc/X11/xinit/xinitrc.d"
  install -dm755 "$pkgdir/usr/share/licenses/dbus/"
  install -m755 ../30-dbus "$pkgdir/etc/X11/xinit/xinitrc.d/30-dbus"
  install -m644 COPYING "$pkgdir/usr/share/licenses/dbus/COPYING"
}

package_libdbus(){
  pkgdesc="DBus library"
  depends=('glibc')

  cd dbus-$pkgver
  make DESTDIR="$pkgdir" -C dbus install
  make DESTDIR="$pkgdir" install-data-am
  install -dm755 "$pkgdir/usr/share/licenses/libdbus/"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/libdbus/COPYING"
}
