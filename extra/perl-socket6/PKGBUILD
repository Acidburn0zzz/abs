# Port: net/p5-Socket6
pkgname='perl-socket6'
pkgver=0.28
pkgrel=2
pkgdesc="getaddrinfo/getnameinfo support module"
arch=('i386' 'amd64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
url='http://search.cpan.org/dist/Socket6'
source=("http://search.cpan.org/CPAN/authors/id/U/UM/UMEMOTO/Socket6-${pkgver}.tar.gz")
sha256sums=('bfd49ab99f3197c99285fed4683c4edc06277c1e4453f593e694d7bff0974586')

build() {
  cd Socket6-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Socket6-$pkgver
  make test
}

package() {
  cd Socket6-$pkgver
  make DESTDIR="$pkgdir" install
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
