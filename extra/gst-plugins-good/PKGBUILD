# $Id: PKGBUILD 207962 2014-03-15 19:47:26Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gst-plugins-good
pkgver=1.4.5
pkgrel=2
pkgdesc="GStreamer Multimedia Framework Good Plugins"
arch=('amd64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('libpulse' 'jack' 'libsoup' 'gst-plugins-base-libs' 'wavpack' 'aalib' 'taglib' 'libdv' 'libshout' 'libvpx' 'gdk-pixbuf2' 'libcaca' 'libxdamage' 'v4l-utils' 'cairo')
makedepends=('gstreamer' 'speex' 'flac' 'python' 'gtk-doc')
options=(!emptydirs)
source=(${url}/src/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('79b1b5f3f7bcaa8a615202eb5e176121eeb8336960f70687e536ad78dbc7e641')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental --enable-gtk-doc \
    --with-package-name="GStreamer Good Plugins (Pac BSD)" \
    --with-package-origin="http://www.archbsd.net/"

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  gmake
}

# Disabled for now: souphttpsrc times out, and takes a damn long time to do so
#check() {
#  cd $pkgname-$pkgver
#  make check
#}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="${pkgdir}" install
}
