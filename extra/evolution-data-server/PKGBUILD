# $Id: PKGBUILD 192423 2013-08-11 13:38:21Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-data-server
pkgver=3.10.0
pkgrel=3
pkgdesc="Centralized access to appointments and contacts"
arch=(i686 x86_64)
depends=(gnome-online-accounts nss libgweather libical db libgdata)
makedepends=(intltool gperf gobject-introspection vala python2 gnome-common)
options=('!libtool')
install=$pkgname.install
url="http://www.gnome.org"
license=(GPL)
port_patches=(patch-calendar_libedata-cal_e-cal-backend-intervaltree.c
	patch-camel_camel-net-utils.c
	patch-camel_camel-sasl-gssapi.c
	patch-configure
	patch-docs_reference_camel_Makefile)
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
	${port_patches[@]})
sha256sums=('73c4ecfed65651cc72966b90081d7b72f9b42573585bc89797efea65ccfdbd2a'
            'c1de9fe146f0512cf6c0219341c8409a9e4d955c3eb6a85f7d07183972b9328e'
            '3e1c83dd00f01dfa0f9d67d2327de110b3e232a5f9a882ccce09a02392df7de6'
            '9da8cccf504f40b8ee9e068b465cdf0a1a858999dd88d8e8e96f1e4cdffbf1e2'
            '27022d00575ba1b9283d9420aa231c2770ff403b0b6c0442b6d679faefdbe00e'
            '1f5bf8140e476d24e50a5a9b7dd08ec6c19ca8837dfff73c929f0c818f36d2d2'
            '1d2dad831e1ee49aaf736645271ea67d364a2daa3a9ac0e541773abe39c5083e'
            '59d7cecd9bd8bffe59f6487639e03d2c38e1a4699b2372ca08fe2520521ae09f'
            'bba3cb5a996a837259c083b4ab4a7bd8adf997499b5894785e6f503c0dfbffa3'
            'c7906f3e2f28783c908db81876f38238d30b66d3fa53bfa66c6abe7a126abc47'
            '2b1c4254f380502f927a6fbb577d1d1cdeff2a8fe1a8239295f7dd4433e1cab4'
            '3bec77a92411d5ff4543278a175bcdf149a263e8c1c40598407e305b83ea89e8')

prepare() {
  cd "$pkgname-$pkgver"

  for _patch in ${port_patches[@]}; do
    msg "Patching port patch $_patch"
    patch -p0 -i "${srcdir}/$_patch"
  done

  sed -i '' -e 	's|heimlibs=".*"|heimlibs="-L/usr/lib -lgssapi -lheimntlm -lkrb5 -lhx509 -lcom_err -lcrypto -lasn1 -lroken -lcrypt"|g' \
	    -e	's|-Wl,--no-undefined||g' \
	    -e	's|-Wmissing-include-dirs||g' \
		configure
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --with-openldap=yes \
      --libexecdir=/usr/libexec/evolution-data-server \
      --with-krb5=/usr --with-libdb=/usr \
      --enable-vala-bindings --disable-uoa PYTHON=python2

  gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  gmake
}

package() {
  cd "$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install
}
