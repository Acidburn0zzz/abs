pkgname=evolution-data-server
pkgver=3.16.5
pkgrel=2
pkgdesc="Centralized access to appointments and contacts"
arch=('amd64')
depends=(gnome-online-accounts nss libgweather libical db libgdata openldap)
makedepends=(intltool gperf gobject-introspection vala python2 gnome-common)
options=('libtoolfix')
install=$pkgname.install
url="http://www.gnome.org"
license=(GPL)
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('e44ad4143402957cab8cab1a6b3d7057d8b111d268c6b9ad4e4008850f5b244c')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i '' -e 's|heimlibs=".*"|heimlibs="-L/usr/lib -lgssapi -lheimntlm -lkrb5 -lhx509 -lcom_err -lcrypto -lasn1 -lroken -lcrypt"|g' \
	-e 's|-Wl,--no-undefined||g' \
	-e 's|-Wmissing-include-dirs||g' \
	configure
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --with-openldap=yes \
      --libexecdir=/usr/libexec/evolution-data-server \
      --with-krb5=/usr --with-libdb=/usr \
      --enable-vala-bindings --disable-uoa PYTHON=python2 \
      LIBS="-lintl"

  gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  gmake
}

package() {
  cd "$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install
}
