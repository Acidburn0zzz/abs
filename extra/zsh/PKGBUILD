# Port: shells/zsh
pkgbase=zsh
pkgname=('zsh' 'zsh-doc')
pkgver=5.2
pkgrel=2
pkgdesc='A very advanced and programmable command interpreter (shell) for UNIX'
arch=('i386' 'amd64')
url='http://www.zsh.org/'
license=('custom')
# use the ncurses package
depends=('pcre' 'gdbm' 'ncurses')
makedepends=('texinfo')
install=zsh.install
source=("http://www.zsh.org/pub/zsh-${pkgver}.tar.xz"
        "http://www.zsh.org/pub/zsh-${pkgver}-doc.tar.xz"
        'zprofile')
sha256sums=('f17916320ffaa844bbd7ce48ceeb5945fc5f3eff64b149b4229bbfbdf3795a9d'
            '328352cf3d5d0ec4b6e31dcefd25ff5b4c0e6b8077d1fe84448ebb50d6ada52a'
            'b77f3879b0e88990aa70a01d974f7cbf06f870f8de5093fa4b7b264bc54575c0')

prepare() {
	cd "${srcdir}/${pkgbase}-${pkgver}"

	# Set correct keymap path
	sed -i '' -e 's#/usr/share/keymaps#/usr/share/vt/keymaps#g' Completion/Unix/Command/_loadkeys

	# Fix usb.ids path
	sed -i '' -e 's#/usr/share/misc/usb.ids#/usr/share/hwdata/usb.ids#g' Completion/Linux/Command/_lsusb

	# Remove unneeded and conflicting completion scripts
	for _fpath in AIX BSD Cygwin Darwin Debian Mandriva openSUSE Redhat Solaris; do
		rm -rf Completion/$_fpath
		gsed "s#\s*Completion/$_fpath/\*/\*##g" -i Src/Zle/complete.mdd
	done
	rm -f  Completion/Linux/Command/_{pkgtool,rpmbuild,yast}
	rm -f  Completion/Unix/Command/_{osc,systemd}

	sed -i '' -e '/LIBS/s/-lrt//' configure

	# Remove unneeded and conflicting completion scripts
	rm -rf Completion/{AIX,Linux,Cygwin,Darwin,Debian,Mandriva,openSUSE,Redhat,Solaris}
}

build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"

	export LDFLAGS="${LDFLAGS//,--as-needed/}"
	CPPFLAGS+=" -DBOOL_DEFINED"

	./configure --prefix=/usr \
		--bindir=/usr/bin \
		--enable-etcdir=/etc/zsh \
		--enable-zshenv=/etc/zsh/zshenv \
		--enable-zlogin=/etc/zsh/zlogin \
		--enable-zlogout=/etc/zsh/zlogout \
		--enable-zprofile=/etc/zsh/zprofile \
		--enable-zshrc=/etc/zsh/zshrc \
		--enable-maildir-support \
		--with-term-lib='ncursesw ncurses' \
		--enable-multibyte \
		--enable-function-subdirs \
		--enable-fndir=/usr/share/zsh/functions \
		--enable-scriptdir=/usr/share/zsh/scripts \
		--with-tcsetpgrp \
		--enable-pcre \
		--enable-cap \
		--enable-zsh-secure-free \
		zsh_cv_sys_path_dev_fd=no
	MAKEFLAGS=-j1
	# let's not 'make info' anymore...
	gmake
}

#check() {
#	cd "${srcdir}/${pkgname}-${pkgver}"
#	HOME="${srcdir}" gmake check
#}

package_zsh() {
	pkgdesc='A very advanced and programmable command interpreter (shell) for UNIX'
	depends=('pcre' 'ncurses' 'gdbm')
	backup=('etc/zsh/zprofile')
	install=zsh.install

	cd "${srcdir}/${pkgbase}-${pkgver}"
	gmake DESTDIR="${pkgdir}/" install
	install -dm755 "${pkgdir}/etc/zsh/"
	install -m644 "${srcdir}/zprofile" "${pkgdir}/etc/zsh/zprofile"
	install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -m644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_zsh-doc() {
	pkgdesc='Info, HTML and PDF format of the ZSH documentation'
	install=zsh-doc.install

	cd "${srcdir}/${pkgbase}-${pkgver}"
	gmake DESTDIR="${pkgdir}/" install.info install.html
	install -dm755 "${pkgdir}"/usr/share/{doc/zsh,licenses/${pkgname}}
	install -m644 Doc/zsh.pdf "${pkgdir}/usr/share/doc/zsh/zsh.pdf"
	install -m644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

