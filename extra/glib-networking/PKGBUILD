# $Id: PKGBUILD 185433 2013-05-14 09:44:30Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=glib-networking
pkgver=2.40.1
pkgrel=1
pkgdesc="Network-related giomodules for glib"
arch=(i686 x86_64)
url="http://www.gtk.org/"
license=(GPL2)
depends=(glib2 libproxy gnutls 'ca_root_nss' 'p11-kit' 'gsettings-desktop-schemas')
makedepends=(intltool)
options=('!libtool' 'libtoolfix' 'debug' '!strip')
install=glib-networking.install
port_patches=(patch-tls_gnutls_gtlsclientconnection-gnutls.c)
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
	${port_patches[@]})
sha256sums=('9fb3e54d049a480afdb814ff7452e7ab67e5d5f607ade230d7713f19922b5a28'
            '6e38acdd63f120d43a5d93f10daedcb9a814255150c7c6f3ae4f47b9166bfb16')
prepare() {
  cd $pkgname-$pkgver

  for _p in ${port_patches[@]}; do
	msg "Applying patch $_p"
	patch -p0 -i ${srcdir}/$_p
  done

  sed -i '' -e' s|-Wmissing-include-dirs||g' configure
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/$pkgname --disable-static \
    --disable-installed-tests \
    --with-ca-certificates=/usr/share/certs/ca-root-nss.crt
  gmake
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
