# $Id: PKGBUILD 191614 2013-07-27 18:39:53Z eric $
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars at gmail dot com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Maintainer: Brad Fanella <bradfanella@archlinux.us>

pkgname=ibus
pkgver=1.5.4
pkgrel=2
pkgdesc="Next Generation Input Bus for Linux"
arch=('i686' 'x86_64')
url="http://ibus.googlecode.com"
license=('LGPL')
depends=('hicolor-icon-theme' 'gtk2' 'gtk3' 'python2-gobject' 'libnotify' 'dconf')
makedepends=('gobject-introspection' 'iso-codes' 'intltool' 'gconf' 'vala')
options=('!libtool')
install=ibus.install
source=(http://ibus.googlecode.com/files/${pkgname}-${pkgver}.tar.gz)
sha1sums=('2ce4b4d728a4685fd77360d0097b0e568f920c7b')

build() {
  cd ${pkgname}-${pkgver}
  export PYTHON=python2
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/libexec/ibus \
    --sysconfdir=/etc \
    --disable-gconf \
    --enable-dconf \
    --disable-memconf \
    --enable-ui \
    --enable-python-library
  gmake
}

package() {
  cd ${pkgname}-${pkgver}
  gmake DESTDIR="${pkgdir}" install
}
