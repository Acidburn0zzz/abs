pkgbase=ibus
pkgname=(ibus libibus)
pkgver=1.5.11
pkgrel=3
pkgdesc="Next Generation Input Bus"
arch=('i386' 'amd64')
url="http://ibus.googlecode.com"
license=('LGPL')
depends=('dconf' 'gtk2' 'gtk3' 'hicolor-icon-theme' 'libnotify' 'python-dbus' 'python-gobject' 'iso-codes' 'python2-gobject2' 'python2-dbus' 'python2-gobject' 'librsvg')
makedepends=('gobject-introspection' 'vala' 'intltool' 'git' 'gnome-common' 'gtk-doc' 'gconf' 'qt5-base')
options=('!emptydirs')
source=("git+https://github.com/ibus/ibus.git#tag=$pkgver")

prepare() {
  cd ${pkgbase}

  sed -i '' -e 's|bin/bash|usr/bin/bash|g' -e 's|mktemp -d --tmpdir="$PWD"|mktemp -d|g' \
	data/dconf/make-dconf-override-db.sh
}

build() {
  cd ${pkgbase}
  ./autogen.sh \
    --prefix=/usr \
    --libexecdir=/usr/libexec/ibus \
    --sysconfdir=/etc \
    --disable-gconf \
    --enable-dconf \
    --disable-wayland \
    --enable-gtk-doc \
    --disable-memconf \
    --enable-ui \
    --enable-python-library \
    --with-python=python3 \
    LIBS="-lintl"
  gmake
}

package_ibus() {
  depends+=("libibus=$pkgver")
  install=ibus.install

  cd ${pkgbase}
  gmake DESTDIR="${pkgdir}" install
  gmake -C src DESTDIR="${pkgdir}" uninstall
  gmake -C bindings DESTDIR="${pkgdir}" uninstall
  rm -r "$pkgdir/usr/lib/python2.7/site-packages/gi" # compiled pyc/pyo files were not cleaned
  gmake DESTDIR="${pkgdir}" uninstall-pkgconfigDATA
}

package_libibus() {
  pkgdesc="IBus support library"
  depends=('glib2')

  cd ${pkgbase}
  gmake -C src DESTDIR="${pkgdir}" install
  gmake -C bindings DESTDIR="${pkgdir}" install
  gmake DESTDIR="${pkgdir}" install-pkgconfigDATA
}

sha512sums=('SKIP')

