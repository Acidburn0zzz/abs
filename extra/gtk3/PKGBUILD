# Port: x11-toolkits/gtk30
pkgbase=gtk3
pkgname=(gtk3 gtk-update-icon-cache)
pkgver=3.18.8
pkgrel=2
pkgdesc="GObject-based multi-platform GUI toolkit (v3)"
arch=('i386' 'amd64')
url="http://www.gtk.org/"
install=gtk3.install
depends=('atk>=2.6.0' 'libxkbcommon' 'cairo' 'libxcursor' 'libxinerama' 'libxrandr' 
	'libxi' 'libxcomposite' 'libxdamage' 'pango>=1.34.1-3' 
	'shared-mime-info' 'at-spi2-atk>=2.6.2-2' 'libepoxy' 'gdk-pixbuf2')
makedepends=('gobject-introspection')
options=('libtoolfix')
license=('LGPL')
patchdir=("gtk+-$pkgver")
source=(https://download.gnome.org/sources/gtk+/${pkgver:0:4}/gtk+-$pkgver.tar.xz
settings.ini)
md5sums=('9671acb41dc13561d19233f1a75cf184'
         '52bca1105d029c5142909b9e349bb89c')

prepare() {
  cd "gtk+-$pkgver"

  sed -i '' -e 's|[{]libdir[}]/locale|{datadir}/locale|g' \
   configure

}

build() {
  cd "gtk+-$pkgver"

  CXX=/bin/false ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-gtk2-dependency \
    --disable-schemas-compile \
    --enable-x11-backend \
    --disable-wayland-backend \
    --enable-introspection=yes
  
  gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  gmake
}

package_gtk3() {
    depends+=('gtk-update-icon-cache' 'adwaita-icon-theme')
    optdepends=('libcanberra: gtk3-widget-factory demo')
    install=gtk3.install

    cd "gtk+-$pkgver"
    gmake DESTDIR="$pkgdir" install
    install -dm755 "$pkgdir/usr/share/gtk-3.0"
    install -m644 ../settings.ini "$pkgdir/usr/share/gtk-3.0/settings.ini"

    # split this out to use with gtk2 too
    rm "$pkgdir/usr/bin/gtk-update-icon-cache"
}

package_gtk-update-icon-cache() {
    pkgdesc="GTK+ icon cache updater"
    depends=(gdk-pixbuf2 hicolor-icon-theme)
    install=gtk-update-icon-cache.install

    cd gtk+-$pkgver/gtk
    install -dm755 "$pkgdir/usr/bin"
    install -m755 gtk-update-icon-cache "$pkgdir/usr/bin/gtk-update-icon-cache"
}

