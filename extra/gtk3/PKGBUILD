# Maintainer: Anthony Donnelly <Amzo@archbsd.net>

pkgname=gtk3
pkgver=3.16.4
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit (v3)"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
install=gtk3.install
depends=('atk>=2.6.0' 'libxkbcommon' 'cairo' 'gtk-update-icon-cache>=2.24.16-5' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'pango>=1.34.1-3' 'shared-mime-info' 'at-spi2-atk>=2.6.2-2' 'libepoxy')
makedepends=('gobject-introspection')
options=('libtoolfix')
backup=(etc/gtk-3.0/settings.ini)
license=('LGPL')
patchdir=("gtk+-$pkgver")
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz
        settings.ini)
md5sums=('c7497aaf6730524a127597b768a73bd7'
         '52bca1105d029c5142909b9e349bb89c')

prepare() {
  cd "gtk+-$pkgver"

  sed -i '' -e 's|@LN_S@|/bin/ln -sf|' gtk/Makefile.in
  sed -i '' -e 's|file,cups|file,cups,lpr|' \
    gtk/Makefile.in
  sed -i '' -e 's|-DG_DISABLE_CHECKS||' \
    configure

}

build() {
  cd "gtk+-$pkgver"

  CXX=/bin/false ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-gtk2-dependency \
    --disable-schemas-compile \
    --enable-x11-backend \
    --disable-wayland-backend \
    --enable-introspection=yes
  
  gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  gmake
}

package() {
  cd "gtk+-$pkgver"
  gmake DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/etc/gtk-3.0/"
  install -m644 "$srcdir/settings.ini" "$pkgdir/etc/gtk-3.0/settings.ini"
}
