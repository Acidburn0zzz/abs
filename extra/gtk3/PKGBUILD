# Port: x11-toolkits/gtk30
pkgbase=gtk3
pkgname=(gtk3 gtk-update-icon-cache)
pkgver=3.22.15
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit (v3)"
arch=('i386' 'amd64')
url="http://www.gtk.org/"
install=gtk3.install
depends=('atk>=2.6.0' 'libxkbcommon' 'cairo' 'libxcursor' 'libxinerama' 'libxrandr' 
	'libxi' 'libxcomposite' 'libxdamage' 'pango>=1.34.1-3' 
	'shared-mime-info' 'at-spi2-atk>=2.6.2-2' 'libepoxy' 'gdk-pixbuf2')
makedepends=('gobject-introspection' 'ginstall')
options=('libtoolfix')
license=('LGPL')
patchdir=("gtk+-$pkgver")
source=(https://download.gnome.org/sources/gtk+/${pkgver:0:4}/gtk+-$pkgver.tar.xz
        settings.ini
        gtk-query-immodules-3.0.hook
        gtk-update-icon-cache.hook
        gtk-update-icon-cache.script)
md5sums=('9a1177854c4c5609293e017c53bc8950'
         '52bca1105d029c5142909b9e349bb89c'
         '47d92cc1080c92e8a022334b620f0629'
         'c792ef5bb74ceba564b4d8ad306a6592'
         '0ca18b2689f0d2e8acb61a3dcf154e7c')

prepare() {
  cd "gtk+-$pkgver"

  sed -i '' -e 's|[{]libdir[}]/locale|{datadir}/locale|g' \
   configure

}

build() {
  cd "gtk+-$pkgver"

  CXX=/bin/false ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-gtk2-dependency \
    --disable-schemas-compile \
    --enable-x11-backend \
    --disable-wayland-backend \
    --enable-introspection=yes
  
  gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  gmake
}

package_gtk3() {
    depends+=('gtk-update-icon-cache' 'adwaita-icon-theme')
    optdepends=('libcanberra: gtk3-widget-factory demo')
    install=gtk3.install

    cd "gtk+-$pkgver"
    gmake DESTDIR="$pkgdir" install

    ginstall -Dm644 ../settings.ini "$pkgdir/usr/share/gtk-3.0/settings.ini"
    ginstall -Dm644 ../gtk-query-immodules-3.0.hook "$pkgdir/usr/share/libalpm/hooks/gtk-query-immodules-3.0.hook"

    # split this out to use with gtk2 too
    rm "$pkgdir/usr/bin/gtk-update-icon-cache"
}

package_gtk-update-icon-cache() {
    pkgdesc="GTK+ icon cache updater"
    depends=(gdk-pixbuf2 hicolor-icon-theme)
    install=gtk-update-icon-cache.install

    cd gtk+-$pkgver/gtk
    install -dm755 "$pkgdir/usr/bin"
    install -m755 gtk-update-icon-cache "$pkgdir/usr/bin/gtk-update-icon-cache"
}

