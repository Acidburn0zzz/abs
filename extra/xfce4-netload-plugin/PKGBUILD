pkgname=xfce4-netload-plugin
pkgver=1.2.4
pkgrel=1
pkgdesc="A netload plugin for the Xfce panel"
arch=('i686' 'amd64')
url="http://xfce-goodies.berlios.de/"
license=('GPL')
groups=('xfce4-goodies')
depends=('xfce4-panel')
makedepends=('intltool')
install=xfce4-netload-plugin.install
source=(http://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('fface918822c012d806717116566e54947ad7613b9290b45a18e2d5ffb8730d3')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"
	
	sed -i '' -e 's, || __FreeBSD_kernel__,,' \
		panel-plugin/net.c \
		panel-plugin/net.h \
		panel-plugin/os.h
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/libexec \
    --localstatedir=/var \
    --disable-debug
  gmake
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install
}
# vim:set ts=2 sw=2 et:
