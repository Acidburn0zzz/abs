# $Id: PKGBUILD 191808 2013-07-30 23:19:36Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gdm
pkgname=(gdm libgdm)
pkgver=3.12.2
pkgrel=3
pkgdesc="Gnome Display Manager"
arch=('i386' 'amd64')
license=(GPL)
url="http://www.gnome.org"
options=()
depends=(gnome-shell gnome-session upower xorg-xrdb xorg-server xorg-xhost gnome_subr accountsservice)
makedepends=(itstool intltool yelp-tools gobject-introspection)
checkdepends=('check')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgbase/${pkgver::4}/$pkgbase-$pkgver.tar.xz
	0001-Add-Arch-Linux-PAM-config-files.patch
	client.conf.in
	gdm-autologin.in
	gdm-launch-environment.in
	gdm-password.in
	gdm.in
	locale.conf.in)
sha256sums=('cc91fff5afd2a7c3e712c960a0b60744774167dcfc16f486372e1eb3c0aa1cc4'
            '49f2c70f96c796378ff749bb7283f23fb055424b2179d31e6235d6034c791bfd'
            '6888a7643dd4d35566fd700e417156e6b745fcec784d5518fa009fdb59d2662c'
            '94f5e91b20ac7a9be99cd4721f52b3a91178cb081e0aa5e8cded6b9f70027cbd'
            'd513f30483e7c0912efc97b8abfb73b72c717326da9efaaf6c2a2aa08d2374fc'
            '999c973570daa3c39a75ebae5ef57db0dec88fa63737701d56624add438eac7a'
            'd5ed05cf42242dcfda2a30e53cfab5542cafb5578bf73228fadd4186517316da'
            'c1cfe0cb56beae207b4aa807f4d28e67d7c93eab245acab509dc8e249326c425')

prepare() {
  cd $pkgbase-$pkgver

  patch -Np1 -i ../0001-Add-Arch-Linux-PAM-config-files.patch

  AUTOPOINT='intltoolize --automake -c' autoreconf -fi

  for i in ${port_patches[@]}; do
	msg "Applying patch $i"
	patch -p0 -i ${srcdir}/$i
  done

  sed -i '' -e 's|root:root|root:wheel|g' \
     data/Makefile.in
  sed -i '' -e 's|/usr/local|/usr|g' \
     daemon/gdm-session-worker.c
  sed -i '' -e 's|/at-spi-registryd|/at-spi2-registryd|g' \
     gui/simple-chooser/gdm-host-chooser.c \
     gui/simple-chooser/chooser-main.c
  rm data/applications/gdm-simple-greeter.desktop \
     data/applications/gdm-simple-greeter.desktop.in \
     data/applications/gnome-shell.desktop \

}

build() {
  cd $pkgbase-$pkgver
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/gdm \
    --localstatedir=/var \
    --disable-static \
    --disable-schemas-compile \
    --with-at-spi-registryd-directory=/usr/lib/at-spi2-core \
    --with-check-accelerated-directory=/usr/lib/gnome-session \
    --without-tcp-wrappers \
    --with-console-kit \
    --with-authentication-agent-directory=/usr/lib/polkit-gnome \
    --with-initial-vt=9 \
    --with-default-pam-config=arch \
    --with-user=gdm \
    --with-group=gdm \
    --with-systemd=no LIBS="-lm -lutil -lintl" ac_cv_header_execinfo_h=no

  gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  gmake
}

check() {
  cd $pkgbase-$pkgver
  gmake check
}

package_gdm() {
  depends+=(libgdm)
  optdepends=('fprintd: fingerprint authentication')
  backup=(etc/pam.d/gdm-autologin etc/pam.d/gdm-fingerprint etc/pam.d/gdm-launch-environment
          etc/pam.d/gdm-password etc/pam.d/gdm-smartcard etc/gdm/custom.conf
          etc/gdm/Xsession
          etc/gdm/PostSession/Default etc/gdm/PreSession/Default)
  groups=(gnome)
  install=gdm.install

  cd $pkgbase-$pkgver
  gmake DESTDIR="$pkgdir" install

  install -dm755 "${pkgdir}/etc/rc.d"
  install -m755 "${srcdir}/gdm.in" "${pkgdir}/etc/rc.d/gdm"

  chmod 711 "$pkgdir/var/log/gdm"

### Split libgdm
  gmake -C libgdm DESTDIR="$pkgdir" uninstall
  mv "$pkgdir/usr/share/glib-2.0/schemas/org.gnome.login-screen.gschema.xml" "$srcdir"

## Port extras
  install  data/gdm.conf-custom \
    ${pkgdir}/etc/gdm/custom.conf.default
  mkdir -p ${pkgdir}/usr/share/xsessions
  mkdir -p ${pkgdir}/etc/gdm/Sessions
  mkdir -p ${pkgdir}/etc/dm/Sessions

  mkdir -p ${pkgdir}/etc/gdm/home
  chmod 1770 ${pkgdir}/etc/gdm/home
  mkdir -p ${pkgdir}/etc/gdm/home/.pulse/

  install ${srcdir}/gdm-autologin.in ${pkgdir}/etc/pam.d/gdm-autologin
  install ${srcdir}/gdm-launch-environment.in ${pkgdir}/etc/pam.d/gdm-launch-environment
  install ${srcdir}/gdm-password.in ${pkgdir}/etc/pam.d/gdm-password

  install ${srcdir}/client.conf.in ${pkgdir}/etc/gdm/home/.pulse/client.conf
  install ${srcdir}/locale.conf.in ${pkgdir}/etc/gdm/locale.conf

}

package_libgdm() {
  pkgdesc="GDM support library"

  cd $pkgbase-$pkgver
  gmake -C libgdm DESTDIR="$pkgdir" install
}
