# Port: textproc/py-mako

pkgname=('python-mako' 'python2-mako')
pkgver=1.0.7
pkgrel=1
pkgdesc="Hyperfast and lightweight templating for the Python2 platform"
arch=('amd64')
url="http://www.makotemplates.org/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-markupsafe' 'python2-markupsafe'
	     'python-beaker' 'python2-beaker')
#checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock'
#              'python-babel' 'python2-babel' 'python-pygments' 'python2-pygments'
#              'python-dogpile.cache' 'python2-dogpile.cache')
source=("https://pypi.io/packages/source/M/Mako/Mako-$pkgver.tar.gz"{,.asc})
sha256sums=('4e02fde57bd4abb5ec400181e4c314f56ac3e49ba4fb8b0d50bba18cb27d25ae'
            'SKIP')

prepare() {
    cp -r Mako-$pkgver python2-Mako-$pkgver
}

build() {
    cd Mako-$pkgver
    python3 setup.py build

    cd ../python2-Mako-$pkgver
    python2 setup.py build
}

#check() {
#    cd Mako-$pkgver
#    python3 setup.py test
#
#    cd ../python2-Mako-$pkgver
#    python2 setup.py test
#}

package_python-mako() {
	depends=('python-markupsafe' 'python-beaker' 'python-setuptools')

	cd "${srcdir}/Mako-${pkgver}"
	python3 setup.py install --root="${pkgdir}" --optimize=1
	install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_python2-mako() {
	depends=('python2-markupsafe' 'python2-beaker' 'python2-setuptools')

	cd "${srcdir}/python2-Mako-${pkgver}"
	python2 setup.py install --root="${pkgdir}" --optimize=1
	install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

	mv "${pkgdir}/usr/bin/mako-render" "${pkgdir}/usr/bin/mako-render2"
}
