# $Id: PKGBUILD 165647 2012-08-28 07:31:01Z jgc $
# Maintainer: Daniel Isenmann <daniel [at] archlinux.org>
# Contributor: dorphell <dorphell@gmx.net>

pkgname=gc
pkgver=7.4.2
pkgrel=1
pkgdesc="A garbage collector for C and C++"
arch=('i386' 'amd64')
url="http://www.hpl.hp.com/personal/Hans_Boehm/gc/"
license=('GPL')
source=("http://www.hboehm.info/gc/gc_source/${pkgname}-${pkgver}.tar.gz")
depends=('gcc-libs' 'libatomic_ops')
options=('!libtool')
md5sums=('12c05fd2811d989341d8c6d81f66af87')
prepare() {
  cd ${pkgname}-${pkgver}
  gsed -i 's#pkgdata#doc#' doc/doc.am
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  LDFLAGS="$LDFLAGS -pthread" ./configure --prefix=/usr --enable-cplusplus \
	--disable-static -enable-threads=posix --disable-redirect-malloc
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  gsed -i -e 's/GC_MALLOC 1L/gc 3/' doc/gc.man
  install -m755 -d "${pkgdir}/usr/share/man/man3"
  install -m644 doc/gc.man "${pkgdir}/usr/share/man/man3/gc.3"

}
