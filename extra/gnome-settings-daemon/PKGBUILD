
# $Id: PKGBUILD 192484 2013-08-12 21:17:34Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-settings-daemon
pkgver=3.12.0
pkgrel=1
pkgdesc="The GNOME Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dconf' 'gnome-desktop' 'gsettings-desktop-schemas' 'hicolor-icon-theme' 'libcanberra-pulse' 'libnotify'
         'pulseaudio' 'upower' 'ibus' 'librsvg' 'colord' 'libcups' 'geocode-glib' 'libgweather' 'dbus-glib' 'geoclue2')
makedepends=('intltool' 'libxslt' 'docbook-xsl')
options=('!emptydirs' '!libtool')
install=gnome-settings-daemon.install
url="http://www.gnome.org"
groups=('gnome')
port_patches=(
	patch-plugins_datetime_gsd-timezone-monitor_c
	patch-plugins_keyboard_gsd-keyboard-manager_c
)
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
	${port_patches[@]})
sha256sums=('734fb6cea021a82fca6990f9724e848f27ad9b2b4d4aaef7ae796ff8c21fdaf5'
            '82bf99e115b8aae3a0c6fe350262bb8653981b2c5460603d86f3a334cfe2938c'
            '5dafcb9446e4d28b0ddc8d345e48d15ffba118cfcbb742a0cc9db6ee31ffefbf')

prepare() {
	cd $pkgname-$pkgver

	for i in ${port_patches[@]}; do
		msg "Applying patch ${i}"
		patch -p0 -i "${srcdir}/${i}"
	done
}

build() {
	cd $pkgname-$pkgver

	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
		--libexecdir=/usr/libexec/$pkgname --disable-static --without-colord \
		--disable-rfkill

	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
	gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

	gmake
}

package() {
	cd $pkgname-$pkgver
	gmake DESTDIR="$pkgdir" install
}
