# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>
# Contributor: Matt Harrison <matt at mistbyte dot com>
# Contributor: Renzo Carbonara <gnuk0001@gmail.com>
# Contributor: Bogdan Szczurek <thebodzio@gmail.com>

pkgname=ruby-sass
_gemname=${pkgname#ruby-}
pkgver=3.2.10
pkgrel=1
pkgdesc="Tools and Ruby libraries for the CSS3 extension languages: Sass and SCSS."
arch=('any')
url="http://sass-lang.com/"
license=('MIT')
depends=('ruby' 'ruby-yard' 'ruby-maruku')
makedepends=('rubygems')
optdepends=('rb-inotify>=0.5.1' 'ruby-listen=0.7.2')
source=("http://gems.rubyforge.org/gems/${_gemname}-${pkgver}.gem")
noextract=("${_gemname}-${pkgver}.gem")
sha256sums=('03894e68201da1934891e4707e1ed7813a59c9dee9d61a170fdfa10f8973ce2a')

package() {
	local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"
	gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" ${_gemname}-$pkgver.gem
	install -dm755 "$pkgdir/usr/share/licenses/$pkgname/"
	install -m644 "$pkgdir$_gemdir/gems/${_gemname}-$pkgver/MIT-LICENSE" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
