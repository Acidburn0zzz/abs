# Port: graphics/cairo

pkgname=cairo
pkgver=1.14.8
pkgrel=1
pkgdesc="Cairo vector graphics library"
arch=('i386' 'amd64')
license=('LGPL' 'MPL')
url="http://cairographics.org/"
depends=('libpng' 'libxrender' 'libxext' 'fontconfig' 'pixman>=0.28.0' 'glib2' 'mesa' 'libgl')
makedepends=('mesa-libgl' 'gtk-doc' 'valgrind'
             'ttf-dejavu' 'gsfonts' )
optdepends=('xcb-util: for XCB backend')
provides=('cairo-xcb')
replaces=('cairo-xcb')
options=('libtoolfix')
source=(http://cairographics.org/releases/$pkgname-$pkgver.tar.xz)
sha1sums=('c6f7b99986f93c9df78653c3e6a3b5043f65145e')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i '' -e '/strings/s/ - / -a /' \
    -e '/test/s/==/=/g' \
    -e '/LIBS/s/-lrt//' -e '/LIBS/s|-ldld||' \
    -e '/LIBS/s|-ldl||' \
    configure

  sed -i '' -e 's|-lcairo|-lcairo -lpthread|' \
    src/*.pc.in

  sed -i '' -e '/@CAIRO_HAS_PNG_FUNCTIONS_TRUE@.*=/d' \
    Makefile.in

  sed -i '' -e 's|-ldl||g' \
    util/*/Makefile.in \
    boilerplate/Makefile.in

}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-directfb \
    --disable-gallium \
    --disable-glesv3 \
    --enable-tee \
    --enable-gl \
    --enable-egl \
    --enable-svg \
    --enable-ps \
    --enable-pdf \
    --enable-gobject

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

