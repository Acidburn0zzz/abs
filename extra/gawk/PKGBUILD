# $Id: PKGBUILD 174267 2013-01-03 13:58:07Z allan $
# Maintainer:
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=gawk
pkgver=4.1.1
pkgrel=6
pkgdesc="GNU version of awk"
arch=('i686' 'x86_64')
url="http://www.gnu.org/directory/GNU/gawk.html"
license=('GPL')
groups=('base-devel')
depends=('mpfr' 'gmp' 'gettext-runtime')
provides=('awk')
install=gawk.install
_port_patches=(
	'patch-Makefile'
	'patch-extension_Makefile.in'
)
source=(ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig}
	${_port_patches[@]})
md5sums=('45f5b09aa87b4744c4c53bf274e96ed0'
         'SKIP'
         '06505feb80f569ddeba27dc64eb4f932'
         '7a00601f6c5e5704af4eeb82c4c15867')

prepare() {
	cd ${srcdir}/${pkgname}-${pkgver}
	for _p in ${_port_patches[@]}; do
		patch -p0 -i ../${_p}
	done

	sed -i '' -e 's/ -export-dynamic/ -Wl,--export-dynamic/' \
		configure
}

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	# gawk. STAHP
	AWK=/usr/bin/awk ./configure --prefix=/usr --libexecdir=/usr/lib
	make 
}

check() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make check
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
	rm -f "${pkgdir}/usr/bin/awk"
}
