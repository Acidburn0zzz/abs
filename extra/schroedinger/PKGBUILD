# Port: multimedia/schroedinger

pkgname=schroedinger
pkgver=1.0.11
pkgrel=2
pkgdesc="An implemenation of the Dirac video codec in ANSI C code"
arch=('i386' 'amd64')
url="http://www.diracvideo.org/"
license=('GPL2' 'LGPL2.1' 'MPL' 'MIT')
depends=('orc')
options=('libtoolfix')
source=(http://distcache.FreeBSD.org/ports-distfiles/$pkgname-$pkgver.tar.gz)
md5sums=('da6af08e564ca1157348fb8d92efc891')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i '' -e 's|^pkgconfigdir=.*|pkgconfigdir="${prefix}/libdata/pkgconfig"|' \
    configure

  sed -i '' '/^wavelet_max_gain_OBJECTS =/s/=/= tmp-orc.$(OBJEXT)/' \
    testsuite/Makefile.in

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING.MIT "$pkgdir/usr/share/licenses/$pkgname/COPYING.MIT"
}
