# Port: graphics/poppler
pkgbase=poppler
pkgname=('poppler')
pkgver=0.40.0
pkgrel=1
arch=('i386' 'amd64')
license=('GPL')
makedepends=('libjpeg' 'fontconfig' 'openjpeg' 'pkgconfig' 'lcms2' 'icu')
options=('!emptydirs')
url="http://poppler.freedesktop.org/"
source=(http://poppler.freedesktop.org/${pkgbase}-${pkgver}.tar.xz)
sha256sums=('bc0da278d6038ffc9cd2b8c1bba530a6ec7c072a593219a48f0a52a600ee6ead')

build() {
  cd ${pkgbase}-${pkgver}

  sed -i '' -e '/CXXFLAGS.*-ansi/d' configure
  sed -i '' -e 's|-fno-check-new||g' configure

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --disable-cairo-output \
      --enable-xpdf-headers \
      --enable-libjpeg --enable-zlib \
      --disable-poppler-qt4 \
      --disable-poppler-qt5 \
      --disable-poppler-glib
  gmake
}

package_poppler() {
  pkgdesc="PDF rendering library based on xpdf 3.0"
  depends=('libjpeg' 'fontconfig' 'openjpeg' 'lcms2')
  optdepends=('poppler-data: encoding data to display PDF documents containing CJK characters')
  conflicts=("poppler-qt3<${pkgver}")

  cd "${srcdir}/${pkgbase}-${pkgver}"
  gsed -e 's/^glib_subdir =.*/glib_subdir =/' \
      -e 's/^qt4_subdir =.*/qt4_subdir =/' \
      -e 's/^qt5_subdir =.*/qt5_subdir =/' -i Makefile
  gmake DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}"/usr/libdata/pkgconfig/poppler-{glib,qt4,qt5}.pc
}
