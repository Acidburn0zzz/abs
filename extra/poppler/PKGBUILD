pkgbase=poppler
pkgname=('poppler' 'poppler-glib' 'poppler-qt4' 'poppler-qt5')
pkgver=0.37.0
pkgrel=2
arch=('i386' 'amd64')
license=('GPL')
makedepends=('libjpeg' 'gcc-libs' 'cairo' 'fontconfig' 'openjpeg' 'gtk2' 'qt4' 'pkgconfig' 'lcms2' 'gobject-introspection' 'icu' 'qt5-base' 'git')
options=('!emptydirs')
url="http://poppler.freedesktop.org/"
source=(http://poppler.freedesktop.org/${pkgbase}-${pkgver}.tar.xz
        git://anongit.freedesktop.org/poppler/test)
sha256sums=('b89f9c5eae3bbb1046b0f767714afd75eca102a0406a3a30856778d42a685bee'
            'SKIP')

prepare() {
  ln -sf test-${_testtag} test
#  cd ${pkgbase}-${pkgver}
#  sed -i -e '/AC_PATH_XTRA/d' configure.ac
#  sed -i "s:AM_CONFIG_HEADER:AC_CONFIG_HEADERS:" configure.ac
#  autoreconf -fi
}

build() {
  cd ${pkgbase}-${pkgver}

  sed -i '' -e '/CXXFLAGS.*-ansi/d' configure
  sed -i '' -e 's|-fno-check-new||g' configure

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-cairo-output \
      --enable-xpdf-headers \
      --enable-libjpeg --enable-zlib \
      --enable-poppler-qt4 \
      --enable-poppler-qt5 \
      --enable-poppler-glib
  gmake
}

check() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ## one test failure which is safe to ignore
  LANG=en_US.UTF8 gmake check || true
}

package_poppler() {
  pkgdesc="PDF rendering library based on xpdf 3.0"
  depends=('libjpeg' 'gcc-libs' 'cairo' 'fontconfig' 'openjpeg' 'lcms2')
  optdepends=('poppler-data: encoding data to display PDF documents containing CJK characters')
  conflicts=("poppler-qt3<${pkgver}")

  cd "${srcdir}/${pkgbase}-${pkgver}"
  gsed -e 's/^glib_subdir =.*/glib_subdir =/' \
      -e 's/^qt4_subdir =.*/qt4_subdir =/' \
      -e 's/^qt5_subdir =.*/qt5_subdir =/' -i Makefile
  gmake DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}"/usr/libdata/pkgconfig/poppler-{glib,qt4,qt5}.pc
}

package_poppler-glib() {
  pkgdesc="Poppler glib bindings"
  depends=("poppler=${pkgver}" 'glib2')

  cd "${pkgbase}-${pkgver}"
  gmake -C poppler DESTDIR="${pkgdir}" install-libLTLIBRARIES
  gmake -C glib DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/libdata/pkgconfig"
  install -m644 poppler-glib.pc "${pkgdir}/usr/libdata/pkgconfig/"
  rm -f "${pkgdir}"/usr/lib/libpoppler.*
  rm -f "${pkgdir}/usr/bin/poppler-glib-demo"
}

package_poppler-qt4() {
  pkgdesc="Poppler Qt4 bindings"
  provides=("poppler-qt=${pkgver}")
  replaces=('poppler-qt')
  conflicts=('poppler-qt')
  depends=("poppler=${pkgver}" 'qt4')

  cd "${pkgbase}-${pkgver}"
  gmake -C poppler DESTDIR="${pkgdir}" install-libLTLIBRARIES
  gmake -C qt4 DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/libdata/pkgconfig"
  install -m644 poppler-qt4.pc "${pkgdir}/usr/libdata/pkgconfig/"
  rm -f "${pkgdir}"/usr/lib/libpoppler.*
}

package_poppler-qt5() {
  pkgdesc="Poppler Qt5 bindings"
  depends=("poppler=${pkgver}" 'qt5-base')

  cd "${pkgbase}-${pkgver}"
  gmake -C poppler DESTDIR="${pkgdir}" install-libLTLIBRARIES
  gmake -C qt5 DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/libdata/pkgconfig"
  install -m644 poppler-qt5.pc "${pkgdir}/usr/libdata/pkgconfig/"
  rm -f "${pkgdir}"/usr/lib/libpoppler.*
}
