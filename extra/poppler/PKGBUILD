# Port: graphics/poppler

pkgbase=poppler
pkgname=('poppler' 'poppler-glib' 'poppler-qt4')
pkgver=0.57.0
pkgrel=2
arch=(amd64)
license=('GPL')
makedepends=('libjpeg' 'cairo' 'fontconfig' 'gtk3' 'qt4' 'pkgconfig' 'lcms2' 'openjpeg'
             'gobject-introspection' 'icu' 'git' 'nss' 'gtk-doc' 'curl' 'poppler-data')
options=('!emptydirs')
url="https://poppler.freedesktop.org/"
source=(https://poppler.freedesktop.org/${pkgbase}-${pkgver}.tar.xz
        test::git+https://anongit.freedesktop.org/git/poppler/test/#commit=0d2bfd4)
sha256sums=('0ea37de71b7db78212ebc79df59f99b66409a29c2eac4d882dae9f2397fe44d8'
            'SKIP')

build() {
  cd ${pkgbase}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-cairo-output \
      --enable-xpdf-headers \
      --enable-libjpeg --enable-zlib \
      --enable-poppler-qt4 \
      --disable-poppler-qt5 \
      --enable-poppler-glib \
      --enable-libcurl \
      --enable-gtk-doc \
      --enable-gtk-test

  gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool

  gmake
}

package_poppler() {
  pkgdesc="PDF rendering library based on xpdf 3.0"
  depends=('libjpeg' 'cairo' 'fontconfig' 'lcms2' 'nss' 'curl')
  optdepends=('poppler-data: encoding data to display PDF documents containing CJK characters')
  conflicts=("poppler-qt3<${pkgver}")

  cd "${srcdir}/${pkgbase}-${pkgver}"
  gsed -e 's/^glib_subdir =.*/glib_subdir =/' \
      -e 's/^qt4_subdir =.*/qt4_subdir =/' \
      -e 's/^qt5_subdir =.*/qt5_subdir =/' -i Makefile
  gmake DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}"/usr/lib/pkgconfig/poppler-{glib,qt4,qt5}.pc
}

package_poppler-glib() {
  pkgdesc="Poppler glib bindings"
  depends=("poppler=${pkgver}" 'glib2')

  cd "${pkgbase}-${pkgver}"
  gmake -C poppler DESTDIR="${pkgdir}" install-libLTLIBRARIES
  gmake -C glib DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/lib/pkgconfig"
  install -m644 poppler-glib.pc "${pkgdir}/usr/lib/pkgconfig/"
  rm -f "${pkgdir}"/usr/lib/libpoppler.*
  rm -f "${pkgdir}/usr/bin/poppler-glib-demo"
}

package_poppler-qt4() {
  pkgdesc="Poppler Qt4 bindings"
  provides=("poppler-qt=${pkgver}")
  replaces=('poppler-qt')
  conflicts=('poppler-qt')
  depends=("poppler=${pkgver}" 'qt4')

  cd "${pkgbase}-${pkgver}"
  gmake -C poppler DESTDIR="${pkgdir}" install-libLTLIBRARIES
  gmake -C qt4 DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/lib/pkgconfig"
  install -m644 poppler-qt4.pc "${pkgdir}/usr/lib/pkgconfig/"
  rm -f "${pkgdir}"/usr/lib/libpoppler.*
}
