pkgname=akonadi
pkgver=1.13.0
pkgrel=8
pkgdesc="PIM layer, which provides an asynchronous API to access all kind of PIM data"
arch=('i386' 'amd64')
url='http://community.kde.org/KDE_PIM/Akonadi'
license=('LGPL')
depends=('shared-mime-info' 'qt4' 'libxslt' 'mariadb' 'boost')
makedepends=('extra-cmake-modules' 'postgresql')
optdepends=('postgresql: PostgreSQL backend')
install="${pkgname}.install"
source=("http://download.kde.org/stable/akonadi/src/$pkgname-$pkgver.tar.bz2")
md5sums=('84eb2e471bd6bdfe54a2a2f1d858c07d')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

