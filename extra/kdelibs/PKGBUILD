# $Id: PKGBUILD 189486 2013-07-03 17:00:31Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdelibs
pkgver=4.14.11
_kdeappver=15.08.0
pkgrel=2
pkgdesc="KDE Core Libraries"
arch=('i386' 'amd64')
url='https://projects.kde.org/projects/kde/kdelibs'
license=('GPL' 'LGPL' 'FDL')
depends=('attica' 'libxss' 'grantlee' 'qca' 'libdbusmenu-qt' 'polkit-qt'
        'shared-mime-info' 'enchant' 'giflib' 'jasper' 'openexr' 'strigi'
        'docbook-xsl' 'upower' 'libxcursor' 'phonon-qt4'
        'libxtst' 'qtwebkit' 'icu')
makedepends=('cmake' 'automoc4' 'avahi' 'libgl' 'hspell' 'mesa')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${_kdeappver}/src/${pkgname}-${pkgver}.tar.xz"
        'kde-applications-menu.patch' 'archbsd-menu.patch' 'qt4.patch')
sha1sums=('b25177d6247db8b83faed710f5e713a81c8f8e81'
          '86ee8c8660f19de8141ac99cd6943964d97a1ed7'
          '3634d86ba78a5b24ade02765451a4e6f2d718136'
          'ed1f57ee661e5c7440efcaba7e51d2554709701c')

prepare() {
       mkdir build
       cd ${pkgname}-${pkgver}
       # avoid file conflict with gnome-menus
       patch -p1 -i "${srcdir}"/kde-applications-menu.patch
       # add Archlinux menu entry
       patch -p1 -i "${srcdir}"/archbsd-menu.patch
       # qmake refers to Qt5
       patch -p1 -i "${srcdir}"/qt4.patch
}

build() {
       cd build
       cmake ../${pkgname}-${pkgver} \
               -DCMAKE_BUILD_TYPE=Release \
               -DKDE4_BUILD_TESTS=OFF \
               -DCMAKE_SKIP_RPATH=ON \
               -DKDE_DISTRIBUTION_TEXT='Pac BSD' \
               -DCMAKE_INSTALL_PREFIX=/usr \
               -DSYSCONF_INSTALL_DIR=/etc \
               -DHTML_INSTALL_DIR=/usr/share/doc/kde/html \
               -DKDE_DEFAULT_HOME='.kde4' \
               -DWITH_HUpnp=On \
               -DWITH_SOLID_UDISKS2=ON \
	       -DWITH_ACL=Off	
       make
}

package() {
       cd "${srcdir}"/build
       make DESTDIR="${pkgdir}" install

       # cert bundle seems to be hardcoded
       # link it to the one from ca-certificates
       rm -f "${pkgdir}"/usr/share/apps/kssl/ca-bundle.crt
       ln -sf /usr/share/certs/ca-root-nss.crt "${pkgdir}"/usr/share/apps/kssl/ca-bundle.crt
}
