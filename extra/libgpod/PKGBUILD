# $Id: PKGBUILD 204781 2014-01-27 07:47:56Z jgc $
# Maintainer: Daniel Isenmann <daniel@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=libgpod
pkgver=0.8.3
pkgrel=8
pkgdesc="A shared library to access the contents of an iPod"
arch=(i386 amd64)
license=('LGPL')
depends=('gdk-pixbuf2' 'mutagen' 'sg3_utils' 'libimobiledevice' 'libplist' 'sqlite3')
makedepends=('intltool' 'swig' 'docbook-xsl' 'pygobject2-devel' 'gtk-sharp-2')
optdepends=('gtk-sharp-2: Mono bindings'
            'python2-gobject2: python bindings')
url="http://www.gtkpod.org/libgpod/"
source=(http://downloads.sourceforge.net/sourceforge/gtkpod/${pkgname}-${pkgver}.tar.bz2
        libgpod-0.8.2-pkgconfig_overlinking.patch
        libgpod-fixswig.patch)
options=('!emptydirs' 'libtoolfix')
md5sums=('f8a0b7a34e768e33a708e8dd172bd6f8'
         '234237b643e42a6e9dd3f5000f4cd26e'
         'fb399cf847f21f3e315c01e14c79e815')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../libgpod-0.8.2-pkgconfig_overlinking.patch
  patch -Np1 -i ../libgpod-fixswig.patch
}

build() {
  cd $pkgname-$pkgver
  export CXXFLAGS="-std=gnu89"
  export LDFLAGS="${LDFLAGS} -lintl" 
  ./configure --prefix=/usr \
      --with-python=/usr/bin/python2 \
      --disable-udev
  gmake PREFIX=/usr
}

package() {
  cd $pkgname-$pkgver
  gmake PREFIX=/usr DESTDIR="${pkgdir}" install
}
