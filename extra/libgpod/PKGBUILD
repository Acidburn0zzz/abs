# $Id: PKGBUILD 204781 2014-01-27 07:47:56Z jgc $
# Maintainer: Daniel Isenmann <daniel@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=libgpod
pkgver=0.8.3
pkgrel=1
pkgdesc="A shared library to access the contents of an iPod"
arch=(i686 x86_64)
license=('LGPL')
depends=('gdk-pixbuf2' 'mutagen' 'sg3_utils' 'libimobiledevice' 'libplist')
makedepends=('intltool' 'swig' 'docbook-xsl' 'pygobject2-devel' 'gtk-sharp-2')
optdepends=('gtk-sharp-2: Mono bindings'
            'python2-gobject2: python bindings')
url="http://www.gtkpod.org/libgpod/"
source=(http://downloads.sourceforge.net/sourceforge/gtkpod/${pkgname}-${pkgver}.tar.bz2)
options=('!emptydirs')
md5sums=('f8a0b7a34e768e33a708e8dd172bd6f8')

prepare() {
  cd $pkgname-$pkgver
  sed -i '' -e '/noinst_PROGRAMS =/ s,^,@HAVE_HAL_TRUE@,' \
		tools/Makefile.in
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
      --with-python=/usr/bin/python2 \
      --disable-udev 
  gmake PREFIX=/usr
}

package() {
  cd $pkgname-$pkgver
  gmake PREFIX=/usr DESTDIR="${pkgdir}" install
}
