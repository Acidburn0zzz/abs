# $Id: PKGBUILD 198552 2013-10-30 15:33:40Z allan $
# Maintainer: Anthony Donnelly <Amzo@Archbsd.net>

pkgname=xfce4-diskperf-plugin
pkgver=2.5.5
pkgrel=1
pkgdesc="Plugin for the Xfce4 panel displaying instant disk/partition performance"
arch=('i686' 'amd64')
license=('BSD')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-diskperf-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel')
makedepends=('intltool')
source=(http://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('cbeb0c00f97362eef3f135afb77947aef73e938bae0386250a122ac6644b521b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/libexec \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  gmake
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install
	install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
