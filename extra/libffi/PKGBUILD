# $Id: PKGBUILD 157383 2012-04-28 09:25:57Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libffi
pkgver=3.0.13
pkgrel=2
pkgdesc="A portable, high level programming interface to various calling conventions"
arch=('i686' 'x86_64')
url="http://sourceware.org/libffi"
license=('MIT')
# if you don't have this use makepkg --nocheck
checkdepends=('dejagnu')
install=libffi.install
source=(ftp://sourceware.org/pub/libffi/libffi-${pkgver}.tar.gz
        patch-src__arm__ffi.c)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  [[ "$CARCH" == "x86_64" ]] && CFLAGS="${CFLAGS} -fPIC"

  # arm specific patch, but I'll apply it anyways, as Blub has been playing with arm
  patch -p0 -i "${srcdir}/patch-src__arm__ffi.c"
  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -dm755 "$pkgdir"/usr/include
  ln -sf ../lib/libffi-${pkgver}/include/ffi.h "$pkgdir"/usr/include/
  ln -sf ../lib/libffi-${pkgver}/include/ffitarget.h "$pkgdir"/usr/include/

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install    -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
md5sums=('45f3b6dbc9ee7c7dfbbbc5feba571529'
         '0fec9f0b962939ce85c8c7690daba420')

