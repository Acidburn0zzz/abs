# $Id: PKGBUILD 191835 2013-07-31 09:23:27Z jgc $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gtkhtml4
_pkgbasename=gtkhtml
pkgver=4.10.0
pkgrel=3
pkgdesc="A lightweight HTML renderer/editor widget for GTK3"
arch=('i386' 'amd64')
license=('GPL')
depends=('gtk3' 'enchant' 'iso-codes' 'gnome-icon-theme' 'libsoup')
makedepends=('intltool')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$_pkgbasename/${pkgver%.*}/$_pkgbasename-$pkgver.tar.xz)
sha256sums=('ca3b6424fb2c7ac5d9cb8fdafb69318fa2e825c9cf6ed17d1e38d9b29e5606c3')

build() {
    cd "$_pkgbasename-$pkgver"
    ./configure --prefix=/usr --sysconfdir=/etc \
        --libexecdir=/usr/lib/gtkhtml4 \
        --localstatedir=/var --disable-static LIBS="-lintl"
    gmake
}

package() {
    cd "$_pkgbasename-$pkgver"
    gmake DESTDIR="$pkgdir" install
}
