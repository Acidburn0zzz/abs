# Port: x11/libxcb
pkgname=libxcb
pkgver=1.11.1
pkgrel=1
pkgdesc="X11 client-side library"
arch=('i386' 'amd64')
url="http://xcb.freedesktop.org/"
depends=('xcb-proto>=1.7' 'libxdmcp' 'libxau' 'xorg-util-macros' 'libpthread-stubs')
makedepends=('pkgconf' 'libxslt' 'python')
license=('custom')
source=($url/dist/$pkgname-$pkgver.tar.bz2)
sha1sums=('28e47aa699d2c2cffed31d1aa2473a9fefe77f17')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  find . -type f | xargs sed -i '' -e 's|-pR|-R|g' -e 's|-fpR|-fR|g'

  export FONTROOTDIR=/usr/share/fonts
  ./configure --prefix=/usr \
    --enable-xinput \
    --enable-xkb \
    --disable-static \
    --without-doxygen --disable-build-docs \
    --with-fontrootdir=$FONTROOTDIR LIBS="`pkg-config --libs pthread-stubs`"
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname"
}
