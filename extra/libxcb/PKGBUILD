# Port: x11/libxcb

pkgname=libxcb
pkgver=1.12
pkgrel=1
pkgdesc="X11 client-side library"
arch=('amd64')
url="http://xcb.freedesktop.org/"
depends=('xcb-proto>=1.7' 'libxdmcp' 'libxau' 'xorg-util-macros' 'libpthread-stubs')
makedepends=('pkgconf' 'libxslt' 'python')
license=('custom')
source=($url/dist/$pkgname-$pkgver.tar.bz2)
sha256sums=('4adfb1b7c67e99bc9c2ccb110b2f175686576d2f792c8a71b9c8b19014057b5b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  find . -type f | xargs sed -i '' -e 's|-pR|-R|g' -e 's|-fpR|-fR|g'

  export FONTROOTDIR=/usr/share/fonts
  ./configure --prefix=/usr \
    --enable-xinput \
    --enable-xkb \
    --disable-static \
    --without-doxygen --disable-build-docs \
    --with-fontrootdir=$FONTROOTDIR LIBS="`pkg-config --libs pthread-stubs`"
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname"
}
