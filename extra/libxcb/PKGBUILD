pkgname=libxcb
pkgver=1.11.1
pkgrel=1
pkgdesc="X11 client-side library"
arch=('amd64')
url="http://xcb.freedesktop.org/"
depends=('xcb-proto>=1.7' 'libxdmcp' 'libxau' 'xorg-util-macros' 'libpthread-stubs')
makedepends=('pkgconf' 'libxslt' 'python')
license=('custom')
source=($url/dist/$pkgname-$pkgver.tar.bz2
        libxcb-1.1-no-pthread-stubs.patch
	patch-tests-check_public.c)
sha1sums=('28e47aa699d2c2cffed31d1aa2473a9fefe77f17'
          'cf78222031a6c6c89373b227820f23326041c26e'
          'fe8f00016e6cdb4dff52c1eaba9288e300c85deb')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/libxcb-1.1-no-pthread-stubs.patch"
  patch -p0 -i "$srcdir/patch-tests-check_public.c"
 
  find . -type f | xargs sed -i '' -e 's|-pR|-R|g' -e 's|-fpR|-fR|g'

  export FONTROOTDIR=/usr/share/fonts
  ./autogen.sh --prefix=/usr \
    --enable-xinput \
    --enable-xkb \
    --disable-static \
    --without-doxygen --disable-build-docs \
    --with-fontrootdir=$FONTROOTDIR LIBS="`pkg-config --libs pthread-stubs`"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname"
}
