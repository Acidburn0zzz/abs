# $Id: PKGBUILD 184359 2013-05-06 19:36:30Z foutrelis $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: MaÃ«l Lavault <moimael@neuf.fr>

pkgbase=abiword
pkgname=('abiword' 'abiword-plugins')
pkgver=2.8.6
pkgrel=9
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.abisource.com"
makedepends=('pkg-config' 'asio' 'boost' 'fribidi' 'goffice0.8' 'libwmf' 'wv' 'link-grammar' 
             'aiksaurus' 'libxslt' 'enchant' 'libots' 'libwpg' 'librsvg' 'loudmouth' 'libsoup' 'psiconv')
options=('!makeflags' )
port_patch=(patch-Makefile.in
	patch-abiword.desktop
	patch-default-font
	patch-plugins_bmp_xp_ie_impGraphic_BMP.cpp
	patch-plugins_garble_xp_abiword-garble-png.cpp
	patch-plugins_mathview_itex2mml_itex2MML.y
	patch-src_af_util_xp_ut_iconv.cpp
	patch-src_af_util_xp_ut_png.cpp
	patch-src_wp_impexp_gtk_ie_impGraphic_GdkPixbuf.cpp)
source=(http://www.abisource.com/downloads/${pkgbase}/${pkgver}/source/${pkgbase}-${pkgver}.tar.gz
        compat_libwpg_0_9.patch
	abiword-2.8.6-no-undefined.patch
        abiword-2.8.6-libpng15.patch)
md5sums=('f883b0a7f26229a9c66fd6a1a94381aa'
         '09446a2ebba8288fc611e88f7cf17298'
         '42e2614b6e0aad457d91f6ce20b6f225'
         '40e370e7e88c5437140ef18037abfbd3')

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  for i in ${port_patches[@]}; do
	msg "Applying patch $i"
	patch -p0 -i ${srcdir}/$i
  done

  sed -i '' -e 's|glib/.*\.h>|glib.h>|g' \
		goffice-bits/goffice/app/goffice-app.h \
		src/af/util/xp/ut_go_file.h
  sed -i '' -e 's|" == "|" = "|g' configure

  export CC=clang
  export CPP=clang-cpp
  export CXX=clang++
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  patch -Np1 -i "${srcdir}/compat_libwpg_0_9.patch"
  # fix a gcc 4.6.x build issue, patch from Fedora
#  patch -Np1 -i "${srcdir}/abiword-2.8.6-no-undefined.patch"
  patch -Np0 -i "${srcdir}/abiword-2.8.6-libpng15.patch"

  ./configure --prefix=/usr \
              --enable-clipart \
              --enable-templates \
              --enable-collab-backend-tcp \
              --enable-collab-backend-service \
              --disable-collab-backend-sugar \
              --enable-plugins --without-gnomevfs \
              --with-gio \
              --with-goffice \
              --disable-static \
              --with-psiconv-config=/nothere
  gmake
}

package_abiword() {
  pkgdesc="A fully-featured word processor"
  depends=('fribidi' 'wv' 'goffice0.8' 'librsvg' 'enchant' 'desktop-file-utils')
  install=$pkgname.install
  optdepends=('abiword-plugins')
  conflicts=("abiword-plugins<${pkgver}-${pkgrel}")

  cd "${srcdir}/${pkgbase}-${pkgver}"
  gsed -i plugins/Makefile \
      -e 's/ collab / /' \
      -e 's/ wpg / /' \
      -e 's/ wmf / /' \
      -e 's/ grammar / /' \
      -e 's/ aiksaurus / /' \
      -e 's/ latex / /' \
      -e 's/ ots / /' \
      -e 's/ wordperfect / /'
  gmake DESTDIR="${pkgdir}" install
}

package_abiword-plugins() {
  pkgdesc="Additional plugins for Abiword"
  depends=("abiword=${pkgver}-${pkgrel}" 'loudmouth' 'libwpg' 'libwmf' 'link-grammar' 
           'aiksaurus' 'libxslt' 'libsoup' 'libots' 'libgsf')

  cd "${srcdir}/${pkgbase}-${pkgver}/plugins"
  for dir in collab wpg wmf grammar aiksaurus latex ots wordperfect; do
    gmake -C ${dir} DESTDIR="${pkgdir}" install
  done
}
