# $Id: PKGBUILD 183685 2013-04-26 16:04:28Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gstreamer
pkgver=1.6.0
pkgrel=3
pkgdesc="GStreamer Multimedia Framework"
arch=('i386' 'amd64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('libxml2' 'glib2')
makedepends=('intltool' 'pkgconfig' 'gtk-doc' 'gobject-introspection' 'python2')
source=(${url}/src/gstreamer/gstreamer-${pkgver}.tar.xz)
sha256sums=('52ef885647afef11c8b7645a9afefe04aa09e8971c4b932e7717872ab8a30fcc')

build() {
  cd "${srcdir}/gstreamer-${pkgver}"
 
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/libexec \
    --with-package-name="GStreamer (PacBSD)" \
    --with-package-origin="http://www.PacBSD.org/" \
    --disable-static \
    --disable-tests \
    --disable-examples \
    --disable-failing-tests \
    --disable-fatal-warnings \
    --disable-gtk-doc \
    ac_cv_func_register_printf_function="no"

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  gmake
}

check() {
  cd "${srcdir}/gstreamer-${pkgver}"

  #Unexpected error from C library during 'pthread_mutex_unlock': Operation not permitted.
  gmake check
}

package() {
  cd "${srcdir}/gstreamer-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
}
