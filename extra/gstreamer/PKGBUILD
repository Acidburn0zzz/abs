# Port: multimedia/gstreamer1
pkgname=gstreamer
pkgver=1.12.2
pkgrel=1
pkgdesc="GStreamer Multimedia Framework"
arch=('i386' 'amd64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('libxml2' 'glib2')
optdepends=('sh: feedback script')
makedepends=('intltool' 'pkgconfig' 'gtk-doc' 'gobject-introspection' 'python2' 'bison')
source=(${url}/src/gstreamer/gstreamer-${pkgver}.tar.xz)
sha256sums=('9fde3f39a2ea984f9e07ce09250285ce91f6e3619d186889f75b5154ecf994ba')

build() {
  cd "${srcdir}/gstreamer-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/libexec \
    --with-package-name="GStreamer (PacBSD)" \
    --with-package-origin="http://www.pacbsd.org/" \
    --enable-gtk-doc --disable-static --disable-tests \
    --disable-examples --disable-failing-tests --disable-fatal-warnings \
    ac_cv_func_register_printf_function="no"

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  gmake
}

check() {
  cd "${srcdir}/gstreamer-${pkgver}"
  gmake check || true
}

package() {
  cd "${srcdir}/gstreamer-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
}

