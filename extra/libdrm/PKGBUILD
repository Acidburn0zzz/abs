# Maintainer: Anthony Donnelly <Amzo@archbsd.net>

pkgname=libdrm
pkgver=2.4.58
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=(i686 x86_64)
license=('custom')
depends=('libpciaccess' 'libpthread-stubs')
replaces=('libdrm-new' 'libdrm-nouveau')
url="http://dri.freedesktop.org/"
options=("libtoolfix")
source=(http://dri.freedesktop.org/$pkgname/$pkgname-$pkgver.tar.bz2
        COPYING)

prepare() {
  cd $pkgname-$pkgver

  # man/Makefile,in is missing
  sed -i '' -e '/86|x86_64) INTEL=yes/ s@x86_64@&|amd64@' configure

  #libtoolize --force
  autoreconf --force --install
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --enable-libkms \
    --disable-vmwgfx
  gmake
}

check() {
  cd $pkgname-$pkgver
  gmake -k check
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/"
}
sha256sums=('b155fae6b9c9a3b02ef8b77f58c7c219194c996a4018dc55ba66c03996a365dd'
            '9631d4f694952e3e6ae5a05534c2e93e994e47d3413677a3a00e45c8cef6db93')
