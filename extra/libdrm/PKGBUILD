# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libdrm
pkgver=2.4.52
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=(i686 x86_64)
license=('custom')
depends=('libpciaccess')
makedepends=('cairo')
replaces=('libdrm-new' 'libdrm-nouveau')
url="http://dri.freedesktop.org/"
_port_patches=(
	extra-configure
	extra-tests__radeon__radeon_ttm.c
	extra-tests_modetest_Makefile.in
	extra-tests_modetest_modetest.c
	extra-tests_modetest_strchrnul.c
	extra-xf86drmMode.c
)
source=(http://dri.freedesktop.org/$pkgname/$pkgname-$pkgver.tar.bz2
        COPYING
        ${_port_patches[@]})
sha256sums=('fa693c2f1f61befcefbdcc396673e38481110bac9db610afa4b8afb2be0218c1'
            '9631d4f694952e3e6ae5a05534c2e93e994e47d3413677a3a00e45c8cef6db93'
            '4fa2a5f05a82042827ad6dafe48a419a8a2b118695e4ee5613f9a46f785b2ba9'
            '3a467693a27f02301794abe86e7bb9c291901660d4c78989b840d73b8e60c5ce'
            '1b8258a3dcd550228519cd74f869242fa7266be596bd19407643c67b16ec2a50'
            '5016eb84619b10275f2c2ae785cc6d7cc580b696989b06cc43cd215bd8844fff'
            'abb6821f559992111c366199a75f8c0277c7f21d09c27f0d34e8158a6a04083a'
            '87ee4bf81818dc8e1929f1d640ed3002b5cff1f1b458247801060866f6ec4e4a')

prepare() {
  cd $pkgname-$pkgver

  # man/Makefile,in is missing
  sed -i '' -e '/86|x86_64) INTEL=yes/ s@x86_64@&|amd64@' configure

  #libtoolize --force
  autoreconf --force --install

  # NOTE: HAS TO BE AFTER autoreconf
  for i in ${_port_patches[@]}; do
    msg "port patch $i"
    patch -p0 -i "${srcdir}/$i"
  done

  gsed -i -e '/#include <sys\/mman\.h>/i#define ERESTART (-1)' \
    libkms/vmwgfx.c
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --enable-libkms
  gmake
}

check() {
  cd $pkgname-$pkgver
  gmake -k check
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/"
}
