# Maintainer: Anthony Donnelly <Amzo@archbsd.net>

pkgname=libdrm
pkgver=2.4.65
pkgrel=6
pkgdesc="Userspace interface to kernel DRM services"
arch=('i386' 'amd64')
license=('custom')
depends=('libpciaccess' 'libpthread-stubs')
makedepends=('valgrind' 'xorg-util-macros')
replaces=('libdrm-new' 'libdrm-nouveau')
url="http://dri.freedesktop.org/"
options=("libtoolfix")
source=(http://dri.freedesktop.org/$pkgname/$pkgname-$pkgver.tar.bz2
        COPYING)
sha256sums=('71960ac8bde7d710992b1bc8879935e8300a870c36bd06f22412d0447e3d96c4'
            '9631d4f694952e3e6ae5a05534c2e93e994e47d3413677a3a00e45c8cef6db93')

prepare() {
  cd $pkgname-$pkgver
  unset ac_cv_build
  unset ac_cv_host

  case $CARCH in
    i386)
      build="$(uname -m)-pacbsd-freebsd${OSREL/-*/}"
      ;;
    amd64)
      build="x86_64-pacbsd-freebsd${OSREL/-*/}"
      ;;
  esac

  # man/Makefile,in is missing
  sed -i '' -e 's|{libdir}/pkgconfig|{prefix}/libdata/pkgconfig|g' \
    -e 's,i?86|x86_64),i?86|amd64|x86_64),g' \
    configure

  #libtoolize --force
  autoreconf --force --install
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --enable-libkms \
    --disable-vmwgfx --disable-nouveau --build="${build}"
  gmake
}

check() {
  cd $pkgname-$pkgver

  gmake -k check
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/"
}
