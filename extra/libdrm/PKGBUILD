# Maintainer: Anthony Donnelly <Amzo@archbsd.net>

pkgname=libdrm
pkgver=2.4.65
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=('amd64')
license=('custom')
depends=('libpciaccess' 'libpthread-stubs')
makedepends=('valgrind' 'xorg-util-macros')
replaces=('libdrm-new' 'libdrm-nouveau')
url="http://dri.freedesktop.org/"
options=("libtoolfix")
source=(http://dri.freedesktop.org/$pkgname/$pkgname-$pkgver.tar.bz2
        COPYING)
sha256sums=('71960ac8bde7d710992b1bc8879935e8300a870c36bd06f22412d0447e3d96c4'
            '9631d4f694952e3e6ae5a05534c2e93e994e47d3413677a3a00e45c8cef6db93')

prepare() {
  cd $pkgname-$pkgver

  # man/Makefile,in is missing
  sed -i '' -e 's|{libdir}/pkgconfig|{prefix}/libdata/pkgconfig|g' \
    -e 's,i?86|x86_64),i?86|amd64|x86_64),g' \
    configure

  #libtoolize --force
  autoreconf --force --install
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --enable-libkms \
    --disable-vmwgfx --disable-nouveau
  gmake
}

check() {
  cd $pkgname-$pkgver

  gmake -k check
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/"
}
