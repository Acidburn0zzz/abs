# $Id: PKGBUILD 186412 2013-05-26 09:25:29Z bluewind $
# Maintainer: kevin <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=perl-net-dns
_realname=Net-DNS
pkgver=0.83
pkgrel=1
pkgdesc="Perl Module: Interface to the DNS resolver."
arch=('i686' 'amd64')
license=('PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl-digest-hmac' 'perl-net-ip' 'perl')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/N/NL/NLNETLABS/${_realname}-$pkgver.tar.gz)
md5sums=('f1d48107ff6b366479ad035783486d7a')

build() {
  cd "$srcdir/${_realname}-${pkgver}"
  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
# vim: ts=2 sw=2 et ft=sh
