# $Id: PKGBUILD 183600 2013-04-24 15:18:44Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=pangomm
pkgname=('pangomm' 'pangomm-docs')
pkgver=2.36.0
pkgrel=3
arch=('i386' 'amd64')
license=('LGPL')
makedepends=('pango' 'glibmm' 'glibmm-docs' 'cairomm' 'pkgconf')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver::4}/${pkgname}-${pkgver}.tar.xz)
url="http://gtkmm.sourceforge.net/"
sha256sums=('a8d96952c708d7726bed260d693cece554f8f00e48b97cccfbf4f5690b6821f0')
url="http://gtkmm.sourceforge.net/"

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  #Doesn't find gmake?
  export MAKE=gmake
  export CXXFLAGS+=" -std=c++11"

  ./configure --prefix=/usr
  gmake
}
package_pangomm() {
  pkgdesc="C++ bindings for pango"
  depends=('pango' 'glibmm' 'cairomm')
  cd "${srcdir}/${pkgbase}-${pkgver}"
  gsed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  gmake DESTDIR="${pkgdir}" install
}

package_pangomm-docs() {
  pkgdesc="Developer documentation for pangomm"
  cd "${srcdir}/${pkgbase}-${pkgver}/docs"
  gmake DESTDIR="${pkgdir}" install
}
