pkgname=libjpeg-turbo
pkgver=1.4.2
pkgrel=1
pkgdesc='JPEG image codec with accelerated baseline compression and decompression'
url='http://libjpeg-turbo.virtualgl.org/'
arch=('i386' 'amd64')
license=('custom')
makedepends=('nasm')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('2666158ccd5318513f875867bbc4af52f6eb9f0b')

provides=('libjpeg=8.0.2' 'turbojpeg')
conflicts=('libjpeg' 'turbojpeg')
replaces=('libjpeg' 'turbojpeg')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --with-jpeg8 --mandir=/usr/share/man
	gmake
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	gmake test
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	gmake \
		DESTDIR="${pkgdir}" \
		docdir="/usr/share/doc/${pkgname}" \
		exampledir="/usr/share/doc/${pkgname}" \
		install
	
	install -d "${pkgdir}/usr/share/licenses/libjpeg-turbo"
	ln -t "${pkgdir}/usr/share/licenses/libjpeg-turbo" -s ../../doc/libjpeg-turbo/README{,-turbo.txt}
	install -m644 jpegint.h "${pkgdir}/usr/include" # required by other software
}

