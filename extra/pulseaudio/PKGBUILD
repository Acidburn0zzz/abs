#
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgbase=pulseaudio
pkgname=(pulseaudio libpulse)
pkgdesc="A featureful, general-purpose sound server"
pkgver=5.0
pkgrel=8
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/PulseAudio"
license=(GPL LGPL)
makedepends=(libxtst libsm libsamplerate libtool
             speex tdb dbus gconf intltool jack 
             orc json-c gtk3 check)
options=(!emptydirs !libtool)
#source=(http://freedesktop.org/software/$pkgbase/releases/$pkgbase-$pkgver.tar.xz
_port_patches=(
        patch-src_daemon_default.pa.in
        patch-src_modules_module-detect.c
        patch-src_modules_oss_module-oss.c
        patch-src_modules_oss_oss-util.c
        patch-src_pulse_introspect.c
        patch-src_pulse_thread-mainloop.c
        patch-src_pulse_util.c
        patch-src_pulsecore_atomic.h)
source=(http://freedesktop.org/software/$pkgbase/releases/$pkgbase-$pkgver.tar.xz
        "${_port_patches[@]}")
sha256sums=('99c13a8b1249ddbd724f195579df79484e9af6418cecf6a15f003a7f36caf939'
            '73a9f6bdc51c7bc42819b6417b87bed54c7570c12e8a4cc91fdd0d4d7e3617c0'
            'ebc526167d5873a86bec8fcfe8460404e3cf79ba607b3d52c0093fe9e85622a0'
            'a44ce7fb537269dfd372c5fa0555bc35be0fb8b7831c6cb4885fa95de9245fd0'
            '8d263b05d58fa9517bd24ac541382a3589ebaa6b49bb6060f3efbec3f4ccc8e7'
            '7dfd0cbbec0215c52efc3e9e2123dd0e63a18a2c37c8d6c9820d0cce6cb13f9f'
            '85104634a9339dbeee16c08f306dd5ee2759bc503e26f7d9750f99c2cc93842e'
            '5bf51093999344c37fb12734139d2b5ff681f5dba1e6439efad3a268586ea305'
            '86e4de6bd0046204d3b86af11c75f53fcb80de4f365968b217fae929fd5570f8')

prepare() {
  cd $pkgbase-$pkgver

  for patch in "${_port_patches[@]}"; do
    msg "patch $patch"
    patch -p0 -i "$srcdir/$patch"
  done

  sed -i '' -e 's|%%LOCALBASE%%|/usr/|g' \
      src/daemon/default.pa.in
}

build() {
  cd $pkgbase-$pkgver

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/libexec \
    --localstatedir=/var \
    --with-database=simple \
    --disable-udev \
    --disable-gtk2 \
    --disable-lirc \
    --disable-tcpwrap \
    --disable-rpath \
    --disable-default-build-tests \
    LIBS="-lm -lintl" \
    OPENSSL_CFLAGS="-I/usr/include" \
    OPENSSL_LIBS="-lcrypto -lssl" \
    ac_cv_header_linux_input_h=  

  # fight unused direct deps

  gmake
}

package_pulseaudio() {
  depends=("libpulse=$pkgver-$pkgrel" libltdl speex tdb orc
           libsamplerate consolekit)
  optdepends=('gconf: configuration through gconf (paprefs)'
              'jack: jack support'
              'python2-pyqt: Equalizer GUI (qpaeq)')
  backup=(etc/pulse/{daemon.conf,default.pa,system.pa})

  cd $pkgbase-$pkgver
  gmake -j1 DESTDIR="$pkgdir" install \
    bashcompletiondir=/usr/share/bash-completion/completions

  # Disable autospawn by default
  gsed -e '/autospawn/iautospawn=no' \
      -i "$pkgdir/etc/pulse/client.conf"

  # Speed up pulseaudio shutdown
  # Lower resample quality, saves CPU
  gsed -e '/exit-idle-time/iexit-idle-time=0' \
      -e '/resample-method/iresample-method=speex-float-0' \
      -i "$pkgdir/etc/pulse/daemon.conf"

  # Disable cork-request module, can result in e.g. media players unpausing
  # when there's a Skype call incoming
  gsed -e 's|/usr/bin/pactl load-module module-x11-cork-request|#&|' \
      -i "$pkgdir/usr/bin/start-pulseaudio-x11"

  # Make ConsoleKit optional
  gsed -e $'/load-module module-console-kit/{i.nofail\n;a.fail\n;}' \
      -i "$pkgdir/etc/pulse/default.pa"

  rm "$pkgdir/etc/dbus-1/system.d/pulseaudio-system.conf"

### Split libpulse

  mkdir -p "$srcdir"/libpulse/{etc/pulse,usr/{bin,lib/pulseaudio,share/man/man{1,5}}}

  mv {"$pkgdir","$srcdir/libpulse"}/etc/pulse/client.conf

  mv "$pkgdir"/usr/bin/pa{cat,ctl,dsp,mon,play,rec,record} \
     "$srcdir/libpulse/usr/bin"

  mv "$pkgdir"/usr/lib/libpulse{,-simple,-mainloop-glib}.so* \
     "$srcdir/libpulse/usr/lib"

  mv "$pkgdir"/usr/lib/pulseaudio/libpulsedsp.so \
     "$pkgdir"/usr/lib/pulseaudio/libpulsecommon-*.so \
     "$srcdir/libpulse/usr/lib/pulseaudio"

  mv {"$pkgdir","$srcdir/libpulse"}/usr/lib/cmake
  mv {"$pkgdir","$srcdir/libpulse"}/usr/lib/pkgconfig

  mv {"$pkgdir","$srcdir/libpulse"}/usr/include

  mv "$pkgdir"/usr/share/man/man1/pa{cat,ctl,dsp,play}.1 \
     "$srcdir/libpulse/usr/share/man/man1"

  mv {"$pkgdir","$srcdir/libpulse"}/usr/share/man/man5/pulse-client.conf.5
  mv {"$pkgdir","$srcdir/libpulse"}/usr/share/vala
}

package_libpulse() {
  pkgdesc="$pkgdesc (client library)"
  depends=(dbus libxtst libsm libsndfile json-c)
  optdepends=('alsa-plugins: ALSA support'
              'avahi: zeroconf support')
  backup=(etc/pulse/client.conf)

  mv "$srcdir"/libpulse/* "$pkgdir"
}
