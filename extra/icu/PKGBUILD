# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Art Gramlich <art@gramlich-net.com>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=icu
pkgver=54.1
pkgrel=1
pkgdesc="International Components for Unicode library"
arch=(i686 x86_64)
url="http://www.icu-project.org/"
license=('custom:"icu"')
source=(http://download.icu-project.org/files/${pkgname}4c/${pkgver}/${pkgname}4c-${pkgver//./_}-src.tgz
	    icu.8198.revert.icu5431.patch)
md5sums=('e844caed8f2ca24c088505b0d6271bc0'
         'ebd5470fc969c75e52baf4af94a9ee82')

prepare() {
  cd icu/source
  # fix Malayalam encoding https://bugzilla.redhat.com/show_bug.cgi?id=654200
  patch -Rp3 -i ${srcdir}/icu.8198.revert.icu5431.patch
}

build() {
  cd ${srcdir}/icu/source

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --mandir=/usr/share/man \
	      --enable-weak-threads
  gmake
}

check() {
  cd icu/source
  gmake -k check # passes all
}

package() {
  cd ${srcdir}/icu/source
  gmake -j1 DESTDIR=${pkgdir} install

  # Install license
  install -dm755 ${pkgdir}/usr/share/licenses/icu/
  install -m644 ${srcdir}/icu/license.html ${pkgdir}/usr/share/licenses/icu/license.html
}
