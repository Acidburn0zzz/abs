# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Art Gramlich <art@gramlich-net.com>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=icu
pkgver=53.1
pkgrel=1
pkgdesc="International Components for Unicode library"
arch=(i686 x86_64)
url="http://www.icu-project.org/"
license=('custom:"icu"')
source=(#http://download.icu-project.org/files/${pkgname}4c/${pkgver}/${pkgname}4c-${pkgver/./_}-src.tgz
        http://download.icu-project.org/files/${pkgname}4c/${pkgver}/${pkgname}4c-${pkgver//./_}-src.tgz
	patch-config__mh-bsd-gcc
	patch-install)
md5sums=('b73baa6fbdfef197608d1f69300919b9'
         'f7d1f090364f4fae01626e087abccb78'
         '2717f21ede04b55edae471bdf3b32ca0')

prepare() {
  cd ${srcdir}/icu/source

  for i in patch-config__mh-bsd-gcc patch-install; do
    msg "patch $i"
    patch -p0 -i "${srcdir}/$i"
  done
}

build() {
  cd ${srcdir}/icu/source

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --mandir=/usr/share/man \
              --disable-sample \
              --with-data-packaging=library \
              --enable-release \
              --enable-shared \
              --enable-static
  gmake
}

package() {
  cd ${srcdir}/icu/source
  gmake -j1 DESTDIR=${pkgdir} install

  # Install license
  install -dm755 ${pkgdir}/usr/share/licenses/icu/
  install -m644 ${srcdir}/icu/license.html ${pkgdir}/usr/share/licenses/icu/license.html
}
