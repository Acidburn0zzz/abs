pkgname=webkit2gtk
pkgver=2.8.5
pkgrel=3
pkgdesc="GTK+ Web content engine library for GTK+ 2.0"
arch=('amd64')
url="http://webkitgtk.org/"
license=(custom)
depends=('libxt' 'libxslt' 'enchant' 'geoclue2' 'gst-plugins-base-libs'
	 'libsecret' 'libwebp' 'harfbuzz-icu' 'gtk3' 'libnotify')
makedepends=('gtk2' 'gperf' 'gobject-introspection' 'ruby' 'gtk-doc' 'cmake' 'python')
optdepends=('gtk2: Netscape plugin support'
            'gst-plugins-base: free media decoding'
            'gst-plugins-good: media decoding'
            'gst-libav: nonfree media decoding')
provides=("libwebkit=${pkgver}")
conflicts=(libwebkit)
replaces=(libwebkit)
# our patches are explicitly for clang with libc++
options=(!emptydirs libtoolfix)
patchdir=(webkitgtk-${pkgver})
source=(http://webkitgtk.org/releases/webkitgtk-$pkgver.tar.xz)

prepare() {
  mkdir build

  cd webkitgtk-$pkgver
  gsed -i '1s/python$/&2/' Tools/gtk/generate-gtkdoc
  rm -r Source/ThirdParty/gtest/
  rm -r Source/ThirdParty/qunit/
}

build() {
  cd build
  cmake -DPORT=GTK -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=ON -DCMAKE_INSTALL_PREFIX=/usr \
        -DLIB_INSTALL_DIR=/usr/lib -DLIBEXEC_INSTALL_DIR=/usr/libexec/webkit2gtk-4.0 \
        -DENABLE_GTKDOC=ON ../webkitgtk-$pkgver
  gmake
}

package() {
  cd build
  gmake DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/usr/share/licenses/webkit2gtk"
  cd "$srcdir/webkitgtk-$pkgver/Source"
  for f in $(find -name 'COPYING*' -or -name 'LICENSE*'); do
    echo $f >> "$pkgdir/usr/share/licenses/webkit2gtk/LICENSE"
    cat $f >> "$pkgdir/usr/share/licenses/webkit2gtk/LICENSE"
    echo "" >> "$pkgdir/usr/share/licenses/webkit2gtk/LICENSE"
  done
}

md5sums=('df79991848a5096d3a75289ebce547ae')
