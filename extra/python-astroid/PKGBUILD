pkgbase=python-astroid
pkgname=('python2-astroid' 'python-astroid')
pkgver=1.3.8.20150929
pkgrel=1
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('i386' 'amd64')
url="http://www.logilab.org/project/logilab-common"
license=('LGPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
checkdepends=('python-logilab-common' 'python2-logilab-common')
source=("hg+https://bitbucket.org/logilab/astroid#revision=$_commit")
sha1sums=('SKIP')

prepare() {
  cp -a astroid-${pkgver}{,-py3}
}

build() {
  cd astroid-${pkgver}
  python2 setup.py build

  cd ../astroid-${pkgver}-py3
  python3 setup.py build
}

check() {
  cd astroid-${pkgver}
  PYTHONPATH="$PWD/../build/lib:$PYTHONPATH" python2 -Wi -m unittest discover -s build/lib/astroid/tests -p "unittest*.py"

  cd ../astroid-${pkgver}-py3
  PYTHONPATH="$PWD/../build/lib:$PYTHONPATH" python -Wi -m unittest discover -s build/lib/astroid/tests -p "unittest*.py"
}

package_python2-astroid() {
  depends=('python2-logilab-common' 'python2-six')
  replaces=('python2-logilab-astng')
  conflicts=('python2-logilab-astng')

  cd astroid-${pkgver}

  python2 setup.py install --optimize=1 --skip-build --prefix=/usr --root="${pkgdir}"
}

package_python-astroid() {
  depends=('python-logilab-common' 'python-six')
  replaces=('python-logilab-astng')
  conflicts=('python-logilab-astng')

  cd astroid-${pkgver}-py3

  python3 setup.py install --optimize=1 --skip-build --prefix=/usr --root="${pkgdir}"
}

