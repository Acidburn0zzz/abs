pkgbase=python-astroid
pkgname=('python2-astroid' 'python-astroid')
pkgver=1.3.8.20150929
pkgrel=5
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('i386' 'amd64')
url="http://www.logilab.org/project/logilab-common"
license=('LGPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six' 'mercurial')
source=("hg+https://bitbucket.org/logilab/astroid#revision=$_commit")
sha1sums=('SKIP')

prepare() {
  cp -a astroid{,-py3}
}

build() {
  cd astroid
  python2 setup.py build

  cd ../astroid-py3
  python3 setup.py build
}

package_python2-astroid() {
  depends=('python2-logilab-common' 'python2-six')
  replaces=('python2-logilab-astng')
  conflicts=('python2-logilab-astng')

  cd astroid

  python2 setup.py install --optimize=1 --skip-build --prefix=/usr --root="${pkgdir}"
}

package_python-astroid() {
  depends=('python-logilab-common' 'python-six')
  replaces=('python-logilab-astng')
  conflicts=('python-logilab-astng')

  cd astroid-py3

  python3 setup.py install --optimize=1 --skip-build --prefix=/usr --root="${pkgdir}"
}

