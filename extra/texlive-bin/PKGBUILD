
pkgname=texlive-bin
pkgver=20150521
pkgrel=2
_biber_ver=1.9 # for biblatex 2.9 only.
pkgdesc="TeX Live binaries"
license=('GPL')
arch=('amd64')
depends=('cairo' 'pixman' 'graphite' 't1lib' 'gd' 'poppler'
         'libsigsegv' 'zziplib' 'libpng' 'libjpeg' 'freetype2'
         'icu' 'harfbuzz' 'harfbuzz-icu' 'gmp' 'mpfr' 'libpaper')
makedepends=('perl' 'libffcall' 'ginstall')
optdepends=('ed: for texconfig')
provides=('lcdf-typetools' 'kpathsea' 'pdfjam')
conflicts=('pdfjam')
options=('!libtool' '!strip')
url='http://tug.org/texlive/'
install="texlive.install"
source=("http://distcache.FreeBSD.org/ports-distfiles/TeX/texlive-${pkgver}-source.tar.xz"
        "http://downloads.sourceforge.net/project/biblatex-biber/biblatex-biber/${_biber_ver}/binaries/FreeBSD/biber-amd64-freebsd.tar.xz"
        "http://downloads.sourceforge.net/project/biblatex-biber/biblatex-biber/${_biber_ver}/binaries/FreeBSD/biber-i386-freebsd.tar.xz"
        "texmf.cnf")
noextract=(biber-amd64-freebsd.tar.xz
           biber-i386-freebsd.tar.xz)
sha1sums=('a32df8d5d6b61f87f7dab907e864fb51b14f4143'
          '7d8a5d6b7e6783b21bba91c896c2b9ac6dee7781'
          '6b3b724660c480833e363d1f5ddd45cbe746751f'
          'efb60a682c5aa786d2aaf7a196f1bee97f9aeee2')

build() {
	cd "$srcdir"

	if [ "${CARCH}" = "amd64" ]; then
		export CFLAGS="${CFLAGS} -fPIC"
		export CXXFLAGS="${CXXFLAGS} -fPIC"
		tar xf biber-amd64-freebsd.tar.xz
	else
		tar xf biber-i386-freebsd.tar.xz
	fi

	cd texlive-${pkgver}-source

	## prevent compiling Xdvi with libXp
	sed -i ''-e 's|-lXp ||' texk/xdvik/configure
	test ! -d Work && mkdir Work
	cd Work

	echo "--> Initial configuration..."
	# we use temporary prefix to avoid messing the existing
	# $pkgdir/usr/share/texmf tree
	# system zlib is disabled due to issues with zlib 1.2.6 (FS#28221)
	../configure --prefix=/usr -C \
		--sysconfdir=/etc \
		--datarootdir=/usr/share \
		--datadir=/usr/share \
		--mandir=/usr/share/man \
		--disable-native-texlive-build \
		--with-banner-add="/Pac BSD" \
		--disable-multiplatform \
		--disable-dialog \
		--disable-psutils \
		--disable-t1utils \
		--disable-bibtexu \
		--disable-xz \
		--enable-shared \
		--disable-static \
		--with-system-zlib \
		--with-system-zziplib \
		--with-system-pnglib \
		--with-system-ncurses \
		--with-system-t1lib \
		--with-system-gd \
		--with-system-poppler \
		--with-system-xpdf \
		--with-system-freetype2 \
		--with-system-pixman \
		--with-system-cairo \
		--with-system-harfbuzz \
		--with-system-graphite \
		--with-system-icu \
		--with-system-gmp \
		--with-system-mpfr \
		--with-system-libpaper \
		--with-freetype2-libdir=/usr/lib \
		--with-freetype2-include=/usr/include/freetype2 \
		--with-xdvi-x-toolkit=xaw \
		--disable-dump-share \
		--disable-aleph \
		--enable-luatex \
		--disable-xindy --disable-xindy-rules --disable-xindy-docs

	#############################################################
	### make
	echo "-------------------------------------------------------"
	echo "--> Building the whole beast ..."
	echo "-------------------------------------------------------"
	gmake
}

package() {
	cd "$srcdir"

	#############################################################
	### install
	cd texlive-${pkgver}-source

	echo "-------------------------------------------------------"
	echo "--> Proceeding with make install ..."
	echo "-------------------------------------------------------"

	cd Work

	gmake DESTDIR="${pkgdir}" texmf="$pkgdir"/usr/share/texmf install
	rm -rf "${pkgdir}"/usr/{texmf,share/texmf-dist}

	## install biber
	install -m755 "${srcdir}/biber" "${pkgdir}/usr/bin/biber"

	## symlink engines by hand. texlinks has moved to texlive-core...
	mkdir -p ${pkgdir}/usr/bin
	ln -s eptex ${pkgdir}/usr/bin/platex
	ln -s euptex ${pkgdir}/usr/bin/uplatex
	ln -s luatex ${pkgdir}/usr/bin/dvilualatex
	ln -s luatex ${pkgdir}/usr/bin/dviluatex
	ln -s luatex ${pkgdir}/usr/bin/lualatex
	ln -s luatex ${pkgdir}/usr/bin/lualollipop
	ln -s pdftex ${pkgdir}/usr/bin/amstex
	ln -s pdftex ${pkgdir}/usr/bin/cslatex
	ln -s pdftex ${pkgdir}/usr/bin/csplain
	ln -s pdftex ${pkgdir}/usr/bin/eplain
	ln -s pdftex ${pkgdir}/usr/bin/etex
	ln -s pdftex ${pkgdir}/usr/bin/jadetex
	ln -s pdftex ${pkgdir}/usr/bin/latex
	ln -s pdftex ${pkgdir}/usr/bin/lollipop
	ln -s pdftex ${pkgdir}/usr/bin/mex
	ln -s pdftex ${pkgdir}/usr/bin/mllatex
	ln -s pdftex ${pkgdir}/usr/bin/mltex
	ln -s pdftex ${pkgdir}/usr/bin/pdfetex
	ln -s pdftex ${pkgdir}/usr/bin/pdfcslatex
	ln -s pdftex ${pkgdir}/usr/bin/pdfcsplain
	ln -s pdftex ${pkgdir}/usr/bin/pdfjadetex
	ln -s pdftex ${pkgdir}/usr/bin/pdflatex
	ln -s pdftex ${pkgdir}/usr/bin/pdfmex
	ln -s pdftex ${pkgdir}/usr/bin/pdfxmltex
	ln -s pdftex ${pkgdir}/usr/bin/texsis
	ln -s pdftex ${pkgdir}/usr/bin/utf8mex
	ln -s pdftex ${pkgdir}/usr/bin/xmltex
	ln -s xetex ${pkgdir}/usr/bin/xelatex
	ln -s xetex ${pkgdir}/usr/bin/xelollipop

	#############################################################
	# remove dangling symlinks
	_bibtexextra_scripts="
		bibexport
		bibdoiadd
		bibzbladd
		listbib
		ltx2crossrefxml
		multibibliography
		urlbst
	"
	_core_scripts="
		a2ping
		a5toa4
		adhocfilelist
		afm2afm
		allcm
		allec
		allneeded
		arara
		arlatex
		autoinst
		bundledoc
		checkcites
		chkweb
		cjk-gs-integrate
		context
		contextjit
		ctanify
		ctanupload
		de-macro
		depythontex
		deweb
		dosepsbin
		dtxgen
		dvi2fax
		dviasm
		dvired
		e2pall
		epstopdf
		findhyph
		fmtutil
		fmtutil-sys
		fontinst
		fragmaster
		installfont-tl
		kpsepath
		kpsetool
		kpsewhere
		kpsexpand
		latex-git-log
		latex2man
		latexdiff
		latexdiff-vc
		latexfileversion
		latexindent
		latexmk
		latexpand
		latexrevise
		listings-ext.sh
		ltxfileinfo
		ltximg
		lua2dox_filter
		luaotfload-tool
		luatools
		match_parens
		mf2pt1
		mkjobtexmf
		mkt1font
		mktexfmt
		mptopdf
		mtxrun
		mtxrunjit
		ot2kpx
		pdf180
		pdf270
		pdf90
		pdfatfi
		pdfbook
		pdfcrop
		pdfflip
		pdfjam
		pdfjam-pocketmod
		pdfjam-slides3up
		pdfjam-slides6up
		pdfjoin
		pdfnup
		pdfpun
		pfarrei
		pkfix
		pkfix-helper
		ps2eps
		ps2frag
		pslatex
		purifyeps
		pythontex
		repstopdf
		rpdfcrop
		rungs
		simpdftex
		sty2dtx
		texconfig
		texconfig-dialog
		texconfig-sys
		texcount
		texdef
		texdiff
		texdirflatten
		texdoc
		texdoctk
		texexec
		texfot
		texlinks
		texliveonfly
		texloganalyser
		texmfstart
		thumbpdf
		typeoutfileinfo
		updmap
		updmap-sys
		vpl2ovp
		vpl2vpl
"
	_games_scripts="rubikrotation"
	_htmlxml_scripts="ht htcontext htlatex htmex httex httexi htxelatex htxetex mk4ht"
	_langcyrillic_scripts="rubibtex rumakeindex"
	_langextra_scripts="ebong"
	_langgreek_scripts="mkgrkindex"
	_langjapanese_scripts="convbkmk ptex2pdf
		kanji-fontmap-creator
		kanji-config-updmap
		kanji-config-updmap-sys
"
   _langkorean_scripts="jamo-normalize komkindex ttf2kotexfont"
	_latexextra_scripts="
		authorindex
		exceltex
		makeglossaries
		pdfannotextractor
		perltex
		ps4pdf
		splitindex
		svn-multi
		vpe
		yplan"

	__music_scripts="lily-glyph-commands lily-image-commands lily-rebuild-pdfs
		m-tx musixtex musixflx pmx2pdf pmxchords"

	_pictures_scripts="cachepic epspdf epspdftk fig4latex getmapdl mathspic mkpic"
	_pstricks_scripts="pedigree pst2pdf"
	_science_scripts="pygmentex ulqda"

	for s in \
		${_bibtexextra_scripts} \
		${_core_scripts}       \
		${_games_scripts}       \
		${_htmlxml_scripts}    \
		${_langcyrillic_scripts} \
		${_langextra_scripts}    \
		${_langgreek_scripts}    \
		${_langjapanese_scripts} \
		${_langkorean_scripts} \
		${_latexextra_scripts} \
		${_music_scripts}      \
		${_pictures_scripts}   \
		${_pstricks_scripts}   \
		${_science_scripts}    \
		tlmgr; do
			! greadlink -e "$pkgdir"/usr/bin/$s && rm "$pkgdir"/usr/bin/$s
	done
	###################################################################

	install -dm755 "${pkgdir}/usr/share/texmf/web2c"
  install -m655 "${srcdir}/texmf.cnf" "${pkgdir}/usr/share/texmf/web2c/"
}
