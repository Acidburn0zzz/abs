pkgname=memcached
pkgver=1.4.24
pkgrel=1
pkgdesc="A distributed memory object caching system"
arch=('amd64')
url="http://memcached.org/"
license=('GPL')
depends=('libevent')
optdepends=('perl: for memcached-tool usage')
install=memcached.install
source=(http://www.memcached.org/files/$pkgname-$pkgver.tar.gz
	memcached.in)
sha256sums=('08a426c504ecf64633151eec1058584754d2f54e62e5ed2d6808559401617e55'
            '368c5650c96b7bd64a280c63fd0bea128f0456886d7ee339540a5b7ebd232379')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  export CFLAGS+=" -std=c99"
  sed -i '' -e 's|\-Werror||g' configure
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export CFLAGS+=" -std=c99"
  ./configure --prefix=/usr --disable-dtrace \
		--disable-coverage

  gmake -j1
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  gmake test || true # Fails due to missing program 'prove'
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install
  install -dm755 ${pkgdir}/usr/bin
  install -dm755 ${pkgdir}/etc/rc.d
  install -m 755 scripts/memcached-tool "$pkgdir"/usr/bin/memcached-tool
  install  -m 644 ../memcached.in "$pkgdir"/etc/rc.d/memcached
}
