# $Id: PKGBUILD 199383 2013-11-11 21:37:43Z eric $
# Maintainer : Anthony Donnelly <Amzo@PacBSD.org>

pkgbase=twisted
pkgname=(python-twisted python2-twisted)
pkgver=15.4.0
pkgrel=2
pkgdesc="Asynchronous networking framework written in Python"
arch=('i386' 'amd64')
url="http://twistedmatrix.com/"
license=('MIT')
makedepends=('python-crypto' 'python2-crypto' 'python-zope-interface' 'python2-zope-interface'
             'python-setuptools' 'python2-setuptools' 'python-service-identity'
             'python2-service-identity' 'git')
checkdepends=('subversion' 'python2-sphinx' 'pygtk' 'xorg-server-xvfb' 'tk'
              'python-gobject' 'python2-gobject' 'python-gobject2' 'python2-gobject2')
source=("git+https://github.com/twisted/twisted.git#tag=twisted-$pkgver")
sha1sums=('SKIP')

prepare() {
  cp -a "twisted"{,-py2}

  cd "twisted-py2"
  sed -i '' 's:^#!.*bin.*python:#!/usr/bin/python2:' \
    twisted/python/test/pullpipe.py \
    twisted/mail/test/pop3testserver.py \
    twisted/trial/test/scripttest.py
}

build() {
  cd "$srcdir/twisted"
  python3 setup.py build

  cd "$srcdir/twisted-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/twisted"
  LC_CTYPE="en_US.utf8 xvfb-run python admin/run-python3-tests" || warning "Tests failed"

  cd "$srcdir/twisted-py2"
  LC_CTYPE="en_US.utf8 xvfb-run python2 bin/trial twisted" || warning "Tests failed"
}

package_python-twisted() {
  depends=('python-crypto' 'python-zope-interface')
  optdepends=('python-pyopenssl'
              'python-service-identity: for TLS client hostname verification'
              'python-pyasn1: for using conch'
              'tk: for using tkconch')

  cd "twisted"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  # cp -a "${pkgdir}/usr/twisted/*" "${pkgdir}/usr/lib/python3.5/site-packages/twisted/"
  # rm -r "${pkgdir}/usr/twisted"

  mv "${pkgdir}/usr/bin/trial"{,3}

  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-twisted() {
  depends=('python2-crypto' 'python2-zope-interface')
  optdepends=('python2-pyopenssl'
              'python2-service-identity: for TLS client hostname verification'
              'python2-soappy: for twisted.web.soap'
              'python2-pyasn1: for using conch'
              'pygtk: for using manhole'
              'tk: for using tkconch')
  provides=("twisted=$pkgver")
  conflicts=('twisted<14.0.0')
  replaces=('twisted<14.0.0')

  cd "twisted-py2"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

sha1sums=('SKIP')
