# Maintainer: Anthony Donnelly <Amzo@archbsd.net>

pkgname=xorg-font-util
pkgver=1.3.1
pkgrel=3
pkgdesc='X.Org font utilities'
url='http://xorg.freedesktop.org/'
arch=('i386' 'amd64')
license=('custom')
makedepends=('xorg-util-macros')
source=("${url}/releases/individual/font/font-util-${pkgver}.tar.bz2")
sha1sums=('0b16add3637c64b0bbaf1dd223b71b0421100c20')

replaces=('font-util')
provides=('font-util')
conflicts=('font-util')
groups=('xorg-fonts' 'xorg')

build() {
	cd "${srcdir}/font-util-${pkgver}"

	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--with-mapdir=/usr/share/fonts/util \
		--with-fontrootdir=/usr/share/fonts

	make
}

package() {
	cd "${srcdir}/font-util-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
