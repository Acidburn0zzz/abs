# Maintainer: Anthony Donnelly <Amzo@archbsd.net>

pkgname=jasper
pkgver=1.900.1
pkgrel=18
pkgdesc="A software-based implementation of the codec specified in the emerging JPEG-2000 Part-1 standard"
arch=('i386' 'amd64')
url="http://www.ece.uvic.ca/~mdadams/jasper/"
license=('custom:JasPer2.0')
depends=('libjpeg' 'freeglut' 'libxmu' 'glu')
options=('libtoolfix')
source=(http://www.ece.uvic.ca/~mdadams/${pkgname}/software/${pkgname}-${pkgver}.zip)
sha1sums=('9c5735f773922e580bf98c7c7dfda9bbed4c5191')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  unset ac_cv_build
  unset ac_cv_host

  case $CARCH in
    i386)
      build="$(uname -m)-pacbsd-freebsd${OSREL/-*/}"
      ;;
    amd64)
      build="x86_64-pacbsd-freebsd${OSREL/-*/}"
      ;;
  esac
  ./configure --prefix=/usr --mandir=/usr/share/man --enable-opengl --enable-shared \
	--build="$build"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
