# Maintainer : RÃ©my Oudompheng <remy@archlinux.org>

pkgname=pyalpm
pkgver=0.7
pkgrel=3
pkgdesc="Libalpm bindings for Python 3"
arch=('i386' 'amd64')
url="http://projects.archlinux.org/users/remy/pyalpm.git/"
license=('GPL')
depends=('python>=3.4' 'pacman>=4.1')
source=("ftp://ftp.archlinux.org/other/pyalpm/$pkgname-$pkgver.tar.gz"
        archbsd-buildfix.patch)
sha1sums=('62fafb93d30c27b5a5a09df3e802d6e50fed9802'
          '6fb94edb7c73ba7040aaf8274bd03008bd48c5ac')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # fix a -Werror-caused error
  patch -p0 -i "${srcdir}/archbsd-buildfix.patch"
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # workaround Python packaging issue (FS#34658)
  export CFLAGS="$CFLAGS -Wno-format"

  python setup.py build
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  python setup.py install --root=${pkgdir}
}

