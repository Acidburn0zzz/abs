# $Id: PKGBUILD 209780 2014-04-04 05:24:27Z fyan $
# Maintainer : Anthony Donnelly <Amzo@PacBSD.org>

pkgbase=python-zope-interface
pkgname=('python-zope-interface' 'python2-zope-interface')
pkgver=4.1.3
pkgrel=3
license=('ZPL')
arch=('amd64')
url="http://pypi.python.org/pypi/zope.interface"
makedepends=('python-setuptools' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/z/zope.interface/zope.interface-${pkgver}.tar.gz")
md5sums=('9ae3d24c0c7415deb249dd1a132f0f79')

prepare() {
  cp -a zope.interface-${pkgver}{,-python2}
}

build() {
  #build python3 module
  cd "${srcdir}/zope.interface-${pkgver}"
  python setup.py build

  #build python2 module
  cd "${srcdir}/zope.interface-${pkgver}-python2"
  python2 setup.py build
}

check() {
  cd "${srcdir}/zope.interface-${pkgver}"
  python setup.py test

  cd "${srcdir}/zope.interface-${pkgver}-python2"
  python2 setup.py test
}

package_python-zope-interface() {
  pkgdesc=('Zope Interfaces for Python 3.x')
  depends=('python')
 
  cd "${srcdir}/zope.interface-${pkgver}"
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  cp src/zope/__init__.py "${pkgdir}/usr/lib/python3.5/site-packages/zope/"
}

package_python2-zope-interface(){
  pkgdesc=('Zope Interfaces for Python 2.x')
  depends=('python2')
  replaces=('zope-interface')
  provides=('zope-interface')

  cd "${srcdir}/zope.interface-${pkgver}-python2"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  cp src/zope/__init__.py "${pkgdir}/usr/lib/python2.7/site-packages/zope/"
}
