pkgname=smpeg
pkgver=0.4.5
pkgrel=2
pkgdesc="SDL MPEG Player Library"
arch=('i386' 'amd64')
url="http://icculus.org/smpeg/"
license=('LGPL')
depends=('sdl')
makedepends=('mesa' 'glu' 'subversion')
optdepends=('glu: to use glmovie')
source=("$pkgname-$pkgver::svn://svn.icculus.org/smpeg/tags/release_${pkgver//./_}")
md5sums=('SKIP')
options=('libtoolfix')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  export CXXFLAGS="${CXXFLAGS} -std=c++11 -stdlib=libc++"

  ./autogen.sh
  ./configure --prefix=/usr --mandir=/usr/share/man --disable-static

  gmake
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  gmake DESTDIR=${pkgdir} install
}
