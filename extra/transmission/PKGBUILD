pkgbase=transmission
pkgname=('transmission-cli' 'transmission-gtk') # 'transmission-qt')
pkgver=2.92
pkgrel=1
arch=('i386' 'amd64')
url="http://www.transmissionbt.com/"
license=('MIT')
makedepends=('gtk3' 'intltool' 'curl' 'libevent') #'qt5-base')
patchdir=($pkgbase-$pkgver)
source=(http://mirrors.m0k.org/transmission/files/$pkgbase-$pkgver.tar.xz
	transmission.in
	http://data.gpo.zugaina.org/musl/net-p2p/transmission/files/transmission-daemon.initd.9
	http://data.gpo.zugaina.org/musl/net-p2p/transmission/files/transmission-daemon.confd.4)
md5sums=('3fce404a436e3cd7fde80fb6ed61c264'
         'e8a2e6e6987141a38134cc8d43c3961e'
         '164d1fe30c830bc2942f0b01796da734'
         '766945a4090b705a3e5662c704ed5de1')

prepare() {
  cd $pkgbase-$pkgver

  sed -i '' -e	's|_pthread_flags=".*"|_pthread_flags="-pthread"|g' \
    -e 's|rt_clock_gettime=yes|rt_clock_gettime=no|g' \
    -e 's|" == "|" = "|g' configure

}

build() {
  cd $pkgbase-$pkgver

  ./configure --prefix=/usr
  gmake
#  pushd qt
#  qmake qtr.pro
#  gmake
}

package_transmission-cli() {
  pkgdesc="Fast, easy, and free BitTorrent client (CLI tools, daemon and web client)"
  depends=('curl' 'libevent' 'systemd')
  install=transmission-cli.install

  cd $pkgbase-$pkgver

  for dir in daemon cli web utils
  do
      gmake -C "$dir" DESTDIR="$pkgdir" install
  done

  install -dm755 "${pkgdir}"/etc/{rc.d,init.d,conf.d}
  install -m755 "${srcdir}/transmission.in" "${pkgdir}/etc/rc.d/tranmission"
  install -m755 "${srcdir}/transmission-daemon.initd.9" "${pkgdir}/etc/init.d/tranmission"
  install -m644 "${srcdir}/transmission-daemon.confd.4" "${pkgdir}/etc/conf.d/tranmission"
}

package_transmission-gtk() {
  pkgdesc="Fast, easy, and free BitTorrent client (GTK+ GUI)"
  depends=('curl' 'libevent' 'gtk3' 'desktop-file-utils' 'hicolor-icon-theme')
  optdepends=('notification-daemon: Desktop notification support'
  	      'transmission-cli: daemon and web support')
  install=transmission-gtk.install

  cd $pkgbase-$pkgver

  gmake -C gtk DESTDIR="$pkgdir" install
  gmake -C po DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/share/licenses/transmission-gtk"
  install -m644 COPYING "$pkgdir/usr/share/licenses/transmission-gtk/COPYING"
}

package_transmission-qt() {
  pkgdesc="Fast, easy, and free BitTorrent client (Qt GUI)"
  depends=('curl' 'qt5-base' 'libevent' 'libxkbcommon-x11')
  optdepends=('transmission-cli: daemon and web support')
  install=transmission-qt.install

  cd $pkgbase-$pkgver

  gmake -C qt INSTALL_ROOT="$pkgdir"/usr install

  install -dm755 "$pkgdir/usr/share/licenses/transmission-qt/"
  install -m644 COPYING "$pkgdir/usr/share/licenses/transmission-qt/COPYING"

  install -dm755 "$pkgdir/usr/share/pixmaps"
  install -m644 qt/icons/transmission.png "$pkgdir/usr/share/pixmaps/transmission-qt.png"

  install -dm755 "$pkgdir/usr/share/applications"
  install -m644 qt/transmission-qt.desktop "$pkgdir/usr/share/applications/transmission-qt.desktop"
}

