# Port: multimedia/libmatroska

pkgname=libmatroska
pkgver=1.4.5
pkgrel=1
pkgdesc="Matroska library"
arch=('i386' 'amd64')
url="http://dl.matroska.org/downloads/libmatroska/"
license=('LGPL')
depends=('libebml')
source=("http://dl.matroska.org/downloads/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('79023fa46901e5562b27d93a9dd168278fa101361d7fd11a35e84e58e11557bc')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i '' -e 's|$${libdir}/pkgconfig|@prefix@/libdata/pkgconfig|' \
    Makefile.in
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --disable-static

  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  gmake prefix="${pkgdir}/usr" install
}

