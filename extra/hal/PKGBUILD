# Port: sysutils/hal

pkgname=hal
pkgver=0.5.14
pkgrel=1
pkgdesc="Hardware Abstraction Layer for simplifying device access"
url="http://www.freedesktop.org/Software/hal"
arch=('i386' 'amd64')
license=('GPL' 'custom')
depends=('consolekit' 'libvolume_id' 'pciids' 'polkit' 'policykit' 'libv4l')
install=hal.install
source=("http://hal.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz"
	"10-mouse-sysmouse.fdi"
	"mount-fuse"
	"hald"
	"hald.in")
md5sums=('e9163df591a6f38f59fdbfe33e73bf20'
         '263d8c2160e8379413f2a032df78cbe0'
         '87001e78d5f0edc7dfe53b82ebf72ea5'
         '3d8830433e2deafcebce0eb7f712710d'
         '4ec0d6f2e1c77c0a59bf7f599affccaf')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
 
  sed -i '' -e '/^scriptdir = /s|[(]libexecdir[)]/scripts|(libexecdir)/hal/scripts|' \
	tools/Makefile.in \
	tools/freebsd/Makefile.in
  sed -i '' -e 's|[(]libexecdir[)]/scripts|(libexecdir)/hal/scripts|' \
	hald/Makefile.in \
	tools/Makefile.in
  sed -i '' -e 's|user="0"|group="operator"|g' \
	hal.conf.in

}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --disable-gtk-doc \
		--sysconfdir=/etc \
		--with-backend=freebsd \
		--disable-docbook-docs \
		--with-pci-ids=/usr/share/pciids \
		--disable-usb-ids \
		--without-libpci \
		--disable-usb \
		--disable-smbios \
		--disable-apm \
		--disable-pmu \
		--enable-policy-kit \
		--enable-console-kit \
		--with-socket-dir=/var/run/hald \
		--with-eject=/usr/sbin/cdcontrol \
		--with-pid-file=/var/run/hald/hald.pid \
		--with-deprecated-keys \
		--localstatedir=/var

  gmake

}



package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  gmake DESTDIR="${pkgdir}" install

  install -dm755  ${pkgdir}/usr/share/dist
  install -m644  hal.conf ${pkgdir}/usr/share/dist
  install -dm755 ${pkgdir}/usr/share/hal/fdi/policy/10osvendor
  install -m644  ${srcdir}/10-mouse-sysmouse.fdi \
	       ${pkgdir}/usr/share/hal/fdi/policy/10osvendor
  install -m644  ${srcdir}/mount-fuse \
	    	${pkgdir}/usr/share/hal/mount-fuse

  install -dm755 "${pkgdir}"/etc/{rc.d,init.d}
  install -m755 "${srcdir}/hald" "${pkgdir}/etc/init.d/"
  install -m755 "${srcdir}/hald.in" "${pkgdir}/etc/rc.d/hald"

  mkdir -p "${pkgdir}"/etc/hal/fdi/preprobe
  mkdir -p "${pkgdir}"/etc/hal/fdi/policy
  mkdir -p "${pkgdir}"/etc/hal/fdi/information
  mkdir -p "${pkgdir}"/usr/share/hal/fdi/information/20thirdparty
  mkdir -p "${pkgdir}"/usr/share/hal/fdi/policy/20thirdparty
  mkdir -p "${pkgdir}"/usr/share/hal/fdi/preprobe/10osvendor
  mkdir -p "${pkgdir}"/usr/share/hal/fdi/preprobe/20thirdparty

  install -d -o 560 -g 560 "${pkgdir}"/var/run/hald
  install -d -o 560 -g 560 "${pkgdir}"/var/cache/hald
  install -d -o 560 -g 560 "${pkgdir}"/var/lib/hal

}
