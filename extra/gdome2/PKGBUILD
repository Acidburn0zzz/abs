# $Id: PKGBUILD 150508 2012-02-18 11:44:22Z pierre $
# Maintainer:
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gdome2
pkgver=0.8.1
pkgrel=9
pkgdesc="Gnome DOM Engine"
url="http://gdome2.cs.unibo.it/"
arch=('i386' 'amd64')
license=('LGPL')
depends=('libxml2' 'glib2')
options=(libtoolfix)
source=("http://gdome2.cs.unibo.it/tarball/${pkgname}-${pkgver}.tar.gz"
        gdome2-0.8.1-libxml2.patch)
md5sums=('bfc114e59eec50cbda8e4ece751ff022'
         'bde155107f30bf65d441adf70efc3c00')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p0 -i "${srcdir}/gdome2-0.8.1-libxml2.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export CFLAGS="${CFLAGS} -fPIC"
  ./configure --prefix=/usr --mandir=/usr/share/man
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
}
