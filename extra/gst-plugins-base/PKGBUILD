pkgbase=gst-plugins-base
pkgname=('gst-plugins-base-libs' 'gst-plugins-base')
pkgver=1.6.0
pkgrel=3
pkgdesc="GStreamer Multimedia Framework Base Plugins"
arch=('i386' 'amd64')
license=('LGPL')
makedepends=('pkgconfig' 'gstreamer' 'orc' 'libxv' 'alsa-lib' 'cdparanoia' 'libvisual' 'libvorbis' 'libtheora' 'pango' 'gobject-introspection' 'python2')
options=(!emptydirs)
patchdir=($pkgbase-$pkgver)
url="http://gstreamer.freedesktop.org/"
source=(${url}/src/$pkgbase/$pkgbase-${pkgver}.tar.xz)
sha256sums=('314fd1b707f65caf8eb6164d9422fc51e2b220a890ccd2de6ec03a2883d77231')

build() {
  cd $pkgbase-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --with-package-name="GStreamer Base Plugins (PacBSD)" \
    --with-package-origin="http://www.pacbsd.org/" \
    LDFLAGS="${LDFLAGS} -lintl"

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  gmake

  gsed -e 's/^SUBDIRS_EXT =.*/SUBDIRS_EXT =/' -i Makefile
}

check() {
  cd $pkgbase-$pkgver
  # Testsuite fails on one test. Some refcount leak
  gmake check || true
}

package_gst-plugins-base-libs() {
  pkgdesc="GStreamer Multimedia Framework Base Plugin libraries"
  depends=('gstreamer' 'orc' 'libxv')

  cd $pkgbase-$pkgver
  gmake DESTDIR="${pkgdir}" install
}

package_gst-plugins-base() {
  depends=("gst-plugins-base-libs=$pkgver" 'alsa-lib' 'cdparanoia' 'libvisual' 'libvorbis' 'libtheora' 'pango')

  cd $pkgbase-$pkgver
  gmake -C gst-libs DESTDIR="${pkgdir}" install
  gmake -C ext DESTDIR="${pkgdir}" install
  gmake -C gst-libs DESTDIR="${pkgdir}" uninstall
}
