# $Id: PKGBUILD 183686 2013-04-26 16:05:42Z heftig $
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgbase=gst-plugins-base
pkgname=('gst-plugins-base-libs' 'gst-plugins-base')
pkgver=1.4.5
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Base Plugins"
arch=('i686' 'amd64')
license=('LGPL')
makedepends=('pkgconf' 'gstreamer' 'orc' 'libxv' 'alsa-lib' 'cdparanoia' 'libvisual' 'libvorbis' 'libtheora' 'pango' 'gobject-introspection')
options=(!emptydirs)
patchdir=($pkgbase-$pkgver)
url="http://gstreamer.freedesktop.org/"
source=(${url}/src/$pkgbase/$pkgbase-${pkgver}.tar.xz)
sha256sums=('77bd8199e7a312d3d71de9b7ddf761a3b78560a2c2a80829d0815ca39cbd551d')

build() {
  cd $pkgbase-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --with-package-name="GStreamer Base Plugins (PacBSD)" \
    --with-package-origin="http://www.pacbsd.org/" \
    LDFLAGS="${LDFLAGS} -lintl"

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  gmake

  gsed -e 's/^SUBDIRS_EXT =.*/SUBDIRS_EXT =/' -i Makefile
}

check() {
  cd $pkgbase-$pkgver
  # Testsuite fails on one test. Some refcount leak
  gmake check || true
}

package_gst-plugins-base-libs() {
  pkgdesc="GStreamer Multimedia Framework Base Plugin libraries"
  depends=('gstreamer' 'orc' 'libxv')

  cd $pkgbase-$pkgver
  gmake DESTDIR="${pkgdir}" install
}

package_gst-plugins-base() {
  depends=("gst-plugins-base-libs=$pkgver" 'alsa-lib' 'cdparanoia' 'libvisual' 'libvorbis' 'libtheora' 'pango')

  cd $pkgbase-$pkgver
  gmake -C gst-libs DESTDIR="${pkgdir}" install
  gmake -C ext DESTDIR="${pkgdir}" install
  gmake -C gst-libs DESTDIR="${pkgdir}" uninstall
}
