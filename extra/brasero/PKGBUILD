# $Id: PKGBUILD 200317 2013-11-25 16:54:50Z heftig $
# Maintainer: Allan McRae <allan@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=brasero
pkgver=3.10.0
pkgrel=1
pkgdesc="A disc burning application for Gnome"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/brasero"
license=('GPL')
options=('!emptydirs')
depends=('gtk3' 'gst-plugins-good' 'totem-plparser' 'cdrkit' 'cdrdao' 'desktop-file-utils' 'hicolor-icon-theme' 'shared-mime-info'  'libcanberra' 'dvd+rw-tools' 'dconf' 'libsm' 'libtracker-sparql' 'libnotify' 'gvfs')
makedepends=('gtk-doc' 'yelp-tools' 'gnome-common' 'intltool' 'libburn' 'libisofs' 'libnautilus-extension' 'gobject-introspection' 'gconf' 'itstool' 'docbook-xsl')
optdepends=('libburn: alternative back-end'
            'libisofs: libburn back-end'
            'dvdauthor: video project'
            'vcdimager: video project')
replaces=('nautilus-cd-burner')
install=brasero.install
groups=('gnome-extra')
port_patches=(
	patch-libbrasero-burn_burn-process.c
	patch-plugins-growisofs-burn-growisofs.c
)
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
	${port_patches[@]})
sha256sums=('88fd9135e796f58caeb484d00f5d95916c67f97505e68d2027ab84d3f9245f37'
            '086841d8d850c1165735c39f9e6a222856e81341f1ebcfa3e1f057a2abc6d0fe'
            '97fc251af097adfd5eeec5a3a0573ccd6b3ee0c9d858c7e1c69028c2c085516c')

prepare() {
	cd "$pkgname-$pkgver"
	for i in ${port_patches[@]}; do
		msg "Applying patch ${i}"
		patch -p0 -i "${srcdir}/${i}"
	done
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-caches \
      --disable-schemas-compile --disable-inotify \
			--enable-search=no --enable-playlist=no \
			CPPFLAGS="${CPPFLAGS} -DHAVE_CAM_LIB_H=1" \
      CFLAGS="${CFLAGS} -Wno-error=format-nonliteral"
  gmake
}

package() {
  cd "$pkgname-$pkgver"
  gmake DESTDIR=$pkgdir install
}

