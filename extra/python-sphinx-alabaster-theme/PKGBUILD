# Port: textproc/py-alabaster

pkgbase=python-sphinx-alabaster-theme
pkgname=(python-sphinx-alabaster-theme python2-sphinx-alabaster-theme)
pkgver=0.7.6
pkgrel=1
arch=('amd64')
pkgdesc="Modified Kr Sphinx doc theme"
url='https://github.com/bitprophet/alabaster'
license=(BSD)
makedepends=(python-setuptools python2-setuptools)
source=("https://github.com/bitprophet/alabaster/archive/${pkgver}.tar.gz")
sha256sums=('5446f84c69fd327cfe2fa1644b4f6453710e67c6479e79e736f7bedb6e2bcb8e')

build() {
  cp -r ${srcdir}/alabaster-${pkgver} ${srcdir}/alabaster-${pkgver}-py2
  
  cd "$srcdir"/alabaster-${pkgver}
  python setup.py build

  cd "$srcdir"/alabaster-${pkgver}-py2
  python2 setup.py build
}

package_python-sphinx-alabaster-theme() {
  cd "$srcdir"/alabaster-${pkgver}
  python setup.py install --root="$pkgdir" --optimize=1

  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-sphinx-alabaster-theme() {
  cd "$srcdir"/alabaster-${pkgver}-py2 
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
