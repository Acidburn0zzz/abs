# Port: devel/sfml
pkgname=sfml
pkgver=2.1
pkgrel=1
pkgdesc='A simple, fast, cross-platform, and object-oriented multimedia API'
arch=('i386' 'amd64')
url='http://www.sfml-dev.org/'
license=('zlib')
depends=('libsndfile' 'libxrandr' 'libjpeg' 'openal' 'glew' 'freetype2'
'xcb-util-image')
makedepends=('mesa' 'cmake' 'doxygen' 'git')
install=sfml.install
source=("http://www.sfml-dev.org/download/${pkgname}/${pkgver}/SFML-${pkgver}-sources.zip")
md5sums=('2de81448733f3f46964f23f41cd42e92')

build() {
  cd "$srcdir"/SFML

  mkdir build && cd build
  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DSFML_BUILD_EXAMPLES=1 \
      -DSFML_BUILD_DOC=1 \
      -DSFML_INSTALL_PKGCONFIG_FILES=1
  make
  make doc
}

package() {
  cd "$srcdir"/SFML/build

  make DESTDIR="$pkgdir/" install

  install -dm755 "$pkgdir/usr/share/cmake-3.0/Modules/"
  install -m644 "$pkgdir/usr/share/SFML/cmake/Modules/FindSFML.cmake" "$pkgdir/usr/share/cmake-3.0/Modules/FindSFML.cmake"

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 ../license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

