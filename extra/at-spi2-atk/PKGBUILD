# Maintainer: Anthony Donnelly <Amzo@archbsd.net>

pkgname=at-spi2-atk
pkgver=2.16.0
pkgrel=1
pkgdesc="A GTK+ module that bridges ATK to D-Bus at-spi"
arch=('i686' 'amd64')
url="http://www.gnome.org"
license=('GPL2')
depends=('at-spi2-core' 'libx11' 'atk')
makedepends=('intltool')
install=at-spi2-atk.install
options=('libtoolfix')
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('78efc45ec36383bf785f8636e64a8d866defeb020e00a08f92978f1fc3772ff9')

build() {
  cd $pkgname-$pkgver

  sed -i '' -e '/AC_PATH_XTRA/d' configure.ac
  autoreconf --force --install

  ./configure --prefix=/usr --sysconfdir=/etc \
    --disable-schemas-compile
  gmake
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install

}

# vim:set ts=2 sw=2 et:
