# Maintainer: omicrondelta74 <gpwriter@telus.net>
#
# Based upon the original ArchLinux package build maintained by
#             Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
#             Contributor: Chris Brannon <cmbrannon@cox.net>
#
# ports_patches included from FreeBSD ports tree

pkgname=emacs
pkgver=24.3
pkgrel=3
pkgdesc="The extensible, customizable, self-documenting real-time display editor"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/emacs/emacs.html"
license=('GPL3')
depends=('librsvg' 'giflib' 'libxpm' 'gtk3' 'hicolor-icon-theme' 'gconf'
         'desktop-file-utils' 'alsa-lib' 'imagemagick')
provides=('emacs')
conflicts=('emacs')
install=$pkgname.install
source=("ftp://ftp.gnu.org/gnu/emacs/emacs-$pkgver.tar.gz"
        archbsd-verify-h.patch)
md5sums=('d20441025efd4931ef64cc2bb18eddc9'
         'a33f326f32172360022216a62909aaf5')

prepare() {
	cd ${srcdir}/emacs-$pkgver

	patch -p0 -i "${srcdir}/archbsd-verify-h.patch"
}

build() {
  cd ${srcdir}/emacs-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --with-x-toolkit=gtk3 --with-xft

  gmake
}

package() {
  cd ${srcdir}/emacs-$pkgver
  gmake DESTDIR=${pkgdir} install
  
  # remove conflict with ctags package
  mv ${pkgdir}/usr/bin/{ctags,ctags.emacs}
  mv ${pkgdir}/usr/share/man/man1/{ctags.1.gz,ctags.emacs.1}

  # remove conflict with texinfo
  rm "$pkgdir"/usr/share/info/info.info.gz

  # fix user/root permissions on usr/share files
  find ${pkgdir}/usr/share/emacs/$pkgver -exec chown root:wheel {} \;

  # remove .desktop file and icons
  rm -rf ${pkgdir}/usr/share/{applications,icons}

  # fix perms on /var/games
  chmod 775 ${pkgdir}/var/games
  chmod 775 ${pkgdir}/var/games/emacs
  chmod 664 ${pkgdir}/var/games/emacs/*
  chown -R root:games ${pkgdir}/var/games
}


