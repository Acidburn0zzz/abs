# $Id: PKGBUILD 189725 2013-07-06 02:03:20Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgbase=pygobject
pkgname=(python-gobject python2-gobject pygobject-devel)
pkgver=3.18.0
pkgrel=1
arch=('i386' 'amd64')
url="https://live.gnome.org/PyGObject"
license=(LGPL)
makedepends=(python python2 python-cairo python2-cairo gobject-introspection gnome-common)
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgbase/${pkgver:0:4}/$pkgbase-$pkgver.tar.xz)
sha256sums=('1c3ba1112d3713cd5c86260312bfeb0de1f84f18808e51072c50b29d46156dc9')

prepare() {
  mkdir build-py2 build-py3 devel
  cd ${pkgbase}-${pkgver}
  gsed -i '/Werror=format/d' configure # gcc 4.8
}

build() {
  cd build-py2
  ../${pkgbase}-${pkgver}/configure --prefix=/usr --with-python=/usr/bin/python2
  gmake

  cd ../build-py3
  ../${pkgbase}-${pkgver}/configure --prefix=/usr
  gmake
}

package_python-gobject() {
  pkgdesc="Python 3 bindings for GObject"
  depends=('gobject-introspection-runtime' "pygobject-devel=$pkgver" 'python' 'cairo')

  cd build-py3
  gmake DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/{include,libdata/pkgconfig}
}

package_python2-gobject() {
  pkgdesc="Python 2 bindings for GObject"
  depends=('gobject-introspection-runtime' "pygobject-devel=$pkgver" 'python2' 'cairo')

  cd build-py2
  gmake DESTDIR="$pkgdir" install
  mv "$pkgdir"/usr/{include,libdata/pkgconfig} "$srcdir/devel"
  python2 -m compileall "$pkgdir"//usr/lib/python2.7/site-packages/gi
}

package_pygobject-devel() {
  pkgdesc="Development files for the pygobject bindings"

  cd "devel"
  mkdir -p "$pkgdir"/usr/{include,libdata}
  mv include "$pkgdir/usr/"
  mv pkgconfig "$pkgdir/usr/libdata/"
}

