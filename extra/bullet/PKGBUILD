pkgbase=bullet
pkgname=('bullet' 'bullet-docs')
pkgver=2.83.6
pkgrel=2
pkgdesc="A 3D Collision Detection and Rigid Body Dynamics Library for games and animation"
arch=('i386' 'amd64')
url="http://www.bulletphysics.com/Bullet/"
license=('custom:zlib')
depends=()
makedepends=('cmake' 'doxygen' 'graphviz' 'ttf-dejavu' 'mesa')
patchdir=("${pkgbase}3-${pkgver}")
source=("https://github.com/bulletphysics/bullet3/archive/${pkgver}.tar.gz")
md5sums=('44cb2464336a2082b2c144194c2a2668')

build() {
  cd ${pkgbase}3-${pkgver}

  sed -i '' -e 's|malloc.h|stdlib.h|g' examples/ThirdPartyLibs/Gwen/Macros.h

  rm -r examples

  # get a clean build dir
  [[ -d build ]] && rm -rf build
  mkdir build && cd build 

  # generate CMake Makefile
  cmake .. \
       -DCMAKE_INSTALL_PREFIX=/usr \
       -DBUILD_SHARED_LIBS=1 \
       -DBUILD_DEMOS=0 \
       -DBUILD_MULTITHREADING=1 \
       -DBUILD_EXTRAS=1 \
       -DINSTALL_LIBS=1 \
       -DINSTALL_EXTRA_LIBS=1 \
       -DCMAKE_BUILD_TYPE=Release # set =Debug for debugging version

  # compile
  gmake

  # generate docs
  cd ..
  gsed -i 's/GENERATE_HTMLHELP.*//g' Doxyfile
  doxygen
}

package_bullet() {
  optdepends=('bullet-docs: documentation')

  cd ${pkgbase}3-${pkgver}/build

  # install it
  gmake DESTDIR=${pkgdir} install

  # install license
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgbase}
  install -m644 ../LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgbase}/LICENSE
}

package_bullet-docs() {
  pkgdesc="Documentation for bullet"
  depends=()

  cd ${pkgbase}3-${pkgver}

  # install docs
  install -dm755 ${pkgdir}/usr/share/doc/bullet
  install -m644 docs/Bullet_User_Manual.pdf ${pkgdir}/usr/share/doc/bullet/Bullet_User_Manual.pdf
  install -m644 docs/BulletQuickstart.pdf ${pkgdir}/usr/share/doc/bullet/BulletQuickstart.pdf
  cp -r html ${pkgdir}/usr/share/doc/bullet/html  
}
# vim: sw=2 ts=2 et:
