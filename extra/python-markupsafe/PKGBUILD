# Port: textproc/py-MarkupSafe

pkgname=('python-markupsafe' 'python2-markupsafe')
pkgver=1.0
pkgrel=1
pkgdesc="Implements a XML/HTML/XHTML Markup safe string for Python"
arch=('amd64')
url="http://pypi.python.org/pypi/MarkupSafe"
license=('custom')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.io/packages/source/M/MarkupSafe/MarkupSafe-${pkgver}.tar.gz")
sha256sums=('a6be69091dac236ea9c6bc7d012beab42010fa914c459791d627dad4910eb665')

build() {
  cp -r MarkupSafe-${pkgver} python2-MarkupSafe-${pkgver}
  cd "${srcdir}/MarkupSafe-${pkgver}"
  python setup.py build

  cd "${srcdir}/python2-MarkupSafe-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${srcdir}/MarkupSafe-${pkgver}"
  python setup.py test

  cd "${srcdir}/python2-MarkupSafe-${pkgver}"
  python2 setup.py test
}

package_python-markupsafe() {
  depends=('python')

  cd "${srcdir}/MarkupSafe-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -dm755 "${pkgdir}/usr/share/licenses/python-markupsafe/"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/python-markupsafe/LICENSE"
}

package_python2-markupsafe() {
  depends=('python2')

  cd "${srcdir}/python2-MarkupSafe-${pkgver}"
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -dm755 "${pkgdir}/usr/share/licenses/python2-markupsafe/"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/python2-markupsafe/LICENSE"
}
