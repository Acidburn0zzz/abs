pkgbase=vte3
pkgname=(vte3 vte-common)
pkgver=0.42.1
pkgrel=1
pkgdesc="Virtual Terminal Emulator widget for use with GTK3"
arch=('i386' 'amd64')
license=('LGPL')
options=('!emptydirs')
makedepends=('intltool' 'gobject-introspection' 'gtk3' 'vala')
url="http://www.gnome.org"
patchdir="vte-$pkgver/"
source=(http://download.gnome.org/sources/vte/${pkgver::4}/vte-$pkgver.tar.xz)
sha256sums=('9e00e2e9e94f4202b979c871f407a8815890bc0f4a8448a08534f76ab0fc9d34')

build() {
  cd "vte-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/libexec/vte \
      --localstatedir=/var --disable-static \
      --enable-introspection --enable-gnome-pty-helper \
      LIBS="-lintl"
  gmake
}

package_vte3(){
  depends=('gtk3' 'vte-common')
  cd "vte-$pkgver"
  gmake DESTDIR="$pkgdir" install

  rm "$pkgdir"/usr/libexec/vte/gnome-pty-helper
  rm "$pkgdir"/etc/profile.d/vte.sh
}

package_vte-common() {
  pkgdesc="Common files used by vte and vte3"
  depends=('libc')
  cd "vte-$pkgver"

  gmake -C gnome-pty-helper DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir"/etc/profile.d
  install -m644 src/vte.sh "$pkgdir"/etc/profile.d/vte.sh
}

