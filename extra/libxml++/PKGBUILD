# $Id$
# Maintainer: damir <damir@archlinux.org>

pkgbase=libxml++
pkgname=('libxml++' 'libxml++-docs')
pkgver=2.40.0
pkgrel=1
arch=('i386' 'amd64')
license=('LGPL')
url="http://libxmlplusplus.sourceforge.net/"
makedepends=('gmake' 'pkgconfig' 'glibmm-docs' 'libxml2' 'glibmm')
options=('!emptydirs')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgbase}/${pkgver:0:4}/${pkgbase}-${pkgver}.tar.xz)
sha256sums=('a7b9703203696972cae50436e150979ef187d9b03c95519bdd89623b4068162a')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  MAKE=gmake ./configure --prefix=/usr --enable-static
  gmake
}

package_libxml++() {
  pkgdesc="C++ bindings to libxml2"
  depends=('libxml2' 'glibmm')
  replaces=('libxml++2')
  provides=("libxml++2=${pkgver}")
  conflicts=('libxml++2')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  gsed -i -e 's/install-data-am: install-data-local install-dist_referenceDATA/install-data-am: /' Makefile
  gmake DESTDIR="${pkgdir}" install
}

package_libxml++-docs() {
  pkgdesc="Developer documentation for libxml++"
  depends=('glibmm-docs')
  cd "${srcdir}/${pkgbase}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install-data-local install-dist_referenceDATA
}
