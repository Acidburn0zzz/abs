# Port: audio/lame
pkgname=lame
pkgver=3.99.5
pkgrel=1
pkgdesc="A high quality MPEG Audio Layer III (MP3) encoder"
arch=('i386' 'amd64')
url="http://lame.sourceforge.net/"
makedepends=('nasm')
license=('LGPL')
options=('libtoolfix')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('84835b313d4a8b68f5349816d33e07ce')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i '' -e 's,test -d \.libs || mkdir,mkdir -p,' \
    libmp3lame/i386/Makefile.in
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  LIBS="$LIBS -ltinfo" \
  ./configure --prefix=/usr \
              --enable-nasm \
              --enable-shared \
              --enable-mp3rtp \
              --disable-gtktest
  gmake
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  gmake DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
