# $Id: PKGBUILD 180567 2013-03-23 15:06:13Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=gnutls
pkgver=3.3.2
pkgrel=1
pkgdesc="A library which provides a secure layer over a reliable transport layer"
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL')
url="http://www.gnutls.org/"
install=gnutls.install
options=('!libtool' '!zipman')
depends=('gcc-libs>=4.9.0-3' 'libtasn1>=3.4' 'readline' 'nettle>=2.4' 'p11-kit>=0.12')
makedepends=('valgrind')
source=(ftp://ftp.gnutls.org/gcrypt/gnutls/v3.3/${pkgname}-${pkgver}.tar.xz{,.sig})
md5sums=('590851c331b1851c0ed3388879922c06'
         'SKIP')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  find . -name libtool -exec cp /usr/bin/libtool '{}' \;
  find . -name ltmain.sh -exec cp /usr/share/libtool/config/ltmain.sh '{}' \;
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
    --with-zlib \
    --disable-static \
    --disable-guile \
    --disable-valgrind-tests

  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  # lots of .png files are put into infodir and are gzipped by makepkg! this may need to be fixed by using !zipman
  # gzip -9 all files in infodir and manpages manually
  find "$pkgdir/usr/share/info" -name '*.info*' -exec gzip -9 {} \;
  find "$pkgdir/usr/share/man" -exec gzip -9 {} \;
}
