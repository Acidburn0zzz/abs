pkgname=gdb
pkgver=7.10
pkgrel=1
pkgdesc="The GNU Debugger"
arch=('amd64')
url="http://www.gnu.org/software/gdb/"
license=('GPL3')
depends=('ncurses' 'expat' 'python2')
makedepends=('texinfo')
backup=('etc/gdb/gdbinit')
install=gdb.install
source=(http://ftp.gnu.org/gnu/gdb/${pkgname}-${pkgver}.tar.xz{,.sig}
        fbsd-threads.c)
md5sums=('2a35bac41fa8e10bf04f3a0dd7f7f363'
         'SKIP'
         '680fb1df44b2e69c656e03df9b21c0e7')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  gsed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  # bsd-threads
  cp "${srcdir}/fbsd-threads.c" gdb/fbsd-threads.c

}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  export CPPFLAGS="${CPPFLAGS} -DRL_NO_COMPAT"
  ./configure --prefix=/usr --disable-nls \
    --with-python=/usr/bin/python2 \
    --with-system-gdbinit=/etc/gdb/gdbinit \
    --program-suffix=${pkgver//./} \
    --without-libunwind-ia64 \
    --with-system-readline
  gmake
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  gmake DESTDIR=${pkgdir} install

  # install "custom" system gdbinit
  install -dm755 $pkgdir/etc/gdb
  touch $pkgdir/etc/gdb/gdbinit

  # resolve conflicts with binutils
  rm $pkgdir/usr/include/{ansidecl,bfd,bfdlink,dis-asm,plugin-api,symcat}.h
  rm $pkgdir/usr/share/info/bfd.info
  rm $pkgdir/usr/lib/{libbfd,libopcodes}.a

  # remove the waste...
  rm ${pkgdir}/usr/lib/charset.alias

  # world-conflicts
  rm -rf ${pkgdir}/usr/share/info
}
