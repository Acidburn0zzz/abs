# Maintainer: Anthony Donnelly <Amzo@archbsd.net>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=xmlto
pkgver=0.0.26
pkgrel=4
pkgdesc="Convert xml to many other formats"
arch=('i386' 'amd64')
url="http://cyberelk.net/tim/software/xmlto/"
license=('GPL')
depends=('libxslt' 'perl-yaml-syck' 'perl-test-pod' 'getopt')
makedepends=('docbook-xsl' 'bash')
source=("https://fedorahosted.org/releases/x/m/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('c90a47c774e0963581c1ba57235f64f4')

prepare() {
  cd "${pkgname}-${pkgver}"
	
  # remove non-standard type -t
  find . -type f -exec sed -i '' -e 's/type -t/which/;' '{}' '+'
  # remove GNUISM
  sed -i '' -e '/^GEN_MANPAGE/{s/\$</$>/;}' Makefile.in
}

build() {
  cd "$srcdir/${pkgname}-${pkgver}"

  GETOPT=/usr/local/bin/getopt \
  BASH=/usr/bin/bash \
  ./configure --prefix=/usr \
              --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
