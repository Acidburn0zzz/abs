# Port: multimedia/ffmpegthumbnailer

pkgname=ffmpegthumbnailer
pkgver=2.1.1
pkgrel=1
pkgdesc="Lightweight video thumbnailer that can be used by file managers."
url="http://code.google.com/p/ffmpegthumbnailer/"
license=('GPL2')
arch=('i386' 'amd64')
depends=('ffmpeg' 'libjpeg' 'libpng')
makedepends=('cmake')
optdepends=('gvfs: support for gio uris')
options=('!gnu_configure')
source=("https://github.com/dirkvdb/$pkgname/archive/$pkgver.tar.gz")
sha1sums=('a5e7f15a95990349d6b54a13d723d209e19eab35')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir -p build
  cd build
  cmake .. \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DENABLE_GIO=ON \
	-DENABLE_THUMBNAILER=ON
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  gmake DESTDIR="${pkgdir}" install
}
