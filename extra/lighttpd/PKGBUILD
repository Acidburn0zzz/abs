pkgname=lighttpd
pkgver=1.4.35
pkgrel=3
pkgdesc='A secure, fast, compliant and very flexible web-server'
license=('custom')
arch=('i686' 'x86_64')
url="http://www.lighttpd.net/"
depends=('pcre' 'libldap')
makedepends=('fcgi' 'libmariadbclient' 'lua51' 'libxml2' 'e2fsprogs' 'sqlite' 'gdbm' 'pkgconfig')
optdepends=('libxml2: mod_webdav'
            'lua51: mod_cml/mod_magnet'
            'libmariadbclient: mod_mysql_vhost'
            'sqlite: mod_webdav')
backup=('etc/lighttpd/lighttpd.conf')
options=('emptydirs')
source=("http://download.lighttpd.net/lighttpd/releases-1.4.x/lighttpd-${pkgver}.tar.xz"
        "http://download.lighttpd.net/lighttpd/releases-1.4.x/lighttpd-${pkgver}.tar.xz.asc"
        'lighttpd.conf' 'lighttpd.conf.d' 'lighttpd.rc.d')
sha256sums=('113e9b72ccbd1da5deb0774bf93cf0ca15dc82aad2da0f04e5ab27d37d3f30a3'
            'SKIP'
            'fece4581bebf39768571962dedce176b2b5f487c0abb5c1cfb35395de216c01f'
            '6f1c23fd96bacda46c2cef1c73802430515e8de442d6454bacb18c8e197ca8f9'
            'c8a06fc76f5d2ac08a0e93d28d69feccfca751d898549ff37d0e4d197108c42a')

build() {
	cd $srcdir/$pkgname-$pkgver

	./configure --prefix=/usr \
		--sbindir=/usr/bin \
		--libdir=/usr/lib/lighttpd/ \
		--sysconfdir=/etc/lighttpd \
		--with-mysql \
		--with-ldap \
		--with-attr \
		--with-openssl \
		--with-kerberos5 \
		--without-fam \
		--with-webdav-props \
		--with-webdav-locks \
		--with-gdbm \
		--with-memcache \
		--with-lua
	gmake
}

check() {
	cd $srcdir/$pkgname-$pkgver
	gmake check
}

package() {
	cd $srcdir/$pkgname-$pkgver
	gmake DESTDIR=$pkgdir install

	install -dm755 "$pkgdir"/etc/lighttpd/
	install -m644 "$srcdir"/lighttpd.conf $pkgdir/etc/lighttpd/lighttpd.conf

	install -dm755 "$pkgdir"/var/{log,cache}/lighttpd/

	install -dm755 "$pkgdir/etc/init.d/"
	install -dm755 "$pkgdir/etc/conf.d/"

	install -m755 "${srcdir}/lighttpd.rc.d"   "$pkgdir/etc/init.d/lighttpd"
	install -m755 "${srcdir}/lighttpd.conf.d" "$pkgdir/etc/conf.d/lighttpd"

	pushd doc/config >/dev/null
	install -dm755 $pkgdir/usr/share/doc/lighttpd/config/{conf.d,vhosts.d}
	find . -type f ! -name 'Makefile*' -exec install -m644 {} ${pkgdir}/usr/share/doc/lighttpd/config/{} \;
	popd >/dev/null

	install -dm755 $pkgdir/usr/share/licenses/$pkgname/COPYING
	install -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
