pkgname=lighttpd
pkgver=1.4.37
pkgrel=2
pkgdesc='A secure, fast, compliant and very flexible web-server'
license=('custom')
arch=('i386' 'amd64')
url="http://www.lighttpd.net/"
depends=('pcre' 'libldap' 'gdbm')
makedepends=('fcgi' 'libmariadbclient' 'lua51' 'libxml2' 'e2fsprogs' 'sqlite' 'pkgconfig')
optdepends=('libxml2: mod_webdav'
            'lua51: mod_cml/mod_magnet'
            'libmariadbclient: mod_mysql_vhost'
            'sqlite: mod_webdav')
backup=('etc/lighttpd/lighttpd.conf')
options=('emptydirs')
install="lighttpd.install"
source=("http://download.lighttpd.net/lighttpd/releases-1.4.x/lighttpd-${pkgver}.tar.xz"
        "http://download.lighttpd.net/lighttpd/releases-1.4.x/lighttpd-${pkgver}.tar.xz.asc"
        'lighttpd.conf' 'lighttpd.in')
sha256sums=('804e6f60567ca1da2b3927f92b7e9332b93aca9560f282ca135b86b7558979bd'
            'SKIP'
            '246b0cdf37d9662261387297ad940270068c8fa3d2a0dc3eb3f5e5362d44a6e1'
            '0991db233cfc57dc7db30703f4ee0759b4ea712dd55070a70b7379a3c958fc7b')

build() {
	cd $srcdir/$pkgname-$pkgver

	./configure --prefix=/usr \
		--sbindir=/usr/sbin \
		--libdir=/usr/lib/lighttpd/ \
		--sysconfdir=/etc/lighttpd \
		--with-mysql \
		--with-ldap \
		--with-attr \
		--with-openssl \
		--with-kerberos5 \
		--without-fam \
		--with-webdav-props \
		--with-webdav-locks \
		--with-gdbm \
		--with-memcache \
		--with-lua
	gmake
}

check() {
	cd $srcdir/$pkgname-$pkgver
	gmake check
}

package() {
	cd $srcdir/$pkgname-$pkgver
	gmake DESTDIR=$pkgdir install

	install -dm755 "$pkgdir"/etc/lighttpd/
	install -m644 "$srcdir"/lighttpd.conf $pkgdir/etc/lighttpd/lighttpd.conf

	install -dm755 "$pkgdir"/var/{log,cache}/lighttpd/

	install -dm755 "$pkgdir/etc/rc.d/"

	install -m755 "${srcdir}/lighttpd.in" "$pkgdir/etc/rc.d/lighttpd"

	pushd doc/config >/dev/null
	install -dm755 $pkgdir/usr/share/doc/lighttpd/config/{conf.d,vhosts.d}
	find . -type f ! -name 'Makefile*' -exec install -m644 {} ${pkgdir}/usr/share/doc/lighttpd/config/{} \;
	popd >/dev/null

	install -dm755 $pkgdir/usr/share/licenses/$pkgname/COPYING
	install -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
