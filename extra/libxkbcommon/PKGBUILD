pkgbase=libxkbcommon
pkgname=(libxkbcommon libxkbcommon-x11)
pkgver=0.5.0
pkgrel=4
pkgdesc="Keyboard handling library using XKB data"
arch=(i386 amd64)
url="http://xkbcommon.org/"
license=(custom)
depends=(xkeyboard-config)
makedepends=(doxygen xorg-util-macros)
source=("http://xkbcommon.org/download/$pkgname-$pkgver.tar.xz")
md5sums=('2e1faeafcc609c30af3a561a91e84158')

prepare() {
  mkdir -p x11/usr/{include/xkbcommon,libdata/pkgconfig}
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  gmake
}

package_libxkbcommon() {
  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/share/licenses/libxkbcommon"
  install -m644 COPYING "$pkgdir/usr/share/licenses/libxkbcommon/LICENSE"

### Split libxkbcommon-x11

  install -dm755 "$srcdir/x11/usr/lib/"
  mv "$pkgdir"/usr/lib/*x11* "$srcdir/x11/usr/lib/"
  install -dm755 "$srcdir/x11/usr/libdata/pkgconfig"
  mv "$pkgdir"/usr/libdata/pkgconfig/*x11* "$srcdir/x11/usr/libdata/pkgconfig"
  mv "$pkgdir"/usr/include/xkbcommon/*x11* "$srcdir/x11/usr/include/xkbcommon"
}

package_libxkbcommon-x11() {
  pkgdesc="Keyboard handling library using XKB data for X11 XCB clients"
  depends=(libxkbcommon libxcb)

  mv x11/* "$pkgdir"

  install -dm755 "$pkgdir/usr/share/licenses/libxkbcommon-x11"
  install -m644 $pkgbase-$pkgver/COPYING "$pkgdir/usr/share/licenses/libxkbcommon-x11/LICENSE"
}
