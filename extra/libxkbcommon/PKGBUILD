# Port: x11/libxkbcommon
pkgbase=libxkbcommon
pkgname=(libxkbcommon libxkbcommon-x11)
pkgver=0.7.1
pkgrel=1
pkgdesc="Keyboard handling library using XKB data"
arch=(i386 amd64)
url="http://xkbcommon.org/"
license=(custom)
depends=(xkeyboard-config)
makedepends=(xorg-util-macros bison)
source=("http://xkbcommon.org/download/$pkgname-$pkgver.tar.xz")
md5sums=('947ba609cb0239b9462127d5cf8908ee')

prepare() {
  mkdir -p x11/usr/{include/xkbcommon,libdata/pkgconfig}
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static \
    --without-doxygen
  gmake
}

package_libxkbcommon() {
  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install

### Split libxkbcommon-x11

  install -dm755 "$srcdir/x11/usr/lib/"
  mv "$pkgdir"/usr/lib/*x11* "$srcdir/x11/usr/lib/"
  install -dm755 "$srcdir/x11/usr/libdata/pkgconfig"
  mv "$pkgdir"/usr/libdata/pkgconfig/*x11* "$srcdir/x11/usr/libdata/pkgconfig"
  mv "$pkgdir"/usr/include/xkbcommon/*x11* "$srcdir/x11/usr/include/xkbcommon"
}

package_libxkbcommon-x11() {
  pkgdesc="Keyboard handling library using XKB data for X11 XCB clients"
  depends=(libxkbcommon libxcb)

  mv x11/* "$pkgdir"

}
