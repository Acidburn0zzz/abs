# $Id: PKGBUILD 191829 2013-07-31 09:01:10Z jgc $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Flamelab <panosfilip@gmail.com

pkgname=gnome-shell
pkgver=3.8.1
pkgrel=7
pkgdesc="The next generation GNOME Shell"
arch=(i686 x86_64)
url="http://live.gnome.org/GnomeShell"
license=(GPL2)
depends=(evolution-data-server gcr gjs gnome-menus
         gnome-session gnome-settings-daemon gnome-themes-standard gsettings-desktop-schemas
         libcanberra-pulse libcroco libsecret mutter caribou
         telepathy-logger telepathy-mission-control unzip libgdm)
makedepends=(intltool gtk-doc gnome-control-center)
optdepends=('gnome-control-center: System settings')
options=('!libtool' '!emptydirs')
install=gnome-shell.install
groups=(gnome)
port_patches=(patch-js_gdm_loginDialog.js
	patch-js_ui_panel.js
	patch-src_Makefile.in
	patch-src_configure
	patch-js_ui_sessionMode.js
	patch-src_shell-global.c)
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver::3}/$pkgname-$pkgver.tar.xz
	${port_patches[@]})
sha256sums=('db0160d7c3fec3858805f218f961534d1a3f1486e005492433d4a28c40f83ac7'
            'a4c9cd0351dac9689dd2b40f5ee6e672014499153cc61ff2e96e09472590ee54'
            '2054cc0c66bc5579c53fe04b3b17351a06694c41a1e118f62fdc327c779d574d'
            'a110f814a199853408b1191fe6617d217a43fefdf56898d54dd61d3c0d4bef7f'
            '5c8b630cf31616a5e42d65c2f580152b5b40fc549e745d4c2f5d0934528247a2'
            'a55878073a827a254b4f6081a4fdf4580b22505daacebecc83877838d0b4758d'
            '4219f2bb365b7ece96e95b4e241a55f702d8b10df3c28806d605f0710dcb3b81')

prepare() {
  cd $pkgname-$pkgver

  for i in ${port_patches[@]}; do
        msg "patching $i"
        patch -p0 -i ${srcdir}/$i
  done
#  sed -i '' -e 's|libnm-glib libnm-util ||g' \
#		configure
  sed -i '' -e 's|UBDIRS = data js src browser-plugin|UBDIRS = data js src|g' \
		Makefile.in
  sed -i '' -e 's|\-DG_DISABLE_DEPRECATED||g' \
		src/Makefile.in \
		browser-plugin/Makefile.in 

#  sed -i '' -e 's|/bin/sh|/usr/bin/bash|g' \
#		config/install-sh

#  chmod +x config/install-sh

}

build() {
  cd $pkgname-$pkgver
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/libexec/gnome-shell \
      --localstatedir=/var --disable-static \
      --enable-compile-warnings=no #--enable-introspection=no

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  gmake
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
}
