# $Id: PKGBUILD 191829 2013-07-31 09:01:10Z jgc $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Flamelab <panosfilip@gmail.com

pkgname=gnome-shell
pkgver=3.12.0
pkgrel=1
pkgdesc="The next generation GNOME Shell"
arch=(i686 x86_64)
url="http://live.gnome.org/GnomeShell"
license=(GPL2)
depends=(evolution-data-server gcr gjs gnome-menus
         gnome-session gnome-settings-daemon gnome-themes-standard gsettings-desktop-schemas
         libcanberra-pulse libcroco libsecret mutter caribou
         telepathy-logger telepathy-mission-control unzip libgdm)
makedepends=(intltool gtk-doc gnome-control-center)
optdepends=('gnome-control-center: System settings')
options=('!libtool' '!emptydirs')
install=gnome-shell.install
groups=(gnome)
port_patches=(
	patch-js_gdm_loginDialog.js
	patch-js_ui_sessionMode.js
	patch-src_data-to-c.pl
	patch-src_shell-global.c
)
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver::4}/$pkgname-$pkgver.tar.xz
	${port_patches[@]})
sha256sums=('60ac9f8ad473f266ffca70acf76c0d494e0c5067c5a148d944146e94654a0dde'
            'a4c9cd0351dac9689dd2b40f5ee6e672014499153cc61ff2e96e09472590ee54'
            '4d7de042d82eb8ed3d0359037c580673285dd9ccc7fca7f787c6675a0e2ca6b6'
            'ff0a9f7794a1cd52cc96809ac41679eb1cc3864831002ac45d2557d9821f6a4b'
            '9000ff3e688c738d0ca1c35c7f0188771bd3db1d92979a5909cdc98bec5e76db')

prepare() {
  cd $pkgname-$pkgver

  for i in ${port_patches[@]}; do
        msg "patching $i"
        patch -p0 -i ${srcdir}/$i
  done

  sed -i '' -e 's|\-DG_DISABLE_DEPRECATED||g' \
		src/Makefile.in \
		browser-plugin/Makefile.in 

  sed -i '' -e 's|libnm-glib libnm-util ||g' \
		configure
}

build() {
  cd $pkgname-$pkgver
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/libexec/gnome-shell \
      --localstatedir=/var --disable-static \
      --enable-compile-warnings=no \
      --with-ca-certificates=/usr/share/certs/ca-root-nss.crt

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  gmake
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
}
