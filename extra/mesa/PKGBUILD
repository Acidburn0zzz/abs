# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Wolfgang Bumiller <blub@speed.at>

#
#
# NOTE:
#   makepkg -R not supported without first executing the fakeinstall at the end of build() again
#
#

pkgbase=mesa
pkgname=('ati-dri' 'intel-dri' 'mesa' 'mesa-libgl')
pkgver=10.2.5
pkgrel=10
arch=('i686' 'x86_64')
makedepends=('glproto>=1.4.16'
             'libdrm>=2.4.39'
             'libxxf86vm>=1.1.2'
             'libxdamage>=1.1.3'
             'expat>=2.1.0'
             'libx11>=1.5.0'
             'libxt>=1.1.3' 
             'presentproto'
             'dri2proto>=2.8'
             'libxml2'
             'imake'
             'python2'
             'libvdpau>=0.5'
             'gindent'
             'llvm'
             'dri3proto'
             'libdevq'
             'libxshmfence')
url="http://mesa3d.sourceforge.net"
license=('custom')
options=('!libtool')
port_patches=(
	configure.ac
	patch-configure
	patch-src__loader__loader.c
	patch-src_gallium_winsys_svga_drm_vmw-screen-ioctl.c
)
source=(LICENSE 
	ftp://ftp.freedesktop.org/pub/mesa/${pkgver}/MesaLib-${pkgver}.tar.bz2
	${port_patches[@]})
md5sums=('5c65a0fe315dd347e09b1f2826a1df5a'
         'f82b1c2b42f44073d8b889d2a7ee9292'
         '1e7d8b4a9024f2c042342deb11fdc426'
         '45585cd65c019242eb7f54b93d083f5c'
         '26ec7f49e3690de2188824ec38f3467b'
         'd54e19aa82da09c6cab0901a35e08105')

prepare() {
    cd ${srcdir}/?esa-*

    for _p in ${port_patches[@]}; do
        msg "Applying patch ${_p}"
        patch -p0 -i "${srcdir}/${_p}"
    done

# *facepalm* ...
    sed -i '' -e 's@\(ln -f \.libs/libglapi\.so\.0\)\.0\.0 @\1 @' src/mapi/shared-glapi/Makefile.am
    sed -i '' -e 's@\(ln -sf libglapi\.so\.0\)\.0\.0 @\1 @' src/mapi/shared-glapi/Makefile.am
    sed -i '' -e 's|\(ln -f \.libs/libGLESv1_CM\.so\.1\)\.1\.0 |\1 |' src/mapi/es1api/Makefile.am
    sed -i '' -e 's|\(ln -f \.libs/libGLESv2\.so\.2\)\.0\.0 |\1 |' src/mapi/es2api/Makefile.am
    sed -i '' -e 's|\(ln -f \.libs/lib@GL_LIB@\.so\.1\)\.2\.0 |\1 |' src/glx/Makefile.am
    sed -i '' -e 's|\(ln -f \.libs/libEGL\.so\.1\)\.0\.0 |\1 |' src/egl/main/Makefile.am

    autoreconf -vfi

# fix glibc-ism
    sed -i '' -e 's/program_invocation_short_name/getprogname()/' \
        src/gallium/drivers/r300/r300_chipset.c
    sed -i '' -e 's|<byteswap.h>|<sys/endian.h>|' \
              -e 's| bswap_32|bswap32|' \
        src/gallium/drivers/r600/r600_asm.c \
        src/gallium/drivers/r600/r600_shader.c \
        src/gallium/drivers/r600/r600_state_common.c 
}

build() {
    cd ${srcdir}/?esa-*

    export CFLAGS="${CFLAGS} -fPIC"
    export CXXFLAGS="${CXXFLAGS} -fPIC"
    export INDENT=/usr/bin/gindent

# not default:
#    --enable-gallium-egl enable optional EGL state tracker (not required for
#                          EGL support in Gallium with OpenGL and OpenGL ES)
#                          [default=disable]
#    --enable-xa             enable build of the XA X Acceleration API                          [default=no]
#

    ./configure \
        --prefix=/usr/ \
        --sysconfdir=/etc \
        --with-dri-driverdir=/usr/lib/dri \
	--enable-gallium-llvm \
	--disable-gallium-egl \
	--with-gallium-drivers=r300,r600,radeonsi,svga,swrast \
        --with-dri-drivers=i915,i965,r200,radeon,swrast \
        --with-egl-platforms=x11,drm \
	--enable-shared-glapi \
        --enable-glx-tls \
        --enable-gbm \
        --enable-dri \
        --enable-glx \
        --disable-osmesa \
        --enable-gles1 \
        --enable-gles2 \
        --enable-texture-float \
        --enable-xa \
        --enable-vdpau \
        --enable-dri3

    # fix a build break with new llvm
    #sed -i '' -e "s:-Werror=implicit-function-declaration::" src/gallium/drivers/r600/Makefile

    gmake
    mkdir "$srcdir/fakeinstall"
    gmake DESTDIR="$srcdir/fakeinstall" install
}

package_ati-dri() {
 pkgdesc="Mesa drivers for AMD/ATI Radeon"
 depends=("mesa-libgl=${pkgver}" "llvm-libs=3.4.1" "libpthread-stubs")
 conflicts=('xf86-video-ati<6.9.0-6')

  install -m755 -d ${pkgdir}/usr/lib/vdpau/
  mv -v ${srcdir}/fakeinstall/usr/lib/vdpau/libvdpau_{r600,radeonsi}.* ${pkgdir}/usr/lib/vdpau/

  install -m755 -d ${pkgdir}/usr/lib/dri
  mv -v ${srcdir}/fakeinstall/usr/lib/dri/{r200,r300,r600,radeon,radeonsi}_dri.so ${pkgdir}/usr/lib/dri/
  
  install -m755 -d "${pkgdir}/usr/share/licenses/ati-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/ati-dri/"
}

package_intel-dri() {
  pkgdesc="Mesa drivers for Intel"
  depends=("mesa-libgl=${pkgver}" "libpthread-stubs")

  install -m755 -d ${pkgdir}/usr/lib/dri
  mv -v ${srcdir}/fakeinstall/usr/lib/dri/{i915,i965}_dri.so ${pkgdir}/usr/lib/dri/

  install -m755 -d "${pkgdir}/usr/share/licenses/intel-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/intel-dri/"
}

package_mesa() {
  pkgdesc="an open-source implementation of the OpenGL specification"
  depends=('libdrm' 'libvdpau' 'libxxf86vm' 'libxdamage' 'expat' "llvm-libs=3.4.1" "libpthread-stubs" 'libdevq')
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  provides=('libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')
  conflicts=('libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')
  replaces=('libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')

  # rename libgl.so to not conflict with blobs - may break gl.pc ?

	#Don't use mv, breaks package_mesa_libgl
	cp -Rv ${srcdir}/fakeinstall/* ${pkgdir}

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa/"

  install -m755 -d ${pkgdir}/usr/lib/mesa
  # move libgl/EGL/glesv*.so to not conflict with blobs - may break .pc files ?
  mv -v ${pkgdir}/usr/lib/libGL.so* 	${pkgdir}/usr/lib/mesa/
  mv -v ${pkgdir}/usr/lib/libEGL.so* 	${pkgdir}/usr/lib/mesa/
  mv -v ${pkgdir}/usr/lib/libGLES*.so*	${pkgdir}/usr/lib/mesa/

}

package_mesa-libgl() {
  pkgdesc="Mesa 3-D graphics library"
  depends=("mesa=${pkgver}" "libxshmfence")
  provides=("libgl=${pkgver}")
  replaces=('libgl')
 
  # See FS#26284
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
  ln -s libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"
	
	cp ${srcdir}/fakeinstall/usr/lib/{libGL.{la,so,so.1},libEGL.{la,so,so.1},libGLESv1_CM.{la,so,so.1},libGLESv2.{la,so,so.2}} ${pkgdir}/usr/lib/

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa-libgl"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa-libgl/"
}
