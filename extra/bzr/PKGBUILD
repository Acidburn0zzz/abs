# $Id$
# Maintainer :
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=bzr
pkgver=2.7.0
pkgrel=1
pkgdesc="A decentralized revision control system (bazaar)"
arch=('i386' 'amd64')
url="http://bazaar.canonical.com/en/"
license=('GPL')
depends=('python2')
optdepends=('python2-paramiko: for sftp support')
source=("http://launchpad.net/${pkgname}/${pkgver%.*}/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha256sums=('0d451227b705a0dd21d8408353fe7e44d3a5069e6c4c26e5f146f1314b8fdab3'
            'SKIP')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i '' -e 's|man/man1|share/man/man1|' setup.py
  sed -i '' -e 's|/usr/bin/env python|/usr/bin/env python2|' \
    bzrlib/{plugins/bash_completion/bashcomp.py,tests/ssl_certs/create_ssls.py,patiencediff.py,_patiencediff_py.py}
}

build() {
  cd ${pkgname}-${pkgver}
  python2 setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1

  # bash-completion
  install -dm755 "${pkgdir}/usr/share/bash-completion/completions/bzr"
  install -m644 contrib/bash/bzr \
    "${pkgdir}/usr/share/bash-completion/completions/bzr"
}
