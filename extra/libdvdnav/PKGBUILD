# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgname=libdvdnav
pkgver=5.0.2
pkgrel=1
pkgdesc="The library for xine-dvdnav plugin."
arch=('i686' 'amd64')
license=('GPL')
url="http://dvdnav.mplayerhq.hu/"
depends=('libdvdread' 'gmake' 'git')
source=(git://git.videolan.org/libdvdnav.git#tag=$pkgver
        Fix-reading-DVD-label.patch)
sha256sums=('SKIP'
            'f4300e3f38cdcb985c8f3c765ad0503350a96b91496c3cd63c7de5a5cb937119')

prepare() {
  cd $pkgname
  patch -Np1 -i ../Fix-reading-DVD-label.patch
  autoreconf -fi
}

build() {
  cd "${srcdir}/${pkgname}"

  sed -i '' -e 's|^threadlib="-lpthread"|threadlib="-pthread"|g' \
		configure
  ./configure --prefix=/usr
  gmake
}

package() {
  cd "${srcdir}/${pkgname}"

  gmake DESTDIR="${pkgdir}" install
}
