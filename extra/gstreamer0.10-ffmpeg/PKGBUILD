# $Id: PKGBUILD 201497 2013-12-12 23:22:05Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gstreamer0.10-ffmpeg
libav_version="0.7.7"
pkgver=0.10.13
pkgrel=7
pkgdesc="Gstreamer FFMpeg Plugin"
arch=('i386' 'amd64')
license=('GPL')
depends=('gstreamer0.10-base' 'ffmpeg')
makedepends=('pkgconfig' 'yasm' 'sdl' 'git' 'gtk-doc' 'gcc')
url="http://www.gstreamer.net"
options=(libtoolfix)
groups=('gstreamer0.10-plugins')
source=(http://gstreamer.freedesktop.org/src/gst-ffmpeg/gst-ffmpeg-0.10.13.tar.bz2
	http://libav.org/releases/libav-0.7.7.tar.xz)
sha256sums=('76fca05b08e00134e3cb92fa347507f42cbd48ddb08ed3343a912def187fbb62'
            '2d7b70c2bdaf8fea2e7d51838ce04e6c616cf90486134c247642fbdeafb21599')

prepare() {
  cd "${srcdir}"

  mv gst-ffmpeg-${pkgver}/gst-libs/ext/libav gst-ffmpeg-${pkgver}/gst-libs/ext/libav.old
  mv libav-${libav_version} gst-ffmpeg-${pkgver}/gst-libs/ext/libav
  cp gst-ffmpeg-${pkgver}/gst-libs/ext/libav.old/config.* \
	gst-ffmpeg-${pkgver}/gst-libs/ext/libav/


}
build() {
  cd ${srcdir}/gst-ffmpeg-${pkgver}

  export CFLAGS="${CFLAGS} -fPIC"
  export CXXFLAGS="${CXXFLAGS} -fPIC"
  export CPPFLAGS="${CPPFLAGS} -fPIC"
  export LDFLAGS="${LDFLAGS} -Wl,-Bsymbolic"
  sed -i '' 's/[[:<:]]ARCH[[:>:]]/LIBAV_ARCH/' \
		gst-libs/ext/libav/Makefile \
		gst-libs/ext/libav/common.mak \
		gst-libs/ext/libav/configure \
		gst-libs/ext/libav/libavcodec/Makefile \
		gst-libs/ext/libav/libavfilter/Makefile

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-static --enable-gtk-doc --enable-pic \
	--with-ffmpeg-extra-configure="--enable-runtime-cpudetect --enable-shared --enable-pic --enable-static"
  gmake
}

check() {
  cd ${srcdir}/gst-ffmpeg-${pkgver}
  gmake check
}

package() {
  cd ${srcdir}/gst-ffmpeg-${pkgver}
  gmake DESTDIR="${pkgdir}" install
}
