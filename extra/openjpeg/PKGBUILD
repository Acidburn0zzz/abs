# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=openjpeg
pkgver=1.5.0
pkgrel=2
pkgdesc="An open source JPEG 2000 codec"
arch=(i686 x86_64)
license=('BSD')
url="http://www.openjpeg.org"
patches=(
	patch-src_include_malloc
)
source=(http://openjpeg.googlecode.com/files/openjpeg-${pkgver}.tar.gz
	${patches[@]})
sha1sums=('dce705ae45f137e4698a8cf39d1fbf22bc434fa8'
          'a8fe07c2b4004f2c6d0d9e8b108eaa5fcc23c502')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  for i in ${patches[@]}; do
    patch -p0 -i ${srcdir}/${i}
  done
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # make sure we use system libs
  rm -rf thirtparty
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/openjpeg"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/openjpeg/LICENSE"

# what is that
  rm -rf "${pkgdir}/usr/report.txt"
}
