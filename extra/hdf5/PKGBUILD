pkgname=hdf5
pkgver=1.8.15
pkgrel=2
arch=('i386' 'amd64')
pkgdesc="General purpose library and file format for storing scientific data"
url="http://www.hdfgroup.org/HDF5/"
license=('custom')
depends=('zlib')
options=('libtoolfix')
source=(ftp://ftp.hdfgroup.org/HDF5/current/src/${pkgname}-${pkgver/_/-}-patch1.tar.bz2)
sha1sums=('82ed248e5d0293bc1dba4c13c9b2880a26643ee0')

prepare() {
  cd $srcdir/${pkgname}-${pkgver/_/-/}-patch1
  sed -i '' -e 's|-lpthread|-pthread|g' configure
}

build() {
  cd $srcdir/${pkgname}-${pkgver/_/-/}-patch1

  export CFLAGS="${CFLAGS/O2/O0}"
  export CXXFLAGS="${CFLAGS}"
  ./configure --prefix=/usr --disable-static \
    --enable-hl \
    --enable-threadsafe \
    --enable-production \
    --with-pic \
    --docdir=/usr/share/doc/hdf5/ \
    --with-pthread=/usr/lib/ \
    --disable-sharedlib-rpath
  make
}

package() {
  cd $srcdir/${pkgname}-${pkgver/_/-}-patch1

  make -j1 DESTDIR=${pkgdir} install

  install -d -m755 $pkgdir/usr/share/licenses/${pkgname}
  install -m644 $srcdir/${pkgname}-${pkgver/_/-}-patch1/COPYING \
          $pkgdir/usr/share/licenses/${pkgname}/LICENSE 
}

