# $Id: PKGBUILD 166664 2012-09-15 07:49:23Z andyrtr $

pkgname=hunspell
pkgver=1.3.3
pkgrel=7
pkgdesc="Spell checker and morphological analyzer library and program"
arch=('i686' 'amd64')
url="http://hunspell.sourceforge.net/"
license=('GPL' 'LGPL' 'MPL')
depends=('readline' 'gettext-runtime')
optdepends=('perl: for ispellaff2myspell')
source=(http://downloads.sourceforge.net/hunspell/hunspell-$pkgver.tar.gz)
md5sums=('4967da60b23413604c9e563beacc63b4')

build() {
  cd "$srcdir/hunspell-$pkgver"
  ./configure --prefix=/usr --disable-static \
	--with-ui --with-readline --with-experimental
  make
}

package() {
  cd "$srcdir/hunspell-$pkgver"
  make -j1 DESTDIR="$pkgdir" install
  
  # add generic hunspell.so for development and projects not using pkgconfig flags - FS#30592
  pushd $pkgdir/usr/lib
  ln -s libhunspell-?.?.so libhunspell.so
  popd
}
