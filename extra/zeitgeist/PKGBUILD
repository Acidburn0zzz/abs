# $Id: PKGBUILD 183481 2013-04-21 22:21:01Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=zeitgeist
pkgver=0.9.15
pkgrel=4
pkgdesc="Service logging user activities and events"
arch=('i386' 'amd64')
url="http://zeitgeist-project.com/"
license=(GPL2 LGPL2.1)
depends=(sqlite json-glib telepathy-glib gtk3)
makedepends=(intltool gobject-introspection vala raptor python2-rdflib git gnome-common)
provides=(zeitgeist-datahub)
conflicts=(zeitgeist-datahub)
replaces=(zeitgeist-datahub)
options=()
source=("$pkgname::git://anongit.freedesktop.org/$pkgname/$pkgname#commit=d2c966f"
	'patch-Makefile'
	'patch-log.vala')
sha256sums=('SKIP'
            '5ea3c3467599bc46a00757a28c2a914c609939aded33db11651bc13a05eeab46'
            'ab61f5b30ba2a6e6d482801e5a32e2b4e8f1437960332f05cd09242af5c6842b')

prepare() {
  cd $pkgname

  gsed -i 's/python -/$PYTHON -/' configure.ac
  gsed -i '1s/python$/python2/' data/ontology2code

  mkdir m4
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  export PYTHON=/usr/bin/python2

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname
  gmake
}

check() {
  cd $pkgname
  # Log test fails in chroot, as we have no X and no proper dbus
  gmake -k check || :
}

package() {
  cd $pkgname
  gmake DESTDIR="$pkgdir" install #zeitgeistdocdir=/usr/share/doc/zeitgeist install
  #install -D -m644 data/zeitgeist-daemon.bash_completion "$pkgdir/usr/share/bash-completion/completions/zeitgeist-daemon"
}
