pkgname=nmap
pkgver=7.00
pkgrel=1
pkgdesc='Utility for network discovery and security auditing'
url='http://nmap.org/'
arch=('i386' 'amd64')
license=('GPL')
makedepends=('pygtk')
optdepends=('pygtk: zenmap'
            'sudo: privilege escalation for zenmap'
            'xterm: privilege escalation for zenmap'
            'kdebase-runtime: privilege escalation for zenmap'
            'gksu: privilege escalation for zenmap')
depends=('pcre' 'lua52') # openssl & libpcap in world
source=("http://nmap.org/dist/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('9f442301c615c608a385831c3505cdfed9b0795cd100f1a258b04509802802ae')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	export PYTHON=python2
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/libexec \
		--mandir=/usr/share/man \
		--with-libpcap=included \

	gmake
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	gmake DESTDIR="${pkgdir}" install
	rm "${pkgdir}"/usr/bin/uninstall_zenmap
	install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
