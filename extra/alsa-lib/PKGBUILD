# $Id: PKGBUILD 166377 2012-09-07 09:11:49Z tpowa $
# Maintainer: judd <jvinet@zeroflux.org>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=alsa-lib
pkgver=1.0.28
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support"
arch=('i686' 'x86_64')
url="http://www.alsa-project.org"
optdepends=('python2: for python smixer plugin')
makedepends=('python2')
license=('GPL')
options=(libtoolfix)
install=alsa.install
source=(
        ftp://ftp.alsa-project.org/pub/lib/$pkgname-$pkgver.tar.bz2
        asound.conf.sample
)
md5sums=('c9e21b88a2b3e6e12ea7ba0f3b271fc3'
         'fc95ef8027c74ec8f5045c041bb16bea')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  CPPFLAGS+=" -I${startdir}/includes"

# from ports
  sed -i '' -e '/LIBS/ { s/-ldl//g; s/-lrt//g; }' \
  	        -e '/-D_GNU_SOURCE/d' \
  	        -e '/lt_cv_dlopen/s/-ldl//g' \
  	  configure
  sed -i '' -e '/LIBADD/s/-ldl//g' \
  	modules/mixer/simple/Makefile.in

  ./configure --prefix=/usr \
              --disable-aload --disable-python \
              --with-pkgconfdir=/usr/lib/pkgconfig
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -dm755 "${pkgdir}/etc"
  install -m644 "${srcdir}/asound.conf.sample" "${pkgdir}/etc/asound.conf.sample"
}
