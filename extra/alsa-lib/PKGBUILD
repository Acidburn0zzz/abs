# $Id: PKGBUILD 166377 2012-09-07 09:11:49Z tpowa $
# Maintainer: Anthony Donnelly <Amzo@archbsd.net>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=alsa-lib
pkgver=1.0.29
pkgrel=4
pkgdesc="An alternative implementation of Linux sound support"
arch=('i386' 'amd64')
url="http://www.alsa-project.org"
optdepends=('python2: for python smixer plugin')
makedepends=('python2')
license=('GPL')
options=(libtoolfix)
install=alsa.install
source=(
        ftp://ftp.alsa-project.org/pub/lib/$pkgname-$pkgver.tar.bz2
        asound.conf.sample
)
md5sums=('de67e0eca72474d6b1121037dafe1024'
         'fc95ef8027c74ec8f5045c041bb16bea')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  CPPFLAGS+=" -I${startdir}/files"

# from ports
  sed -i '' -e '/LIBS/ { s/-ldl//g; s/-lrt//g; }' \
  	        -e '/-D_GNU_SOURCE/d' \
  	        -e '/lt_cv_dlopen/s/-ldl//g' \
  	  configure
  sed -i '' -e '/LIBADD/s/-ldl//g' \
  	modules/mixer/simple/Makefile.in

  ./configure --prefix=/usr \
              --disable-aload --disable-python \
              --with-pkgconfdir=/usr/libdata/pkgconfig
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -dm755 "${pkgdir}/etc"
  install -m644 "${srcdir}/asound.conf.sample" "${pkgdir}/etc/asound.conf.sample"
}
