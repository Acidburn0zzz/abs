# Port: net/liveMedia

pkgname=live-media
pkgver=2016.08.07
pkgrel=1
pkgdesc="A set of C++ libraries for multimedia streaming"
arch=('i386' 'amd64')
license=('LGPL')
url="http://live555.com/liveMedia"
options=('staticlibs')
source=("http://live555.com/liveMedia/public/live.${pkgver}.tar.gz")
sha256sums=('84d9b9e565dd6fec36c1e09ee7767acf92037a9d426da458a255794ed90da673')

build() {
  cd ${srcdir}/live

  gsed \
      -e 's/-DHAVE_SOCKADDR_LEN=1/-fPIC -DHAVE_SOCKADDR_LEN=1/g' \
      -i config.freebsd

  ./genMakefiles freebsd
  gmake
}

package() {
  cd ${srcdir}/live

  for dir in BasicUsageEnvironment groupsock liveMedia UsageEnvironment; do
    install -dm755 ${pkgdir}/usr/{bin,lib,include/${dir}}
    install -m644 ${dir}/*.a "${pkgdir}/usr/lib"
    install -m644 ${dir}/include/*.h* "${pkgdir}/usr/include/${dir}"
  done

  for testprog in `find testProgs -type f -perm 755`; do
    install ${testprog} "${pkgdir}/usr/bin"
  done
}
