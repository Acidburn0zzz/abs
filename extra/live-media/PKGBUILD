# $Id: PKGBUILD 188257 2013-06-13 16:04:25Z giovanni $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb@gmail.com>

pkgname=live-media
pkgver=2013.12.05
pkgrel=1
pkgdesc="A set of C++ libraries for multimedia streaming"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://live555.com/liveMedia"
source=("http://live555.com/liveMedia/public/live.${pkgver}.tar.gz"
	clang-patch)
md5sums=('7f3d0e17b6d894a3140c35f9091a8a70'
         'b5dea84d84711ecf236355784640db37')

build() {
  cd ${srcdir}/live

  patch -p0 -i "${srcdir}/clang-patch"
  ./genMakefiles freebsd
  gmake
}

package() {
  cd ${srcdir}/live

  for dir in BasicUsageEnvironment groupsock liveMedia UsageEnvironment; do
    install -dm755 ${pkgdir}/usr/{bin,lib,include/${dir}}
    install -m644 ${dir}/*.a "${pkgdir}/usr/lib"
    install -m644 ${dir}/include/*.h* "${pkgdir}/usr/include/${dir}"
  done

  for testprog in `find testProgs -type f -perm 755`; do
    install ${testprog} "${pkgdir}/usr/bin"
  done
}
