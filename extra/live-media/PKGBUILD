# $Id: PKGBUILD 188257 2013-06-13 16:04:25Z giovanni $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb@gmail.com>

pkgname=live-media
pkgver=2015.08.07
pkgrel=1
pkgdesc="A set of C++ libraries for multimedia streaming"
arch=('i686' 'amd64')
license=('LGPL')
url="http://live555.com/liveMedia"
options=('staticlibs')
source=("http://live555.com/liveMedia/public/live.${pkgver}.tar.gz")
md5sums=('047008e71c58a1caf4e0c0439c392486')

build() {
  cd ${srcdir}/live

  ./genMakefiles freebsd
  gmake
}

package() {
  cd ${srcdir}/live

  for dir in BasicUsageEnvironment groupsock liveMedia UsageEnvironment; do
    install -dm755 ${pkgdir}/usr/{bin,lib,include/${dir}}
    install -m644 ${dir}/*.a "${pkgdir}/usr/lib"
    install -m644 ${dir}/include/*.h* "${pkgdir}/usr/include/${dir}"
  done

  for testprog in `find testProgs -type f -perm 755`; do
    install ${testprog} "${pkgdir}/usr/bin"
  done
}
