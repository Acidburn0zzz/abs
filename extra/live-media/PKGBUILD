# $Id: PKGBUILD 188257 2013-06-13 16:04:25Z giovanni $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb@gmail.com>

pkgname=live-media
pkgver=2015.09.24
pkgrel=1
pkgdesc="A set of C++ libraries for multimedia streaming"
arch=('i386' 'amd64')
license=('LGPL')
url="http://live555.com/liveMedia"
options=('staticlibs')
source=("http://live555.com/liveMedia/public/live.${pkgver}.tar.gz")
md5sums=('9e8545c5713d390aaa16cf143d3b1802')

build() {
  cd ${srcdir}/live

  gsed \
      -e 's/-DHAVE_SOCKADDR_LEN=1/-fPIC -DHAVE_SOCKADDR_LEN=1/g' \
      -i config.freebsd

  ./genMakefiles freebsd
  gmake
}

package() {
  cd ${srcdir}/live

  for dir in BasicUsageEnvironment groupsock liveMedia UsageEnvironment; do
    install -dm755 ${pkgdir}/usr/{bin,lib,include/${dir}}
    install -m644 ${dir}/*.a "${pkgdir}/usr/lib"
    install -m644 ${dir}/include/*.h* "${pkgdir}/usr/include/${dir}"
  done

  for testprog in `find testProgs -type f -perm 755`; do
    install ${testprog} "${pkgdir}/usr/bin"
  done
}
