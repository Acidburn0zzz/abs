# Maintainer: Kevin Piche <kevin@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=recode
pkgver=3.6
pkgrel=14
pkgdesc="Converts files between various character sets and usages"
arch=('i686' 'x86_64')
url="http://recode.progiciels-bpi.ca/index.html"
license=('GPL' 'LGPL')
depends=('texinfo' 'gnu-libiconv')
makedepends=('perl' 'gsed')
options=('libtoolfix')
install=recode.install
port_patches=(
	patch-lib-error.c
	patch-src__names.c
	patch-src__outer.c
	patch-src__recodext.h
)
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/pinard/${pkgname}/tarball/v${pkgver}"
        'http://ftp.de.debian.org/debian/pool/main/r/recode/recode_3.6-17.diff.gz'
        'recode-3.6-as-if.patch' 'recode-3.6-gettextfix.diff'
	${port_patches[@]}
)
md5sums=('f82e9a6ede9119268c13493c9add2809'
         '3716ae269074e31add3d29387ff09438'
         'fca7484ba332c8ad59eb02334883cd92'
         'eb602e80a24b5448604bfebeacc55304'
         '43644c5f0fd448f8cb3860baf254c82a'
         '96eb91284f696e08094e66e2e3e5b304'
         '208b65e12b3722f3537717258842ecf2'
         'ee2ac612cec989c31516a6cb8470b930')

build() {
  cd ${srcdir}/pinard-Recode-2127b34
  patch -Np1 -i ${srcdir}/recode-3.6-as-if.patch
  patch -Np1 -i ${srcdir}/recode-3.6-gettextfix.diff
  patch -Np1 -i ${srcdir}/recode_3.6-17.diff

  gsed -i '1i#include <stdlib.h>' src/argmatch.c

  for i in ${port_patches[@]}; do
    patch -p0 -i "${srcdir}/$i"
  done

  ./configure --prefix=/usr --mandir=/usr/share/man \
    --infodir=/usr/share/info --without-included-gettext
  gmake

  perl -pi -e '
  	if (/Project-Id-Version/) {
  		s/3\.[45](r|\.[0-9])?/3.6/;
  		s/: (Free )?recode/: GNU recode/;
  	}
  ' i18n/*.po
}

package() {
  cd ${srcdir}/pinard-Recode-2127b34
  make DESTDIR=${pkgdir} install

  rm -f "${pkgdir}/usr/lib/charset.alias"
  rm ${pkgdir}/usr/share/info/dir
}
