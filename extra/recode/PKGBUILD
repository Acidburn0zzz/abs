# Port: converters/recode

pkgname=recode
pkgver=3.6
pkgrel=1
pkgdesc="Converts files between various character sets and usages"
arch=('i386' 'amd64')
url="http://recode.progiciels-bpi.ca/index.html"
license=('GPL' 'LGPL')
depends=('texinfo' 'libiconv' 'gettext-runtime')
makedepends=('perl' 'gsed' 'gettext-tools')
install=recode.install
patchdir=(Recode-${pkgver})
options=(!buildflags iconv)
source=(https://codeload.github.com/pinard/Recode/tar.gz/v3.6?dummy=/pinard-Recode-v${pkgver}_GH0.tar.gz)
md5sums=('977d5e836dc6540b120d00ecdf14d25d')

prepare() {
  cd Recode-${pkgver}
  unset ac_cv_build
  unset ac_cv_host

  # run libtoolfix after patching
  libtoolfix
  gsed -i '1i#include <stdlib.h>' src/argmatch.c
 
  perl -pi -e '
    if (/Project-Id-Version/) {
       s/3\.[45](r|\.[0-9])?/3.6/;
       s/: (Free )?recode/: GNU recode/;
    } 
    ' i18n/*.po
    
  sed -i '' 's,"iconv.h","/usr/local/include/iconv.h",' \
		src/libiconv.c
  cp lib/error.c src

  rm doc/${pkgname}.info*

  cp ${startdir}/files/ru.po i18n/


}

build() {
  cd Recode-${pkgver}

  LDFLAGS="-L/usr/local/lib -liconv"
 ./configure --prefix=/usr --mandir=/usr/share/man \
   --infodir=/usr/share/info --without-included-gettext \
   --target=amd64-unknown-freebsd10.3
 make

}

package() {
  cd Recode-${pkgver}
  make DESTDIR=${pkgdir} install

  rm -f "${pkgdir}/usr/lib/charset.alias"
  rm ${pkgdir}/usr/share/info/dir
}
