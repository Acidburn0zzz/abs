# Maintainer: Anthony Donnelly <amzo@archbsd.net>

pkgname=recode
pkgver=3.6
pkgrel=18
pkgdesc="Converts files between various character sets and usages"
arch=('i386' 'amd64')
url="http://recode.progiciels-bpi.ca/index.html"
license=('GPL' 'LGPL')
depends=('texinfo' 'gnu-libiconv' 'gettext-runtime')
makedepends=('perl' 'gsed' 'gettext-tools' 'gcc')
install=recode.install
options=('!clang')
patchdir=(pinard-Recode-2127b34)
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/pinard/${pkgname}/tarball/v${pkgver}"
        'http://ftp.de.debian.org/debian/pool/main/r/recode/recode_3.6-17.diff.gz'
        'recode-3.6-as-if.patch' 'recode-3.6-gettextfix.diff')
md5sums=('f82e9a6ede9119268c13493c9add2809'
         '3716ae269074e31add3d29387ff09438'
         'fca7484ba332c8ad59eb02334883cd92'
         'eb602e80a24b5448604bfebeacc55304')

prepare() {
  cd ${srcdir}/pinard-Recode-2127b34
  patch -Np1 -i "${srcdir}/recode-3.6-as-if.patch"
  patch -Np1 -i "${srcdir}/recode-3.6-gettextfix.diff"
  patch -Np1 -i "${srcdir}/recode_3.6-17.diff"

  # run libtoolfix after patching
  libtoolfix
  gsed -i '1i#include <stdlib.h>' src/argmatch.c
  sed -i '' 's,"iconv.h","/usr/local/include/iconv.h",' \
		src/libiconv.c
  cp lib/error.c src

  rm doc/${pkgname}.info*

  sed -i '' -e '/^AM_C_PROTOTYPES/d' -e '/^AUTOMAKE_OPTIONS.*ansi2knr/d' \
    configure.in src/Makefile.am

}

build() {
  cd ${srcdir}/pinard-Recode-2127b34

  rm -f acinclude.m4
  autoreconf -fi
  libtoolize

  ./configure --prefix=/usr --mandir=/usr/share/man \
    --infodir=/usr/share/info --without-included-gettext
  gmake

  perl -pi -e '
  	if (/Project-Id-Version/) {
  		s/3\.[45](r|\.[0-9])?/3.6/;
  		s/: (Free )?recode/: GNU recode/;
  	}
  ' i18n/*.po
}

package() {
  cd ${srcdir}/pinard-Recode-2127b34
  make DESTDIR=${pkgdir} install

  rm -f "${pkgdir}/usr/lib/charset.alias"
  rm ${pkgdir}/usr/share/info/dir
}
