#
# Contributor: Jeff Brodnax <tullyarcher@bellsouth.net>
# Contributor: Paul Mattal <paul@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=(postfix postfix-tools)
pkgbase=postfix
pkgver=3.0.2
pkgrel=2
pkgdesc='Fast, easy to administer, secure mail server'
url='http://www.postfix.org/'
license=('custom')
arch=('amd64')
# postfix doesn't support db6
_postfix_depends=('pcre' 'libsasl' 'libldap'
                  'libmariadbclient' 'postgresql-libs' 'sqlite' 'tinycdb')
_postfix_tools_depends=()
makedepends=("${_postfix_depends[@]}" gcc gnu-binutils)
source=("ftp://ftp.porcupine.org/mirrors/postfix-release/official/${pkgname}-${pkgver}.tar.gz"{,.sig}
        'aliases.patch' postfix.in init-d.postfix)
sha1sums=('9d75ca3c207441a38a79a2c8ceaa398c2b9cdc4e'
          'SKIP'
          '5fc3de6c7df1e5851a0a379e825148868808318b'
          '4c09ea546ae2cd86f65a4f4f642bd2b1f477c743'
          '62b4c5f1f0048bb8bc817a703a016fafece5e86c')

install=install

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	gmake -j1 makefiles DEBUG='' CCARGS=' \
		-DUSE_SASL_AUTH -DUSE_CYRUS_SASL -I/usr/include/sasl \
		-DHAS_LDAP \
		-DUSE_TLS \
		-DHAS_MYSQL -I/usr/include/mysql \
		-DHAS_PGSQL -I/usr/include/postgresql \
		-DHAS_SQLITE \
		-DHAS_CDB \
		-DDEF_COMMAND_DIR=\"/usr/bin\" \
		-DDEF_DAEMON_DIR=\"/usr/lib/postfix\" \
		-DDEF_SENDMAIL_PATH=\"/usr/bin/sendmail\" \
		-DDEF_README_DIR=\"/usr/share/doc/postfix\" \
		-DDEF_SAMPLE_DIR=\"/etc/postfix/sample\" \
		-DDEF_MANPAGE_DIR=\"/usr/share/man\" \
		-DNO_DB \
	' AUXLIBS=' \
		-lsasl2 \
		-lldap -llber \
		-lssl -lcrypto \
		-lmysqlclient -lz -lm \
		-lpq \
		-lsqlite3 -lpthread \
		-lcdb \
	' OPT="${CFLAGS} ${LDFLAGS}"

	gmake
}

package_postfix() {
	pkgdesc='Fast, easy to administer, secure mail server'
	depends=("${_postfix_depends[@]}")
	backup=('etc/postfix/'{access,aliases,canonical,generic,header_checks,main.cf,master.cf,relocated,transport,virtual})
	provides=('smtp-server' 'smtp-forwarder')
	conflicts=('smtp-server' 'smtp-forwarder')
	install=install
	cd "${srcdir}/postfix-${pkgver}"

	sh postfix-install -non-interactive install_root="${pkgdir}"

	install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	install -dm755 "${pkgdir}"/etc/{rc.d,init.d}
	install -m755 "${srcdir}"/postfix.in "${pkgdir}"/etc/rc.d/postfix
	install -m755 "${srcdir}"/init-d.postfix "${pkgdir}"/etc/init.d/postfix

	cd "${pkgdir}"
	patch -p0 -i "${srcdir}"/aliases.patch

	#Files provided by world
	rm "$pkgdir"/usr/bin/{mailq,newaliases}
	rm "$pkgdir"/usr/share/man/man1/{mailq,newaliases}.1
	rm "$pkgdir"/usr/share/man/man5/aliases.5
}

package_postfix-tools() {
	pkgdesc='SMTP/QMQP tools from the postfix distribution'
	depends=("${_postfix_tools_depends[@]}")
	cd "${srcdir}/postfix-${pkgver}"

	install -dm755 "$pkgdir/usr/share/man/man1"
	install -dm755 "$pkgdir/usr/bin"
	for name in posttls-finger {smtp,qmqp}-{sink,source}; do
		install -m644 "man/man1/${name}.1" "$pkgdir/usr/share/man/man1/${name}.1"
		install -m755 "bin/${name}" "$pkgdir/usr/bin/${name}"
	done

	install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
