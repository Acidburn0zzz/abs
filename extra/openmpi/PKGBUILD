# Port: net/openmpi
pkgname=openmpi
pkgver=1.10.3
pkgrel=1
pkgdesc="High performance message passing library (MPI)"
arch=('i386' 'amd64')
url="http://www.open-mpi.org"
license=('custom')
depends=('gcc-fortran' 'valgrind' 'libltdl>=2.4.2-11' 'hwloc>=1.7.1')
install=${pkgname}.install
options=('!clang')
source=(http://www.open-mpi.org/software/ompi/v${pkgver%.*}/downloads/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('7484bb664312082fd12edc2445b42362089b53b17fb5fce12efd4fe452cc254d')

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"

   # Make sure we use the system ltdl librariry rather than the ones in the tarball
   rm -rf opal/libltdl

   ./configure --prefix=/usr \
               --sysconfdir=/etc/${pkgname} \
               --mandir=/usr/share/man \
               --enable-mpi-fortran=usempi \
               --libdir=/usr/lib/${pkgname} \
               --with-threads=posix \
               --enable-smp-locks \
               --with-valgrind \
               --enable-memchecker \
               --enable-debug \
               --enable-pretty-print-stacktrace \
               --without-slurm \
               --with-hwloc=/usr \
               --with-libltdl=/usr \
               ompi_cv_fortran_ignore_tkr_data=0 \
               FC=/usr/local/bin/gfortran \
               FFLAGS="-Wl,-rpath=/usr/local/lib" \
               FCFLAGS="-Wl,-rpath=/usr/local/lib"

   make
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   make DESTDIR="${pkgdir}" install

   # FS#28583
   install -d -m 755 "${pkgdir}"/usr/libdata/pkgconfig
   for i in ompi-c.pc ompi-cxx.pc ompi-f77.pc ompi-f90.pc ompi.pc; do
      ln -sf /usr/libdata/openmpi/pkgconfig/${i} "${pkgdir}"/usr/libdata/pkgconfig/
   done

   # Openmpi's otfinfo conflicts with the one from texlive
   mv "${pkgdir}"/usr/bin/otfinfo{,mpi}

   # Remove dangling symlink
   rm "${pkgdir}"/usr/share/vampirtrace/config.log

   install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}/
   install -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

   install -dm755 "${pkgdir}/usr/libdata/ldconfig"
   echo "/usr/lib/$pkgname" >> "${pkgdir}/usr/libdata/ldconfig/openmpi"
}
