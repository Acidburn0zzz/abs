# $Id: PKGBUILD 203567 2014-01-13 17:11:57Z andyrtr $
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=amarok
pkgver=2.8.0
pkgrel=5
pkgdesc="The powerful music player for KDE"
arch=("i686" "x86_64")
url="http://amarok.kde.org/"
license=('GPL2' 'LGPL2.1' 'FDL')
depends=('kdebase-runtime' 'mariadb' 'qtscriptgenerator'
        'liblastfm' 'ffmpeg' 'libofa' 'qjson')
makedepends=('pkgconf' 'automoc4' 'cmake' 'libgpod' 'libmtp' 'loudmouth'
             'libmygpo-qt' 'mesa' 'clamz' 'taglib')
optdepends=("libgpod: support for Apple iPod audio devices"
            "libmtp: support for portable media devices"
            "loudmouth: backend needed by mp3tunes for syncing"
            "ifuse: support for Apple iPod Touch and iPhone"
            "libmygpo-qt: gpodder.net Internet Service"
            "clamz: allow to download songs from Amazon.com")
install="${pkgname}.install"
port_patches=(
	patch-src__core-impl__playlists__types__file__xspf__XSPFPlaylist.cpp
	patch-src__musicbrainz__MusicDNSAudioDecoder.cpp
	patch-src__playlist__proxymodels__SortScheme.cpp
)
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
	${port_patches[@]})
sha1sums=('e76ccd53c05d57f9457d74cd08c2c41383c00937'
          'f6453ab686859e0fb9bfa9ce114fe0760f14799d'
          '00aeae87a747f5d9248935f8abe69ad89b0f55c1'
          'c6b1ac656114394a9ea68199b0c461f350588e12')

prepare() {
  mkdir build
  cd ${pkgname}-${pkgver}
  for i in ${port_patches[@]}; do
    msg "Applying patch ${i}"
    patch -p0 -i "${srcdir}/${i}"
  done

  sed -i '' -e 's|/usr/local|/usr|' \
		cmake/modules/FindMySQLAmarok.cmake
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package(){
  cd build
  make DESTDIR="${pkgdir}" install
}
