# $Id: PKGBUILD 171997 2012-11-25 21:50:28Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=fontforge
pkgver=20150824
pkgrel=2
pkgdesc='Outline and bitmap font editor'
arch=('amd64')
url='http://fontforge.sourceforge.net/'
license=('BSD')
depends=('libxkbui' 'libxi' 'libxml2' 'pango' 'giflib' 'libtiff' 'python2' 'libspiro'
         'desktop-file-utils' 'gtk-update-icon-cache' 'hicolor-icon-theme' 'uthash')
makedepends=('git')
options=( '!makeflags')
source=("https://github.com/fontforge/fontforge/archive/${pkgver}.tar.gz"
        'http://fontforge.org/cidmaps.tgz')
sha1sums=('ef57ecfcb438b7d9f38b19c4c5293de7fd274000'
          'efbc7c9d3e95159f5600dc71f9cccb370e46bb94')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver//_/-}"

	./bootstrap --force

	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--enable-python-extension \

	gmake
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver//_/-}"
	gmake DESTDIR="${pkgdir}" install

	install -d "${pkgdir}/usr/share/fontforge"
	install -m644 ../Adobe-* "${pkgdir}/usr/share/fontforge"

	install -d "${pkgdir}/usr/share/icons"
	cp -r desktop/icons "${pkgdir}/usr/share/icons/hicolor"
	rm -fr "${pkgdir}/usr/share/icons/hicolor/src"

	install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
