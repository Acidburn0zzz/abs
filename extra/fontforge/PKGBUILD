# Port: print/fontforge
pkgname=fontforge
pkgver=20160404
pkgrel=1
pkgdesc='Outline and bitmap font editor'
arch=('i386' 'amd64')
url='http://fontforge.sourceforge.net/'
license=('BSD')
depends=('libxkbui' 'libxi' 'libxml2' 'pango' 'giflib' 'libtiff' 'python2' 'libspiro'
         'desktop-file-utils' 'gtk-update-icon-cache' 'hicolor-icon-theme' 'uthash')
makedepends=('git')
options=( '!makeflags')
install=install
source=("https://github.com/fontforge/fontforge/archive/${pkgver}.tar.gz"
        'http://fontforge.org/cidmaps.tgz')
sha256sums=('1cc5646fccba2e5af8f1b6c1d0d6d7b6082d9546aefed2348d6c0ed948324796'
            '1bf9c7eb8835e6ed94e62cb49f1141bc046c562849e52e6c3c7f1d7cfc95c7b3')

build() {
	cd "${srcdir}/${pkgname}-${pkgver//_/-}"

	./bootstrap --force

	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--enable-python-extension \

	gmake
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver//_/-}"
	gmake DESTDIR="${pkgdir}" install

	install -d "${pkgdir}/usr/share/fontforge"
	install -m644 ../Adobe-* "${pkgdir}/usr/share/fontforge"

	install -d "${pkgdir}/usr/share/icons"
	cp -r desktop/icons "${pkgdir}/usr/share/icons/hicolor"
	rm -fr "${pkgdir}/usr/share/icons/hicolor/src"

	install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
