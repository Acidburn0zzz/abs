pkgname=deluge
pkgver=1.3.12
pkgrel=1
pkgdesc="A BitTorrent client with multiple user interfaces in a client/server model"
arch=('any')
url="http://deluge-torrent.org/"
license=('GPL3')
depends=(python2-xdg libtorrent-rasterbar twisted python2-pyopenssl
         python2-chardet python2-setuptools)
makedepends=(intltool pygtk librsvg python2-mako)
optdepends=('python2-notify: libnotify notifications'
            'pygtk: needed for gtk ui'
            'librsvg: needed for gtk ui'
            'python2-mako: needed for web ui')
install=deluge.install
source=(http://download.deluge-torrent.org/source/$pkgname-$pkgver.tar.lzma
	deluged.in
	deluged.init
	deluged.conf
	deluge_web.in)
md5sums=('56f5c1cd3f671fecc58a9168f5bd6d48'
         '95283f72126b577aff5bcfc8e3786ca2'
         '76f6c11f5e7f5569e40a2efb9df77df3'
         '1d6bfa6b87273faf327d862c97f3f98a'
         'c3835f678e389c6e292672a01f010f73')

prepare() {
  cd $pkgname-$pkgver
  sed -i '' '1s/python$/&2/' deluge/ui/Win32IconImagePlugin.py
}

build() {
  cd $pkgname-$pkgver
  python2 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  gsed -i '1s/python$/&2/' \
    "$pkgdir/usr/lib/python2.7/site-packages/deluge/ui/Win32IconImagePlugin.py"

  cd ..

  install -dm755 ${pkgdir}/etc/{rc.d,init.d,conf.d}
  install -m755 ${srcdir}/deluged.in ${pkgdir}/etc/rc.d/deluged
  install -m755 ${srcdir}/deluge_web.in ${pkgdir}/etc/rc.d/deluged_web
  install -m755 ${srcdir}/deluged.init ${pkgdir}/etc/init.d/deluged
  install -m644 ${srcdir}/deluged.conf ${pkgdir}/etc/conf.d/deluged
 
  install -d "$pkgdir/srv"
  install -d -m 775 -o 125 -g 125 "$pkgdir/srv/deluge"
}
