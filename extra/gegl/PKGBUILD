pkgname=gegl
pkgver=0.3.0
pkgrel=3
pkgdesc="Graph based image processing framework"
arch=('i386' 'amd64')
url="http://www.gegl.org/"
license=('GPL3' 'LGPL3')
depends=('babl' 'gtk2' 'json-glib')
makedepends=('intltool' 'ruby' 'lua' 'openexr' 'ffmpeg' 'librsvg' 'jasper' 'exiv2')
optdepends=('openexr: for using the openexr plugin' \
            'ffmpeg: for using the ffmpeg plugin' \
            'librsvg: for using the svg plugin' \
            'jasper: for using the jasper plugin')
source=(http://ftp.gimp.org/pub/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
options=(!makeflags)
sha1sums=('d6b77996740bc885fd42f5c639c9d3fce6211855')

prepare() {
  cd ${pkgname}-${pkgver}

  sed -i '' -e 's|x86_64|amd64|g' \
    configure

}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

unset LDFLAGS
  ./configure  --prefix=/usr  --with-sdl --with-openexr --with-librsvg \
    --with-libavformat --with-jasper --disable-docs LIBS="-lintl"

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  gmake
}

check() {
  export PATH="$srcdir/path:$PATH" PYTHON=/usr/bin/python2
  cd ${pkgname}-${pkgver}
  gmake -k check || :
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
}
