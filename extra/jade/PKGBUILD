pkgname=jade
pkgver=1.2.1
pkgrel=25
_bsdpatch=35
pkgdesc="James Clark's DSSSL Engine"
url='http://www.jclark.com/jade/'
arch=('i386' 'amd64')
license=('custom')
replaces=('openjade')
makedepends=('ginstall')
options=('libtoolfix' '!makeflags')
port_patches=(
	patch-CharsetRegistry.cxx
	patch-CharsetRegistry.h
	patch-CodingSystemKit.cxx
	patch-Makefile.in
	patch-Ptr.cxx
	patch-Ptr.h
	patch-TeXFOTBuilder.cxx
	patch-TransformFOTBuilder.cxx
	patch-assert.cxx
	patch-config.h
	patch-koi8-r.h
	patch-koi8r.dcl
	patch-lib__instmac.m4
)
source=("ftp://ftp.jclark.com/pub/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "http://dist.bsdlab.org/jade_${pkgver}-${_bsdpatch}.diff.gz"
	${port_patches[@]})
md5sums=('4239670ca6b578bff68b8c2e7cd1225f'
         '5e4397961036d32a314afb88d8212b31'
         'b3962d24efab99ae9734c86d31374bd5'
         '2304ce3d75a7973b74cc226515af7ae8'
         '86407a1a9d32fab9675b59bf63b675d8'
         'c0e71f29706fc258f9c8521fbca26143'
         '847fe1f5beaec192eb6c8042a947c443'
         '68042255327be9be4a30742dba2a70bd'
         '2ab5e7ea485ad98a7a7e97904ffd8f29'
         'effa786e44a5ec592098f0c10955c572'
         '46fe625bb4ef8dea3d2e123147e457c1'
         'bbde75a752cedc180a46cd3df58b1fc6'
         '7ce31a2b85b14e66e1be1b2bc68de6bd'
         '4e06bff26ffbd14e0371f308831b3822'
         'e4e37bdb8a9db6fd27c9f5643b4ca010')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i "${srcdir}"/jade_${pkgver}-${_bsdpatch}.diff

  for i in "${port_patches[@]}"; do
	patch -p0 -i "${srcdir}/${i}"
  done

  chmod +x configure
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr --enable-default-catalog=/usr/share/sgml/catalog
  gmake clean
  gmake
}

package() {
  cd ${pkgname}-${pkgver}
  install -d -m755 "${pkgdir}"/usr/lib
  gmake prefix="${pkgdir}"/usr install
  mv "${pkgdir}"/usr/bin/sx "${pkgdir}"/usr/bin/sgml2xml
  ginstall -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
