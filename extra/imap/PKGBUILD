# $Id: PKGBUILD 197175 2013-10-23 13:52:03Z pierre $

pkgname=imap
pkgver=2007f
pkgrel=2
pkgdesc="An IMAP/POP server"
arch=('i386' 'amd64')
license=('APACHE')
url="http://www.washington.edu/imap"
depends=()
provides=('imap-server' 'pop3-server')
conflicts=('courier-mta' 'courier-imap')

## NOTE: These patches must be _carefully_ selected and fixed.
# (some hunks removed, some changed)
# and they come from both mail/imap-uw and mail/cclient
_port_patches=(patch-aa
               patch-ab
               patch-ae
               patch-ah
               patch-ai
               patch-src-osdep-unix-env_unix.c)
source=("ftp://ftp.cac.washington.edu/imap/${pkgname}-${pkgver}.tar.gz"
        'imap' 'ipop2' 'ipop3' "${_port_patches[@]}")
options=('staticlibs' '!makeflags')
backup=(etc/xinetd.d/{imap,ipop2,ipop3} etc/ssl/certs/{imapd,ipop3d}.pem)
md5sums=('2126fd125ea26b73b20f01fcd5940369'
         '3ae5b3b333bc8ea2da106f6a97d7bd8d'
         '448f988dc5f9bdb2223dcea3abc4f5f1'
         '1499b13015075f0aafba04324a6f523a'
         '699b171eb200b588f8054a5980538772'
         '953684856d5ca8698551680d654b73a6'
         '51beeed3e1f9134294a908c69944485e'
         'd1051386d2b1d1b4d61dc3b97078349b'
         'e5af0089729125f4e82f044dbd6149c6'
         '7505dd0b74a91d74b0060725c4b8f81b')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  for i in "${_port_patches[@]}"; do
    msg "patch $i"
    patch -p0 -i "${srcdir}/$i"
  done
  cd "$srcdir"
  gsed \
    -e "s:-g -fno-omit-frame-pointer -O6:\${CFLAGS}:" \
    -e "s:SSLDIR=/usr/local/ssl:SSLDIR=/usr:" \
    -e "s:SSLCERTS=\$(SSLDIR)/certs:SSLCERTS=/etc/ssl/certs:" \
    -i $srcdir/$pkgname-$pkgver/src/osdep/unix/Makefile
}

build() {
  cd $srcdir/$pkgname-$pkgver
  # NOTE: if you wish to enforce SSL, use SSLTYPE=unix.nopwd

  if [ "$CARCH" == "x86_64" ]; then
         yes "y" | make bsf SPECIALAUTHENTICATORS=ssl SSLTYPE=unix EXTRACFLAGS="${CFLAGS} -fPIC"
    else yes "y" | make bsf SPECIALAUTHENTICATORS=ssl SSLTYPE=unix
  fi

  # create ssl certs for secure imap
  for i in imapd ipop3d; do
    PEM1=$srcdir/pem1
    PEM2=$srcdir/pem2
    /usr/bin/openssl req -newkey rsa:1024 -keyout $PEM1 \
      -nodes -x509 -days 365 -out  $PEM2 << EOF
--
SomeState
SomeCity
SomeOrganization
SomeOrganizationalUnit
localhost.localdomain
root@localhost.localdomain
EOF

    cat $PEM1 >  ${i}.pem
    echo ""    >> ${i}.pem
    cat $PEM2 >> ${i}.pem
    rm $PEM1 $PEM2
    umask 022
  done
}

package() {
  cd $srcdir/$pkgname-$pkgver
  install -d $pkgdir/usr/bin
  install -dm755 $pkgdir/usr/bin
  install -m755 imapd/imapd $pkgdir/usr/bin/imapd
  install -m755 ipopd/ipop2d $pkgdir/usr/bin/ipop2d
  install -m755 ipopd/ipop3d $pkgdir/usr/bin/ipop3d
  install -dm755 $pkgdir/usr/include/imap
  for i in c-client mail imap4r1 rfc822 linkage misc smtp nntp \
    osdep env_unix env fs ftl nl tcp sslio utf8 utf8aux; do
    install -m644 c-client/${i}.h $pkgdir/usr/include/imap/${i}.h
  done
  install -dm755 $pkgdir/usr/lib
  install -D -m644 c-client/c-client.a $pkgdir/usr/lib/c-client.a
  ln -sf c-client.a $pkgdir/usr/lib/libc-client.a

  # install certs
  install -dm755 $pkgdir/etc/ssl/certs
  install -m600 imapd.pem $pkgdir/etc/ssl/certs/imapd.pem
  install -m600 ipop3d.pem $pkgdir/etc/ssl/certs/ipop3d.pem

  # install xinetd.d configs
  install -dm755 $pkgdir/etc/xinetd.d
  install -m644 ../imap $pkgdir/etc/xinetd.d/imap
  install -m644 ../ipop2 $pkgdir/etc/xinetd.d/ipop2
  install -m644 ../ipop3 $pkgdir/etc/xinetd.d/ipop3
}
