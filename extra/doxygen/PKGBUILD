# $Id: PKGBUILD 178954 2013-03-01 10:20:02Z andrea $
# Maintainer: Anthony donnelly <amzo@PacBSD.org>

pkgbase=doxygen
pkgname=('doxygen')
pkgver=1.8.9.1
pkgrel=1
license=('GPL')
arch=(i686 x86_64)
url="http://www.doxygen.org/"
makedepends=('gcc-libs' 'flex' 'qt4' 'ghostscript' 'gnu-libiconv'
             'graphviz' 'python2' 'texlive-latexextra' 'texlive-core')
source=(http://ftp.stack.nl/pub/users/dimitri/${pkgname}-${pkgver}.src.tar.gz)
md5sums=('3d1a5c26bef358c10a3894f356a69fbc')
sha1sums=('eb6b7e5f8dc8302e67053aba841f485017f246fd')

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  for pro in src/*.pro.in addon/doxywizard/doxywizard.pro.in; do \
    echo "unix:LIBS += -L/usr/local/lib -liconv" >> $pro
  done

  chmod +x configure

}
build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  rm -rf ${srcdir}/${pkgname}-${pkgver}/libmd5
  find ${srcdir}/${pkgname}-${pkgver} -name "configure" -type f | xargs sed -i '' -e 's|-lpthread|-pthread|g'

  sed -i '' -e 's/gcc/clang/g' -e 's/g++/clang++/g' tmake/lib/freebsd-g++/tmake.conf

  chmod +x configure

  QTDIR=/usr ./configure --prefix /usr --with-doxywizard
  gmake
}

package() {
  pkgdesc="A documentation system for C++, C, Java, IDL and PHP"
  depends=('gcc-libs')
  optdepends=('graphviz: for caller/callee graph generation'
              'qt4: for doxywizard')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  gmake INSTALL="${pkgdir}"/usr MAN1DIR=share/man/man1 install
}
