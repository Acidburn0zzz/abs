# Maintainer: Anthony donnelly <amzo@PacBSD.org>

pkgbase=doxygen
pkgname=('doxygen' 'doxygen-docs')
pkgver=1.8.10
pkgrel=1
license=('GPL')
arch=(i386 amd64)
url="http://www.doxygen.org/"
makedepends=('gcc-libs' 'flex' 'qt4' 'ghostscript' 'gnu-libiconv'
             'graphviz' 'python2' 'texlive-latexextra' 'texlive-core' 'cmake')
source=(http://ftp.stack.nl/pub/users/dimitri/${pkgname}-${pkgver}.src.tar.gz)
patchdir=(${pkgbase}-${pkgver})
options=('iconv')
md5sums=('79767ccd986f12a0f949015efb5f058f')
sha1sums=('6bad93cbdb1b2efacba14769d39bcc7bc9c0753b')

prepare() {
  mkdir build

  cd "${srcdir}/${pkgname}-${pkgver}"
  # Install the man pages in the right place
  gsed -i 's:DESTINATION man/man1:DESTINATION "${CMAKE_INSTALL_PREFIX}/share/man/man1":g' \
    doc/CMakeLists.txt

}
build() {
  cd build

  cmake "../${pkgbase}-${pkgver}" \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DDOC_INSTALL_DIR:PATH=share/doc/doxygen \
    -DQT_QMAKE_EXECUTABLE:FILE=/usr/lib/qt4/bin/qmake \
    -DPYTHON_EXECUTABLE:FILE=/usr/bin/python2 \
    -Dbuild_doc:BOOL=ON \
    -Dbuild_wizard:BOOL=ON

  gmake
  gmake docs
}

check(){
  cd build
  gmake tests
}

package_doxygen() {
  pkgdesc="A documentation system for C++, C, Java, IDL and PHP"
  depends=('gcc-libs')
  optdepends=('graphviz: for caller/callee graph generation'
              'qt4: for doxywizard')

  cd build
  gmake DESTDIR="$pkgdir" install

  # Docs are in 'doxygen-docs'
  rm -rf "${pkgdir}/usr/share/doc"
}

package_doxygen-docs() {
  pkgdesc="Developer documentation for doxygen"

  cd build
  gmake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/usr/bin"
  rm -rf "${pkgdir}/usr/share/man"
}

