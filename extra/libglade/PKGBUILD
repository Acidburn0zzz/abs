# Port: devel/libglade2

pkgname=libglade
pkgver=2.6.4
pkgrel=1
pkgdesc="Allows you to load glade interface files in a program at runtime"
arch=('i386' 'amd64')
license=('LGPL')
depends=('gtk2>=2.16.0' 'libxml2>=2.7.3')
makedepends=('python2' 'pkgconf')
optdepends=('python2: libglade-convert script')
options=('libtoolfix')
install=glade.install
source=(https://download.gnome.org/sources/${pkgname}/2.6/${pkgname}-${pkgver}.tar.bz2
	libglade-2.0.1-nowarning.patch)
url="http://www.gnome.org"
md5sums=('d1776b40f4e166b5e9c107f1c8fe4139'
         '4f879c0ce550004905aa0fb24f25c353')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/libglade-2.0.1-nowarning.patch"
  PYTHON=python2 ./configure --sysconfdir=/etc --prefix=/usr --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 libglade-convert "${pkgdir}/usr/bin/"
}
