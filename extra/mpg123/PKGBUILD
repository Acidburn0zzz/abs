# $Id: PKGBUILD 186257 2013-05-23 01:22:39Z eric $
# Maintainer: Anthony Donnelly <amzo@archbsd.net>

pkgname=mpg123
pkgver=1.22.4
pkgrel=1
pkgdesc="A console based real time MPEG Audio Player for Layer 1, 2 and 3"
arch=('i386' 'amd64')
url="http://sourceforge.net/projects/mpg123"
license=('GPL2' 'LGPL2.1')
depends=('libltdl' 'alsa-lib')
makedepends=('sdl' 'jack' 'libpulse')
optdepends=('sdl: for sdl audio support'
            'jack: for jack audio support'
            'libpulse: for pulse audio support')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2{,.sig})
validpgpkeys=('D021FF8ECF4BE09719D61A27231C4CBC60D5CAFE')
sha1sums=('6ca59baceeebe361cc508214ba725a56d4fea743'
          'SKIP')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-int-quality --with-module-suffix=.so \
    --with-audio="alsa oss sdl jack pulse" --with-default-audio=oss
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 scripts/conplay "${pkgdir}/usr/bin/conplay"
}
