# Port: multimedia/libva
pkgname=libva
pkgver=1.7.0
pkgrel=1
pkgdesc="Video Acceleration (VA) API for Linux"
arch=('i386' 'amd64')
url="http://freedesktop.org/wiki/Software/vaapi"
license=('MIT')
depends=('libgl' 'libdrm' 'libxfixes')
makedepends=('mesa' 'v4l-compat')
options=('libtoolfix')
source=(http://cgit.freedesktop.org/${pkgname}/snapshot/$pkgname-${pkgver}.tar.bz2)
sha1sums=('6eaeb8522f2dd119fd6669d9817d47ab35d4c6fe')

build() {
  cd "$pkgname-$pkgver"

  sed -i '' -e 's|\($$libdir\)/dri|\1/va|' \
    -e 's|$${libdir}/\(pkgconfig\)|${PREFIX}/libdata/\1|' \
    configure.ac

  sed -i '' -e 's/va\(info\)/\1/g' -e 's/info\.c/vainfo\.c/' \
    test/vainfo/Makefile.am

  sed -i '' '/^export VA_HEADER_/d' \
    doc/Makefile.am

  sed -i '' -e 's/-ldl//' va/Makefile.am

  mkdir -p m4
  autoreconf -v --install

  CPPFLAGS="$CPPFLAGS -isystem/usr/include" \
  LDFLAGS="$LDFLAGS -L/usr/lib" \
  CFLAGS+=" -I/usr/include -DHAVE_LINUX_INTEGER_TYPES -DNDEBUG "
  ./configure --prefix=/usr --program-prefix=va

  sed -i ''  's/-lpthread/-pthread/' \
		test/putsurface/Makefile
  gmake
}

package() {
  cd "$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
