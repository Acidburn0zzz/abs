# $Id: PKGBUILD 187158 2013-06-04 16:22:02Z giovanni $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=mercurial
pkgver=3.5.1
pkgrel=3
pkgdesc="A scalable distributed SCM tool"
arch=('i386' 'amd64')
url="http://mercurial.selenic.com/"
license=('GPL')
depends=('python2')
optdepends=('tk: for the hgk GUI')
backup=('etc/mercurial/hgrc')
source=("http://mercurial.selenic.com/release/${pkgname}-${pkgver}.tar.gz"
        'mercurial.profile')
md5sums=('796a778075ec6358235e9258f9a54224'
         '43e1d36564d4c7fbe9a091d3ea370a44')

 prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gsed -i -e 's#env python#env python2#' "mercurial/lsprof.py"
 }

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  python2 setup.py install --root="${pkgdir}/" --optimize=1

  install -d ${pkgdir}/usr/share/man/{man1,man5}
  install -m644 doc/hg.1 "${pkgdir}/usr/share/man/man1"
  install -m644 doc/{hgrc.5,hgignore.5} "${pkgdir}/usr/share/man/man5"
  install -m755 contrib/hgk "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/zsh/site-functions"
  install -dm755 "${pkgdir}/usr/share/bash-completion/completions"
  install -m644 contrib/zsh_completion "${pkgdir}/usr/share/zsh/site-functions/_hg"
  install -m644 contrib/bash_completion "${pkgdir}/usr/share/bash-completion/completions/hg"
  install -d "${pkgdir}/usr/share/emacs/site-lisp"
  install -m644 contrib/{mq.el,mercurial.el} "${pkgdir}/usr/share/emacs/site-lisp"

  vimpath="${pkgdir}/usr/share/vim/vimfiles"
  install -dm755 "${vimpath}/syntax"
  install -m644 contrib/vim/HGAnnotate.vim "${vimpath}/syntax/HGAnnotate.vim"

  # set some variables
  install -m755 -d ${pkgdir}/etc/profile.d
  install -m755 ${srcdir}/mercurial.profile "${pkgdir}/etc/profile.d/mercurial.sh"

  # install configuration file
  install -m755 -d ${pkgdir}/etc/mercurial
  #install -m644 contrib/sample.hgrc "${pkgdir}/etc/mercurial/hgrc"

  # FS#38825 - Add certs config to package
  echo -e "\n[web]\ncacerts = /usr/share/certs/ca-root-nss.crt\n" >> "${pkgdir}/etc/mercurial/hgrc"
}
