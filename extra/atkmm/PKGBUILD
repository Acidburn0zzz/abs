# Port: accessibility/atkmm

pkgbase=atkmm
pkgname=(atkmm atkmm-docs)
pkgver=2.22.7
pkgrel=1
arch=('i386' 'amd64')
makedepends=('atk' 'glibmm' 'glibmm-docs' 'libsigc++')
license=('LGPL')
options=('libtoolfix' '!emptydirs')
url="http://gtkmm.sourceforge.net/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgbase}/${pkgver:0:4}/${pkgbase}-${pkgver}.tar.xz)
sha256sums=('bfbf846b409b4c5eb3a52fa32a13d86936021969406b3dcafd4dd05abd70f91b')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  export CXXFLAGS+=" -std=c++11"
  MAKE=gmake ./configure --prefix=/usr
  gmake
}

package_atkmm() {
  pkgdesc="C++ bindings for atk"
  depends=('atk' 'glibmm' 'libsigc++')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  gsed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  gmake DESTDIR="${pkgdir}" install
}

package_atkmm-docs() {
  pkgdesc="Developer documentation for atkmm"
  cd "${srcdir}/${pkgbase}-${pkgver}"
  gmake -C doc DESTDIR="${pkgdir}" install
}
