# $Id: PKGBUILD 183599 2013-04-24 15:17:51Z heftig $
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgbase=atkmm
pkgname=(atkmm atkmm-docs)
pkgver=2.24.1
pkgrel=7
arch=('i386' 'amd64')
makedepends=('atk' 'glibmm' 'glibmm-docs' 'libsigc++')
license=('LGPL')
options=('!libtool' '!emptydirs')
url="http://gtkmm.sourceforge.net/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgbase}/${pkgver:0:4}/${pkgbase}-${pkgver}.tar.xz)
sha256sums=('26c41d8da37d04eef9f219c6ce87d94852e1cacaad823050e520e1c08a36ed23')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  #Doesn't find gmake?
  mkdir ${srcdir}/bin
  ln -s /usr/bin/gmake ${srcdir}/bin/make
  export PATH=${srcdir}/bin:$PATH

  ./configure --prefix=/usr
  gmake
}

package_atkmm() {
  pkgdesc="C++ bindings for atk"
  depends=('atk' 'glibmm' 'libsigc++')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  gsed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  gmake DESTDIR="${pkgdir}" install
}

package_atkmm-docs() {
  pkgdesc="Developer documentation for atkmm"
  cd "${srcdir}/${pkgbase}-${pkgver}"
  gmake -C doc DESTDIR="${pkgdir}" install
}
