# $Id: PKGBUILD 169712 2012-10-27 01:52:51Z stephane $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=wget
pkgver=1.15
pkgrel=1
pkgdesc="A network utility to retrieve files from the Web"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/wget/wget.html"
license=('GPL3')
depends=('libidn' 'pcre')
optdepends=('ca-certificates: HTTPS downloads')
backup=('etc/wgetrc')
install=wget.install
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig})
sha1sums=('e9fb1d25fa04f9c69e74e656a3174dca02700ba1'
          'SKIP')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cat >> doc/sample.wgetrc <<EOF

# default root certs location
ca_certificate=/usr/share/certs/ca-root-nss.crt
EOF
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --with-ssl=openssl --prefix=/usr --sysconfdir=/etc --enable-nls
  gmake
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}/usr/lib/charset.alias"
}
