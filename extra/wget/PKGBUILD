# $Id: PKGBUILD 169712 2012-10-27 01:52:51Z stephane $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=wget
pkgver=1.14
pkgrel=4
pkgdesc="A network utility to retrieve files from the Web"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/wget/wget.html"
license=('GPL3')
depends=('libidn' 'pcre')
optdepends=('ca-certificates: HTTPS downloads')
backup=('etc/wgetrc')
install=wget.install
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig}
        wget-pod-fixes.patch)
sha1sums=('cfa0906e6f72c1c902c29b52d140c22ecdcd617e'
          'SKIP'
          'aae4be72657c7a8e9dbedb7109cd2d8562a325d2')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/wget-pod-fixes.patch"
  cat >> doc/sample.wgetrc <<EOF

# default root certs location
ca_certificate=/etc/ssl/certs/ca-certificates.crt
EOF
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure -with-ssl=openssl --prefix=/usr --sysconfdir=/etc --enable-nls
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}/usr/lib/charset.alias"
}
