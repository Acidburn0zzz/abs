# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>
# Maintainer: Wolfgang Bumiller <archbsd at bumiller dot com>

pkgname=valgrind
_pkgname=valgrind-freebsd
pkgver=3.8.1
pkgrel=4
pkgdesc="A tool to help find memory-management problems in programs"
arch=('i686' 'x86_64')
license=('GPL')
url="http://valgrind.org/"
depends=('freebsd-world>=1:10.1' 'freebsd-world<=1:10.2' 'perl')
#makedepends=('gdb') provided by world
options=('!emptydirs')
source=(http://mirror.shatow.net/freebsd/valgrind/${_pkgname}-${pkgver}.tar.bz2)

build() {
  cd ${srcdir}/${_pkgname}-${pkgver}

  if [ "${CARCH}" = "x86_64" ]; then
    ./configure --prefix=/usr --mandir=/usr/share/man --enable-only64bit
  else
    ./configure --prefix=/usr --mandir=/usr/share/man
  fi

  gmake
}

package() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  gmake DESTDIR=${pkgdir} install
}
sha1sums=('8fd1ac960d4c687bd6a959416f812f6686e16295')
