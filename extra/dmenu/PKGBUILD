# Port x11/dmenu

pkgname=dmenu
pkgver=4.7
pkgrel=1
pkgdesc="A generic menu for X"
url="http://tools.suckless.org/dmenu/"
arch=('amd64')
license=('MIT')
depends=('sh' 'libxinerama' 'libxft' 'freetype2')
source=("http://dl.suckless.org/tools/${pkgname}-${pkgver}.tar.gz")
sha256sums=('a75635f8dc2cbc280deecb906ad9b7594c5c31620e4a01ba30dc83984881f7b9')

build(){
  cd "${pkgname}-${pkgver}"

  make \
    X11INC=/usr/include \
    X11LIB=/usr/lib/X11 \
    FREETYPEINC=/usr/include/freetype2
}

package() {
  cd "${pkgname}-${pkgver}"
  make PREFIX=/usr DESTDIR=$pkgdir install
  install -dm755 $pkgdir/usr/share/licenses/$pkgname/
  install -m644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
