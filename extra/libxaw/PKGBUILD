# Maintainer: Anthony Donnelly <Amzo@archbsd.net>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=libxaw
pkgver=1.0.13
pkgrel=2
pkgdesc="X11 Athena Widget library"
arch=('i386' 'amd64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxmu' 'libxpm')
makedepends=('xorg-util-macros' 'xmlto')
source=(${url}/releases/individual/lib/libXaw-${pkgver}.tar.bz2)
sha256sums=('8ef8067312571292ccc2bbe94c41109dcf022ea5a4ec71656a83d8cce9edb0cd')

build() {
  cd "${srcdir}/libXaw-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
    --enable-malloc0returnsnull
  make
}

package() {
  cd "${srcdir}/libXaw-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
