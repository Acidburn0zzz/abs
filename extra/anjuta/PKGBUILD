# $Id: PKGBUILD 203276 2014-01-07 09:24:12Z heftig $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Harley Laue <losinggeneration@yahoo.com>

pkgbase=anjuta
pkgname=('libanjuta' 'anjuta')
pkgver=3.18.0
pkgrel=2
pkgdesc="GNOME Integrated Development Environment (IDE)"
arch=('i386' 'amd64')
license=(GPL)
makedepends=(gdl vte3 autogen devhelp glade libgda subversion neon gnome-icon-theme vala dconf
             gtk-doc yelp-tools gnome-common intltool gobject-introspection itstool gjs
             python2 llvm)
url="http://www.anjuta.org/"
options=('!emptydirs')
patchdir=("$pkgbase-$pkgver")
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgbase/${pkgver:0:4}/$pkgbase-$pkgver.tar.xz)
sha256sums=('6a3fec0963f04bc62a9dfb951e577a3276d39c3414083ef73163c3fea8e741ba')

prepare() {
  cd "$pkgbase-$pkgver"


  sed -i '' -e 's|"make|"gmake|g' \
		plugins/build-basic-autotools/plugin.c

  sed -i '' -e 's|values.h|limits.h|g' \
                plugins/project-wizard/property.h \
                plugins/project-wizard/install.h

  sed -i '' -e 's|-fprofile-arcs||g' \
		libanjuta/tests/Makefile.in \
		libanjuta/tests/Makefile.am

  sed -i '' -e 's|ac_ct_CXX" = "x"|CXX" = "x"|g' \
		configure

}

build() {
  cd "$pkgbase-$pkgver"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --enable-compile-warnings=minimum \
    PYTHON=/usr/bin/python2 --with-symbol-db-shm=/tmp \
    --disable-packagekit --disable-vala LIBS="-lintl" \
    --enable-introspection=yes CFLAGS="${CFLAGS} -fPIC"

  gmake
}

package_libanjuta(){
  pkgdesc="Anjuta runtime library"
  depends=(gdl)
  conflicts=('anjuta<3.4.4')

  cd "$pkgbase-$pkgver/libanjuta"

  # j1: Race during linking
  gmake -j1 DESTDIR="$pkgdir" install
}

package_anjuta(){
  pkgdesc="GNOME Integrated Development Environment (IDE)"
  depends=(libanjuta vte3 autogen devhelp glade libgda subversion neon gnome-icon-theme vala dconf)
  groups=('gnome-extra')

  install=anjuta.install

  cd "$pkgbase-$pkgver"
  gmake -j1 DESTDIR="$pkgdir" install

  cd libanjuta
  gmake -j1 DESTDIR="$pkgdir" uninstall
}
