# Port: print/harfbuzz
pkgbase=(harfbuzz)
pkgname=(harfbuzz harfbuzz-icu)
pkgver=1.2.7
pkgrel=1
pkgdesc="OpenType text shaping engine."
arch=('i386' 'amd64')
url="http://www.freedesktop.org/wiki/Software/HarfBuzz"
license=(MIT)
makedepends=(glib2 freetype2 graphite cairo icu gobject-introspection)
optdepends=('cairo: hb-view program')
options=("libtoolfix")
source=(http://www.freedesktop.org/software/harfbuzz/release/${pkgbase}-${pkgver}.tar.bz2)
sha256sums=('bba0600ae08b84384e6d2d7175bea10b5fc246c4583dc841498d01894d479026')

build() {
  cd $pkgbase-$pkgver

  ./configure --prefix=/usr \
    --with-glib --with-freetype --with-cairo --with-icu --with-graphite2
  gmake
}

package_harfbuzz() {
  depends=(glib2 freetype2 graphite)
  optdepends=('cairo: hb-view program')

  cd $pkgbase-$pkgver
  gmake DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/share/licenses/harfbuzz"
  install -m644 COPYING "$pkgdir/usr/share/licenses/harfbuzz/COPYING"

# Split harfbuzz-icu
  mkdir -p ../hb-icu/usr/{include/harfbuzz,libdata/pkgconfig,lib}; cd ../hb-icu
  msg "Moving pc files"
  mv -v "$pkgdir"/usr/lib/libharfbuzz-icu* usr/lib/
  mv -v "$pkgdir"/usr/libdata/pkgconfig/harfbuzz-icu.pc usr/libdata/pkgconfig/
  mv -v "$pkgdir"/usr/include/harfbuzz/hb-icu.h usr/include/harfbuzz/
}

package_harfbuzz-icu(){
  pkgdesc="$pkgdesc (ICU integration)"
  depends=(harfbuzz icu)

  mv hb-icu/* "$pkgdir"

  install -dm755 "$pkgdir/usr/share/licenses/harfbuzz-icu"
  install -m644 $pkgbase-$pkgver/COPYING "$pkgdir/usr/share/licenses/harfbuzz-icu/COPYING"
}
