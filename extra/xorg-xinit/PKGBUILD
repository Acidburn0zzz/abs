# Port: x11/xinit

pkgname=xorg-xinit
pkgver=1.3.4
pkgrel=1
pkgdesc="X.Org initialisation program "
arch=('i386' 'amd64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libx11' 'xorg-xauth')
groups=('xorg')
makedepends=('xorg-util-macros')
optdepends=('xorg-twm' 'xterm')
backup=('etc/X11/xinit/xserverrc'
        'etc/X11/xinit/xinitrc')
options=(!emptydirs)
patchdir=(xinit-${pkgver})
source=(${url}/releases/individual/app/xinit-${pkgver}.tar.bz2
        06_move_serverauthfile_into_tmp.diff
        xserverrc)
options=(!emptydirs)
sha256sums=('75d88d7397a07e01db253163b7c7a00b249b3d30e99489f2734cac9a0c7902b3'
            '6c6f053555815620c4459aafa96aeae265caecab06e6f84c8bac546e04a4ef17'
            '86835010b5350c41035495b7b845db0eecf1e4b7eeaf152b12a881fa502e5387')

prepare() {
  cd xinit-${pkgver}
  patch -Np1 -i ../06_move_serverauthfile_into_tmp.diff
  sed -i '' -e 's/XSLASHGLOB.sh/XSLASHGLOB/' xinitrc.cpp
}

build() {
  cd "${srcdir}/xinit-${pkgver}"

  ./configure --prefix=/usr --with-xinitdir=/etc/X11/xinit
  make
}

package() {
  cd "${srcdir}/xinit-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m644 "${srcdir}/xserverrc" "${pkgdir}/etc/X11/xinit/xserverrc"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
