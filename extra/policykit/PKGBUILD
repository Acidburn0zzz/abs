# Port: sysutils/policykit

_pkgname=PolicyKit
pkgname=policykit
pkgver=0.9
pkgrel=2
pkgdesc="Framework for controlling access to system-wide components"
arch=(i386 amd64)
license=('LGPL')
url="http://www.freedesktop.org/wiki/Software/polkit"
depends=('expat' 'dbus-glib' 'docbook-xsl')
makedepends=('intltool')
install=policykit.install
patchdir=("$_pkgname-$pkgver")
source=("http://hal.freedesktop.org/releases/PolicyKit-$pkgver.tar.gz")
md5sums=('802fd13ae41f73d79359e5ecb0a98716')

build() {
  cd $_pkgname-$pkgver
   
   export CPPFLAGS="${CPPFLAGS} -I/usr/local/include"

  ./configure --prefix=/usr --disable-gtk-doc \
		--sysconfdir=/etc \
		--with-pam-include=system \
		--with-os-type=freebsd \
		--localstatedir=/var \
		--with-polkit-user=polkit \
		--with-polkit-group=polkit \
		--libexecdir=/usr/lib
  gmake
}

package() {
  cd $_pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir"/etc/PolicyKit

  install -m644  data/PolicyKit.conf \
	"$pkgdir"/etc/PolicyKit/PolicyKit.conf

  install -m644 data/PolicyKit.conf \
	    "$pkgdir"/etc/PolicyKit/PolicyKit.conf.dist

  install -d -o root -m 0770 -g 562 "${pkgdir}"/var/run/PolicyKit
  install -d -o root -m 0770 -g 562 "${pkgdir}"/var/lib/PolicyKit
  install -d -o 562 -m 0755 -g wheel "${pkgdir}"/var/lib/PolicyKit-public
  install -d "${pkgdir}"/var/lib/misc
  touch -f "${pkgdir}"/var/lib/misc/PolicyKit.reload
  
  chown 562:562 "${pkgdir}"/var/lib/misc/PolicyKit.reload
  
  chmod 0664 "${pkgdir}"/var/lib/misc/PolicyKit.reload
 for i in /usr/lib/polkit-read-auth-helper \
   /usr/lib/polkit-set-default-helper \
   /usr/lib/polkit-grant-helper \
   /usr/lib/polkit-grant-helper-pam \
   /usr/lib/polkit-explicit-grant-helper \
   /usr/lib/polkit-revoke-helper \
   /var/lib/misc/PolicyKit.reload; do
            chgrp 562 "${pkgdir}/${i}"
 done

}
