_pkgname=PolicyKit
pkgname=policykit
pkgver=0.9
pkgrel=9
pkgdesc="Framework for controlling access to system-wide components"
arch=(i686 x86_64)
license=('LGPL')
url="http://www.freedesktop.org/wiki/Software/polkit"
depends=('expat' 'dbus-glib' 'docbook-xsl')
makedepends=('intltool')
install=policykit.install
port_patches=(patch-Makefile.in
	patch-consolekit03
	patch-data_Makefile.in
	patch-data_PolicyKit.conf.in
	patch-polkitd_Makefile.in
	patch-polkitd_org.freedesktop.PolicyKit.service.in
	patch-src_kit_kit-spawn.c
	patch-src_kit_kit-string.c
	patch-src_polkit-grant_polkit-grant.c
	patch-src_polkit_polkit-context.c)
source=("http://hal.freedesktop.org/releases/PolicyKit-$pkgver.tar.gz"
	${port_patches[@]})
md5sums=('802fd13ae41f73d79359e5ecb0a98716'
         '35d36d9b75a81a7d231145d3e8438e68'
         '6574f4e9de6ac950c43b2c113fe18ffd'
         '8d43c080b65fab691f36a53224bc338a'
         '141718bd4c9e28c77c6fdda834b40fb4'
         '836e8e248bfa2fd3ca37ebf89b2670fa'
         'ae5ce593102c37caf3aa37d9efe05db8'
         '91b6266044df6b9194d3c918e2b639db'
         'f02dacb854c652110abc4bc0b41122e9'
         '2356b1a18a68f0b1aa0fb46ff8107827'
         '63ae105a040045b8953d734da95859fe')

prepare() {
  cd $_pkgname-$pkgver

  for _patch in ${port_patches[@]}; do
    msg "Patching port patch $_patch"
    patch -p0 -i "${srcdir}/$_patch"
  done
}

build() {
  cd $_pkgname-$pkgver
   
   export CPPFLAGS="${CPPFLAGS} -I/usr/local/include"

  ./configure --prefix=/usr --disable-gtk-doc \
		--sysconfdir=/etc \
		--with-pam-include=system \
		--with-os-type=freebsd \
		--localstatedir=/var \
		--with-polkit-user=polkit \
		--with-polkit-group=polkit \
		--libexecdir=/usr/lib
  gmake
}

package() {
  cd $_pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir"/etc/PolicyKit

  install -m644  data/PolicyKit.conf \
	"$pkgdir"/etc/PolicyKit/PolicyKit.conf

  install -m644 data/PolicyKit.conf \
	    "$pkgdir"/etc/PolicyKit/PolicyKit.conf.dist

}
