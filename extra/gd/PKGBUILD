# Port: graphics/gd
pkgname=gd
_pkgname=libgd
pkgver=2.1.1
pkgrel=1
pkgdesc="Library for the dynamic creation of images by programmers"
arch=('i386' 'amd64')
url="http://www.libgd.org/"
license=('custom')
depends=('libpng' 'fontconfig' 'libjpeg' 'libvpx' 'libtiff')
optdepends=('perl: bdftogd script')
patchdir=("${_pkgname}-${pkgver}")
source=("https://bitbucket.org/libgd/gd-libgd/downloads/libgd-${pkgver}.tar.xz")
sha256sums=('9ada1ed45594abc998ebc942cef12b032fbad672e73efc22bc9ff54f5df2b285')


build() {
  cd ${_pkgname}-${pkgver}

  #Add -pthread to ldflags
  export LDFLAGS="${LDFLAGS} -pthread"

  ./configure \
    --prefix=/usr \
    --disable-rpath \
    --with-vpx=/usr \
    --with-tiff=/usr

  gmake
}

package() {
  cd ${_pkgname}-${pkgver}

  gmake DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
