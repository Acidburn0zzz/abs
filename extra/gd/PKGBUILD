# Port: graphics/gd
pkgname=gd
_pkgname=libgd
pkgver=2.2.2
pkgrel=1
pkgdesc="Library for the dynamic creation of images by programmers"
arch=('i386' 'amd64')
url="http://www.libgd.org/"
license=('custom')
depends=('libpng' 'fontconfig' 'libjpeg' 'libvpx' 'libtiff')
optdepends=('perl: bdftogd script')
patchdir=("${_pkgname}-${pkgver}")
source=("https://github.com/libgd/libgd/releases/download/gd-${pkgver}/libgd-${pkgver}.tar.xz")
sha256sums=('489f756ce07f0c034b1a794f4d34fdb4d829256112cb3c36feb40bb56b79218c')

build() {
  cd ${_pkgname}-${pkgver}

  #Add -pthread to ldflags
  export LDFLAGS="${LDFLAGS} -pthread"

  ./configure \
    --prefix=/usr \
    --disable-rpath \
    --with-vpx=/usr \
    --with-tiff=/usr

  gmake
}

package() {
  cd ${_pkgname}-${pkgver}

  gmake DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
