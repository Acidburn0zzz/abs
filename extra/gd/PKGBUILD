pkgname=gd
pkgver=2.1.1
pkgrel=2
pkgdesc="Library for the dynamic creation of images by programmers"
arch=('i686' 'amd64')
url="http://www.libgd.org/"
license=('custom')
depends=('libpng' 'fontconfig' 'libjpeg' 'libvpx' 'libtiff')
makedepends=('git')
optdepends=('perl: bdftogd script')
source=("${pkgname}::git+https://github.com/libgd/libgd.git#tag=${pkgname}-${pkgver}"
        gd-2.1.1-libvpx-1.4.0.patch)
md5sums=('SKIP'
         '9114dd8259aaa88b0a09188fe7b19afc')

prepare() {
  cd ${pkgname}
  ./bootstrap.sh
  patch -p1 -i "${srcdir}/gd-2.1.1-libvpx-1.4.0.patch"
}

build() {
  cd ${pkgname}

  #Add -pthread to ldflags
  export LDFLAGS="${LDFLAGS} -pthread"

  ./configure \
    --prefix=/usr \
    --disable-rpath \
    --with-vpx=/usr \
    --with-tiff=/usr

  gmake
}

package() {
  cd ${pkgname}

  gmake DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
