# $Id: PKGBUILD 172058 2012-11-27 12:16:31Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=apr-util
pkgver=1.5.2
pkgrel=1
pkgdesc="The Apache Portable Runtime"
arch=('i686' 'x86_64')
url="http://apr.apache.org/"
depends=('apr' 'gdbm' 'expat' 'db' 'libldap' 'unixodbc')
options=('!libtool')
license=('APACHE')
source=("http://www.apache.org/dist/apr/apr-util-${pkgver}.tar.bz2")
md5sums=('89c1348aa79e898d7c34a6206311c9c2')

build() {
  cd "${srcdir}/apr-util-${pkgver}"
  ./configure --prefix=/usr --with-apr=/usr \
    --without-pgsql --without-mysql --without-sqlite2 --without-sqlite3 \
    --with-berkeley-db=/usr --with-gdbm=/usr --with-ldap
  make
}

#check() {
#  cd "${srcdir}/apr-util-${pkgver}"
#  make -j1 check
#}

package() {
  cd "${srcdir}/apr-util-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
