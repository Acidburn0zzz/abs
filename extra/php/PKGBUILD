# $Id: PKGBUILD 187846 2013-06-07 04:00:16Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=php
pkgname=('php'
         'php-cgi'
         'php-apache'
         'php-fpm'
         'php-embed'
         'php-pear'
         'php-enchant'
         'php-gd'
         'php-intl'
         'php-ldap'
         'php-mcrypt'
         'php-mssql'
         'php-odbc'
         'php-pgsql'
         'php-pspell'
         'php-snmp'
         'php-sqlite'
         'php-tidy'
         'php-xsl')
pkgver=5.5.13
pkgrel=8
arch=('i686' 'x86_64')
license=('PHP')
url='http://www.php.net'
makedepends=('apache' 'imap' 'postgresql-libs' 'libldap' 'postfix' 'libvpx'
             'sqlite' 'unixodbc' 'net-snmp' 'libzip' 'enchant' 'freetds'
             'libmcrypt' 'tidyhtml' 'aspell' 'libltdl' 'libpng' 'libjpeg' 'icu'
             'curl' 'libxslt' 'db' 'gmp' 'freetype2' 'readline')
port_patches=(patch-TSRM_threads.m4
              patch-Zend__zend.h
              patch-Zend_zend_list.c
              patch-Zend_zend_list.h
              patch-acinclude.m4
              patch-ext_standard_image.c
              patch-ext_zlib_config0.m4
              patch-main__php_config.h.in
              patch-sapi_apache2filter_config.m4
              patch-sapi_apache2handler_config.m4
              patch-sapi_fpm_config.m4
              db6.patch
)
source=("http://www.php.net/distributions/${pkgbase}-${pkgver}.tar.gz"
        "http://www.php.net/distributions/${pkgbase}-${pkgver}.tar.gz.asc"
        'php.ini.patch' 'apache.conf' 'php-fpm.conf.in.patch'
        "${port_patches[@]}")
md5sums=('32d0fc26fccdb249a918c0e01ffb7b82'
         'SKIP'
         '510f9092ebfe6e378e12817e197a88b6'
         'dec2cbaad64e3abf4f0ec70e1de4e8e9'
         '6bc1fc4ceee039d21ff3b56aa845cc5a'
         'bdfc82eccaafa34abc53d6781e058f15'
         '27c6d99ae2f7615243467846c36bbd5a'
         '234b89d84afa6c511002b1180c2ecc31'
         '0a09d3a7bb4dbbdf20504ad5e86761c3'
         '69a6a83299621cc5af350f6563228bf1'
         '946247b20d44f4be139ae1754f990b1c'
         '7617b055976d387af24d8a72436233de'
         'e720931b4611a0bae067c90f66062808'
         '8c1891c654e251bd717351052720f500'
         'ba6a8e3e2e86497151b4b34b9446ba1e'
         '1756da7c72d0dff5f66dd6d62e81e409'
         '60b4c3231f927fe84a9db00ba8442df4')

prepare() {
	cd ${srcdir}/${pkgbase}-${pkgver}

	patch -p0 -i ${srcdir}/php.ini.patch
	patch -p0 -i ${srcdir}/php-fpm.conf.in.patch
	# Just because our Apache 2.4 is configured with a threaded MPM by default does not mean we want to build a ZTS PHP.
	# Let's supress this behaviour and build a SAPI that works fine with the prefork MPM.
	gsed '/APACHE_THREADED_MPM=/d' -i sapi/apache2handler/config.m4 -i configure
	
	for _patch in ${port_patches[@]}; do
	    msg "Patching port patch $_patch"
	    patch -p0 -i "${srcdir}/$_patch"
	done

	touch ext/php_config.h
}

build() {
	export LIBS="${LIBS} -lssl -lcrypto"
	local _phpconfig="--srcdir=../${pkgbase}-${pkgver} \
		--config-cache \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--sysconfdir=/etc/php \
		--localstatedir=/var \
		--with-layout=GNU \
		--with-config-file-path=/etc/php \
		--with-config-file-scan-dir=/etc/php/conf.d \
		--disable-rpath \
		--mandir=/usr/share/man \
		--without-pear \
		"

	local _phpextensions="--enable-bcmath=shared \
		--enable-calendar=shared \
		--enable-dba=shared \
		--enable-exif=shared \
		--enable-ftp=shared \
		--enable-gd-native-ttf \
		--enable-intl=shared \
		--enable-mbstring \
		--enable-phar=shared \
		--enable-posix=shared \
		--enable-shmop=shared \
		--enable-soap=shared \
		--enable-sockets=shared \
		--enable-sysvmsg=shared \
		--enable-sysvsem=shared \
		--enable-sysvshm=shared \
		--enable-zip=shared \
		--with-bz2=shared \
		--with-curl=shared \
		--with-db4=/usr \
		--with-enchant=shared,/usr \
		--with-freetype-dir=/usr \
		--with-gd=shared \
		--with-gdbm \
		--with-gettext=shared \
		--with-gmp=shared \
		--with-iconv=shared \
		--with-icu-dir=/usr \
		--with-imap-ssl \
		--with-imap=shared \
		--with-jpeg-dir=/usr \
		--with-vpx-dir=/usr \
		--with-ldap=shared \
		--with-ldap-sasl \
		--with-mcrypt=shared \
		--with-mhash \
		--with-mssql=shared \
		--with-mysql-sock=/var/run/mysqld/mysqld.sock \
		--with-mysql=shared,mysqlnd \
		--with-mysqli=shared,mysqlnd \
		--with-openssl=shared \
		--with-pcre-regex=/usr \
		--with-pdo-mysql=shared,mysqlnd \
		--with-pdo-odbc=shared,unixODBC,/usr \
		--with-pdo-pgsql=shared \
		--with-pdo-sqlite=shared,/usr \
		--with-pgsql=shared \
		--with-png-dir=/usr \
		--with-pspell=shared \
		--with-snmp=shared \
		--with-sqlite3=shared,/usr \
		--with-tidy=shared \
		--with-unixODBC=shared,/usr \
		--with-xmlrpc=shared \
		--with-xsl=shared \
		--with-zlib \
		"

	EXTENSION_DIR=/usr/lib/php/modules
	export EXTENSION_DIR
	PEAR_INSTALLDIR=/usr/share/pear
	export PEAR_INSTALLDIR

	cd ${srcdir}/${pkgbase}-${pkgver}

	# php
	mkdir ${srcdir}/build-php
	cd ${srcdir}/build-php
	ln -s ../${pkgbase}-${pkgver}/configure
	./configure ${_phpconfig} \
		--disable-cgi \
		--with-readline \
		--enable-pcntl \
		${_phpextensions}
	gmake

	# cgi and fcgi
	# reuse the previous run; this will save us a lot of time
	cp -a ${srcdir}/build-php ${srcdir}/build-cgi
	cd ${srcdir}/build-cgi
	./configure ${_phpconfig} \
		--disable-cli \
		--enable-cgi \
		${_phpextensions}
	gmake

	# apache
	cp -a ${srcdir}/build-php ${srcdir}/build-apache
	cd ${srcdir}/build-apache
	./configure ${_phpconfig} \
		--disable-cli \
		--with-apxs2 \
		${_phpextensions}
	gmake

	# fpm
	cp -a ${srcdir}/build-php ${srcdir}/build-fpm
	cd ${srcdir}/build-fpm
	./configure ${_phpconfig} \
		--disable-cli \
		--enable-fpm \
		--with-fpm-user=http \
		--with-fpm-group=http \
		${_phpextensions}
	make

	# embed
	cp -a ${srcdir}/build-php ${srcdir}/build-embed
	cd ${srcdir}/build-embed
	./configure ${_phpconfig} \
		--disable-cli \
		--enable-embed=shared \
		${_phpextensions}
	gmake

	# pear
	cp -a ${srcdir}/build-php ${srcdir}/build-pear
	cd ${srcdir}/build-pear
	./configure ${_phpconfig} \
		--disable-cgi \
		--with-readline \
		--enable-pcntl \
		--with-pear \
		${_phpextensions}
	gmake
}

# check() {
# 	cd ${srcdir}/build-php
# 	export SNMP_TIMEOUT=1
# 	export SNMP_RETRIES=0
# 	export NO_INTERACTION=1
# 	make test
# 	echo
# }

package_php() {
	pkgdesc='An HTML-embedded scripting language'
	depends=('pcre' 'libxml2' 'curl' 'gmp' 'readline' 'gnu-libiconv' 'gdbm' 'db' 'gettext-runtime')
	backup=('etc/php/php.ini')

	cd ${srcdir}/build-php
	gmake -j1 INSTALL_ROOT=${pkgdir} install
	install -d -m755 ${pkgdir}/usr/share/pear
	# install php.ini
	install -dm755 ${pkgdir}/etc/php
	install -m644 ${srcdir}/${pkgbase}-${pkgver}/php.ini-production ${pkgdir}/etc/php/php.ini
	install -d -m755 ${pkgdir}/etc/php/conf.d/

	# remove static modules
	rm -f ${pkgdir}/usr/lib/php/modules/*.a
	# remove modules provided by sub packages
	rm -f ${pkgdir}/usr/lib/php/modules/{enchant,gd,intl,ldap,mcrypt,mssql,odbc,pdo_odbc,pgsql,pdo_pgsql,pspell,snmp,sqlite3,pdo_sqlite,tidy,xsl}.so
	# remove empty directory
	rmdir ${pkgdir}/usr/include/php/include
}

package_php-cgi() {
	pkgdesc='CGI and FCGI SAPI for PHP'
	depends=('php')

	install -dm755 ${pkgdir}/usr/bin
	install -m755 ${srcdir}/build-cgi/sapi/cgi/php-cgi ${pkgdir}/usr/bin/php-cgi
}

package_php-apache() {
	pkgdesc='Apache SAPI for PHP'
	depends=('php' 'apache' 'gnu-libiconv')
	backup=('etc/httpd/conf/extra/php5_module.conf')
	
	install -dm755 ${pkgdir}/usr/lib/httpd/modules
	install -dm755 ${pkgdir}/etc/httpd/conf/extra
	install -m755 ${srcdir}/build-apache/libs/libphp5.so ${pkgdir}/usr/lib/httpd/modules/libphp5.so
	install -m644 ${srcdir}/apache.conf ${pkgdir}/etc/httpd/conf/extra/php5_module.conf
}

package_php-fpm() {
	pkgdesc='FastCGI Process Manager for PHP'
	depends=('php')
	backup=('etc/php/php-fpm.conf')
	install='php-fpm.install'

	install -dm755 ${pkgdir}/usr/bin
	install -m755 ${srcdir}/build-fpm/sapi/fpm/php-fpm ${pkgdir}/usr/bin/php-fpm
	install -dm755 ${pkgdir}/usr/share/man/man8
	install -m644 ${srcdir}/build-fpm/sapi/fpm/php-fpm.8 ${pkgdir}/usr/share/man/man8/php-fpm.8
	install -dm755 ${pkgdir}/etc/php
	install -m644 ${srcdir}/build-fpm/sapi/fpm/php-fpm.conf ${pkgdir}/etc/php/php-fpm.conf
	install -dm755 ${pkgdir}/etc/php/fpm.d
}

package_php-embed() {
	pkgdesc='Embed SAPI for PHP'
	depends=('php' 'gnu-libiconv')

	install -dm755  ${pkgdir}/usr/lib
	install -dm755 ${pkgdir}/usr/include/php/sapi/embed
	install -m755 ${srcdir}/build-embed/libs/libphp5.so ${pkgdir}/usr/lib/libphp5.so
	install -m644 ${srcdir}/${pkgbase}-${pkgver}/sapi/embed/php_embed.h ${pkgdir}/usr/include/php/sapi/embed/php_embed.h
}

package_php-pear() {
	pkgdesc='PHP Extension and Application Repository'
	depends=('php')
	backup=('etc/php/pear.conf')

	cd ${srcdir}/build-pear
	gmake install-pear INSTALL_ROOT=${pkgdir}
	rm -rf ${pkgdir}/usr/share/pear/.{channels,depdb,depdblock,filemap,lock,registry}
}

package_php-enchant() {
	depends=('php' 'enchant')
	pkgdesc='enchant module for PHP'
	install -dm755 ${pkgdir}/usr/lib/php/modules
	install -m755 ${srcdir}/build-php/modules/enchant.so ${pkgdir}/usr/lib/php/modules/enchant.so
}

package_php-gd() {
	depends=('php' 'libpng' 'libjpeg' 'freetype2' 'libvpx')
	pkgdesc='gd module for PHP'
	install -dm755 ${pkgdir}/usr/lib/php/modules
	install -m755 ${srcdir}/build-php/modules/gd.so ${pkgdir}/usr/lib/php/modules/gd.so
}

package_php-intl() {
	depends=('php' 'icu')
	pkgdesc='intl module for PHP'
	install -dm755 ${pkgdir}/usr/lib/php/modules
	install -m755 ${srcdir}/build-php/modules/intl.so ${pkgdir}/usr/lib/php/modules/intl.so
}

package_php-ldap() {
	depends=('php' 'libldap')
	pkgdesc='ldap module for PHP'
	install -dm755 ${pkgdir}/usr/lib/php/modules
	install -m755 ${srcdir}/build-php/modules/ldap.so ${pkgdir}/usr/lib/php/modules/ldap.so
}

package_php-mcrypt() {
	depends=('php' 'libmcrypt' 'libltdl')
	pkgdesc='mcrypt module for PHP'
	install -dm755 ${pkgdir}/usr/lib/php/modules
	install -m755 ${srcdir}/build-php/modules/mcrypt.so ${pkgdir}/usr/lib/php/modules/mcrypt.so
}

package_php-mssql() {
	depends=('php' 'freetds')
	pkgdesc='mssql module for PHP'
	install -dm755 ${pkgdir}/usr/lib/php/modules
	install -m755 ${srcdir}/build-php/modules/mssql.so ${pkgdir}/usr/lib/php/modules/mssql.so
}

package_php-odbc() {
	depends=('php' 'unixodbc')
	pkgdesc='ODBC modules for PHP'
	install -dm755 ${pkgdir}/usr/lib/php/modules
	install -m755 ${srcdir}/build-php/modules/odbc.so ${pkgdir}/usr/lib/php/modules/odbc.so
	install -m755 ${srcdir}/build-php/modules/pdo_odbc.so ${pkgdir}/usr/lib/php/modules/pdo_odbc.so
}

package_php-pgsql() {
	depends=('php' 'postgresql-libs')
	pkgdesc='PostgreSQL modules for PHP'
	install -dm755 ${pkgdir}/usr/lib/php/modules
	install -m755 ${srcdir}/build-php/modules/pgsql.so ${pkgdir}/usr/lib/php/modules/pgsql.so
	install -m755 ${srcdir}/build-php/modules/pdo_pgsql.so ${pkgdir}/usr/lib/php/modules/pdo_pgsql.so
}

package_php-pspell() {
	depends=('php' 'aspell')
	pkgdesc='pspell module for PHP'
	install -dm755 ${pkgdir}/usr/lib/php/modules
	install -m755 ${srcdir}/build-php/modules/pspell.so ${pkgdir}/usr/lib/php/modules/pspell.so
}

package_php-snmp() {
	depends=('php' 'net-snmp')
	pkgdesc='snmp module for PHP'
	install -dm755 ${pkgdir}/usr/lib/php/modules
	install -m755 ${srcdir}/build-php/modules/snmp.so ${pkgdir}/usr/lib/php/modules/snmp.so
}

package_php-sqlite() {
	depends=('php' 'sqlite')
	pkgdesc='sqlite module for PHP'
	install -dm755 ${pkgdir}/usr/lib/php/modules
	install -m755 ${srcdir}/build-php/modules/sqlite3.so ${pkgdir}/usr/lib/php/modules/sqlite3.so
	install -m755 ${srcdir}/build-php/modules/pdo_sqlite.so ${pkgdir}/usr/lib/php/modules/pdo_sqlite.so
}

package_php-tidy() {
	depends=('php' 'tidyhtml')
	pkgdesc='tidy module for PHP'
	install -dm755 ${pkgdir}/usr/lib/php/modules
	install -m755 ${srcdir}/build-php/modules/tidy.so ${pkgdir}/usr/lib/php/modules/tidy.so
}

package_php-xsl() {
	depends=('php' 'libxslt' 'gnu-libiconv')
	pkgdesc='xsl module for PHP'
	install -dm755 ${pkgdir}/usr/lib/php/modules
	install -m755 ${srcdir}/build-php/modules/xsl.so ${pkgdir}/usr/lib/php/modules/xsl.so
}
