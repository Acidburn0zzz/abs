# Maintainer: Anthony Donnelly <Amzo@archbsd.net>
pkgname=libxcursor
pkgver=1.1.14
pkgrel=3
pkgdesc="X cursor management library"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxfixes' 'libxrender')
makedepends=('xorg-util-macros')
source=(${url}/releases/individual/lib/libXcursor-${pkgver}.tar.bz2
	index.theme)
md5sums=('1e7c17afbbce83e2215917047c57d1b3'
         '1a9667ab2fb40b97c346b46ca5abc49c')

build() {
  cd "${srcdir}/libXcursor-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --with-icondir=/usr/lib/X11/icons \
	      --enable-malloc0returnsnull
  make
}

package() {
  cd "${srcdir}/libXcursor-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/icons/default"
  install -m644 "$srcdir"/index.theme "${pkgdir}/usr/share/icons/default/"

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
