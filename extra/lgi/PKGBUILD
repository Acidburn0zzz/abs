# $Id: PKGBUILD 98427 2013-10-10 07:57:51Z seblu $
# Maintainer: speps <speps at aur dot archlinux dot org>
# Maintainer: SÃ©bastien Luttringer

pkgbase=lgi
pkgname=(lua-lgi)
pkgver=0.9.0
pkgrel=2
pkgdesc='Lua bindings for gnome/gobject using gobject-introspection library'
arch=('i386' 'amd64')
url='https://github.com/pavouk/lgi'
license=('custom:MIT')
depends=('libc' 'glib2' 'libffi' 'lua' 'gobject-introspection')
replaces=('lgi')
conflicts=('lgi')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pavouk/$pkgbase/archive/$pkgver.tar.gz")
md5sums=('cc433a597f23cfabdfc905c6c2cd3d7c')

build() {
  cd $pkgbase-$pkgver
  gmake
}

package_lua-lgi() {
  cd $pkgbase-$pkgver
  gmake \
    LUA_LIBDIR=/usr/lib/lua/5.3 \
    LUA_SHAREDIR=/usr/share/lua/5.3 \
    DESTDIR="$pkgdir/" install

  # dump typelib tool
  install -dm755 "$pkgdir/usr/bin/"
  install -m755 tools/dump-typelib.lua \
    "$pkgdir/usr/bin/dump-typelib"

  # docs
  install -d "$pkgdir/usr/share/doc/$pkgname"
  install -dm755 "$pkgdir/usr/share/doc/"
  install -m644 docs/* \
    "$pkgdir/usr/share/doc/$pkgname"

  # samples
  install -d "$pkgdir/usr/share/$pkgname/samples/gtk-demo"
  install -dm755 "$pkgdir/usr/share/$pkgname/samples"
  install -m644 samples/*.lua \
    "$pkgdir/usr/share/$pkgname/samples/"
  install -dm755 "$pkgdir/usr/share/$pkgname/samples/gtk-demo"
  install -m644 samples/gtk-demo/* \
    "$pkgdir/usr/share/$pkgname/samples/gtk-demo/"

  # license
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
