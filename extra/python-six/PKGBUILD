# Port: devel/py-six

pkgbase=python-six
pkgname=('python2-six' 'python-six')
pkgver=1.10.0
pkgrel=1
pkgdesc="Python 2 and 3 compatibility utilities"
arch=('i386' 'amd64')
url="http://pypi.python.org/pypi/six/"
license=('MIT')
makedepends=('python' 'python2')
source=(http://pypi.python.org/packages/source/s/six/six-$pkgver.tar.gz)
sha256sums=('105f8d68616f8248e24bf0e9372ef04d3cc10104f1980f54d57b2ce73a5ad56a')

build() {
  cd $srcdir
  cp -r six-$pkgver six2-$pkgver
}

check() {
  cd $srcdir/six-$pkgver 
  python3 setup.py check

  cd $srcdir/six2-$pkgver 
  python2 setup.py check
}

package_python-six() {
  depends=('python')
  cd $srcdir/six-$pkgver 
  python3 setup.py install --root "${pkgdir}" --optimize=1
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/six-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-six() {
  depends=('python2')
  cd $srcdir/six2-$pkgver 
  python2 setup.py install --root "${pkgdir}" --optimize=1
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/six-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
