# $Id: PKGBUILD 90996 2013-05-17 06:59:15Z bpiotrowski $
# Maintainer:  Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Miroslaw Szot <mss@czlug.icis.pcz.pl>

pkgbase=nginx
pkgname=('nginx')
pkgver=1.8.0
pkgrel=2
pkgdesc='Lightweight HTTP server and IMAP/POP3 proxy server'
arch=('i386' 'amd64')
depends=('pcre' 'geoip' 'gd' 'zlib')
url="http://nginx.org"
license=('custom')
install=nginx.install
backup=('etc/nginx/fastcgi.conf'
        'etc/nginx/fastcgi_params'
	'etc/nginx/koi-win'
	'etc/nginx/koi-utf'
	'etc/nginx/mime.types'
	'etc/nginx/nginx.conf'
	'etc/nginx/scgi_params'
	'etc/nginx/uwsgi_params'
	'etc/nginx/win-utf'
	'etc/logrotate.d/nginx'
	'etc/conf.d/nginx.conf')

source=(http://nginx.org/download/nginx-$pkgver.tar.gz
	https://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/www-servers/nginx/files/nginx.initd-r2
	nginx.in
	logrotate)
md5sums=('3ca4a37931e9fa301964b8ce889da8cb'
         '735f4bc2bafd9d1c0f7318901891c837'
         '678fd529dc13cb9ac654d21c67d192dc'
         'd7f84ca51ef0a8a343a6ce9d6dd74162')
sha256sums=('23cca1239990c818d8f6da118320c4979aadf5386deda691b1b7c2c96b9df3d5'
            '9d5fca09d0161446a5cec30a0694cdc0656469403a008920a52a190051218b36'
            '802d91c2d470404693d5f182f7f78e0a75238b9b90189b607ccf917e4be5a216'
            '9523a1fdd5eb61bf62f3049f6ee088b198e36d5edcce2d9b08bbeb2930aa5a16')

_base_options=(
	'--prefix=/etc/nginx'
	'--conf-path=/etc/nginx/nginx.conf'
	'--sbin-path=/usr/sbin/nginx'
	'--pid-path=/var/run/nginx/nginx.pid'
	'--lock-path=/var/run/lock/nginx.lock'
	'--user=www'
	'--group=www'
	'--http-log-path=/var/log/nginx/access.log'
	'--error-log-path=/var/log/nginx/error.log'
	'--http-client-body-temp-path=/var/lib/nginx/client-body'
	'--http-proxy-temp-path=/var/lib/nginx/proxy'
	'--http-fastcgi-temp-path=/var/lib/nginx/fastcgi'
	'--http-scgi-temp-path=/var/lib/nginx/scgi'
	'--http-uwsgi-temp-path=/var/lib/nginx/uwsgi'
	'--with-imap'
	'--with-imap_ssl_module'
	'--with-ipv6'
	'--with-pcre-jit'
	'--with-file-aio'
	'--with-http_dav_module'
	'--with-http_gunzip_module'
	'--with-http_gzip_static_module'
	'--with-http_realip_module'
	'--with-http_spdy_module'
	'--with-http_ssl_module'
	'--with-http_stub_status_module'
)

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	sed -i '' -e 's|/run/nginx.pid|/var/run/nginx.pid|g' ${srcdir}/nginx.initd-r2
}

build() {
	cd "$srcdir"/$pkgname-$pkgver

	./configure \
        	${_base_options[@]} \
		--with-http_addition_module \
		--with-http_degradation_module \
		--with-http_flv_module \
		--with-http_geoip_module \
		--with-http_image_filter_module \
		--with-http_mp4_module \
		--with-http_secure_link_module \
		--with-http_sub_module 
#		--add-module=/usr/lib/passenger/ext/nginx \
		#--add-module=../ngx_http_auth_pam_module-1.2

	gmake
}

package_nginx() {
	pkgdesc='Lightweight HTTP server and IMAP/POP3 proxy server'
	conflicts=('nginx-extra')

	cd "$srcdir"/$pkgname-$pkgver
	gmake DESTDIR="$pkgdir" install

	sed -e 's|\<user\s\+\w\+;|user html;|g' \
		-e '44s|html|/usr/share/nginx/html|' \
		-e '54s|html|/usr/share/nginx/html|' \
		-i '' "$pkgdir"/etc/nginx/nginx.conf
	rm "$pkgdir"/etc/nginx/*.default

	install -dm755 "$pkgdir"/var/lib/nginx
	install -dm700 "$pkgdir"/var/lib/nginx/proxy

#	chmod 750 "$pkgdir"/var/log/nginx
#	chown http:log "$pkgdir"/var/log/nginx

	install -dm755 "$pkgdir"/usr/share/nginx
	mv "$pkgdir"/etc/nginx/html "$pkgdir"/usr/share/nginx

	install -dm755 "$pkgdir"/etc/logrotate.d
	install -dm755 "$pkgdir"/etc/{conf.d,init.d,rc.d}
	install -dm755 "$pkgdir"/usr/share/licenses/nginx
	install -dm755 "${pkgdir}/var/run/nginx"
	install -m644 "$srcdir"/logrotate "$pkgdir"/etc/logrotate.d/nginx
	install -m755 "$srcdir"/nginx.in "$pkgdir"/etc/rc.d/nginx
	install -m755 "$srcdir"/nginx.initd-r2  "$pkgdir"/etc/rc.d/nginx
	install -m644 LICENSE "$pkgdir"/usr/share/licenses/nginx/LICENSE

	rm -r "$pkgdir"/var/run
}
