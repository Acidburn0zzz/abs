# Maintainer: Anthony Donnelly <amzo@archbsd.net>

pkgname=libvorbis
pkgver=1.3.5
pkgrel=1
pkgdesc="Vorbis codec library"
arch=('i386' 'amd64')
url="http://www.xiph.org/vorbis/"
license=('BSD')
depends=('libogg')
options=('libtoolfix')
source=(http://downloads.xiph.org/releases/vorbis/${pkgname}-${pkgver}.tar.gz)
md5sums=('7220e089f3be3412a2317d6fde9e3944')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  #-march=i386 optimizes too much, strip it out
  # LIBS+="-lm" ./configure --prefix=/usr --disable-static
  CFLAGS=${CFLAGS/-march=$CARCH} \
  ./configure --prefix=/usr \
              --with-ogg=/usr \
              --disable-docs
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"

}
