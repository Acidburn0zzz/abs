# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=libtasn1
pkgver=3.4
pkgrel=1
pkgdesc="The ASN.1 library used in GNUTLS"
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL')
url="http://www.gnu.org/software/libtasn1/"
depends=('texinfo')
options=('!libtool')
install=libtasn1.install
source=(http://ftp.gnu.org/gnu/libtasn1/${pkgname}-${pkgver}.tar.gz{,.sig})
md5sums=('21ec021c534b0f30b2834ce233c70f15'
         'SKIP')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
      --with-packager=ArchBSD \
      --with-packager-bug-reports="http://bugs.archbsd.net/" \
      --with-packager-version=${pkgver}-${pkgrel}
  gmake
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
}
