# Port: devel/nasm

pkgname=nasm
pkgver=2.13.01
pkgrel=1
pkgdesc="An 80x86 assembler designed for portability and modularity"
url="http://www.nasm.us"
license=('BSD')
arch=('amd64')
source=("http://www.nasm.us/pub/nasm/releasebuilds/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('08f97baf0a7f892128c6413cfa93b69dc5825fbbd06c70928aea028835d198fa')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  export ac_cv_func_strspn=yes # needed to work around an issue with clang 4.0
  ./configure --prefix=/usr

  gmake
}

package() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  gmake INSTALLROOT="${pkgdir}" install install_rdf

  install -d -m755 "${pkgdir}"/usr/share/licenses/nasm/
  install    -m644 LICENSE "${pkgdir}"/usr/share/licenses/nasm/LICENSE
}
