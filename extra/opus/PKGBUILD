# Port: audio/opus

pkgname=opus
pkgver=1.1.2
pkgrel=1
pkgdesc="Codec designed for interactive speech and audio transmission over the Internet"
arch=('i386' 'amd64')
url="http://www.opus-codec.org/"
license=(BSD)
options=('libtoolfix')
source=("http://downloads.us.xiph.org/releases/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('0e290078e31211baa7b5886bcc8ab6bc048b9fc83882532da4a1a45e58e907fd')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-custom-modes
  gmake
}

check() {
  cd $pkgname-$pkgver
  gmake check
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
