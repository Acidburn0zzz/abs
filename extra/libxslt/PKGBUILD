# Port: textproc/libxslt

pkgname=libxslt
pkgver=1.1.29
pkgrel=1
pkgdesc="XML stylesheet transformation library"
arch=('i386' 'amd64')
url="http://xmlsoft.org/XSLT/"
license=('custom')
depends=('libxml2' 'libgcrypt')
checkdepends=('docbook-xml')
options=('libtoolfix')
source=(ftp://xmlsoft.org/libxslt/${pkgname}-${pkgver}.tar.gz)
sha256sums=('b5976e3857837e7617b29f2249ebb5eeac34e249208d31f1fbf7a6ba7a4090ce')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --without-python \
    --with-crypto
  gmake
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
