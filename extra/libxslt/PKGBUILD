# $Id: PKGBUILD 149384 2012-02-07 12:34:45Z jgc $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=libxslt
pkgver=1.1.28
pkgrel=17
pkgdesc="XML stylesheet transformation library"
arch=('i686' 'x86_64')
url="http://xmlsoft.org/XSLT/"
license=('custom')
depends=('libxml2' 'libgcrypt')
makedepends=('libtool>=2.4.2-11')
checkdepends=('docbook-xml')
options=('!libtool' 'libtoolfix')
source=(ftp://xmlsoft.org/libxslt/${pkgname}-${pkgver}.tar.gz)
md5sums=('9667bf6f9310b957254fdcf6596600b7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --without-python \
              --with-html-dir=/usr/share/doc

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
