# Port: misc/e2fsprogs-libuuid

pkgname=e2fsprogs-libuuid
_pkgname=e2fsprogs
pkgver=1.43.6
pkgrel=1
pkgdesc="UUID library from e2fsprogs package"
arch=('amd64')
url=" https://www.kernel.org"
license=('custom')
depends=('freebsd-world')
source=(${url}/pub/linux/kernel/people/tytso/${_pkgname}/v${pkgver}/${_pkgname}-${pkgver}.tar.xz)
sha256sums=('c9b226234ee878a8d89951f3b155cec7416dd0ec09c932c855f1b1df8ad93402')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-elf-shlibs

  mkdir lib/uuid/elfshared

  cd util && gmake subst
  cd ../lib
  gmake -C uuid

}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}/lib/uuid"

  ulimit -t 5 && gmake check
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/lib/uuid"
  sed -i '' -e 's|malloc.h|stdlib.h|g' ../../util/symlinks.c
  gmake DESTDIR=${pkgdir} install
  rm -f ${pkgdir}/usr/share/man/man3/uuid_compare.3
  rm -f ${pkgdir}/usr/share/man/man3/uuid.3	
}
