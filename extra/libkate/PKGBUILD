# Port: multimedia/libkate

pkgname=libkate
pkgver=0.4.1
pkgrel=1
pkgdesc="A karaoke and text codec for embedding in ogg"
url="http://libkate.googlecode.com/"
license=('BSD')
arch=('i386' 'amd64')
depends=('libpng' 'libogg' 'python2')
makedepends=('pkgconf')
optdepends=('wxpython: for KateDJ'
            'liboggz: for KateDJ')
source=("http://libkate.googlecode.com/files/${pkgname}-${pkgver}.tar.gz")
sha256sums=('c40e81d5866c3d4bf744e76ce0068d8f388f0e25f7e258ce0c8e76d7adc87b68')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export PYTHON="/usr/bin/python2"
  gsed "1s/python$/python2/" -i tools/KateDJ/KateDJ

  ./configure --prefix=/usr \
              --disable-static \
              --disable-doc
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  gmake DESTDIR="${pkgdir}" install
  # install BSD license
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
