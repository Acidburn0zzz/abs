# Port: x11-themes/gtk-xfce-engine

pkgbase=gtk-xfce-engine
pkgname=('gtk2-xfce-engine' 'gtk3-xfce-engine')
pkgver=3.2.0
pkgrel=1
arch=('i386' 'amd64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
makedepends=('gtk2' 'gtk3')
options=('libtoolfix')
source=(http://archive.xfce.org/src/xfce/$pkgbase/3.2/$pkgbase-$pkgver.tar.bz2)
sha256sums=('875c9c3bda96faf050a2224649cc42129ffb662c4de33add8c0fd1fb860b47ed')

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/libexec \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  gmake
}

package_gtk2-xfce-engine() {
  pkgdesc="Xfce Gtk+-2.0 engine"
  depends=('gtk2')
  provides=("gtk-xfce-engine=$pkgver-$pkgrel")
  conflicts=('gtk-xfce-engine')
  replaces=('gtk-xfce-engine')

  cd "$srcdir/$pkgbase-$pkgver"
  gmake DESTDIR="$pkgdir" install

  # Remove gtk3 engine and themes
  find "$pkgdir" -name gtk-3.0 -exec rm -r {} +
}

package_gtk3-xfce-engine() {
  pkgdesc="Xfce Gtk+-3.0 engine"
  depends=('gtk3')
  conflicts=('gtk-xfce-engine<=2.99.0-1')

  cd "$srcdir/$pkgbase-$pkgver"
  gmake DESTDIR="$pkgdir" install

  # Remove gtk2 engine and themes
  find "$pkgdir" -name gtk-2.0 -exec rm -r {} +
}

# vim:set ts=2 sw=2 et:
