pkgbase=python-cryptography
pkgname=('python-cryptography' 'python2-cryptography')
pkgver=1.1
pkgrel=3
pkgdesc="A package designed to expose cryptographic recipes and primitives to Python developers"
arch=('i386' 'amd64')
license=('Apache')
url="http://pypi.python.org/pypi/cryptography"
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six' 'python-cffi' 'python2-cffi'
              'python2-enum34' 'python-pyasn1' 'python2-pyasn1' 'python-idna' 'python2-idna' 'python2-ipaddress')
source=("http://pypi.python.org/packages/source/c/cryptography/cryptography-${pkgver}.tar.gz")
sha256sums=('059bc6428b1d0e2317f505698602642f1d8dda5b120ec573a59a430d8cb7a32d')

prepare() {
   cp -a cryptography-${pkgver}{,-python2}
}

build() {
   # Build python 3 module
   cd cryptography-${pkgver}
   python3 setup.py build
 
   # Build python 2 module
   cd ../cryptography-${pkgver}-python2
   python2 setup.py build
}

package_python-cryptography() {
   depends=('python-pyasn1' 'python-six' 'python-cffi' 'python-idna')
 
   cd cryptography-${pkgver}
   python3 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
 
package_python2-cryptography() {
   depends=('python2-pyasn1' 'python2-six' 'python2-cffi' 'python2-enum34' 'python2-idna' 'python2-ipaddress')
 
   cd cryptography-${pkgver}-python2
   python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

