# $Id: PKGBUILD 179856 2013-03-10 16:22:07Z foutrelis $
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgname=xfdesktop
pkgver=4.12.3
pkgrel=1
pkgdesc="A desktop manager for Xfce"
arch=('i686' 'amd64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'thunar' 'garcon' 'hicolor-icon-theme' 'libwnck')
makedepends=('intltool')
conflicts=('xfce4-menueditor')
replaces=('xfce4-menueditor')
install=xfdesktop.install
source=(http://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('a8a8d93744d842ca6ac1f9bd2c8789ee178937bca7e170e5239cbdbef30520ac')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/libexec \
    --localstatedir=/var \
    --disable-static \
    --enable-gio-unix \
    --enable-thunarx \
    --enable-notifications \
    --disable-debug
  gmake
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
