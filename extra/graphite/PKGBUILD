# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=graphite
pkgver=1.2.4
pkgrel=4
arch=('i686' 'x86_64')
url="http://projects.palaso.org/projects/graphitedev"
pkgdesc='reimplementation of the SIL Graphite text processing engine'
license=('LGPL' 'GPL' 'custom')
depends=('gcc-libs')
makedepends=('cmake' 'freetype2' 'python2') 
options=('!emptydirs')
source=(http://downloads.sourceforge.net/project/silgraphite/graphite2/graphite2-${pkgver}.tgz
        graphite2-1.2.0-cmakepath.patch)
md5sums=('2ef839348fe28e3b923bf8cced440227'
         '00353b67941dbc30b76a43253760769e')

build() {
  cd "${srcdir}"

  # python2 fixes
  sed -i '' -e "s:\/usr\/bin\/python:\/usr\/bin\/python2:" graphite2-${pkgver}/tests/{jsoncmp,fuzztest,defuzz,corrupt.py}

  # fix install path - .cmake files contain architecture dependend content - patch taken from FC/Slackware
  pushd graphite2-${pkgver}
  patch -Np1 -i ${srcdir}/graphite2-1.2.0-cmakepath.patch
  popd

  mkdir build
  cd build

  cmake -G "Unix Makefiles" ../graphite2-${pkgver} \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_BUILD_TYPE:STRING=Release \
	-DGRAPHITE2_COMPARE_RENDERER=OFF
  make

}

check() {
  cd "${srcdir}"/build
  sed -i '' -e "s:python:python2:g" tests/CTestTestfile.cmake
  ctest
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="$pkgdir/" install

  # licenses
  mkdir -p "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 "${srcdir}"/graphite2-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/
}
