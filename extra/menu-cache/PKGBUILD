pkgname=menu-cache
pkgver=0.6.0
pkgrel=1
pkgdesc='Caching mechanism for freedesktop.org compliant menus'
arch=('i686' 'x86_64')
license=('GPL2')
url='http://lxde.org/'
groups=('lxde')
depends=('intltool' 'gamin' 'glib2')
options=('libtoolfix')
makedepends=('pkgconf')
source=(https://github.com/lxde/menu-cache/archive/$pkgver.tar.gz)
sha256sums=('1cf0a8731073a0469b6c9390324274fe99ba6579fe737215f8ceffb7998c97d4')

prepare() {
  cd $pkgname-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $srcdir/$pkgname-$pkgver

  find $srcdir/$pkgname-$pkgver -name "configure" -type f | xargs sed -i '' \
                                -e 's|-lpthread|-pthread|g' \
                                -e 's|DATADIRNAME=lib|DATADIRNAME=share|g' \
                                -e 's|{libdir}/locale|{prefix}/share/locale|g'
  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib \
              --disable-static
  gmake
}

package() {
  cd $srcdir/$pkgname-$pkgver
  gmake DESTDIR=$pkgdir install
}
