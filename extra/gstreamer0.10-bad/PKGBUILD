# $Id: PKGBUILD 202975 2013-12-29 17:17:33Z bpiotrowski $
# Maintainer: Anthony Donnelly <amzo@archbsd.net>

pkgbase=gstreamer0.10-bad
pkgname=('gstreamer0.10-bad' 'gstreamer0.10-bad-plugins')
pkgver=0.10.23
pkgrel=15
arch=('i386' 'amd64')
license=('LGPL' 'GPL')
makedepends=('pkg-config' 'gstreamer0.10-base>=0.10.36' 'xvidcore' 'libdca' 'libdc1394' 'neon' 'faac' 'musicbrainz' 'faad2' 'libmms' 'libcdaudio' 'libmpcdec' 'mjpegtools' 'libdvdnav' 'jasper' 'liblrdf' 
'libofa' 'soundtouch' 'libvdpau' 'schroedinger' 'libass' 'libvpx' 'gsm' 'libgme' 'rtmpdump' 'libsndfile' 'librsvg' 'wildmidi' 'opus' 'git' 'spandsp' 'celt')
url="http://gstreamer.freedesktop.org/"
options=(!emptydirs)
patchdir="gst-plugins-bad/"
source=("git://anongit.freedesktop.org/gstreamer-sdk/gst-plugins-bad#commit=57569a4854a0f2d14ef19a8264a4ae9a7a1d1125"
        fix-libmodplug-include.patch
        drop-vpx-compat-defs.patch
        disable-assrender-test.patch
        disable-camerabin-test.patch
        faad2-version-check.patch)
md5sums=('SKIP'
         '447c12a784b13aac9e47e48741d26a67'
         '476a02484af2e9bdc1cb4a703b531cec'
         '025438e630b571bfa90171e36d4240e5'
         '2e17f524b0db1469cfa36703bf459153'
         'dfa7e5723542d0fabebf509cb321535b')

prepare() {
  cd gst-plugins-bad
  gsed -e 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' -i configure.ac
  patch -Np1 -i ../fix-libmodplug-include.patch
  patch -Np1 -i ../drop-vpx-compat-defs.patch
  patch -Np1 -i ../disable-assrender-test.patch
  patch -Np1 -i ../disable-camerabin-test.patch
  patch -Np1 -i ../faad2-version-check.patch
}

build() {
  cd gst-plugins-bad
  NOCONFIGURE=1 ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental --disable-gtk-doc \
    --with-package-name="GStreamer Bad Plugins (PacBSD)" \
    --with-package-origin="http://www.pacbsd.org/" \
    --disable-apexsink \
    --disable-modplug
  gmake
  gsed -e 's/gst sys ext/gst/' -i Makefile
}

check() {
  cd gst-plugins-bad
  #two test failures.
  gmake check || true
}

package_gstreamer0.10-bad() {
  pkgdesc="GStreamer Multimedia Framework Bad Plugin libraries (gst-plugins-bad)"
  depends=('gstreamer0.10-base>=0.10.34')

  cd gst-plugins-bad
  gmake DESTDIR="${pkgdir}" install
}

package_gstreamer0.10-bad-plugins() {
  pkgdesc="GStreamer Multimedia Framework Bad Plugins (gst-plugins-bad)"
  depends=("gstreamer0.10-bad=${pkgver}" 'xvidcore' 'libdca' 'libdc1394' 'neon' 'faac' 'musicbrainz' 'faad2' 'libmms' 'libcdaudio' 'libmpcdec' 'mjpegtools' 'libdvdnav' 'libmodplug' 'jasper' 'liblrdf' 'libofa' 'libvdpau' 'soundtouch' 'libass' 'schroedinger' 'libvpx' 'gsm' 'rtmpdump' 'libgme' 'libsndfile' 'librsvg' 'wildmidi' 'opus' 'celt' 'spandsp')
  groups=('gstreamer0.10-plugins')
  install=gstreamer0.10-bad-plugins.install

  cd gst-plugins-bad
  gmake -C gst-libs DESTDIR="${pkgdir}" install
  gmake -C ext DESTDIR="${pkgdir}" install
  gmake -C sys DESTDIR="${pkgdir}" install
  gmake -C gst-libs DESTDIR="${pkgdir}" uninstall
}
