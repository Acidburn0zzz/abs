# Port: sysutils/htop

pkgname=htop
pkgver=2.0.2
pkgrel=2
pkgdesc="Interactive process viewer"
arch=('i386' 'amd64')
url="http://hisham.hm/htop"
license=('GPL')
depends=('ncurses' 'libexecinfo')
makedepends=('python')
optdepends=('lsof: show files opened by a process')
options=('!emptydirs' 'libtoolfix')
source=("http://hisham.hm/${pkgname}/releases/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('179be9dccb80cee0c5e1a1f58c8f72ce7b2328ede30fb71dcdf336539be2f487')

build() {
  cd "${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-unicode \
    --enable-openvz \
    --enable-vserver

  make
}

package() {
  make -C "${pkgname}-${pkgver}" DESTDIR="${pkgdir}" install
}
