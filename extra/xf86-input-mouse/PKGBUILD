# $Id: PKGBUILD 168559 2012-10-13 11:43:43Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-input-mouse
pkgver=1.9.0
pkgrel=1
pkgdesc="X.org mouse input driver"
arch=(i686 x86_64)
license=('custom')
url="http://xorg.freedesktop.org/"
makedepends=('xorg-server-devel' 'X-ABI-XINPUT_VERSION=20' 'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.15.0' 'X-ABI-XINPUT_VERSION<20' 'X-ABI-XINPUT_VERSION>=21')
options=('!libtool')
groups=('xorg-drivers' 'xorg')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
        patch-src-bsd_mouse.c patch-src_mouse.c patch-src_mouse.h patch-src_mousePriv.h
)
sha256sums=('5d601e4bae53d5e9ead4ecd700f1beb5aeaf78b79e634c4aa381a9ce00276488'
            '48c1080732c2611a278a08c444634a3befa34b0480a008d1656c56109ebc6d90'
            '151bede1704af1162fbefe21760e641559c0f1b2dcda1fefc458cb4d3b6a4df3'
            '284ff3f5429b51677d4d143f99859a3fef0da18c3d4fd4edb045df26cd96de5f'
            '51afa62b8592e718a26483e0d7c36cd0931876492a7b1b82a8195c8e8b4f2f32')
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  for i in patch-src-bsd_mouse.c patch-src_mouse.c patch-src_mouse.h patch-src_mousePriv.h
  do
    patch -p0 -i "${srcdir}/$i"
  done
  
  export CC=clang
  ./configure --prefix=/usr --disable-silent-rules
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
