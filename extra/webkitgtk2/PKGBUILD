# $Id: PKGBUILD 188485 2013-06-14 23:12:24Z heftig $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=webkitgtk2
pkgver=1.10.2
pkgrel=15
pkgdesc="GTK+ Web content engine library for GTK+ 2.0"
arch=(i686 x86_64)
url="http://webkitgtk.org/"
license=(custom)
depends=(libxt libxslt sqlite libsoup enchant libgl geoclue gtk2 gstreamer0.10-base
         icu harfbuzz-icu)
makedepends=(gperf gobject-introspection python2 mesa ruby gtk-doc)
optdepends=()
provides=("libwebkit=${pkgver}")
conflicts=(libwebkit)
replaces=(libwebkit)
# our patches are explicitly for clang with libc++
options=(!libtool !emptydirs clang)
_patches=(patch-configure
          patch-Source_JavaScriptCore_wtf_Atomics.h
          patch-Tools_DumpRenderTree_gtk_DumpRenderTree.cpp)
source=(http://webkitgtk.org/releases/webkitgtk-$pkgver.tar.xz
        "${_patches[@]}")
prepare() {
  cd "${srcdir}/webkitgtk-${pkgver}"

  for i in "${_patches[@]}"; do
    msg "patch $i"
    patch -p0 -i "${srcdir}/$i"
  done

# find isatty
  gsed -i -e '/#include <stdlib\.h>/i#include <unistd.h>' \
    Source/ThirdParty/gtest/include/gtest/internal/gtest-port.h
# SIGCHLD and some others
  gsed -i -e '/#include "config\.h"/i#include <sys/wait.h>' \
          -e 's/SIGCLD/SIGCHLD/g' \
    Source/WebKit2/UIProcess/Plugins/PluginProcessProxy.cpp
# clang/libc++
  find . -name '*.h' -o -name '*.cpp' | xargs gsed -i \
    -e '/ext\/atomicity\.h/d'
}


build() {
  cd "${srcdir}/webkitgtk-${pkgver}"

# hmm... build system fail?
  export LIBS="-lharfbuzz-icu -lEGL"
# we don't have a pkg-config for zlib
  export ZLIB_CFLAGS=" " ZLIB_LIBS=-lz

  # Suppress a *lot* of gcc 4.8 warnings
  CPPFLAGS+=" -Wno-unused-local-typedefs"

  FLEX=/usr/local/bin/flex PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
    --libexecdir=/usr/lib/$pkgname \
    --enable-introspection \
    --disable-webkit2 \
    --with-gstreamer=0.10 \
    --disable-glibtest \
    --with-gtk=2.0

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  gsed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  gmake all stamp-po V=1
}

package() {
  cd webkitgtk-$pkgver
  gmake -j1 DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname/"
  install -Dm644 Source/WebKit/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -r "$pkgdir/usr/share/gtk-doc/html/webkitgtk"
}
sha256sums=('19234f8e4edfaefe91ed06471a2aa8c71d26dd55d33787eede1dcfc9a3a96d9c'
            '12f57bc645bd2dbcbc0747e31fbb0fc752621da409587c0d17d01c2825fd1e2b'
            '6f9bb43527d6d151266cdf03efeb5c736559d4b53c257dfb3105bf2bc07eaf98'
            '1173c0331961b332addff3b42bb2a3da806ce313d3b71ff245c9e60ff4e11afb'
            '76191ad680cf1d860c740e2801f332840d78892a4db1c608262f4506e5135669')
