# $Id: PKGBUILD 160585 2012-06-02 10:29:16Z bluewind $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=xfconf
pkgver=4.12.0
pkgrel=2
pkgdesc="A simple client-server configuration storage and query system"
arch=('i686' 'amd64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4util' 'dbus-glib' 'dbus')
makedepends=('pkgconf' 'perl-extutils-depends' 'perl-extutils-pkgconfig'
             'glib-perl' 'intltool' 'gtk-doc' 'chrpath')
options=('libtoolfix' '!emptydirs')
source=(http://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('9910eaea8405336415d4d7f3056403f8c67bc23ba0dce251eac35205c603b156')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/libexec/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --with-perl-options=INSTALLDIRS="vendor" \
    --disable-debug
  gmake
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  gmake DESTDIR="$pkgdir" install

}

# vim:set ts=2 sw=2 et:
