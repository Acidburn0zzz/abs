# $Id: PKGBUILD 185571 2013-05-15 08:05:30Z bpiotrowski $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=ptlib
pkgver=2.10.11
pkgrel=3
pkgdesc="Portable Windows Library"
arch=('i386' 'amd64')
url="http://www.opalvoip.org"
license=('GPL')
depends=('sdl' 'libpulse' 'v4l-utils' 'libldap')
replaces=('pwlib')
conflicts=('pwlib')
port_patches=(patch-include_ptlib_object.h
	patch-plugins_configure.in
	patch-src_ptlib_unix_svcproc.cxx)
source=(http://downloads.sourceforge.net/opalvoip/ptlib-$pkgver.tar.bz2
	${port_patches[@]}
	ptlib-2.10.10-bison_fixes-1.patch)
sha256sums=('27a8ba1fd147195c0b310897f81d26f73996b77b18147e587638444cfacd681a'
            '9052ffc981495bfd03e4198701a40b35ec2cf0baab29e0514f49add0ade17dbc'
            'bb48bdc4b0df3a6b0f93d3422214879a394c76c9e76132ef86ef390ff7fd4c3c'
            'ec709865b9eb60a79a2238589a0d89f60e4dccda95ec6eb09a7942ef5420e6b9'
            '11027b21f568b2ab84f9c25775e5a697a15304b39aaab6380922ed609aac63b9')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  for i in ${port_patches[@]}; do
	msg "Applying patch $i"
	patch -p0 -i ${srcdir}/$i
  done

  patch -Np1 -i ../ptlib-2.10.10-bison_fixes-1.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib \
      --sysconfdir=/etc --localstatedir=/var \
      --enable-opal --enable-plugins \
      --enable-oss --enable-v4l2 \
      --disable-avc --enable-sdl \
      --enable-ipv6 --enable-v4l \
      --disable-dc --disable-odbc \
      --enable-expat --enable-httpforms
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
}
