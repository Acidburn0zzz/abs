# Port: archivers/p7zip
pkgname=p7zip
pkgver=15.14
pkgrel=1
pkgdesc='Command-line version of the 7zip compressed file archiver'
url='http://p7zip.sourceforge.net/'
license=('GPL' 'custom')
arch=('i386' 'amd64')
depends=('bash')
makedepends=('yasm' 'nasm')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}_${pkgver}_src_all.tar.bz2"
        '7z')
patchdir=("${pkgname}_${pkgver}")
sha1sums=('97f719c55e6f3b1134aff2a4b5ac3a6accb5492f'
          '30a07dbc725c3c8607b2baee685961742ea1b0d2')
options=('!makeflags')

prepare() {
	cd "${srcdir}/${pkgname}_${pkgver}"
	rm GUI/kde4/p7zip_compress.desktop
	export CXXFLAGS="${CXXFLAGS} -fPIC"

	sed -e 's!g\+\+!clang\+\+!' \
		-e 's!gcc!clang!' \
		-e 's/-D_LARGEFILE_SOURCE//' \
		-e 's/-D_FILE_OFFSET_BITS=64//' \
		makefile.freebsd6+ > makefile.machine

	sed -i ''  -e 's!-O!${CFLAGS}!' makefile.machine
}

build() {
	cd "${srcdir}/${pkgname}_${pkgver}"
	make all3 OPTFLAGS="${CXXFLAGS}"
}

package() {
	cd "${srcdir}/${pkgname}_${pkgver}"

	install -dm755 ${pkgdir}/usr/{bin,lib/p7zip}
	for BINARY in 7z 7za 7zr 7zCon.sfx; do
		install -m755 bin/${BINARY} ${pkgdir}/usr/lib/p7zip/
	done

	install -m644 bin/7z.so ${pkgdir}/usr/lib/p7zip/

	install -m755 ${srcdir}/7z ${pkgdir}/usr/bin/
	ln -sf 7z ${pkgdir}/usr/bin/7za
	ln -sf 7z ${pkgdir}/usr/bin/7zr

	chmod -R u+w "${pkgdir}/usr"

}
