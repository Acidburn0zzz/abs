# Port: security/cyrus-sasl2

pkgbase=('cyrus-sasl')
pkgname=('cyrus-sasl' 'cyrus-sasl-gssapi' 'cyrus-sasl-sql' 'libsasl')
#pkgname=libsasl
pkgver=2.1.26
pkgrel=3
pkgdesc="Cyrus Simple Authentication Service Layer (SASL) library"
arch=('i386' 'amd64')
url="http://cyrusimap.web.cmu.edu/"
license=('custom')
options=('!makeflags' 'libtoolfix')
makedepends=('libmariadbclient' 'db' 'sqlite')
source=(ftp://ftp.cyrusimap.org/cyrus-sasl/cyrus-sasl-${pkgver}.tar.gz
        cyrus-sasl-2.1.22-qa.patch
        cyrus-sasl-2.1.26-size_t.patch
        0010_maintainer_mode.patch
        0011_saslauthd_ac_prog_libtool.patch
        0025_ld_as_needed.patch
        0026_drop_krb5support_dependency.patch
        0030-dont_use_la_files_for_opening_plugins.patch
        CVE-2013-4122.patch
        cyrus-sasl-sql.patch)
md5sums=('a7f4e5e559a0e37b3ffc438c9456e425'
         '79b8a5e8689989e2afd4b7bda595a7b1'
         'f45aa8c42b32e0569ab3d14a83485b37'
         'f45d8b60e8f74dd7f7c2ec1665fa602a'
         '9d93880514cb5ff5da969f1ceb64a661'
         '62bf892fe4d1df41ff748e91a1afaf67'
         'b7848957357e7c02d6490102be496bf9'
         '8e7106f32e495e9ade69014fd1b3352a'
         'c5f0ec88c584a75c14d7f402eaeed7ef'
         '82c0f66fdc5c1145eb48ea9116c27931')

prepare() {
  cd "${srcdir}/cyrus-sasl-${pkgver}"

  patch -Np1 -i ../cyrus-sasl-2.1.22-qa.patch
  patch -Np1 -i ../cyrus-sasl-2.1.26-size_t.patch
  patch -Np1 -i ../0010_maintainer_mode.patch
  patch -Np1 -i ../0011_saslauthd_ac_prog_libtool.patch
  patch -Np1 -i ../0025_ld_as_needed.patch
  patch -Np1 -i ../0026_drop_krb5support_dependency.patch
  patch -Np1 -i ../0030-dont_use_la_files_for_opening_plugins.patch
  patch -Np1 -i ../CVE-2013-4122.patch
  patch -Np0 -i ../cyrus-sasl-sql.patch

  gsed 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' -i configure.in

  # port part
  # We need to fix up a wrong library name
  find . ! -type d -exec sed -i '' -e 's/libsasldb\.al/libsasldb.a/g' '{}' ';'
}

build() {
  cd "${srcdir}/cyrus-sasl-${pkgver}"

# These flags are required, LDFLAGS from makepkg.conf *cannot* be used reliably!
  unset CFLAGS
  unset LDFLAGS
  unset CPPFLAGS

  if [ "$CARCH" == "amd64" ]; then
    conf_args=(--build=amd64-portbld-freebsd10.3 \
               CPPFLAGS="-fPIC" )
  else
    conf_args=(--build=i386-portbld-freebsd10.3)
  fi

  rm -f config/config.guess config/config.sub 
  rm -f config/ltconfig config/ltmain.sh config/libtool.m4
  rm -fr autom4te.cache
  libtoolize -c
  aclocal -I config -I cmulocal
  automake -a -c
  autoheader
  autoconf

  pushd saslauthd
  rm -f config/config.guess config/config.sub 
  rm -f config/ltconfig config/ltmain.sh config/libtool.m4
  rm -fr autom4te.cache
  libtoolize -c
  aclocal -I config -I ../cmulocal -I ../config
  automake -a -c
  autoheader
  autoconf
  popd

  ./configure --prefix=/usr \
      --mandir=/usr/share/man \
      --infodir=/usr/share/info \
      --enable-static \
      --enable-shared \
      --enable-alwaystrue \
      --enable-checkapop \
      --enable-cram \
      --enable-digest \
      --disable-otp \
      --disable-srp \
      --disable-srp-setpass \
      --disable-krb4 \
      --enable-gssapi \
      --enable-auth-sasldb \
      --enable-plain \
      --enable-anon \
      --enable-login \
      --enable-ntlm \
      --disable-passdss \
      --enable-sql \
      --with-mysql \
      --with-sqlite3=/usr/lib \
      --disable-macos-framework \
      --with-pam \
      --with-saslauthd=/var/run/saslauthd \
      --without-ldap \
      --with-dblib=berkeley \
      --with-configdir=/etc/sasl2:/etc/sasl:/usr/lib/sasl2 \
      --sysconfdir=/etc \
      --with-plugindir=/usr/lib/sasl2 \
      --with-dbpath=/etc/sasldb2 \
      --with-lib-subdir=lib \
      --with-pkgconfigdir=/usr/lib/pkgconfig \
      --includedir=/usr/include \
      --with-rc=openssl \
      --with-bdb=db-5 \
      --with-authdaemond=/var/run/authdaemond/socket \
      --with-openssl=yes \
      CFLAGS="-O2 -pipe -fno-strict-aliasing" \
      LDFLAGS="-Wl,-rpath,/usr/lib" \
      ${conf_args[@]}

  gmake
}

package_libsasl() {
  pkgdesc="Cyrus Simple Authentication Service Layer (SASL) Library"
  depends=('db' 'sqlite' 'libmariadbclient')
  conflicts=('cyrus-sasl-plugins')

  cd "${srcdir}/cyrus-sasl-${pkgver}"
  for dir in include lib sasldb plugins utils; do
    pushd ${dir}
    gmake DESTDIR="${pkgdir}" install
    popd
  done
  rm -f "${pkgdir}"/usr/lib/sasl2/libsql.so*
  rm -f "${pkgdir}"/usr/lib/sasl2/libgssapiv2.so*
  rm -f "${pkgdir}"/usr/lib/sasl2/libldapdb.so*
  install -m755 -d "${pkgdir}/usr/share/licenses/libsasl"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/libsasl/"
}

package_cyrus-sasl() {
  depends=("libsasl=${pkgver}" 'db')
  pkgdesc="Cyrus saslauthd SASL authentication daemon"
  backup=('etc/conf.d/saslauthd')

  cd "${srcdir}/cyrus-sasl-${pkgver}/saslauthd"
  gmake DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/cyrus-sasl"
  ln -sf ../libsasl/COPYING "${pkgdir}/usr/share/licenses/cyrus-sasl/"
}

package_cyrus-sasl-gssapi() {
  pkgdesc="GSSAPI authentication mechanism for Cyrus SASL"
  depends=("libsasl=${pkgver}")
  replaces=('cyrus-sasl-plugins')

  cd "${srcdir}/cyrus-sasl-${pkgver}/plugins"
  install -m755 -d "${pkgdir}/usr/lib/sasl2"
  cp -a .libs/libgssapiv2.so* "${pkgdir}/usr/lib/sasl2/"

  install -m755 -d "${pkgdir}/usr/share/licenses/cyrus-sasl-gssapi"
  ln -sf ../libsasl/COPYING "${pkgdir}/usr/share/licenses/cyrus-sasl-gssapi/"
}

package_cyrus-sasl-ldap() {
  pkgdesc="ldapdb auxprop module for Cyrus SASL"
  depends=("libsasl=${pkgver}" )
  replaces=('cyrus-sasl-plugins')

  cd "${srcdir}/cyrus-sasl-${pkgver}/plugins"
  install -m755 -d "${pkgdir}/usr/lib/sasl2"
  cp -a .libs/libldapdb.so* "${pkgdir}/usr/lib/sasl2/"

  install -m755 -d "${pkgdir}/usr/share/licenses/cyrus-sasl-ldap"
  ln -sf ../libsasl/COPYING "${pkgdir}/usr/share/licenses/cyrus-sasl-ldap/"
}

package_cyrus-sasl-sql() {
  pkgdesc="SQL auxprop module for Cyrus SASL"
  depends=("libsasl=${pkgver}" 'libmariadbclient' 'sqlite')
  replaces=('cyrus-sasl-plugins')

  cd "${srcdir}/cyrus-sasl-${pkgver}/plugins"
  install -m755 -d "${pkgdir}/usr/lib/sasl2"
  cp -a .libs/libsql.so* "${pkgdir}/usr/lib/sasl2/"

  install -m755 -d "${pkgdir}/usr/share/licenses/cyrus-sasl-sql"
  ln -sf ../libsasl/COPYING "${pkgdir}/usr/share/licenses/cyrus-sasl-sql/"
}
