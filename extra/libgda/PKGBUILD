# $Id: PKGBUILD 203579 2014-01-13 17:12:10Z andyrtr $
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=libgda
pkgver=5.2.4
pkgrel=4
pkgdesc="Data abstraction layer with mysql, pgsql, xml, sqlite providers"
arch=('i386' 'amd64')
license=('GPL')
depends=('gtksourceview3' 'libxslt' 'json-glib' 'db' 'ncurses' 'libsoup'
         'libmariadbclient' 'postgresql-libs' 'python2' 'libsecret'
         'hicolor-icon-theme' 'desktop-file-utils' 'graphviz' 'gdk-pixbuf2'
         'iso-codes' 'openssl' 'libgcrypt' 'libldap')
makedepends=('intltool' 'gobject-introspection' 'gtk-doc' 'vala' 'itstool')
url="http://www.gnome-db.org"
install=libgda.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('2cee38dd583ccbaa5bdf6c01ca5f88cc08758b9b144938a51a478eb2684b765e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gsed -i '1s/python$/&2/' libgda-report/RML/trml*/trml*.py
  ./configure --prefix=/usr --sysconfdir=/etc \
      --with-bdb=/usr --with-bdb-libdir-name=lib \
      --disable-static --enable-json --enable-gda-gi LDFLAGS="${LDFLAGS} -lintl" 
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
}
