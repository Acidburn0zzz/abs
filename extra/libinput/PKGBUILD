# Port: x11/libinput
pkgname=libinput
pkgver=1.6.1
pkgrel=1
pkgdesc="Input device management and event handling library"
url="https://www.freedesktop.org/wiki/Software/libinput/"
arch=(amd64)
license=(custom:X11)
depends=('libevdev' 'libudev' 'libepoll' 'libwacom' 'mtdev')
makedepends=('ginstall')
optdepends=('gtk3: libinput debug-gui')
source=(https://freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha512sums=('b18fdbfa58c14d6a5d183da3c1097d31f7dd9cce41ea69ddfa0bbd1bf8572aaa8e1ecaacdc30e3cc5e21cd320ea106ec86800a24e8f571bce61a02ddc7d43511'
            'SKIP')
validpgpkeys=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF') # Peter Hutterer (Who-T) <office@who-t.net>

prepare() {
  mkdir build
  cd $pkgname-$pkgver

	sed -i '' -e 's|program_invocation_short_name|getprogname()|' \
	    tools/libinput-list-devices.c \
	    tools/ptraccel-debug.c tools/shared.c

  export CFLAGS+=" -I/usr/include/libepoll-shim"
  autoreconf -vi
}

build() {
  cd build
  ../$pkgname-$pkgver/configure --prefix=/usr --libexecdir=/usr/libexec

	gmake
}

package() {
  cd build
  gmake DESTDIR="$pkgdir" install

  cd ../$pkgname-$pkgver
  ginstall -Dvm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # install doc - no Makefile target
  install -dv "$pkgdir/usr/share/doc/libinput"
  cp -Rv doc/html/* "$pkgdir/usr/share/doc/libinput"
}
