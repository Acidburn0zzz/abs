# Port: multimedia/ffmpeg
pkgname=ffmpeg
pkgver=3.3.4
pkgrel=1
pkgdesc="Complete and free Internet live audio and video broadcasting solution for Linux/Unix"
arch=('i386' 'amd64')
url="http://ffmpeg.org/"
license=('GPL')
depends=(
      'alsa-lib' 'fontconfig' 'gnutls' 'gsm' 'lame' 'libass' 'libvdpau'
      'libbluray' 'libmodplug' 'libpulse' 'libtheora' 'libva' 'libvorbis' 'libvpx'
      'opencore-amr' 'openjpeg' 'opus' 'rtmpdump' 'schroedinger' 'sdl' 'speex'
      'v4l-utils' 'libx264' 'xvidcore' 'zlib' 'x265'
)
makedepends=('gmake' 'perl' 'libvdpau' 'yasm' 'x264' 'binutils')
source=(http://ffmpeg.org/releases/$pkgname-$pkgver.tar.bz2{,.asc})
sha256sums=('5ef5e9276c311c74ab2e9d301c2d7ee10e1f2cbd758c6f13d6cb9514dffbac7e'
            'SKIP')

prepare() {
  cd $pkgname-$pkgver
  sed -i '' -e 's|-ldl||;' configure
  sed -i '' -e 's|/usr/bin/perl|/usr/bin/perl}|g' doc/texi2pod.pl

}

build() {
  cd $pkgname-$pkgver

  export PATH="${PATH}:/usr/bin/core_perl"
  ./configure \
    --prefix=/usr \
    --disable-debug \
    --disable-static \
    --enable-avisynth \
    --enable-avresample \
    --enable-decoder=atrac3 \
    --enable-decoder=atrac3p \
    --enable-fontconfig \
    --enable-gpl \
    --enable-libass \
    --enable-libbluray \
    --enable-libfreetype \
    --enable-libgsm \
    --enable-libmodplug \
    --enable-libmp3lame \
    --enable-libopencore_amrnb \
    --enable-libopencore_amrwb \
    --enable-libopenjpeg \
    --enable-libopus \
    --enable-libpulse \
    --enable-librtmp \
    --enable-libschroedinger \
    --enable-libspeex \
    --enable-libtheora \
    --disable-libv4l2 \
    --enable-libvorbis \
    --enable-libvpx \
    --enable-libx264 \
    --enable-libx265 \
    --enable-libxvid \
    --enable-pic \
    --enable-postproc \
    --enable-runtime-cpudetect \
    --enable-shared \
    --enable-swresample \
    --enable-vdpau \
    --enable-version3 \
    --enable-pthreads \
    --cc="${CC}"

  gmake -j1
  gmake -j1 tools/qt-faststart
  gmake -j1 doc/ff{mpeg,play,server}.1
}

package() {
  cd $pkgname-$pkgver
  gmake -j1 DESTDIR="$pkgdir" install install-man
  install -dm755 "$pkgdir/usr/bin"
  install -m755 tools/qt-faststart "$pkgdir/usr/bin/qt-faststart"
}

# vim:set ts=2 sw=2 et:
