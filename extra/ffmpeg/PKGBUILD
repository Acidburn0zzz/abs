# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=ffmpeg
pkgver=2.3.1
pkgrel=1
epoch=1
pkgdesc="Complete and free Internet live audio and video broadcasting solution for Linux/Unix"
arch=('i686' 'x86_64')
url="http://ffmpeg.org/"
license=('GPL')
depends=(
      'alsa-lib' 'fontconfig' 'gnutls' 'gsm' 'lame' 'libass' 'libvdpau'
      'libbluray' 'libmodplug' 'libpulse' 'libtheora' 'libva' 'libvorbis' 'libvpx'
      'opencore-amr' 'openjpeg' 'opus' 'rtmpdump' 'schroedinger' 'sdl' 'speex'
      'v4l-utils' 'libx264' 'xvidcore' 'zlib' 'x265'
)
makedepends=('gmake' 'perl' 'libvdpau' 'yasm' 'x264' 'gcc' 'gnu-binutils')
_port_patches=(
        patch-configure
        patch-libavdevice-bktr.c
        patch-libavdevice-oss_audio.c
        patch-libavfilter-vf_libopencv.c
        patch-libavformat-udp.c
        patch-libavutil-common.h
        patch-libavutil-x86-asm.h
)
source=(http://ffmpeg.org/releases/$pkgname-$pkgver.tar.bz2{,.asc}
        "${_port_patches[@]}")

prepare() {
  cd $pkgname-$pkgver
  for i in "${_port_patches[@]}"; do
    msg "patch $i"
    patch -p0 -i "${srcdir}/$i"
  done

  sed -i '' -e 's|-ldl||;' configure
  sed -i '' -e 's|/usr/bin/perl|/usr/bin/perl}|g' doc/texi2pod.pl
}

build() {
  cd $pkgname-$pkgver

  export PATH="${PATH}:/usr/bin/core_perl"
  ./configure \
    --prefix=/usr \
    --disable-debug \
    --disable-static \
    --enable-avisynth \
    --enable-avresample \
    --enable-decoder=atrac3 \
    --enable-decoder=atrac3p \
    --enable-dxva2 \
    --enable-fontconfig \
    --enable-gpl \
    --enable-libass \
    --enable-libbluray \
    --enable-libfreetype \
    --enable-libgsm \
    --enable-libmodplug \
    --enable-libmp3lame \
    --enable-libopencore_amrnb \
    --enable-libopencore_amrwb \
    --enable-libopenjpeg \
    --enable-libopus \
    --enable-libpulse \
    --enable-librtmp \
    --enable-libschroedinger \
    --enable-libspeex \
    --enable-libtheora \
    --disable-libv4l2 \
    --enable-libvorbis \
    --enable-libvpx \
    --enable-libx264 \
    --enable-libx265 \
    --enable-libxvid \
    --enable-pic \
    --enable-postproc \
    --enable-runtime-cpudetect \
    --enable-shared \
    --enable-swresample \
    --enable-vdpau \
    --enable-version3 \
    --enable-x11grab

  gmake
  gmake tools/qt-faststart
  gmake doc/ff{mpeg,play,server}.1
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install install-man
  install -dm755 "$pkgdir/usr/bin"
  install -m755 tools/qt-faststart "$pkgdir/usr/bin/qt-faststart"
}

# vim:set ts=2 sw=2 et:
md5sums=('bb02e99c39941d7f3dc56658b9324a2b'
         'SKIP'
         '882a4f74e7998fdb3f22eb0af08e8bf0'
         '7c590021750d8c8207ac02c5d708ff87'
         'a36d20edb06fcbe9be2b5fea0be04ca8'
         '9923c6699609eb91f4facf358a1cefc8'
         'cc370b2b72b6e3b94f516990cbc0b89e'
         'fd4e5037c9e6883ba5ece48e17e615c6'
         '7ecfe1d3bfc7d4781474571a5caaf423')
