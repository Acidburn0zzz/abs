pkgname=ghostscript
pkgver=9.18
pkgrel=1
pkgdesc="An interpreter for the PostScript language"
arch=('amd64')
license=('AGPL' 'custom')
depends=('libxt' 'libcups' 'fontconfig' 'jasper' 'libpng>=1.5.7' 'libjpeg' 'libtiff>=4.0.0' 'lcms2' 'dbus' 'libpaper' 'gnu-libiconv')
makedepends=('gtk3' 'gnutls')
optdepends=('texlive-core:      needed for dvipdf'
            'gtk3:              needed for gsx')
url="http://www.ghostscript.com/"
source=(http://downloads.ghostscript.com/public/ghostscript-${pkgver}.tar.bz2
        ghostscript-sys-zlib.patch
        bug_696246.diff)
# http://downloads.ghostscript.com/public/SHA1SUMS
sha1sums=('388fea50a38e422a4c6ff27c184491bf5ecb96e1'
          'e054caf753df4d67221b29a2eac66130653f7556'
          '8bdcb72250f81d8d1f3b3cff4becb673113ed302')


prepare() {
  cd ghostscript-${pkgver}
  patch -Np1 -i ${srcdir}/ghostscript-sys-zlib.patch

  # fix http://bugs.ghostscript.com/show_bug.cgi?id=696246 - should also be FS#46637
  patch -Np1 -i ${srcdir}/bug_696246.diff

  # force it to use system-libs
  # rm -rf jpeg libpng zlib jasper expat tiff lcms freetype 
  rm -rf jpeg libpng zlib jasper expat tiff lcms lcms2 freetype cups/libs # jbig2dec is in community

  autoreconf
}

build() {
  cd ghostscript-${pkgver}

  ./configure --prefix=/usr \
    --enable-dynamic \
    --with-ijs \
    --with-jbig2dec \
    --without-omni \
    --with-x \
    --with-drivers=ALL \
    --with-fontpath=/usr/share/fonts/Type1:/usr/share/fonts \
    --enable-fontconfig \
    --enable-freetype \
    --enable-openjpeg \
    --without-luratech \
    --with-system-libtiff \
    --with-libpaper \
    --disable-compile-inits \ #--help # needed for linking with system-zlib L

  export CFLAGS_STANDARD="${CFLAGS}"
  export SOC_LOADER="dxmainc.c"
  gmake

  cd ijs
  gsed -i "s:AM_PROG_CC_STDC:AC_PROG_CC:g" configure.ac
  ./autogen.sh
  ./configure --prefix=/usr --enable-shared --disable-static
  gmake
}

package() {
  cd ${srcdir}/ghostscript-${pkgver}
  
  gmake DESTDIR=${pkgdir} \
  cups_serverroot=${pkgdir}/etc/cups \
  cups_serverbin=${pkgdir}/usr/lib/cups install install-so

  # install missing doc files # http://bugs.archlinux.org/task/18023
  install -m 644 "${srcdir}"/ghostscript-${pkgver}/doc/{Ps2ps2.htm,gs-vms.hlp,gsdoc.el,pscet_status.txt} "${pkgdir}"/usr/share/ghostscript/$pkgver/doc/
  
  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # remove unwanted localized man-pages
  rm -rf "$pkgdir"/usr/share/man/[^man1]*

  # install IJS
  cd ijs
  gmake DESTDIR="${pkgdir}" install

  # remove filters that are now maintained in cups-filters as upstream home
  rm -rf "$pkgdir"/usr/lib/cups/filter/{gstopxl,gstoraster}

}
