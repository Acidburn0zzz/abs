# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=i3-wm
_pkgsourcename=i3
pkgver=4.7.2
pkgrel=3
pkgdesc="An improved dynamic tiling window manager"
arch=('i686' 'x86_64')
url="http://i3wm.org/"
license=('BSD')
groups=("i3")
depends=('libxcursor' 'xcb-util-keysyms' 'xcb-util-wm' 'libev' 'yajl' 'startup-notification' 'pango' 'perl')
makedepends=('xcb-util-cursor')
optdepends=('dmenu: As menu.'
            'i3lock: For locking your screen.'
            'i3status: To display systeminformation with a bar.')
options=('docs' '!strip')
port_patches=(
	patch-memory-leak
)
source=(http://i3wm.org/downloads/${_pkgsourcename}-${pkgver}.tar.bz2{,.asc}
	${port_patches[@]})
md5sums=('64141f7c23f97cd1e52c52918476c1c8'
         'SKIP'
         '336937747211e85b0aa155b3a515a692')

backup=('etc/i3/config')

prepare() {
  cd "$srcdir/$_pkgsourcename-$pkgver"

  for i in ${port_patches[@]}; do
    patch -p0 -i ${srcdir}/${i}
	done

  sed -i ''  -e 's|-Iinclude|-Iinclude -Isrc|g' \
			  -e 's|-liconv|${ICONV_LIB}|' \
			  -e 's|/usr/local|/usr|g' common.mk
}
build() {
  cd "$srcdir/$_pkgsourcename-$pkgver"
  gmake -j1
}

package() {
  cd "$srcdir/$_pkgsourcename-$pkgver"

  gmake DESTDIR="$pkgdir/" install
  
  install -dm755 ${pkgdir}/usr/share/man/man1
  for i in i3 \
           i3bar \
           i3-config-wizard \
           i3-input \
           i3-msg \
           i3-migrate-config-to-v4 \
           i3-nagbar \
           i3-dmenu-desktop \
           i3-dump-log \
           i3-sensible-{editor,pager,terminal}
  do
    install -m644 man/${i}.1 "${pkgdir}/usr/share/man/man1/${i}.1"
  done

  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}/
  install -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  
  gmake clean
}

# vim:set ts=2 sw=2 et:
