# $Id: PKGBUILD 174146 2013-01-03 05:00:13Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=soundtouch
pkgver=1.8.0
pkgrel=1
pkgdesc="An audio processing library"
arch=('i686' 'amd64')
url="http://www.surina.net/soundtouch/"
license=('LGPL')
depends=('gcc-libs')
options=('!libtool' 'libtoolfix')
source=(http://www.surina.net/soundtouch/${pkgname}-${pkgver}.tar.gz)
sha1sums=('2cee2c63194227b4a58da01a7cd92b8632f139cd')

build() {
  cd "${srcdir}/${pkgname}"
  [ "${CARCH}" = "x86_64" ] && CONFIGFLAG="--with-pic"
  gsed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADER/' configure.ac
  find . -type f | xargs sed -i '' -e 's/-fcheck-new//g'
  ./bootstrap
  ./configure --prefix=/usr --enable-shared ${CONFIGFLAG}
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" pkgdocdir=/usr/share/doc/soundtouch install
}
