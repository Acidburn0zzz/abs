# Port: databases/postgresql95-server
pkgbase=postgresql
pkgname=('postgresql-libs' 'postgresql')
pkgver=9.5.3
_majorver=${pkgver%.*}
pkgrel=1
arch=('i386' 'amd64')
url="http://www.postgresql.org/"
license=('custom:PostgreSQL')
makedepends=('libxml2' 'python2' 'perl' 'tcl>=8.6.0')
patchdir="postgresql-${pkgver}"
source=(http://ftp.postgresql.org/pub/source/v${pkgver}/postgresql-${pkgver}.tar.bz2
	postgresql-run-socket.patch
	postgresql.pam
	postgresql.logrotate
	postgresql.tmpfiles.conf 
	postgresql-check-db-dir
	postgresql.confd
	postgresql.initd
	postgresql.in
	dot.profile.in
	dot.cshrc.in)
sha256sums=('7385c01dc58acba8d7ac4e6ad42782bd7c0b59272862a3a3d5fe378d4503a0b4'
            '4048006e2ed116532bfd8703bf509cdb639233ad2f5bfb6cf94b9ee00523f7c1'
            '57dfd072fd7ef0018c6b0a798367aac1abb5979060ff3f9df22d1048bb71c0d5'
            '6abb842764bbed74ea4a269d24f1e73d1c0b1d8ecd6e2e6fb5fb10590298605e'
            '7e086d70e0dcaa6ce45693b4e826ce92d770192b3aff5642319b1ef279d88dc4'
            '3a3279d290f556bf7a362670e32b491794f47ed218f6b8c6acef366a3291f669'
            '5834d00e9b047a9ed77f07a0de4a8fe2c9fd92337cedb3682e24241036045c79'
            '52ba601718cf844dbd931900039aff4aa5f6282d0fd8521a12bddc2dee5899c8'
            'c6298230e5a42588b68f1c545a8956485a2324fd3c56eb463336fce0327281cd'
            'd0e01b018f3c0500817dc3a158f4695d088a6c49b54576b8c94858073fe7bc7d'
            '8fee9c4978811bd4ff18cc1ef267ab249ce972fd39c29387ff4f3a50f23d8594')

build() {
  cd "${srcdir}/postgresql-${pkgver}"

  ./configure --prefix=/usr \
  --mandir=/usr/share/man \
  --datadir=/usr/share/postgresql \
  --sysconfdir=/etc \
  --with-krb5 \
  --with-libxml \
  --with-openssl \
  --with-perl \
  --with-python PYTHON=/usr/bin/python2 \
  --with-tcl \
  --with-pam \
  --with-system-tzdata=/usr/share/zoneinfo \
  --enable-nls \
  --enable-thread-safety

  gmake world
}

package_postgresql-libs() {
  pkgdesc="Libraries for use with PostgreSQL"
  depends=('readline>=6.0' 'gettext-runtime')
  provides=('postgresql-client')
  conflicts=('postgresql-client')

  cd "${srcdir}/postgresql-${pkgver}"

  # install license
  install -dm755 "${pkgdir}/usr/share/licenses/postgresql-libs/"
  install -dm755 "${pkgdir}/usr/share/man/man1/"
  install -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/postgresql-libs/LICENSE"

  # install libs
  for dir in src/interfaces src/bin/pg_config src/bin/psql; do
    gmake -C ${dir} DESTDIR="${pkgdir}" install
  done

  install -m644 doc/src/sgml/man1/pg_config.1 "${pkgdir}/usr/share/man/man1/pg_config.1"
  install -m644 doc/src/sgml/man1/psql.1 "${pkgdir}/usr/share/man/man1/psql.1"

  cd src/include

  mkdir -p "${pkgdir}"/usr/include/{libpq,postgresql/internal/libpq}

# these headers are needed by the public headers of the interfaces
  install -m644 pg_config.h "${pkgdir}/usr/include/"
  install -m644 pg_config_os.h "${pkgdir}/usr/include/"
  install -m644 pg_config_ext.h "${pkgdir}/usr/include/"
  install -m644 postgres_ext.h "${pkgdir}/usr/include/"
  install -m644 libpq/libpq-fs.h "${pkgdir}/usr/include/libpq/"
  install -m644 pg_config_manual.h "${pkgdir}/usr/include/"

  # these headers are needed by the not-so-public headers of the interfaces
  install -m644 c.h "${pkgdir}/usr/include/postgresql/internal/"
  install -m644 port.h "${pkgdir}/usr/include/postgresql/internal/"
  install -m644 postgres_fe.h "${pkgdir}/usr/include/postgresql/internal/"
  install -m644 libpq/pqcomm.h "${pkgdir}/usr/include/postgresql/internal/libpq/"
}

package_postgresql() {
  pkgdesc="A sophisticated object-relational DBMS"
  backup=('etc/conf.d/postgresql' 'etc/pam.d/postgresql' 'etc/logrotate.d/postgresql')
  depends=("postgresql-libs>=${pkgver}" 'libxml2' 'readline>=6.0')
  optdepends=('python2: for PL/Python support'
              'perl: for PL/Perl support'
              'tcl: for PL/Tcl support')
  install=postgresql.install

  cd "${srcdir}/postgresql-${pkgver}"

  # install
  gmake DESTDIR="${pkgdir}" install
  gmake -C contrib DESTDIR="${pkgdir}" install
  gmake -C doc/src/sgml DESTDIR="${pkgdir}" install-man

  # we don't want these, they are in the -libs package
  for dir in src/interfaces src/bin/pg_config src/bin/psql; do
    gmake -C ${dir} DESTDIR="${pkgdir}" uninstall 
  done
  rm "${pkgdir}/usr/share/man/man1/pg_config.1"
  rm "${pkgdir}/usr/share/man/man1/psql.1"

  # install license
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgbase}/"
  install -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgbase}/LICENSE"

  # clean up unneeded installed items
  rm -rf "${pkgdir}/usr/include/postgresql/internal"
  rm -rf "${pkgdir}/usr/include/libpq"
  rm -rf "${pkgdir}/usr/include/pg_config.h"
  rm -rf "${pkgdir}/usr/include/pg_config_manual.h"
  rm -rf "${pkgdir}/usr/include/pg_config_os.h"
  rm -rf "${pkgdir}/usr/include/postgres_ext.h"
  rm -rf "${pkgdir}/usr/include/pg_config_ext.h" 
  install -dm755 "${pkgdir}/usr/lib/tmpfiles.d/"
  install -m644 "${srcdir}/postgresql.tmpfiles.conf" \
    "${pkgdir}/usr/lib/tmpfiles.d/postgresql.conf"

  install -m755 "${srcdir}/postgresql-check-db-dir" \
    "${pkgdir}/usr/bin/postgresql-check-db-dir"
  
  install -dm755 "${pkgdir}/etc/pam.d/"
  install -m644 "${srcdir}/postgresql.pam" \
    "${pkgdir}/etc/pam.d/postgresql"
  install -dm755 "${pkgdir}/etc/logrotate.d/"
  install -D -m644 "${srcdir}/postgresql.logrotate" \
    "${pkgdir}/etc/logrotate.d/postgresql"
  install -dm755 ${pkgdir}/etc/{profile.d,rc.d,init.d,conf.d}
  install -m755 ${srcdir}/postgresql.in ${pkgdir}/etc/rc.d/postgresql
  install -m755 ${srcdir}/postgresql.initd ${pkgdir}/etc/init.d/postgresql
  install -m755 ${srcdir}/postgresql.confd ${pkgdir}/etc/conf.d/postgresql

  install -m755 ${srcdir}/dot.cshrc.in ${pkgdir}/etc/profile.d/postgresql.csh
  install -m755 ${srcdir}/dot.profile.in ${pkgdir}/etc/profile.d/postgresql.sh
}
