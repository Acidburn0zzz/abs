# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

_pkgname=libxslt
pkgname=python2-libxslt
pkgver=1.1.28
pkgrel=5
pkgdesc="Python interface for XML parser library for GNOME"
arch=(i686 x86_64)
license=('custom')
depends=('python2')
makedepends=('python2-distribute' 'libxslt')
url="http://www.xmlsoft.org/"
port_patches=(
        patch-aa
        patch-ab
        patch-configure
        patch-python__Makefile.in
        patch-python__tests__Makefile.in
)
source=(ftp://xmlsoft.org/libxslt/${_pkgname}-${pkgver}.tar.gz
	${port_patches[@]})
md5sums=('9667bf6f9310b957254fdcf6596600b7'
         '7976ea0c62cd1e65a2a3251fc04528db'
         '8d939abdc8c0d66db7a8f34ae3edac9b'
         'c5efab6f0e502389aef4ae40de950b57'
         'bced8181aee93682f5d6f8aaa182fd53'
         'caef09cdd3107b75324b5e3ce58232b4')

prepare() {
  cd ${_pkgname}-${pkgver}

  for i in ${port_patches[@]}; do
    patch -p0 -i "${srcdir}/$i"
  done

  sed -i '' -e '/^install-data-am:/ s|install-data-local||' \
      doc/Makefile.in
  sed -i '' -e 's|[$](bindir)/xsltproc|../xsltproc/xsltproc|g ; s|[$](bindir)/xmllint|../xmllint/xmllint|g' \
      doc/Makefile.in

}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  ./configure  --prefix=/usr \
		--with-iconv=/usr/local \
		--with-html-dir=/usr/share/doc \
		--with-html-subdir=${_pkgname} \
		--with-python=/usr/bin/python2 \
		

  cd "${srcdir}/${_pkgname}-${pkgver}/python"
  gmake
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/python/"
  gmake DESTDIR="${pkgdir}" install
}
