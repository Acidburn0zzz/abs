pkgname=loudmouth
pkgver=1.4.3
pkgrel=6
pkgdesc="A lightweight Jabber client library written in C/Glib"
arch=('i386' 'amd64')
license=('LGPL')
url="http://groups.google.com/group/loudmouth-dev"
depends=('glib2' 'gnutls' 'libidn')
options=('libtoolfix')
makedepends=('intltool' 'pkg-config' 'gtk-doc')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/1.4/${pkgname}-${pkgver}.tar.bz2
        01-fix-sasl-md5-digest-uri.patch
        03-drop-stanzas-on-fail.patch
        04-use-pkg-config-for-gnutls.patch
        08-fix-ipv6-connect.patch
        gnutls-3.4.0.patch
	patch-loudmouth_Makefile.in)
md5sums=('55339ca42494690c3942ee1465a96937'
         'dc799cea18b24847b1e008c7424010a3'
         'b7b2d81b01a5eee5fd5e21cae67b4af7'
         'bffb25b9551df43255fe1706588582f3'
         '8e3071299776d32b9be27bba7d3f9ae0'
         '868f08345d9c7a42f19290d8caf8aff1'
         '7f1e8b4d5548ca2fd246f841a72299e2')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ../01-fix-sasl-md5-digest-uri.patch
  patch -Np1 -i ../03-drop-stanzas-on-fail.patch
  patch -Np1 -i ../04-use-pkg-config-for-gnutls.patch
  patch -Np1 -i ../08-fix-ipv6-connect.patch
  patch -Np0 -i ../gnutls-3.4.0.patch
  libtoolize --force
  aclocal
  autoconf
  automake --add-missing

  patch -p0 -i ../patch-loudmouth_Makefile.in
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --disable-static \
	LDFLAGS="${LDFLAGS} -Wl,-Bsymbolic"
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
}
