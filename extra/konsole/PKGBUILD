pkgname=konsole
pkgver=15.08.2
pkgrel=1
arch=('i386' 'amd64')
url='http://kde.org/applications/system/konsole/'
pkgdesc="Terminal"
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdebase')
depends=('kdelibs4support' 'knotifyconfig' 'kpty')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
replaces=('kdebase-konsole')
conflicts=('kdebase-konsole<4.14.3-2')
source=("http://download.kde.org/stable/applications/${pkgver}/src/konsole-${pkgver}.tar.xz")
sha1sums=('33bd19c2f460a391dabc0fa718f4a9286a514f07')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../konsole-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  gmake
}

package() {
  cd "${srcdir}"/build
  gmake DESTDIR="${pkgdir}" install
}

