# $Id: PKGBUILD 191708 2013-07-29 10:16:38Z bpiotrowski $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xdg-utils
pkgver=1.1.0.git20150323
pkgrel=2
pkgdesc="Command line tools that assist applications with a variety of desktop integration tasks"
arch=('any')
url="http://portland.freedesktop.org/"
license=('MIT')
depends=('bash' 'xorg-xset') # xset needed inside xdg-screensaver
makedepends=('docbook-xsl' 'lynx' 'xmlto' 'git')
optdepends=('kdebase-runtime: for KDE support in xdg-open'
            'libgnome: for GNOME support in xdg-open'
            'exo: for Xfce support in xdg-open'
            'xorg-xprop: for Xfce support in xdg-open'
            'pcmanfm: for LXDE support in xdg-open'
            'perl-file-mimeinfo: for generic support in xdg-open')
patchdir=(xdg-utils)
source=(git://anongit.freedesktop.org/xdg/xdg-utils#commit=686f6f48f6ff939c757881e3fc7a852b61d32c94)
md5sums=('SKIP')

pkgver() {
	cd xdg-utils
	echo "1.1.0.git$(git log -1 --format="%cd" --date=short | sed 's|-||g')"
}

prepare() {
  cd xdg-utils

  find . -type f | xargs sed -i '' -e 's|readlink|stat|g'

}
build() {
	cd xdg-utils
	./configure --prefix=/usr --mandir=/usr/share/man
	gmake # for release builds
}

package() {
	cd xdg-utils
	gmake DESTDIR="${pkgdir}" install
	install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	
	# install empty directory to prevent xdg-desktop-menu install *.desktop to fail, see FS#33316
	install -dm755 $pkgdir/usr/share/desktop-directories
}
