# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=python
pkgver=3.3.3
pkgrel=2
_pybasever=3.3
pkgdesc="Next generation of the python high-level scripting language"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.python.org/"
depends=('expat' 'gdbm' 'libffi')
makedepends=('tk>=8.6.0' 'sqlite' 'valgrind' 'bluez')
makedepends=('tk>=8.6.0' 'sqlite'                   )
optdepends=('tk: for tkinter (not yet supported)' 'sqlite')
provides=('python3')
replaces=('python3')
options=('!makeflags')
# IMPORTANT: The Makefile.pre.in patch fiddles with cflags, this seems mostly ports specific.
#            Also changes some directories and disables a few things we want to keep.
# IMPORTANT: The patch-setup.py patch from ports disables the installation of 2to3.
#            (For some reason we did get a dead 2to3 symlink though...)
#            When you pull in new ports patches, make sure you fix them!
# IMPORTANT: The libffi-configure patch adds amd64-*-freebsd, but we also need x86_64-*-freebsd!
#            When you pull in new ports patches, fix that!
#_port_patches=(patch-Makefile.pre.in
_port_patches=(patch-Modules-_ctypes-libffi-configure
               patch-Modules-fcntlmodule.c
               patch-Modules___ctypes__libffi__src__arm__ffi.c
               patch-setup.py)
source=(http://www.python.org/ftp/python/${pkgver%rc*}/Python-${pkgver}.tar.xz
        "${_port_patches[@]}")
sha1sums=('af4e75a34bd538c79b9871227c2e7f56569ac107'
          '77ce91534e5896443298197e8f45e2b4e5eb2c87'
          '0e524fae81a19b5c19c63fcc986fa6d191616322'
          '43e3f747b97a24fe8d679d95711eb91c4be2d6e2'
          'c09cb1a97bb7a4c6a379f121606cc0411931e938')

build() {
  cd "${srcdir}/Python-${pkgver}"

  cp -r Lib/plat-freebsd8 Lib/plat-freebsd9
  cp -r Lib/plat-freebsd8 Lib/plat-freebsd10

  for i in "${_port_patches[@]}"; do
    msg "patch $i"
    patch -p0 -i "${srcdir}/$i"
  done

  # Ensure that we are using the system copy of various libraries (expat, zlib and libffi),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*

  LDFLAGS="$LDFLAGS -L. -pthread" \
  CFLAGS="$CFLAGS -pthread" \
  ./configure --prefix=/usr \
              --enable-shared \
              --with-threads \
              --with-computed-gotos \
              --enable-ipv6 \
              --without-valgrind \
              --with-system-expat \
              --with-dbmliborder=gdbm:ndbm \
              --with-system-ffi

  gmake
}

package() {
  cd "${srcdir}/Python-${pkgver}"
  gmake DESTDIR="${pkgdir}" install maninstall

  # Why are these not done by default...
  ln -sf python3               "${pkgdir}/usr/bin/python"
  ln -sf python3-config        "${pkgdir}/usr/bin/python-config"
  ln -sf idle3                 "${pkgdir}/usr/bin/idle"
  ln -sf pydoc3                "${pkgdir}/usr/bin/pydoc"
  ln -sf python${_pybasever}.1 "${pkgdir}/usr/share/man/man1/python3.1"

  # Fix FS#22552
  ln -sf ../../libpython${_pybasever}m.so \
    "${pkgdir}/usr/lib/python${_pybasever}/config-${_pybasever}m/libpython${_pybasever}m.so"

  # Clean-up reference to build directory
  sed -i '' -e "s|$srcdir/Python-${pkgver}:||" "$pkgdir/usr/lib/python${_pybasever}/config-${_pybasever}m/Makefile"

  # License
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
