# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=python
pkgver=3.4.1
pkgrel=3
_pybasever=3.4
pkgdesc="Next generation of the python high-level scripting language"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.python.org/"
depends=('expat' 'gdbm>=1.11' 'libffi')
makedepends=('tk>=8.6.0' 'sqlite' 'valgrind' 'bluez-libs' 'mpdecimal')
makedepends=('tk>=8.6.0' 'sqlite'                         'mpdecimal')
optdepends=('python-setuptools'
            'python-pip'
            'sqlite'
            'mpdecimal: for decimal'
            'tk: for tkinter')
options=('!makeflags')
provides=('python3')
replaces=('python3')
# IMPORTANT: The Makefile.pre.in patch fiddles with cflags, this seems mostly ports specific.
#            Also changes some directories and disables a few things we want to keep.
# IMPORTANT: The libffi-configure patch adds amd64-*-freebsd, but we also need x86_64-*-freebsd!
#            When you pull in new ports patches, fix that!
#_port_patches=(patch-Makefile.pre.in
_port_patches=(
        patch-Modules__fcntlmodule.c
        patch-Lib__distutils__unixccompiler.py)
source=(http://www.python.org/ftp/python/${pkgver%rc*}/Python-${pkgver}.tar.xz
        "${_port_patches[@]}")
sha1sums=('143e098efe7ee7bec8a4904ec4b322f28a067a03'
          'ee116a786ef23d4225e87c9efd637831c5a1e102'
          'f40cc94639e7725381eb7bfd840eb5714d5be9a1')

prepare() {
  cd Python-${pkgver}

  # FS#23997
  sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python|" Lib/cgi.py

  # Ensure that we are using the system copy of various libraries (expat, zlib, libffi, and libmpdec),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*
  rm -r Modules/_decimal/libmpdec

  # FreeBSD:
  for i in "${_port_patches[@]}"; do
    msg "patch $i"
    patch -p0 -i "${srcdir}/$i"
  done
  cp -r Lib/plat-freebsd8 Lib/plat-freebsd9
  cp -r Lib/plat-freebsd8 Lib/plat-freebsd10
}

build() {
  cd "${srcdir}/Python-${pkgver}"


  LDFLAGS="$LDFLAGS -L. -pthread" \
  CFLAGS="$CFLAGS -pthread" \
  ./configure --prefix=/usr \
              --enable-shared \
              --with-threads \
              --with-computed-gotos \
              --enable-ipv6 \
              --without-valgrind \
              --with-system-expat \
              --with-dbmliborder=gdbm:ndbm \
              --with-system-ffi \
              --without-ensurepip

  gmake
}

package() {
  cd "${srcdir}/Python-${pkgver}"
  gmake DESTDIR="${pkgdir}" install maninstall

  # Why are these not done by default...
  ln -sf python3               "${pkgdir}/usr/bin/python"
  ln -sf python3-config        "${pkgdir}/usr/bin/python-config"
  ln -sf idle3                 "${pkgdir}/usr/bin/idle"
  ln -sf pydoc3                "${pkgdir}/usr/bin/pydoc"
  ln -sf python${_pybasever}.1 "${pkgdir}/usr/share/man/man1/python.1"

  # Fix FS#22552
  ln -sf ../../libpython${_pybasever}m.so \
    "${pkgdir}/usr/lib/python${_pybasever}/config-${_pybasever}m/libpython${_pybasever}m.so"

  # Clean-up reference to build directory
  gsed -i "s|$srcdir/Python-${pkgver}:||" "$pkgdir/usr/lib/python${_pybasever}/config-${_pybasever}m/Makefile"

  # License
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
