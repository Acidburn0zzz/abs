# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=python
pkgver=3.5.0
pkgrel=1
_pybasever=3.5
pkgdesc="Next generation of the python high-level scripting language"
arch=('i386' 'amd64')
license=('custom')
url="http://www.python.org/"
depends=('expat' 'gdbm>=1.11' 'libffi' 'readline')
optdepends=('python-setuptools'
            'python-pip')
options=('!makeflags')
provides=('python3')
replaces=('python3')
# IMPORTANT: The Makefile.pre.in patch fiddles with cflags, this seems mostly ports specific.
#            Also changes some directories and disables a few things we want to keep.
# IMPORTANT: The libffi-configure patch adds amd64-*-freebsd, but we also need x86_64-*-freebsd!
#            When you pull in new ports patches, fix that!
#_port_patches=(patch-Makefile.pre.in
patchdir=(Python-${pkgver})
source=("http://www.python.org/ftp/python/${pkgver%rc*}/Python-${pkgver}.tar.xz"
        test_gdb-version-fix.patch
        dont-make-libpython-readonly.patch
        issue25150.patch)

prepare() {
  cd Python-${pkgver}

  # https://bugs.python.org/issue25096
  patch -p1 -i ../test_gdb-version-fix.patch

  # FS#45809
  patch -p1 -i ../dont-make-libpython-readonly.patch

  # https://bugs.python.org/issue25150
  patch -p1 -i ../issue25150.patch

  # FS#23997
  sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python|" Lib/cgi.py

  # Ensure that we are using the system copy of various libraries (expat, zlib, libffi, and libmpdec),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*
  rm -r Modules/_decimal/libmpdec

  # FreeBSD:
  cp -r Lib/plat-freebsd8 Lib/plat-freebsd9
  cp -r Lib/plat-freebsd8 Lib/plat-freebsd10
}

build() {
  cd "${srcdir}/Python-${pkgver}"
  DISABLED_EXTENSIONS="_sqlite3 _tkinter _gdbm"
  LDFLAGS="$LDFLAGS -L. -pthread" \
  CFLAGS="$CFLAGS -pthread" \

  ./configure --prefix=/usr \
              --enable-shared \
              --with-threads \
              --with-computed-gotos \
              --enable-ipv6 \
              --without-valgrind \
              --with-system-expat \
              --with-dbmliborder=gdbm:ndbm \
              --with-system-ffi \
              --without-ensurepip \
	      DISABLED_EXTENSIONS="${DISABLED_EXTENSIONS}" \
	      --without-ensurepip

  gmake
}

package() {
  cd "${srcdir}/Python-${pkgver}"
  gmake DESTDIR="${pkgdir}" install maninstall

  # Why are these not done by default...
  ln -sf python3               "${pkgdir}/usr/bin/python"
  ln -sf python3-config        "${pkgdir}/usr/bin/python-config"
  ln -sf idle3                 "${pkgdir}/usr/bin/idle"
  ln -sf pydoc3                "${pkgdir}/usr/bin/pydoc"
  ln -sf python${_pybasever}.1 "${pkgdir}/usr/share/man/man1/python.1"

  # Fix FS#22552
  ln -sf ../../libpython${_pybasever}m.so \
    "${pkgdir}/usr/lib/python${_pybasever}/config-${_pybasever}m/libpython${_pybasever}m.so"

  # some useful "stuff" FS#46146
  install -dm755 "${pkgdir}"/usr/lib/python${_pybasever}/Tools/{i18n,scripts}
  install -m755 Tools/i18n/{msgfmt,pygettext}.py "${pkgdir}"/usr/lib/python${_pybasever}/Tools/i18n/
  install -m755 Tools/scripts/{README,*py} "${pkgdir}"/usr/lib/python${_pybasever}/Tools/scripts/

  # Clean-up reference to build directory
  gsed -i "s|$srcdir/Python-${pkgver}:||" "$pkgdir/usr/lib/python${_pybasever}/config-${_pybasever}m/Makefile"

  # License
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
md5sums=('d149d2812f10cbe04c042232e7964171'
         '24582c7f66a6482f61269d95ecf9d5b6'
         '60c9f7d02384f22834df06de5eb9c46a'
         'f12282f251ef1b27fe3214b64c6914a6')
