# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=python
pkgver=3.3.2
pkgrel=1
_pybasever=3.3
pkgdesc="Next generation of the python high-level scripting language"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.python.org/"
depends=('expat' 'gdbm' 'libffi')
makedepends=('tk>=8.6.0' 'sqlite' 'valgrind' 'bluez')
makedepends=('tk>=8.6.0' 'sqlite'                   )
optdepends=('tk: for tkinter (not yet supported)' 'sqlite')
provides=('python3')
replaces=('python3')
options=('!makeflags')
source=(http://www.python.org/ftp/python/${pkgver%rc*}/Python-${pkgver}.tar.xz
        patch-Modules-_ctypes-libffi-configure
        patch-Modules-_decimal-libmpdec-mpdecimal.h
        patch-Modules-fcntlmodule.c
        patch-setup.py
)
sha1sums=('87009d0c156c6e1354dfec5c98c328cae93950ad'
          'e6a2d2af27020caed316527705d98e63d96d5d8c'
          '2293abd864b53cad74dc084d9fd0c0b60f8fc45e'
          '0e524fae81a19b5c19c63fcc986fa6d191616322'
          '17b82bdc3bb978378b07ea5090ec3c632a0ec42d')

build() {
  cd "${srcdir}/Python-${pkgver}"

  cp -r Lib/plat-freebsd8 Lib/plat-freebsd9
  cp -r Lib/plat-freebsd8 Lib/plat-freebsd10

  for i in patch-Modules-_ctypes-libffi-configure \
           patch-Modules-_decimal-libmpdec-mpdecimal.h \
           patch-Modules-fcntlmodule.c \
           patch-setup.py
  do
    msg "patch $i"
    patch -p0 -i "${srcdir}/$i"
  done

  # Ensure that we are using the system copy of various libraries (expat, zlib and libffi),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*

  LDFLAGS="$LDFLAGS -L. -pthread" \
  CFLAGS="$CFLAGS -pthread" \
  ./configure --prefix=/usr \
              --enable-shared \
              --with-threads \
              --with-computed-gotos \
              --enable-ipv6 \
              --without-valgrind \
              --with-system-expat \
              --with-dbmliborder=gdbm:ndbm \
              --with-system-ffi

  make
}

package() {
  cd "${srcdir}/Python-${pkgver}"
  make DESTDIR="${pkgdir}" install maninstall

  # Why are these not done by default...
  ln -sf python3               "${pkgdir}/usr/bin/python"
  ln -sf python3-config        "${pkgdir}/usr/bin/python-config"
  ln -sf idle3                 "${pkgdir}/usr/bin/idle"
  ln -sf pydoc3                "${pkgdir}/usr/bin/pydoc"
  ln -sf python${_pybasever}.1 "${pkgdir}/usr/share/man/man1/python3.1"

  # Fix FS#22552
  ln -sf ../../libpython${_pybasever}m.so \
    "${pkgdir}/usr/lib/python${_pybasever}/config-${_pybasever}m/libpython${_pybasever}m.so"

  # Clean-up reference to build directory
  sed -i '' -e "s|$srcdir/Python-${pkgver}:||" "$pkgdir/usr/lib/python${_pybasever}/config-${_pybasever}m/Makefile"

  # License
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
