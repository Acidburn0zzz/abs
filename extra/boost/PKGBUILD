pkgbase=boost
pkgname=('boost-libs' 'boost')
pkgver=1.59.0
_boostver=${pkgver//./_}
pkgrel=3
url="http://www.boost.org/"
arch=('i386' 'amd64')
license=('custom')
makedepends=('icu>=51.1' 'python' 'python2' 'openmpi')
patchdir=(${pkgbase}_${_boostver})
source=(https://downloads.sourceforge.net/project/${pkgbase}/${pkgbase}/${pkgver}/${pkgbase}_${_boostver}.tar.bz2
        0001-Python-Fix-condition-for-make_setter-overload.patch)
sha1sums=('b94de47108b2cdb0f931833a7a9834c2dd3ca46e'
          'bd76d969b6f8fbe95f05fba13b57a001764c216c')

prepare() {
   cd ${pkgbase}_${_boostver}

   # https://github.com/boostorg/python/issues/39
   patch -Np2 -i ../0001-Python-Fix-condition-for-make_setter-overload.patch
}

export _stagedir="${srcdir}/stagedir"

build() {
   export _stagedir="${srcdir}/stagedir"
   local JOBS="$(gsed -e 's/.*\(-j *[0-9]\+\).*/\1/' <<< ${MAKEFLAGS})"

   cd ${pkgbase}_${_boostver}

   ./bootstrap.sh --with-toolset=clang cxxflags="-stdlib=libc++" linkflags="-stdlib=libc++" \
       --with-icu --with-python=/usr/bin/python2

   _bindir="bin.freebsdx86"
   [ "${CARCH}" = "amd64" ] && _bindir="bin.freebsdx86_64"
   install -dm755 "${_stagedir}"/bin
   install -Dm755 tools/build/src/engine/$_bindir/b2 "${_stagedir}"/bin/b2

   # Add an extra python version. This does not replace anything and python 2.x
   # need to be the default.
   echo "using python : 3.5 : /usr/bin/python3 : /usr/include/python3.5m : /usr/lib ;" \
      >> project-config.jam

   # Support for OpenMPI
   echo "using mpi ;" >> project-config.jam

   # boostbook is needed by quickbook
   install -dm755 "${_stagedir}"/share/boostbook
   cp -a tools/boostbook/{xsl,dtd} "${_stagedir}"/share/boostbook/

   # default "minimal" install: "release link=shared,static
   # runtime-link=shared threading=single,multi"
   # --layout=tagged will add the "-mt" suffix for multithreaded libraries
   # and installs includes in /usr/include/boost.
   # --layout=system no longer adds the -mt suffix for multi-threaded libs.
   # install to ${_stagedir} in preparation for split packaging
   "${_stagedir}"/bin/b2 \
      toolset=clang \
      cxxflags="-stdlib=libc++" \
      linkflags="-stdlib=libc++" \
      variant=release \
      debug-symbols=off \
      threading=multi \
      runtime-link=shared \
      link=shared,static \
      python=2.7 \
      cflags="${CPPFLAGS} ${CFLAGS} -O3" linkflags="${LDFLAGS}" \
      --layout=system \
      --prefix="${_stagedir}" \
      ${JOBS} \
      install
}

package_boost() {
   pkgdesc='Free peer-reviewed portable C++ source libraries - development headers'
   depends=("boost-libs=${pkgver}")
   optdepends=('python: for python bindings'
               'python2: for python2 bindings'
               'boost-build: to use boost jam for building your project.')
   options=('staticlibs')

   install -dm755 "${pkgdir}"/usr
   cp -RP "${_stagedir}"/{bin,include,share} "${pkgdir}"/usr

   install -d "${pkgdir}"/usr/lib
   cp -RP "${_stagedir}"/lib/*.a "${pkgdir}"/usr/lib/

   install -d -m755 "${pkgdir}"/usr/share/licenses/boost
   install -m644 "${srcdir}/"${pkgbase}_${_boostver}/LICENSE_1_0.txt \
      "${pkgdir}"/usr/share/licenses/boost/LICENSE_1_0.txt

   ln -s /usr/bin/b2 "$pkgdir"/usr/bin/bjam
}

package_boost-libs() {
   pkgdesc="Free peer-reviewed portable C++ source libraries - Runtime"
   depends=('icu>=51.1')
   optdepends=('openmpi: for mpi support')
   export _stagedir="${srcdir}/stagedir"
   install -d -m 755 "${pkgdir}"/usr
   cp -RP "${_stagedir}"/lib "${pkgdir}"/usr

   install -d -m755 "${pkgdir}"/usr/share/licenses/boost-libs
   install -m 644 "${srcdir}/"${pkgbase}_${_boostver}/LICENSE_1_0.txt \
      "${pkgdir}"/usr/share/licenses/boost-libs/LICENSE_1_0.txt

  rm "${pkgdir}"/usr/lib/*.a

  cd ${pkgdir}/usr/lib
  for l in lib*.so.*; do ln -sf ${l} ${l%.*.*}; done
}
