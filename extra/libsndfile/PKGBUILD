# Port: audio/libsndfile
pkgname=libsndfile
pkgver=1.0.26
pkgrel=1
pkgdesc="A C library for reading and writing files containing sampled sound"
arch=('i386' 'amd64')
url="http://www.mega-nerd.com/libsndfile"
license=('LGPL')
depends=('flac' 'libvorbis')
checkdepends=('python2')
source=(http://www.mega-nerd.com/libsndfile/files/${pkgname}-${pkgver}.tar.gz{,.asc})
sha1sums=('c9092c06f6ed51034c9e809e150083ff1f55e79e'
          'SKIP')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gsed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' src/binheader_writef_check.py \
    src/create_symbols_file.py programs/test-sndfile-metadata-set.py

  ./configure --prefix=/usr \
    --disable-gcc-pipe \
    --disable-sqlite \
    --disable-alsa \
    --disable-octave
  gmake
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
}
