# $Id: PKGBUILD 88660 2013-04-21 13:06:07Z arodseth $
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Bartłomiej Piotrowski <barthalion@gmail.com>
# Contributor: Larry Hajali <larryhaja[at]gmail[dot]com>
# Contributor: Hubert Grzeskowiak <arch at nemesis13 dot de>

pkgname=tinyxml
pkgver=2.6.2
pkgrel=2
pkgdesc='Simple, small, C++ XML parser that can be easily integrated into other programs'
url="http://www.grinninglizard.com/tinyxml/"
arch=('x86_64' 'i686')
license=('zlib')
makedepends=('gmake' 'gcc')
_port_patches=(
               'patch-tinyxml.h'
               )
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}_${pkgver//./_}.tar.gz"
        'entity.patch'
        "$pkgname.pc"
        ${_port_patches[@]})
sha256sums=('15bdfdcec58a7da30adc87ac2b078e4417dbe5392f3afb719f9ba6d062645593'
            'ef493209b0a51160171fd834a7ecdddd02679463b85fb89a2ea254213e47f99b'
            '0e37a568eaebb8f0644f148a3d3efa921ac518217e1c0de46c2859d26516bcc4'
            '76a5abffb501f87b90ebb20406120ee7d34c091c7746cac9334e4e4807183052')

prepare() {
  cd "$srcdir/$pkgname"

  gmake clean

  for _p in ${_port_patches[@]}; do
    patch -p0 -i "$srcdir/$_p"
  done

  # Fix entity encoding.
  patch -p0 -i "$srcdir"/entity.patch

  # Fix Makefile
  #setconf Makefile TINYXML_USE_STL YES
  #setconf Makefile RELEASE_CFLAGS "$CXXFLAGS -fPIC"
  sed -i '' -e "s|^TINYXML_USE_STL.*|TINYXML_USE_STL := YES|" \
            -e "s|^RELEASE_CFLAGS.*|RELEASE_CFLAGS := $CXXFLAGS -fPIC|" \
            -e "s|^RELEASE_LDFLAGS.*|RELEASE_LDFLAGS := $LDFLAGS -fPIC|" \
            Makefile
}

build() {
  cd "$srcdir/$pkgname"

  gmake
  g++ -fPIC "$CXXFLAGS" -shared -o "lib${pkgname}.so.0.$pkgver" \
      -Wl,-soname,"lib${pkgname}.so.0" $(ls *.o | grep -v xmltest)
}

package() {
  cd "$srcdir/$pkgname"

  install -dm 0755 "$pkgdir"/usr/{lib,include}
  install -m 0755 "lib${pkgname}.so.0.$pkgver" "$pkgdir"/usr/lib/
  install -m 0644 "$pkgname.h" tinystr.h "$pkgdir"/usr/include

  # License-files
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname/"
  install -m644 readme.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # pkgconfig-file
  install -dm755 "$pkgdir/usr/lib/pkgconfig"
  install -m644 "$srcdir/$pkgname.pc" "$pkgdir/usr/lib/pkgconfig/$pkgname.pc"

  (cd "$pkgdir/usr/lib"
    ln -s "lib${pkgname}.so.0.$pkgver" "lib${pkgname}.so.0"
    ln -s "lib${pkgname}.so.0.$pkgver" "lib${pkgname}.so"
  )
}

# vim:set ts=2 sw=2 et:
