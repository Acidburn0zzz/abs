# Port: audio/faad
pkgname=faad2
pkgver=2.7
pkgrel=1
pkgdesc="ISO AAC audio decoder"
arch=('i386' 'amd64')
url="http://www.audiocoding.com/"
license=('custom:GPL')
options=('!makeflags' '!gnu_configure')
makedepends=('gmake')
source=(http://downloads.sourceforge.net/sourceforge/faac/${pkgname}-${pkgver}.tar.bz2 LICENSE)
sha1sums=('b0e80481d27ae9faf9e46c8c8dfb617a0adb91b5'
          '00edd9b30d8d5098b5e91c03c595d96a3bbe56fd')

prepare() {

	cd "${srcdir}/${pkgname}-${pkgver}"

        autoreconf -vfi
        gnu_configure

	sed -i '' -e '/AC_CONFIG_FILES(plugins/d' \
		configure.in

	sed -i '' -e 's|^AM_CFLAGS|AM_CPPFLAGS|g'  \
		  -e 's|-iquote |-I|g' libfaad/Makefile.am

	sed -i '' -e 's|= -DUSE_|= -fPIC -DPIC -DUSE_|g' \
		common/mp4ff/Makefile.am

	echo "SUBDIRS = libfaad common frontend" > Makefile.am

}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	export LDFLAGS+=" -lpthread"

	./configure --prefix=/usr

	gmake CFLAGS="${CFLAGS} -fPIC"
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	gmake DESTDIR="${pkgdir}" manmdir=/usr/share/man/man1 install

	mv "${pkgdir}/usr/share/man/man1/faad.man" "${pkgdir}/usr/share/man/man1/faad.1"
	install -dm755 "${pkgdir}/usr/include"
	install -m644 common/mp4ff/mp4ff_int_types.h "${pkgdir}/usr/include/mp4ff_int_types.h"
	install -dm755  "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m644 "${srcdir}/LICENSE"  "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
