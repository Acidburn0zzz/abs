# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: xduugu
# Contributor: Elis Hughes <elishughes@googlemail.com>

pkgname=rtmpdump
pkgver=20140918
pkgrel=7
pkgdesc="A tool to download rtmp streams"
arch=('i386' 'amd64')
url="http://rtmpdump.mplayerhq.hu/"
license=('GPL2' 'LGPL2.1')
makedepends=('git')
options=(!makeflags)
#git://git.ffmpeg.org/rtmpdump 
source=(git://git.ffmpeg.org/rtmpdump#commit=a1900c3)
md5sums=('SKIP')

build() {
	cd "$pkgname"
	sed -i '' \
	    -e 's|^CC|#&|' \
	    -e 's|^LD|#&|' \
	    -e 's|^THREADLIB=.*|THREADLIB=-pthread|' \
	    -e 's|^REQ_OPENSSL=|#&|' \
	    Makefile librtmp/Makefile
	make
}

package() {
	cd "$pkgname"
	make prefix=/usr mandir=/usr/share/man DESTDIR="$pkgdir" install

	sed -i '' -e '/^Libs:/s/$/ -lssl -lcrypto/' "${pkgdir}/usr/lib/pkgconfig/librtmp.pc"
}
