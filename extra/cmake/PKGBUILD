# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=cmake
pkgver=3.0.2
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.cmake.org/"
license=('custom')
depends=('curl' 'shared-mime-info' 'gcc-libs' 'freebsd-world')
makedepends=('expat')
options=('!docs')
source=("http://www.cmake.org/files/v3.0/${pkgname}-${pkgver}.tar.gz"
	InitialCache.cmake)
md5sums=('db4c687a31444a929d2fdc36c4dfb95f'
         '2a34f2de8c09eef51bc54b8d3c32e263')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  ./bootstrap --prefix=/usr \
    --mandir=/share/man \
    --docdir=/share/doc/cmake \
    --system-libs \
    --parallel=$(/usr/bin/getconf NPROCESSORS_ONLN) \
    --init="${srcdir}/InitialCache.cmake"

  gmake
}

package() {
  pkgname="cmake"
  pkgdesc="A cross-platform open-source make system"
  install="${pkgname}.install"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  vimpath="${pkgdir}/usr/share/vim/vimfiles"
  install -d "${vimpath}"/{help,indent,syntax}
  ln -s /usr/share/cmake-3.0/editors/vim/cmake-help.vim "${vimpath}"/help/
  ln -s /usr/share/cmake-3.0/editors/vim/cmake-indent.vim "${vimpath}"/indent/
  ln -s /usr/share/cmake-3.0/editors/vim/cmake-syntax.vim "${vimpath}"/syntax/

  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}-${pkgver}
  install -m644 Copyright.txt "${pkgdir}"/usr/share/licenses/${pkgname}-${pkgver}/LICENSE
}
