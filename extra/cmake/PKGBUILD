pkgname=cmake
pkgver=3.3.2
pkgrel=2
arch=('amd64')
url="http://www.cmake.org/"
license=('custom')
depends=('curl' 'shared-mime-info' 'gcc-libs' 'freebsd-world' 'jsoncpp')
makedepends=('expat')
options=('!docs')
source=("http://www.cmake.org/files/v${pkgver%.*}/${pkgname}-${pkgver}.tar.gz" 'kdelibs-4.14.11.patch')
md5sums=('5febbd11bcaac854a27eebaf4a124be2'
         '1eeec67ab4bff6d92430d52acdbde142')

prepare() {
  cd ${pkgname}-${pkgver}

# fix build of KDE4 applications with kdelibs 4.14.11 http://www.cmake.org/gitweb?p=cmake.git;a=commit;h=b9ec9392da21a3421e48c6961976060d872faffb
  patch -p1 -i ../kdelibs-4.14.11.patch
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  ./bootstrap --prefix=/usr \
    --mandir=/share/man \
    --docdir=/share/doc/cmake \
    --system-libs \
    --parallel=$(/usr/bin/getconf NPROCESSORS_ONLN)

  gmake
}

package() {
  pkgname="cmake"
  pkgdesc="A cross-platform open-source make system"
  install="${pkgname}.install"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  vimpath="${pkgdir}/usr/share/vim/vimfiles"
  install -d "${vimpath}"/{help,indent,syntax}
  ln -s /usr/share/cmake-${pkgver%.*}/editors/vim/cmake-help.vim "${vimpath}"/help/
  ln -s /usr/share/cmake-${pkgver%.*}/editors/vim/cmake-indent.vim "${vimpath}"/indent/
  ln -s /usr/share/cmake-${pkgver%.*}/editors/vim/cmake-syntax.vim "${vimpath}"/syntax/

  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}-${pkgver}
  install -m644 Copyright.txt "${pkgdir}"/usr/share/licenses/${pkgname}-${pkgver}/LICENSE
}
