# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgname=ilmbase
pkgver=2.2.0
pkgrel=4
pkgdesc="Base libraries from ILM for OpenEXR"
arch=('i386' 'amd64')
url="http://www.openexr.com"
license=('custom')
depends=('gcc-libs')
makedepends=('cmake')
source=(http://download.savannah.nongnu.org/releases/openexr/$pkgname-$pkgver.tar.gz
        ilmbase-2.0.1-no_undefined.patch
        ilmbase-1.0.3-pkgconfig.patch)
md5sums=('b540db502c5fa42078249f43d18a4652'
         '27ff3915e03964f85a9e9fb896a93479'
         'ed3eb3dfa2393adc6829cc9ca8caa0ff')

prepare() {
  cd "${srcdir}/$pkgname-$pkgver"
  # patches from fedora
  patch -p1 -i ../ilmbase-2.0.1-no_undefined.patch
  patch -p1 -i ../ilmbase-1.0.3-pkgconfig.patch

  mkdir build
}

build() {
  cd "${srcdir}/$pkgname-$pkgver/build"

  PTHREAD_CFLAGS=-pthread \
  PTHREAD_LIBS=-pthread \
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr ..

  make
}

package() {
  cd "${srcdir}/$pkgname-$pkgver/build"
  make DESTDIR="${pkgdir}" install
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname/"
  install -m644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  for l in libIex libIexMath libImath libIlmThread; do
    ln -s ${l}-2_2.so.12 ${pkgdir}/usr/lib/${l}.so
  done

}
