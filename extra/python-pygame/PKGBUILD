# $Id: PKGBUILD 184423 2013-05-06 19:39:15Z foutrelis $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=python-pygame
pkgname=python2-pygame
pkgver=1.9.1
pkgrel=13
pkgdesc="Python game library"
arch=('i386' 'amd64')
url="http://www.pygame.org/"
license=('LGPL')
depends=('sdl_mixer' 'sdl_ttf' 'sdl_image' 'python2')
conflicts=('python-pygame')
patchdir=(pygame-${pkgver}release)
source=(http://pygame.org/ftp/pygame-${pkgver}release.tar.gz \
        pygame-v4l.patch)
sha1sums=('a45aeb0623e36ae7a1707b5f41ee6274f72ca4fa'
          '7e693fb2ef5ef636f9965ba1a4eb854f8b0b5070')

prepare() {
  cd pygame-${pkgver}release
  find . -type f -exec sed -i 's#/usr/bin/env python#/usr/bin/env python2#' {} +
  patch -p1 -i "${srcdir}/pygame-v4l.patch"

  sed -i '' -e 's|^_camera|#_camera|' \
		-e 's|^pypm|#pypm|' Setup.in
	sed -i '' -e "s|Dependency('PORT|#Dependency('PORT|" \
		config_unix.py

}

package_python2-pygame() {
  cd pygame-${pkgver}release
  python2 config.py -auto
  python2 setup.py install --root="${pkgdir}" --prefix=/usr

# Copying the examples and tests
  cp -R examples lib/* "${pkgdir}/usr/lib/python2.7/site-packages/pygame"
  cp -R test "${pkgdir}/usr/lib/python2.7/site-packages/pygame/tests"

# Fixing permissions
  chmod 644 "${pkgdir}"/usr/include/python2.7/pygame/*
}
