# Maintainer: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Jeramy Rutley <jrutley@gmail.com>

pkgname=('ruby' 'ruby-docs')
pkgver=2.2.3
pkgrel=1
arch=('i686' 'amd64')
url='http://www.ruby-lang.org/en/'
license=('BSD' 'custom')
depends=("libexecinfo")
makedepends=('tk' 'libffi' 'doxygen' 'graphviz' 'libyaml' 'libexecinfo')
options=('!emptydirs' '!makeflags')
source=("ftp://ftp.ruby-lang.org/pub/ruby/${pkgver%.*}/ruby-${pkgver//_/-}.tar.bz2"
        'gemrc')
md5sums=('f49a734729a71774d4a94a9a603114b2'
         '6fb8e7a09955e0f64be3158fb4a27e7a')

build() {
  cd ruby-${pkgver//_/-}

  PKG_CONFIG=/usr/bin/pkg-config ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sharedstatedir=/var/lib \
    --libexecdir=/usr/libexec/ruby \
    --enable-shared \
    --disable-rpath \
    --with-dbm-type=gdbm_compat

  gmake
}

check() {
  cd ruby-${pkgver//_/-}
  # FAIL 1/964 tests failed
  gmake test || true
}

package_ruby() {
  pkgdesc='An object-oriented language for quick and easy programming'
  depends=('libffi' 'libyaml' 'libexecinfo')
  optdepends=('tk: for Ruby/TK'
              'ruby-docs: Ruby documentation')
  provides=('rubygems' 'rake')
  conflicts=('rake')
  backup=('etc/gemrc')
  install='ruby.install'

  cd ruby-${pkgver//_/-}

  gmake DESTDIR="${pkgdir}" install-nodoc

  install -dm755 "${pkgdir}/etc/"
  install -m644 ${srcdir}/gemrc "${pkgdir}/etc/gemrc"
  install -dm755 "${pkgdir}/usr/share/licenses/ruby/"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/ruby/LICENSE"
  install -m644 BSDL "${pkgdir}/usr/share/licenses/ruby/BSDL"
}

package_ruby-docs() {
  pkgdesc='Documentation files for ruby'

  cd ruby-${pkgver//_/-}

  make DESTDIR="${pkgdir}" install-doc install-capi
  install -dm755 "${pkgdir}/usr/share/licenses/ruby-docs/"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/ruby-docs/LICENSE"
  install -m644 BSDL "${pkgdir}/usr/share/licenses/ruby-docs/BSDL"
}
