# Port: lang/ruby24

pkgname=('ruby' 'ruby-docs')
pkgver=2.4.1
pkgrel=2
arch=('amd64')
url='http://www.ruby-lang.org/en/'
license=('BSD' 'custom')
depends=("libexecinfo" 'gmp')
makedepends=('gdbm' 'libffi' 'doxygen' 'graphviz' 'libyaml' 'ttf-dejavu' 'tk')
options=('!emptydirs' 'gnu_configure')
source=("https://cache.ruby-lang.org/pub/ruby/${pkgver:0:3}/ruby-${pkgver}.tar.xz"
        'gemrc')
sha256sums=('4fc8a9992de3e90191de369270ea4b6c1b171b7941743614cc50822ddc1fe654'
            '4bb7eb2fe66e396ed16b589cdb656831407b39ad4e138d88536754c0448ac614')

build() {
  cd "ruby-${pkgver}"

  autoreconf -vfi   
  sed -i '' -E \
    -e 's,-l$pthread_lib,-lpthread,g' \
    -e '/^RUBY_LIB_PATH/s,\.\$\{TEENY\},,' \
    -e '/^RUBY_SITE_LIB_PATH2/s,\.\$\{TEENY\},,' \
    -e '/^RUBY_VENDOR_LIB_PATH2/s,\.\$\{TEENY\},,' \
    configure


  export LDFLAGS+=" -lpthread"  
  LIBS=" -lpthread"
  PKG_CONFIG=/usr/bin/pkg-config ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sharedstatedir=/var/lib \
    --libexecdir=/usr/libexec/ruby \
    --enable-shared \
    --disable-rpath \
    --with-dbm-type=gdbm_compat \

  make
}

check() {
  cd ruby-${pkgver}
  # FAIL 1/964 tests failed
  make test || true
}

package_ruby() {
  pkgdesc='An object-oriented language for quick and easy programming'
  depends=('gdbm' 'libffi' 'libyaml' 'libexecinfo')
  optdepends=('tk: for Ruby/TK'
              'ruby-docs: Ruby documentation')
  provides=('rubygems' 'rake')
  conflicts=('rake')
  backup=('etc/gemrc')
  install='ruby.install'

  cd ruby-${pkgver}

  make DESTDIR="${pkgdir}" install-nodoc

  install -dm755 "${pkgdir}/etc/"
  install -m644 ${srcdir}/gemrc "${pkgdir}/etc/gemrc"
  install -dm755 "${pkgdir}/usr/share/licenses/ruby/"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/ruby/LICENSE"
  install -m644 BSDL "${pkgdir}/usr/share/licenses/ruby/BSDL"
}

package_ruby-docs() {
  pkgdesc='Documentation files for ruby'

  cd ruby-${pkgver}

  make DESTDIR="${pkgdir}" install-doc install-capi
  install -dm755 "${pkgdir}/usr/share/licenses/ruby-docs/"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/ruby-docs/LICENSE"
  install -m644 BSDL "${pkgdir}/usr/share/licenses/ruby-docs/BSDL"
}
