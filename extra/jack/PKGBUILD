# Port: audio/jack

pkgname=jack
_longname=jack-audio-connection-kit
pkgver=0.124.1
pkgrel=2
pkgdesc="A low-latency audio server"
arch=('i386' 'amd64')
license=('GPL' 'LGPL')
depends=('libsamplerate' 'db' 'readline' 'e2fsprogs-libuuid')
makedepends=('doxygen' 'celt')
optdepends=('celt: NetJACK driver')
url="http://jackaudio.org/"
backup=(etc/security/limits.d/99-audio.conf)
provides=("$_longname=$pkgver")
conflicts=("$_longname")
replaces=("$_longname")
install=$pkgname.install
patchdir=("$srcdir/$_longname-$pkgver")
source=("http://jackaudio.org/downloads/$_longname-$pkgver.tar.gz"
        '99-audio.conf'
        '40-hpet-permissions.rules')
md5sums=('d64e90121be8a54860b870a726fb5b5d'
         'ae65b7c9ebe0fff6c918ba9d97ae342d'
         '471aad533ff56c5d3cbbf65ce32cadef')

prepare() {
  cd "$srcdir/$_longname-$pkgver"

  sed -i '' -e 's|define USE_MLOCK|undef USE_MLOCK|' \
      -e 's|alloca.h||' \
      configure

  sed -i '' -e "s,-lpthread,-pthread," \
      configure \
      libjack/Makefile.in \
      jackd/Makefile.in \
      example-clients/Makefile.in \
      jack.pc.in

  sed -i '' -e "s,-lrt,," libjack/Makefile.in \
      jackd/Makefile.in \
      example-clients/Makefile.in

  sed -i '' -e "s,-ldl,," jackd/Makefile.in \
      example-clients/Makefile.in

  sed -i '' -e 's|md5sum|md5 -q|g' \
      jackd/Makefile.in
}

build() {
  cd "$srcdir/$_longname-$pkgver"
  LDFLAGS+=" -L/usr/local/lib/db6"
  CFLAGS+=" -I/usr/local/include/db6" 
  ./configure --prefix=/usr  --enable-portaudio \
              --libdir=/usr/lib

  gmake
}

package() {
  cd "$srcdir/$_longname-$pkgver"

  gmake DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/etc/security/limits.d"
  install -m644 "$srcdir/99-audio.conf" \
    "$pkgdir/etc/security/limits.d/99-audio.conf"

  install -dm755 "$pkgdir/usr/lib/udev/rules.d"
  install -m644 "$srcdir/40-hpet-permissions.rules" \
    "$pkgdir/usr/lib/udev/rules.d/40-hpet-permissions.rules"

}

# vim:set ts=2 sw=2 et:
