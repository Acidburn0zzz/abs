pkgname=libical
pkgver=1.0.1
pkgrel=1
pkgdesc="An open source reference implementation of the icalendar data type and serialization format"
arch=('i386' 'amd64')
url='http://sourceforge.net/projects/freeassociation/'
license=('LGPL' 'MPL')
depends=('libc')
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
md5sums=('af91db06b22559f863869c5a382ad08a')

prepare(){
  cd ${pkgname}-${pkgver}
  sed -i '' -e 's|LIB_INSTALL_DIR}/pkgconfig|CMAKE_INSTALL_PREFIX}/libdata/pkgconfig|g' \
    CMakeLists.txt
}

build() {
  mkdir -p build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DSHARED_ONLY=true \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
