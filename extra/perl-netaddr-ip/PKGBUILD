# $Id: PKGBUILD 192117 2013-08-06 02:08:02Z eric $
# Maintainer:
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Francois Charette <francois.archlinux.org>

pkgname=perl-netaddr-ip
pkgver=4.078
pkgrel=1
pkgdesc="Perl module to manage IP addresses and subnets"
arch=('i386' 'amd64')
url="http://search.cpan.org/dist/NetAddr-IP/"
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/M/MI/MIKER/NetAddr-IP-${pkgver}.tar.gz)
md5sums=('bca8406520b6535a3f9661d385705d97')

build() {
  cd NetAddr-IP-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd NetAddr-IP-${pkgver}
  make test
}

package() {
  cd NetAddr-IP-${pkgver}
  make DESTDIR="${pkgdir}" install

# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;

}
