# $Id: PKGBUILD 157819 2012-04-30 04:19:30Z foutrelis $
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgname=libxfce4ui
pkgver=4.12.1
pkgrel=5
pkgdesc="Commonly used Xfce widgets among Xfce applications"
arch=('i386' 'amd64')
url="http://www.xfce.org/"
license=('GPL2')
depends=('libxfce4util' 'gtk2' 'gtk3' 'xfconf' 'libsm' 'startup-notification'
         'hicolor-icon-theme')
makedepends=('intltool')
#replaces=('libxfcegui4') - later when all is ported
install=libxfce4ui.install
source=(http://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('3d619811bfbe7478bb984c16543d980cadd08586365a7bc25e59e3ca6384ff43')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/libexec \
    --localstatedir=/var \
    --disable-static \
    --disable-debug \
    --with-vendor-info='Pac BSD'
  gmake
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
