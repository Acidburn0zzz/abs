# Port: databases/p5-DBD-mysql

pkgname=perl-dbd-mysql
_realname=DBD-mysql
pkgver=4.043
pkgrel=1
pkgdesc='Perl/CPAN DBD::mysql module for interacting with MySQL via DBD'
arch=('amd64')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/$_realname/"
depends=('libmariadbclient' 'perl-dbi')
makedepends=('mariadb')
patchdir=($_realname-$pkgver)
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/M/MI/MICHIELB/DBD-mysql-${pkgver}.tar.gz")
sha256sums=('629f865e8317f52602b2f2efd2b688002903d2e4bbcba5427cb6188b043d6f99')

build() {
  cd $_realname-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor --testsocket=/tmp/socket.mysql
  make
}

check() {
  cd $_realname-$pkgver
  mkdir -p /tmp/mysql_test
  mysql_install_db \
     --basedir=/usr \
     --datadir=/tmp/mysql_test
  mysqld -P 17999 \
     --socket=/tmp/socket.mysql \
     --datadir=/tmp/mysql_test &
  sleep 10
  DAEMON_PORT=$!
  make test || true
  kill -9 $DAEMON_PORT
}

package() {
  cd $_realname-$pkgver
  make install DESTDIR="$pkgdir"
}
