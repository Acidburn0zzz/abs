# $Id: PKGBUILD 191806 2013-07-30 22:50:38Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Michael Kanis <mkanis_at_gmx_dot_de>

pkgname=mutter
pkgver=3.12.0
pkgrel=2
pkgdesc="A window manager for GNOME"
arch=(i686 x86_64)
license=('GPL')
depends=('clutter' 'dconf' 'gobject-introspection' 'gsettings-desktop-schemas' 'libcanberra' 'startup-notification' 'zenity' 'libsm')
makedepends=('intltool' 'gnome-doc-utils' 'upower' 'gnome-desktop')
url="http://www.gnome.org"
groups=('gnome')
options=('!libtool' '!emptydirs')
install=mutter.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('1dcc5c71a80b74c48e8a3f1ce90bd718ab53b267cd20fe28b1543c358bb42ac6')

prepare() {
  cd "$pkgname-$pkgver"

  export CPPFLAGS="${CPPFLAGS} -fno-omit-frame-pointer"
  sed -i '' -e 's|-Wcast-align||g' \
	configure
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/libexec/mutter \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile --enable-compile-warnings=maximum

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  gmake
}

package() {
  cd "$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install
}
