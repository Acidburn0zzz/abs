pkgname=mutter
pkgver=3.18.2
pkgrel=1
pkgdesc="A window manager for GNOME"
arch=('i386' 'amd64')
license=('GPL')
depends=('clutter' 'dconf' 'gobject-introspection' 'gsettings-desktop-schemas' 'libcanberra' 'startup-notification' 'zenity' 'libsm' 'gnome-desktop' 'libxkbcommon-x11')
makedepends=('intltool' 'gnome-doc-utils' 'upower')
url="http://www.gnome.org"
groups=('gnome')
options=('libtoolfix' '!emptydirs')
install=mutter.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('8a69326f216c7575ed6cd53938b9cfc49b3b359cde95d3b6a7ed46c837261181')

prepare() {
  cd "$pkgname-$pkgver"

  export CPPFLAGS="${CPPFLAGS} -fno-omit-frame-pointer"

  sed -i '' -e 's|-Wcast-align||g' \
	configure
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/libexec/mutter \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile --enable-compile-warnings=maximum

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  gmake
}

package() {
  cd "$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install
}
