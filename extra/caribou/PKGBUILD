# $Id: PKGBUILD 187111 2013-06-04 04:29:06Z heftig $
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Adam Hani Schakaki <krzd@krzd.net>

pkgname=caribou
pkgver=0.4.13
pkgrel=1
pkgdesc="A text entry and UI navigation application being developed as an alternative to the Gnome On-screen Keyboard."
arch=('i686' 'x86_64')
url="http://live.gnome.org/Caribou"
license=(LGPL)
depends=(at-spi2-atk python2-atspi python2-gobject gtk3 libxklavier libgee clutter dconf)
makedepends=(intltool docbook-xsl gtk2)
install=caribou.install
options=(!emptydirs)
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/0.4/$pkgname-$pkgver.tar.xz)
sha256sums=('953ba618621fda8a828d0d797fc916dbe35990dc01d7aa99d15e5e2241ee2782')

build() {
  cd $pkgname-$pkgver
  gsed -i s'|#!/usr/bin/python|#!/usr/bin/python2|'g tools/{fix_gir,make_schema}.py
  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/caribou \
    --disable-gtk2-module \
    --disable-static \
    --disable-schemas-compile \
    LIBS="-lintl"

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  gmake
}

check() {
  cd $pkgname-$pkgver
  gmake check
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
