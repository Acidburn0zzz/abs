# $Id: PKGBUILD 191710 2013-07-29 10:16:55Z tpowa $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=alsa-utils
pkgver=1.0.28
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support"
arch=('i686' 'x86_64')
url="http://www.alsa-project.org"
#can not use >=$pkgver due to use of letters in alsa-lib update versioning
depends=("alsa-lib>1.0.24" 'pciutils' 'psmisc' 'libsamplerate' 'ncurses')
license=('GPL')
makedepends=('xmlto' 'docbook-xsl' 'getopt')
port_patches=(
	patch-alsaloop__alsaloop.c
	patch-alsamixer__mixer_display.c
	patch-alsamixer__volume_mapping.c
	patch-aplay__aplay.c
	patch-aplay__formats.h
)
source=(ftp://ftp.alsa-project.org/pub/utils/$pkgname-${pkgver}.tar.bz2
	${port_patches[@]}
	byteswap.h
	malloc.h)

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  for i in ${port_patches[@]}; do
    patch -p0 -i "${srcdir}/${i}"
  done

  sed -i ''  -e '/LIBS/s/-ldl//g; /LIBRT/s/-lrt//g;' configure

}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Use ncurses in usr/local
  export CPPFLAGS="${CPPFLAGS} -I/usr/local/include -I${srcdir}/"
  export LDFLAGS="${LDFLAGS} -pthread"
  ./configure --prefix=/usr --disable-alsaconf --sbindir=/usr/bin \
    --with-udev-rules-dir=/usr/lib/udev/rules.d --disable-nls

  gmake
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  gmake DESTDIR=${pkgdir} install

  # dir where to save ALSA state
  install -d ${pkgdir}/var/lib/alsa
}
md5sums=('361552d5b1cacd0a1e7ba09e69990211'
         '64fc449e6a7e6320b350e1182374c9b0'
         '0ab571cb6e041ba7f0df977a7d49a103'
         'ef5c0c65f959463c2d40fedd4e05c0e6'
         '467c948b76ee0007c01c24fc43795c9a'
         '3be775c27929ed0f894c6b2bd7237c97'
         '0ca17b1e297293605f0d9d69e7dca4fa'
         '3c7663cdda74f302f41dac28a24b94fd')

