# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>
# Contributor: shamrok <szamrok@gmail.com>

pkgname=smplayer
pkgver=14.9.0.6994
pkgrel=1
pkgdesc="A complete front-end for MPlayer"
arch=('i686' 'amd64')
url="http://smplayer.sourceforge.net/"
license=('GPL')
depends=('qt5-script' 'mpv' 'hicolor-icon-theme' 'libxkbcommon-x11')
makedepends=('qt5-tools')
optdepends=('smplayer-themes: icon themes collection'
            'smplayer-skins: skin themes collection'
            'smtube: browse and play youtube videos'
            'mplayer: for using mplayer instead of mpv')
install=smplayer.install
source=(https://downloads.sourceforge.net/project/$pkgname/Unstable/releases/$pkgname-$pkgver.tar.bz2
        add-missing-QDataStream-include.patch)
md5sums=('666a589b1fc3480a29e87f0ee7efd511'
         '418ab45eb94448fc8a2f44500c9742c2')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../add-missing-QDataStream-include.patch

  mkdir "${srcdir}/bin"
  ln -s /usr/bin/gmake  "${srcdir}"/bin/make
  PATH="${srcdir}/bin/:$PATH"

}

build() {
  cd "$pkgname-$pkgver"
  gmake PREFIX=/usr \
    DOC_PATH="\\\"/usr/share/doc/smplayer\\\"" \
    QMAKE_OPTS=DEFINES+=NO_DEBUG_ON_CONSOLE
}

package() {
  cd "$pkgname-$pkgver"
  gmake DOC_PATH=/usr/share/doc/smplayer \
    DESTDIR="$pkgdir" PREFIX="/usr" install
}

