# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>
# Contributor: shamrok <szamrok@gmail.com>

pkgname=smplayer
pkgver=15.9.0
pkgrel=1
pkgdesc="A complete front-end for MPlayer"
arch=('i386' 'amd64')
url="http://smplayer.sourceforge.net/"
license=('GPL')
depends=('qt5-script' 'mpv' 'hicolor-icon-theme' 'libxkbcommon-x11')
makedepends=('qt5-tools')
optdepends=('smplayer-themes: icon themes collection'
            'smplayer-skins: skin themes collection'
            'smtube: browse and play youtube videos'
            'mplayer: for using mplayer instead of mpv')
install=smplayer.install
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2)
md5sums=('6ab6f96030df63b79f3a28fb3f99107d')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../add-missing-QDataStream-include.patch

  mkdir "${srcdir}/bin"
  ln -s /usr/bin/gmake  "${srcdir}"/bin/make
  PATH="${srcdir}/bin/:$PATH"

}

build() {
  cd "$pkgname-$pkgver"
  gmake PREFIX=/usr \
    DOC_PATH="\\\"/usr/share/doc/smplayer\\\"" \
    QMAKE_OPTS=DEFINES+=NO_DEBUG_ON_CONSOLE
}

package() {
  cd "$pkgname-$pkgver"
  gmake DOC_PATH=/usr/share/doc/smplayer \
    DESTDIR="$pkgdir" PREFIX="/usr" install
}

