# $Id: PKGBUILD 188207 2013-06-12 20:16:30Z heftig $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=webkitgtk
pkgname=(webkitgtk webkitgtk2)
pkgver=2.4.5
pkgrel=5
pkgdesc="GTK+ Web content engine library"
arch=(i686 x86_64)
url="http://webkitgtk.org/"
license=(custom)
depends=(libxt libxslt sqlite libsoup enchant libgl geoclue2 gst-plugins-base-libs
         libsecret libwebp harfbuzz-icu)
makedepends=(gtk3 gtk2 gperf gobject-introspection python2 mesa ruby gtk-doc)
optdepends=('gst-plugins-base: free media decoding'
            'gst-plugins-good: media decoding'
            'gst-libav: nonfree media decoding')
options=(!emptydirs)
source=(http://webkitgtk.org/releases/$pkgname-${pkgver}.tar.xz
	fix-pretty-quotes.patch)
md5sums=('c2e1687bb0314a0948fbf78e2d1e931f'
         '20f4deebac66c560fdbe46fcb411eee7')

prepare() {
  mkdir build-gtk{,2} path

  ln -s /usr/bin/python2 path/python

  cd $pkgbase-$pkgver

  # find isatty
  gsed -i -e '/#include <stdlib\.h>/i#include <unistd.h>' \
    Source/ThirdParty/gtest/include/gtest/internal/gtest-port.h
  # SIGCHLD and some others
  gsed -i -e '/#include "config\.h"/i#include <sys/wait.h>' \
          -e 's/SIGCLD/SIGCHLD/g' \
    Source/WebKit2/UIProcess/Plugins/unix/PluginProcessProxyUnix.cpp
  # basename needs libgen.h
  gsed -i -e '/#include "config\.h"/i#include <libgen.h>' \
    Source/WebKit2/PluginProcess/unix/PluginProcessMainUnix.cpp

  gsed -i \
    -e 's/|amd64-\*-freebsd\*|/&x86_64-*-freebsd*|/' \
    -e 's/stdlib=libstdc++/stdlib=libc++/g' \
    configure

  gsed -i -e '/-lstdc++/d' GNUmakefile.in

  patch -Np0 -i ../fix-pretty-quotes.patch

  msg 'libtool fixup'
  find . -name libtool -exec cp /usr/bin/libtool '{}' \;
  find . -name ltmain.sh -exec cp /usr/share/libtool/config/ltmain.sh '{}' \;
}

_build() (
  _ver="$1"; shift
  cd build-${_ver}

  ../$pkgbase-$pkgver/configure --prefix=/usr \
    --libexecdir=/usr/lib/webkit${_ver} \
    --enable-introspection \
    "$@"

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  gsed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  gmake all stamp-po
)

build() {

# hmm... build system fail?
  export LIBS=-lharfbuzz-icu
# we don't have a pkg-config for zlib
  export ZLIB_CFLAGS=" " ZLIB_LIBS=-lz

  export CFLAGS="${CFLAGS} -fPIC"
  export CXXFLAGS="${CXXFLAGS} -fPIC"
  export LDFLAGS="${LDFLAGS} -lintl -fPIC"

  PATH="$srcdir/path:$PATH"

  _build gtk --enable-gtk-doc --libexecdir=/usr/lib/webkitgtk
  _build gtk2 --disable-webkit2 --with-gtk=2.0 --libexecdir=/usr/lib/webkitgtk2

}

package_webkitgtk() {
  depends+=(gtk3)
  optdepends+=('gtk2: Netscape plugin support')
  provides=("webkitgtk3=${pkgver}" "libwebkit3=${pkgver}")
  conflicts=(webkitgtk3 libwebkit3)
  replaces=(webkitgtk3 libwebkit3)

  gmake -C build-gtk -j1 DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/share/licenses/webkitgtk"
  install -m644 $pkgbase-$pkgver/Source/WebKit/LICENSE \
    "$pkgdir/usr/share/licenses/webkitgtk/LICENSE"
}

package_webkitgtk2() {
  pkgdesc+=" for GTK2"
  depends+=(gtk2)
  provides=("libwebkit=${pkgver}")
  conflicts=(libwebkit)
  replaces=(libwebkit)

  gmake -C build-gtk2 -j1 DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/share/licenses/webkitgtk2"
  install -m644 $pkgbase-$pkgver/Source/WebKit/LICENSE \
    "$pkgdir/usr/share/licenses/webkitgtk2/LICENSE"
}

