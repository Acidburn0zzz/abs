# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xkeyboard-config
pkgver=2.11
pkgrel=1
pkgdesc="X keyboard configuration files"
arch=(any)
license=('custom')
url="http://www.freedesktop.org/wiki/Software/XKeyboardConfig"
depends=('xorg-xkbcomp')
makedepends=('intltool')
provides=('xkbdata')
replaces=('xkbdata')
conflicts=('xkbdata')
source=(http://xorg.freedesktop.org/archive/individual/data/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        revert-comma-in-keypad.patch)
sha256sums=('e7125460892c2b5c3a8d843cb18c24b60c46051e925c2888a61fa672a2f76d76'
            '05b22863ddb1de64575b56714ebd693f2ea639b7a4ae15e25c9a1f0d14dba817')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # fix FS#38743 , not merged upstream
  patch -Np1 -i../revert-comma-in-keypad.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i '' -e 's|/bin/bash|/bin/sh|g' -e 's|==|=|g' rules/merge.sh
  ./configure --prefix=/usr \
      --with-xkb-base=/usr/share/X11/xkb \
      --with-xkb-rules-symlink=xorg \
      --enable-compat-rules=yes
  gmake
}

package() { 
  cd "${srcdir}/${pkgname}-${pkgver}"

  gmake DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}/usr/share/X11/xkb/compiled"

  install -m755 -d "${pkgdir}/var/lib/xkb"
  #ln -sf /var/lib/xkb "${pkgdir}/usr/share/X11/xkb/compiled"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

  mv "${pkgdir}/usr/"{share,lib}"/pkgconfig"
}
