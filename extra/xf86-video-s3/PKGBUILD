# $Id: PKGBUILD 203632 2014-01-13 17:13:15Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-video-s3
pkgver=0.6.5
pkgrel=1
pkgdesc="X.org S3 video driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libc')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=13')
conflicts=('xorg-server<1.13.0' 'X-ABI-VIDEODRV_VERSION<13' 'X-ABI-VIDEODRV_VERSION>=14')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2 build-fix.diff)
sha256sums=('4778cd4f1d61490c3562f9be03664576f065fa3acf22fe0d0689212d7ea3ad39'
            'ad5ce4569c16f28801f4f89d8271fccc5706ef8508a6051ca094854d92a19d1a')

build() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/build-fix.diff
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
