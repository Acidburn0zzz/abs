# Maintainer: Anthony Donnelly <Amzo@archbsd.net>

pkgname=pixman
pkgver=0.32.8
pkgrel=5
pkgdesc="The pixel-manipulation library for X and cairo"
arch=('i386' 'amd64')
url="http://xorg.freedesktop.org"
license=('custom')
makedepends=('libtool' 'perl')
source=(http://xorg.freedesktop.org/releases/individual/lib/${pkgname}-${pkgver}.tar.bz2)
options=('libtoolfix')
sha1sums=('5c57045622265b877c9bf02d531973eadf942140')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --disable-static --disable-gtk

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # stress test fails on i386, it also fails in ports on i386
  make check || true
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
