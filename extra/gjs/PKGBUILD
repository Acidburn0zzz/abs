# $Id: PKGBUILD 191784 2013-07-30 19:02:00Z jgc $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gjs
pkgver=1.36.1
pkgrel=2
pkgdesc="Javascript Bindings for GNOME"
arch=(i686 x86_64)
url="http://live.gnome.org/Gjs"
license=(GPL)
depends=(cairo gobject-introspection 'js>=17.0.0')
options=('!libtool')
source=(http://people.gnome.org/~walters/$pkgname-js17-$pkgver.tar.bz2
        gettext-typo.patch)
sha256sums=('5826f74fcf20750538f74e2b0059f392eb139996b80b70ca217f5f7a0d117e27'
            'c2c3ca377d9d69511d5784599ea33d754aed776d5ceefaf384b8e303b54fd038')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../gettext-typo.patch
}
build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  gmake
}

check() {
  cd $pkgname-$pkgver

  # Fails test. uuidgen invalid arguements:
  gmake check || true
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
}
