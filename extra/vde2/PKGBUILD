pkgname=vde2
pkgver=2.3.2
pkgrel=2
pkgdesc="Virtual Distributed Ethernet for emulators like qemu"
url="http://sourceforge.net/projects/vde/"
license=("GPL" "LGPL" "CUSTOM")
arch=('i386' 'amd64')
depends=('bash' 'openssl')
makedepends=('python')
install=vde2.install
options=(!makeflags)
source=(http://downloads.sourceforge.net/vde/$pkgname-$pkgver.tar.bz2
        dhcpd.conf.sample
        vde-config.sample
        vde-connection.sample)
md5sums=('46fbc5f97f03dc517aa3b2c9d9ea6628'
         '7d9bc56d2e561d849e915000d1c0f269'
         'f47d3372382dc9d67c1174d2796729fe'
         '63033c33565e2030541c5e05e9d9b063')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --sbindir=/usr/sbin --sysconfdir=/etc --libexecdir=/usr/libexec/vde2

  gmake V=1
}

package() {
  cd $srcdir/$pkgname-$pkgver 
  gmake prefix=$pkgdir/usr sysconfdir=$pkgdir/etc sbindir=$pkgdir/usr/bin libexecdir=$pkgdir/usr/libexec/vde2 install

  install -dm755 "$pkgdir"/{etc/vde,usr/share/vde2}
  install -m 644 ../vde-config.sample $pkgdir/etc/vde/vde-config.sample
  install -m 644 ../vde-connection.sample $pkgdir/etc/vde/vde-connection.sample
  install -m 644 ../dhcpd.conf.sample $pkgdir/usr/share/vde2/dhcpd.conf.sample

  # install slirp license
  install -dm755 "$pkgdir/usr/share/licenses/vde2"
  install -m 644 COPYING.slirpvde $pkgdir/usr/share/licenses/vde2/COPYING.slirpvde
}
