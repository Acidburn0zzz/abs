# Contributor: Wolfgang Bumiller <blub at speed dot at>
# Port: net/rsync

pkgname=rsync
pkgver=3.1.2
pkgrel=1
pkgdesc="A file transfer program to keep remote files in sync"
arch=('i386' 'amd64')
url="http://samba.anu.edu.au/rsync/"
license=('GPL3')
depends=('perl' 'gnu-libiconv')
patchoptions="-p1"
backup=('etc/rsyncd.conf')
source=("http://rsync.samba.org/ftp/rsync/$pkgname-$pkgver.tar.gz"
	"https://gitweb.gentoo.org/repo/gentoo.git/plain/net-misc/rsync/files/rsyncd.init.d-r1"
	"https://gitweb.gentoo.org/repo/gentoo.git/plain/net-misc/rsync/files/rsyncd.conf.d"
        'rsyncd.conf' 'rsyncd.in')

build() {
	cd "$srcdir/$pkgname-$pkgver"

	sed -i '' -e 's|CFLAGS|CONFIGUREDCFLAGS|' \
	          -e 's|perl.*||' \
	          Makefile.in

	sed -i '' -e 's|malloc.h|stdlib.h|g' rsync.h zlib/zutil.h

	./configure --prefix=/usr \
	            --disable-debug --enable-ipv6 \
	            --with-rsyncd-conf=/etc/rsyncd.conf \
	            --with-included-popt ac_cv_search_libiconv_open=no
	make
}

check() {
	cd "$srcdir/$pkgname-$pkgver"
	make test
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -dm755 "${pkgdir}"/etc/{rc.d,init.d,conf.d}

	install -m755 ../rsyncd.in      "$pkgdir/etc/rc.d/rsyncd"
	install -m755 ../rsyncd.init.d-r1 "$pkgdir/etc/init.d/rsyncd"
	install -m644 ../rsyncd.conf.d "$pkgdir/etc/conf.d/rsyncd"

	install -m644 ../rsyncd.conf "$pkgdir/etc/rsyncd.conf"
	install -dm755 "$pkgdir/usr/lib/rsync"
	install -m755 support/rrsync "$pkgdir/usr/lib/rsync/rrsync"
}
md5sums=('0f758d7e000c0f7f7d3792610fad70cb'
         '477b9b8fe05c9b2a29aa4ef17aef9cc1'
         'e5e62e8cef29e09c22e8ba8152ec1751'
         '14bc303db10da09425e165661a47d7c3'
         '824fb8d209c2e05c8fc5cb53f3acea65')

