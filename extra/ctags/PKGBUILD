# $Id: PKGBUILD 143723 2011-11-28 17:13:34Z giovanni $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=ctags
pkgver=5.8
pkgrel=3
pkgdesc="Generates an index file of language objects found in source files"
arch=('i386' 'amd64')
license=('GPL')
depends=('libc')
url="http://ctags.sourceforge.net/"
port_patches=(
	patch-Makefile.in
	patch-aa
)
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
	${port_patches[@]})
md5sums=('c00f82ecdcc357434731913e5b48630d'
         '3bb5d6435bf8316c126b3b29113ff28d'
         '0c1ea3fa7e65a9466ca4c9d67853455d')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  for i in ${port_patches[@]}; do
    msg "Applying port patch ${i}"
    patch -p0 -i "${srcdir}/${i}"
  done
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr \
              --disable-external-sort
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make prefix=${pkgdir}/usr install
}
