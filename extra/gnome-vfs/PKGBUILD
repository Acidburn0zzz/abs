# $Id: PKGBUILD 136438 2011-08-29 09:40:33Z ibiru $
# Maintainer: Anthony Donnelly <amzo@archbsd.net>

pkgname=gnome-vfs
pkgver=2.24.4
pkgrel=13
pkgdesc="The GNOME Virtual File System"
arch=(i686 amd64)
license=('LGPL')
depends=('gconf' 'avahi' 'smbclient' 'gnome-mime-data' 'gnutls' 'libgcrypt')
makedepends=('pkgconf' 'intltool' 'gtk-doc' 'gnome-common')
options=('!emptydirs')
url="http://www.gnome.org"
install=gnome-vfs.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.24/gnome-vfs-${pkgver}.tar.bz2
        gcrypt-config.patch
	gnutls-config.patch
        ${port_patches[@]}
)
sha256sums=('62de64b5b804eb04104ff98fcd6a8b7276d510a49fbd9c0feb568f8996444faa'
            'c059e218f310da683778919d36e7862f7e763384805f6453d328fbaf507a8114'
            '66c7cfb12995c0dd94a2caea95c7e3c55981993f05a79c585d60915ff131955d')

prepare(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i '' -e 's|/usr/local|/usr|g' -e 's|%%LOCALBASE%%|/usr|g' libgnomevfs/xdgmime.c libgnomevfs/gnome-vfs-hal-mounts.c
  sed -i '' -e 's|-lacl||g' -e 's|inotify_support=yes|inotify_support=no|g' configure
  sed -i '' -e 's|-DG_DISABLE_DEPRECATED||g' */Makefile.in
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/libexec/gnome-vfs-2.0 \
      --enable-samba --disable-hal \
      --enable-avahi --disable-howl \
      --disable-openssl --disable-gnutls
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-vfs-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
