# Port: security/p5-Net-SSLeay
pkgname=perl-net-ssleay
_cpanname=Net-SSLeay
pkgver=1.81
pkgrel=1
pkgdesc="Perl extension for using OpenSSL"
arch=('i386' 'amd64')
license=('custom:BSD')
url="http://search.cpan.org/dist/${_cpanname}/"
options=(!emptydirs)
depends=('perl')
replaces=('net-ssleay')
provides=('net-ssleay')
patchdir=(${_cpanname}-${pkgver})
source=("http://www.cpan.org/authors/id/M/MI/MIKEM/${_cpanname}-${pkgver}.tar.gz")
sha256sums=('00cbb6174e628b42178e1445c9fd5a3c5ae2cfd6a5a43e03610ba14786f21b7d')

build() {
  cd ${srcdir}/${_cpanname}-${pkgver}

  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL -- INSTALLDIRS=vendor 
  make
}

package() {
  cd ${srcdir}/${_cpanname}-${pkgver}

  make install DESTDIR=${pkgdir} 

  # remove perllocal.pod and .packlist
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete

  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 README "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
