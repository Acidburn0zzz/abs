# $Id: PKGBUILD 188575 2013-06-15 17:19:47Z giovanni $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=perl-net-ssleay
_cpanname=Net-SSLeay
pkgver=1.72
pkgrel=2
pkgdesc="Perl extension for using OpenSSL"
arch=('i386' 'amd64')
license=('custom:BSD')
url="http://search.cpan.org/dist/${_cpanname}/"
options=(!emptydirs)
replaces=('net-ssleay')
provides=('net-ssleay')
source=("http://www.cpan.org/authors/id/M/MI/MIKEM/${_cpanname}-${pkgver}.tar.gz")
md5sums=('a740d7792dbac8ca8c4eb38d3b8fbd01')

build() {
  cd ${srcdir}/${_cpanname}-${pkgver}

  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL -- INSTALLDIRS=vendor 
  make
}

package() {
  cd ${srcdir}/${_cpanname}-${pkgver}

  make install DESTDIR=${pkgdir} 

  # remove perllocal.pod and .packlist
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete

  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 README "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
