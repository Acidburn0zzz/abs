# $Id: PKGBUILD 188610 2013-06-16 23:56:02Z heftig $
# Maintainer:  Jan de Groot <jan@archlinux.org>

pkgname=gnome-desktop
pkgver=3.12.1
pkgrel=1
epoch=1
pkgdesc="Library with common API for various GNOME modules"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(gsettings-desktop-schemas gtk3 libxkbfile xkeyboard-config iso-codes)
makedepends=(intltool gobject-introspection itstool)
url="http://www.gnome.org"
groups=(gnome)
options=(!libtool 'libtoolfix')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver::4}/$pkgname-$pkgver.tar.xz)
sha256sums=('853604f55158343d7220a557f4705aa3270770fa47f5c0453d7fdde61f8c0691')

prepare() {
  cd $pkgname-$pkgver

  #bkc_ forced me to do this
  sed -i '' -e 's/fdatasync/fsync/g' libgnome-desktop/libgsystem/gsystem-file-utils.c

}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-static \
    --with-gnome-distributor="Arch BSD"

  gmake
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
}
