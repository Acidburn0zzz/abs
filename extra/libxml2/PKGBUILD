# Port: textproc/libxml2

pkgname=libxml2
pkgver=2.9.4
pkgrel=1
pkgdesc="XML parsing library, version 2"
arch=('i386' 'amd64')
license=('custom')
depends=('pkgconf')
url="http://www.xmlsoft.org/"
source=(ftp://ftp.xmlsoft.org/${pkgname}/${pkgname}-${pkgver}.tar.gz
        http://www.w3.org/XML/Test/xmlts20080205.tar.gz)
sha256sums=('ffb911191e509b966deb55de705387f14156e1a56b21824357cdf0053233633c'
            '39ffe4a61be4187aac4b64b8fd34aa01bb004387527a1ec5dc9c8dd794a2562e')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  for d in . doc doc/devhelp doc/examples
  do
    sed -i '' -e '/^install-data-am:/ s|install-data-local||' \
      ${d}/Makefile.in
  done

}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure  --prefix=/usr \
                --with-html-dir=/usr/share/doc \
		--with-iconv=/usr \
		--with-html-subdir=${pkgname} \
		--with-lzma=/usr \
		--without-python \
		--with-threads \
		--without-thread-alloc --without-history

  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644  COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
