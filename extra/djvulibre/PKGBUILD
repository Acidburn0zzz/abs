# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: mgushee
# Contributor: Wolfgang Bumiller <archbsd at bumiller dot com>

pkgname=djvulibre
pkgver=3.5.27
pkgrel=5
pkgdesc="Suite to create, manipulate and view DjVu ('déjà vu') documents"
url='http://djvu.sourceforge.net/'
arch=('i386' 'amd64')
license=('GPL2')
makedepends=('librsvg')
depends=('libtiff' 'hicolor-icon-theme')
source=("http://downloads.sourceforge.net/project/djvu/DjVuLibre/${pkgver}/djvulibre-${pkgver}.tar.gz")
sha1sums=('99c4f2c621c063bf8c8a1626030539fe5a8675f9')
conflicts=('libdjvu')
provides=("libdjvu=${pkgver}")
replaces=('libdjvu')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --disable-desktopfiles
	gmake
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	gmake -j1 DESTDIR="${pkgdir}" install
	for sz in 22 32 48 64; do
		install -dm755 "${pkgdir}/usr/share/icons/hicolor/${sz}x${sz}/mimetypes"
		install -m644 desktopfiles/prebuilt-hi${sz}-djvu.png "${pkgdir}/usr/share/icons/hicolor/${sz}x${sz}/mimetypes/image-vnd.djvu.mime.png"
	done
}
