# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Filip Dvorak <fila@pruda.com>

pkgname=enca
pkgver=1.15
pkgrel=1
pkgdesc="Charset analyser and converter"
arch=('i686' 'x86_64')
url="http://cihar.com/software/enca/"
options=('!libtool')
license=('GPL')
depends=('recode')
makedepends=('gmake')
source=("http://dl.cihar.com/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('fef132969d26e649719eae08297a4a52')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --libexecdir=/usr/lib \
              --mandir=/usr/share/man \
              --with-librecode=/usr \
              --enable-external
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  gmake DESTDIR="${pkgdir}" install
}
