# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Filip Dvorak <fila@pruda.com>

pkgname=enca
pkgver=1.16
pkgrel=3
pkgdesc="Charset analyser and converter"
arch=('i386' 'amd64')
url="http://cihar.com/software/enca/"
options=('libtoolfix' '!docs')
license=('GPL')
depends=('recode')
makedepends=('gmake')
source=("http://dl.cihar.com/${pkgname}/${pkgname}-${pkgver}.tar.xz")
md5sums=('0a1483530b1fdb9e2a54786489ee0b69')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --libexecdir=/usr/lib \
              --mandir=/usr/share/man \
              --with-librecode=/usr \
              --enable-external
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  gmake DESTDIR="${pkgdir}" install
}
