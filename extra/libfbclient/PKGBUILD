# Port: databases/firebird25-server
pkgname=libfbclient
pkgver=2.5.6
_pkgver=$pkgver.27020
pkgrel=1
pkgdesc="Client library for Firebird."
arch=('i386' 'amd64')
url="http://www.firebirdsql.org/"
license=('custom')
depends=('libedit' 'icu')
options=('!makeflags')
patchdir=(Firebird-$_pkgver-0)
source=(http://downloads.sourceforge.net/firebird/Firebird-$_pkgver-0.tar.bz2
        LICENSE)
md5sums=('5514798d5dca8c3525525c61cf35283e'
         '0d61798861e159c85dbdf0f30fe2f2ce')

build() {
  cd $srcdir/Firebird-$_pkgver-0

  ./configure --prefix=/usr --with-system-icu --without-fbudf \
    --without-fbsbin --without-fbconf --without-fbdoc --without-fbsample \
    --without-fbsample-db --without-fbintl --without-fbmisc --without-fbhelp \
    --without-fbsecure-db --with-fbmsg=/usr/share/firebird --without-fblog \
    --without-fbglock --without-fbplugins --with-system-editline PTHREAD_LIBS="-lpthread" \
    ac_cv_header_atomic_ops_h="no" ac_cv_lib_atomic_ops_main="no"

  gmake
}

package() {
  cd $srcdir/Firebird-$_pkgver-0

  mkdir -p $pkgdir/usr/{bin,share/{firebird,licenses/$pkgname}}

  cp -R gen/firebird/{lib,include} $pkgdir/usr
  rm -f $pkgdir/usr/lib/libedit.a

  install -m644 gen/firebird/*.msg $pkgdir/usr/share/firebird
  install -m755 gen/firebird/bin/fb_config $pkgdir/usr/bin
  install -m644 ${srcdir}/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

  # Add libgds support FS#30062 FS#30282
  cd $pkgdir/usr/lib
  ln -s libfbclient.so libgds.so.0
  ln -s libfbclient.so libgds.so
}
