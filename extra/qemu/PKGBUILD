pkgbase=qemu

pkgname=('qemu'
         'qemu-arch-extra'
         'qemu-guest-agent'
         'libcacard')
pkgver=2.4.0
pkgrel=3
arch=('amd64')
license=('GPL2' 'LGPL2.1')
url="http://wiki.qemu.org/Index.html"
makedepends=('pixman' 'libjpeg' 'libpng' 'sdl' 'nss' 'glib2'
             'gnutls' 'vde2' 'curl' 'libsasl' 'libgl' 'libpulse' 'usbredir' 
             'snappy' 'libcacard' 'python2')
install=qemu.install
source=(http://wiki.qemu.org/download/${pkgbase}-${pkgver}.tar.bz2
        'qemu-ifdown.sample'
        'qemu-ifup.sample')
replaces=('qemu-kvm')
patchoptions=("-p1")
options=(!strip)
_extra_arches=(aarch64 alpha arm cris lm32 m68k microblaze microblazeel mips
mips64 mips64el mipsel or32 ppc ppc64 s390x
sh4 sh4eb sparc sparc64 moxie ppcemb tricore unicore32 xtensa xtensaeb)
_extra_blob=(QEMU,cgthree.bin QEMU,tcx.bin bamboo.dtb openbios-ppc
openbios-sparc32 openbios-sparc64 palcode-clipper petalogix-ml605.dtb
petalogix-s3adsp1800.dtb ppc_rom.bin s390-ccw.img s390-zipl.rom slof.bin
spapr-rtas.bin u-boot.e500)

build ()
{
  cd ${pkgname}-${pkgver}
  # qemu vs. make 4 == bad
  export ARFLAGS="rv"
  # http://permalink.gmane.org/gmane.comp.emulators.qemu/238740
  export CFLAGS+=' -fPIC'
  # gtk gui breaks keymappings at the moment
  ./configure --prefix=/usr --sysconfdir=/etc --audio-drv-list='pa sdl' \
              --python=/usr/bin/python2 --smbd=/usr/bin/smbd \
              --enable-docs --libexecdir=/usr/libexec/qemu \
              --disable-gtk  --disable-linux-user --disable-linux-aio \
              --disable-kvm --disable-xen \
              --localstatedir=/var \
              --enable-tpm --enable-modules --enable-curl
  gmake V=99

}

package_qemu() {
  pkgdesc='A generic and open source processor emulator which achieves a good emulation speed by using dynamic translation'
  depends=('libc' 'pixman' 'libjpeg' 'libpng' 'sdl' 'nss' 'glib2'
           'gnutls' 'vde2' 'libsasl' 'libgl'
           'libcacard' 'usbredir' 'snappy' 'gcc-libs' 'nspr'
           'ncurses' 'libx11' 'libpulse' 'libssh2' 'curl')
  replaces=('qemu-kvm')
  optdepends=('samba: SMB/CIFS server support'
              'qemu-arch-extra: extra architectures support')
  options=(!strip)
  install=qemu.install

  gmake -C ${pkgname}-${pkgver} DESTDIR="${pkgdir}" libexecdir="/usr/libexec/qemu" install

  cd "${pkgdir}"

  # provided by seabios package
  rm usr/share/qemu/bios.bin
  rm usr/share/qemu/acpi-dsdt.aml
  rm usr/share/qemu/q35-acpi-dsdt.aml
  rm usr/share/qemu/bios-256k.bin
  rm usr/share/qemu/vgabios-cirrus.bin
  rm usr/share/qemu/vgabios-qxl.bin
  rm usr/share/qemu/vgabios-stdvga.bin
  rm usr/share/qemu/vgabios-vmware.bin

  # remove conflicting /var/run directory
  rm -r var

  # remove libcacard files
  rm -r usr/include/cacard
  rm usr/lib/libcacard*
  rm usr/lib/pkgconfig/libcacard.pc
  rm usr/bin/vscclient

  # remove guest agent
  rm usr/bin/qemu-ga

  # remove extra arch
  for _arch in "${_extra_arches[@]}"; do
    rm -f usr/bin/qemu-${_arch} usr/bin/qemu-system-${_arch}
  done
  for _blob in "${_extra_blob[@]}"; do
    rm usr/share/qemu/${_blob}
  done
}

package_qemu-arch-extra() {
  pkgdesc='QEMU with full support for non x86 architectures'
  depends=('libc' 'gcc-libs' 'glib2' 'qemu')
  options=(!strip)

  cd qemu-${pkgver}
  install -dm755 "${pkgdir}"/usr/bin
  for _arch in "${_extra_arches[@]}"; do
    install -m755 ${_arch}-*/qemu-*${_arch} "${pkgdir}"/usr/bin
  done

  cd pc-bios
  install -dm755 "${pkgdir}"/usr/share/qemu
  for _blob in "${_extra_blob[@]}"; do
    install -m644 ${_blob} "${pkgdir}"/usr/share/qemu/${_blob}
  done

  # manually stripping
  find "${pkgdir}"/usr/bin -type f -exec strip {} \;
}

package_qemu-guest-agent() {
  pkgdesc='QEMU Guest Agent'
  depends=('libc' 'gcc-libs' 'glib2')

  install -dm755 "${pkgdir}"/usr/bin/
  install qemu-${pkgver}/qemu-ga "${pkgdir}"/usr/bin/qemu-ga
}

package_libcacard() {
 pkgdesc='Common Access Card (CAC) Emulation'
 depends=('libc' 'nss' 'nspr' 'glib2')

  cd "${pkgdir}"
  install -d usr/{bin,lib/pkgconfig,include/cacard}
  cp -a "${srcdir}"/qemu-${pkgver}/libcacard/*.h usr/include/cacard/
  cp -a "${srcdir}"/qemu-${pkgver}/libcacard.pc usr/lib/pkgconfig/
  cp -a "${srcdir}"/qemu-${pkgver}/.libs/vscclient usr/bin/
  cp -a "${srcdir}"/qemu-${pkgver}/.libs/libcacard.so* usr/lib/
}


# vim:set ts=2 sw=2 et:

md5sums=('186ee8194140a484a455f8e3c74589f4'
         'e5d8e78fc4ca3e06efc7829c15aed957'
         'e5d8e78fc4ca3e06efc7829c15aed957')
