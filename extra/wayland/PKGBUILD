# $Id: PKGBUILD 193712 2013-08-28 20:08:20Z lcarlier $
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: SÃ©bastien Luttringer
# Contributor: Joel Teichroeb <joel@teichroeb.net>

pkgname=wayland
pkgver=1.2.1
pkgrel=1
pkgdesc='A computer display server protocol'
arch=('i686' 'x86_64')
url='http://wayland.freedesktop.org'
license=('MIT')
depends=('libffi' 'expat')
makedepends=('doxygen')
options=(!libtool)
patches=(patch-1
	patch-2
	patch-3
	patch-4
	patch-5
	patch-6
	patch-7
	patch-8
	patch-9
	patch-10
	patch-11
	patch-configure)
source=("http://wayland.freedesktop.org/releases/$pkgname-$pkgver.tar.xz"
	${patches[@]})
sha1sums=('2214b690cb5a4f9695d287f27730c4368e6ef829'
          '32a97cd28905e798f6a32503da25ac682c6c5432'
          '7f5d2236f2e5af0cbe3a90c1eb9097fc3a71f8d4'
          '61fadf468250df76d522cb67e6e4636dbb2bebf0'
          '53bce43e78510e7b24154c155bcf5f2fdb94e4c1'
          'e4e73b846df2413fc55519adab45c3ff667061bd'
          'be10b5d1bd37aaccdaef234e37f778eab42afe56'
          '5498ed79d3d688c2c9c45019e81fe64c3bfb0f5e'
          'd4de78905ba626d2e04575c2405301485dac6103'
          '14f31641b900b91937a885f981399744d043ed2b'
          '0aada9eb18398fe735331707a09c4481673702ac'
          '93aaca28d2f253007ef7b8071a5f4ca2d3bf0548'
          '2d04e9fc9c53d6ddc80ed4df8099029957cd98ab')

prepare() {
  cd $pkgname-$pkgver

  for i in ${patches[@]}; do
	msg "Applying patch $i"
	patch -p1 -i ${srcdir}/$i
  done

  aclocal
  automake --add-missing
  autoreconf

}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
    --disable-static
  gmake
}

package() {
  cd $pkgname-$pkgver

  gmake DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"  
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
