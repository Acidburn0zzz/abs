pkgbase=python-lxml
pkgname=('python-lxml' 'python2-lxml' 'python-lxml-docs')
pkgver=3.4.4
pkgrel=2
arch=('i686' 'amd64')
license=('BSD' 'custom')
url="http://lxml.de/"
makedepends=('libxslt' 'python-cssselect' 'python2-cssselect')
source=(http://pypi.python.org/packages/source/l/lxml/lxml-${pkgver}.tar.gz{,.asc})
md5sums=('a9a65972afc173ec7a39c585f4eea69c'
         'SKIP')
validpgpkeys=('1737D5FB8DACC53CA96A77AB0D3D536908D3A01E')  # Stefan Behnel

prepare() {
  cp -a lxml-$pkgver{,-py2}
}

build() {
  cd lxml-$pkgver
  python setup.py build

  cd ../lxml-$pkgver-py2
  python2 setup.py build
}

# Temporary commented due a conflict with devtools and lxml
check() {
  cd lxml-$pkgver
  # Workaround encoding issue
  export LC_ALL=en_US.UTF-8
  make PYTHON=python2 test
  make PYTHON=python test
}

package_python2-lxml() {
  pkgdesc="Python2 binding for the libxml2 and libxslt libraries"
  depends=('python2' 'libxslt')
  optdepends=('python2-beautifulsoup3: support for parsing not well formed HTML'
              'python-lxml-docs: offline docs')

  cd lxml-$pkgver-py2
  python2 setup.py install --root="${pkgdir}" --skip-build --optimize=1

  install -dm755 "${pkgdir}"/usr/share/licenses/$pkgname 
  install -m644 LICENSES.txt \
    "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
  install -m644 doc/licenses/BSD.txt \
    "${pkgdir}"/usr/share/licenses/$pkgname/BSD.txt
  install -m644 doc/licenses/elementtree.txt \
    "${pkgdir}"/usr/share/licenses/$pkgname/elementtree.txt
}

package_python-lxml() {
  pkgdesc="Python3 binding for the libxml2 and libxslt libraries"
  depends=('python' 'libxslt')
  optdepends=('python-lxml-docs: offline docs')

  cd lxml-$pkgver
  python setup.py install --root="${pkgdir}" --skip-build --optimize=1
  
  install -dm755 "${pkgdir}"/usr/share/licenses/$pkgname
  install -m644 LICENSES.txt \
    "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
  install -m644 doc/licenses/BSD.txt \
    "${pkgdir}"/usr/share/licenses/$pkgname/BSD.txt
  install -m644 doc/licenses/elementtree.txt \
    "${pkgdir}"/usr/share/licenses/$pkgname/elementtree.txt
}

package_python-lxml-docs() {
  pkgdesc="Python binding for the libxml2 and libxslt libraries (docs)"
  options=('docs')

  cd lxml-$pkgver
  install -d "${pkgdir}"/usr/share/doc/${pkgbase}
  cp -r doc/html "${pkgdir}"/usr/share/doc/${pkgbase}

  install -dm755 "${pkgdir}"/usr/share/licenses/$pkgname
  install -m644 LICENSES.txt \
    "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
  install -m644 doc/licenses/BSD.txt \
    "${pkgdir}"/usr/share/licenses/$pkgname/BSD.txt
  install -m644 doc/licenses/elementtree.txt \
    "${pkgdir}"/usr/share/licenses/$pkgname/elementtree.txt
}

