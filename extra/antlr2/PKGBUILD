# $Id: PKGBUILD 206268 2014-02-23 10:52:52Z guillaume $
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: George Giorgidze <giorgidze (at) gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgbase=antlr2
pkgname=('antlr2' 'python2-antlr2')
pkgver=2.7.7
pkgrel=9
arch=('i386' 'amd64')
url="http://www.antlr2.org/"
license=('custom')
makedepends=('java-environment' 'python2' 'freebsd-world')
options=('staticlibs')
port_patches=(
	patch-configure
	patch-lib-cpp-antlr-ANTLRException.hpp
	patch-lib-cpp-antlr-CharBuffer.hpp
	patch-lib-cpp-src-ASTFactory.cpp
)
source=("http://www.antlr2.org/download/antlr-${pkgver}.tar.gz"
        'runantlr2.sh'
        ${port_patches[@]})
md5sums=('01cc9a2a454dd33dcd8c856ec89af090'
         'e4aa827cf78f97c4e5fae086831192d6'
         'f4244dc07721abb871aafd5e0017a638'
         'ca97187ad548083e40f7ef5b3bfe76c2'
         'fffbefdf816f3ada7cea6b7008d1921a'
         'e32f311ee874734502296943c16c4e0a')

prepare() {
  cd "${srcdir}/antlr-${pkgver}"
  for i in "${port_patches[@]}"; do
    patch -p0 -i "${srcdir}/${i}"
  done
}

build() {
  cd "${srcdir}/antlr-${pkgver}"

  ./configure --prefix=/usr \
    --disable-examples \
    --disable-csharp
  gmake
}

package_antlr2() {
  pkgdesc='Language recognition tool providing a framework to build grammatical parsers'
  depends=('java-runtime' 'sh')
  provides=("antlr=${pkgver}")

  cd "${srcdir}"/antlr-${pkgver}/lib/cpp
  gmake prefix="${pkgdir}/usr" install

  install -dm755 "${pkgdir}"/usr/share/java
  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -dm755 "${pkgdir}"/usr/bin
  install -m644 ../../antlr/antlr.jar "${pkgdir}"/usr/share/java/antlr2.jar
  install -m755 "${srcdir}"/runantlr2.sh "${pkgdir}"/usr/bin/runantlr2
  install -m644 "${srcdir}"/antlr-${pkgver}/LICENSE.txt \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-antlr2() {
  pkgdesc='Language recognition tool providing a framework to build grammatical parsers'
  depends=('antlr2' 'python2')
  provides=("python-antlr=${pkgver}")

  cd "${srcdir}"/antlr-${pkgver}/lib/python
  python2 setup.py install --root="${pkgdir}" --optimize=1

  install -d "${pkgdir}"/usr/share/licenses
  ln -s /usr/share/licenses/antlr2 "${pkgdir}"/usr/share/licenses/${pkgname}
}
