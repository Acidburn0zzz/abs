# $Id: PKGBUILD 180848 2013-03-26 23:07:40Z tomegun $
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgname=libmtp
pkgver=1.1.10
pkgrel=2
pkgdesc="Library implementation of the Media Transfer Protocol"
arch=('i386' 'amd64')
url="http://libmtp.sourceforge.net"
license=('LGPL')
depends=('libgcrypt')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('a82e999363c2504025fb50d2e095ec23')

prepare() {
  sed -i ''  -e	'/if test/s|==|=| ; /LIBUSB_REQUIRES/s|libusb-1.0|| ; /endian.h/s|machine|sys|' "${srcdir}/${pkgname}-${pkgver}"/configure
  sed -i '' -e \
		's|<libusb-1.0/|<|' "${srcdir}/${pkgname}-${pkgver}"/src/libusb-glue.h
  sed -i '' -e \
		's|__builtin_bswap|bswap|g' "${srcdir}/${pkgname}-${pkgver}"/src/mtpz.c
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
  LIBUSB_CFLAGS="-I/usr/include" LIBUSB_LIBS="-lusb" LIBS="-lusb"

  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
