# $Id: PKGBUILD 200260 2013-11-24 00:08:07Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Contributor: Ben <ben@benmazer.net>

pkgname=mpd
pkgver=0.19.10
pkgrel=1
pkgdesc='Flexible, powerful, server-side application for playing music'
url='http://www.musicpd.org/'
license=('GPL')
arch=('i386' 'amd64')
depends=('libao' 'ffmpeg' 'libmodplug' 'audiofile' 'libshout' 'libmad' 'curl' 'faad2' 'boost'
         'sqlite' 'jack' 'libmms' 'wavpack' 'avahi' 'libid3tag' 'yajl' 'libmpdclient' 'libpulse')
makedepends=('doxygen')
source=("http://www.musicpd.org/download/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig}
        'conf'
        'musicpd.in')

backup=('etc/mpd.conf')
install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--enable-libmpdclient \
		--enable-jack \
		--enable-soundcloud \
		--enable-pipe-output \
		--enable-pulse \
		--disable-sidplay \
		--disable-alsa --disable-mpc --disable-soundcloud 
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -dm755 "${pkgdir}"/etc/rc.d
	install -m644 ../conf "${pkgdir}"/etc/mpd.conf
	install -m755 ../musicpd.in "${pkgdir}"/etc/rc.d/mpd
	install -d -g 45 -o 45 "${pkgdir}"/var/lib/mpd/playlists
}
sha1sums=('4635b33223d3f1e67eed41f6350ef75515153213'
          'SKIP'
          'c00ede9cbee2fa8650c9bfe17950165388f9e18a'
          '1fcd58a95793ed1de3eb7c15c4914b4937bba4b3')

