# $Id: PKGBUILD 137720 2011-09-10 13:53:53Z pierre $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=aiksaurus
pkgver=1.2.1
pkgrel=11
pkgdesc="A cross-platform, open-source thesaurus"
url="http://aiksaurus.sourceforge.net/"
license=('GPL')
arch=('i386' 'amd64')
options=('!libtool')
depends=('gtk2')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'aiksaurus-gcc44.patch')
md5sums=('3eae03b7c49843ccc9262e52846ea6b4'
         '7f94749172116b1529fc21b718ee7636')

prepare() {
  sed -i '' -e "/^ltmain=/!s|\$$ac_aux_dir/ltmain.sh|/usr/share/libtool/config/ltmain.sh|g" \
  	  -e "/^LIBTOOL=/s|\$$\(top_builddir\)/libtool|/usr/bin/libtool|g" \
	  ${srcdir}/${pkgname}-${pkgver}/aclocal.m4
  find ${srcdir}/${pkgname}-${pkgver} -name "Makefile.in" | xargs sed -i '' -e 's|^LIBTOOL[ ]*=.*|LIBTOOL=/usr/bin/libtool|g'
}
build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  sed -i '' -e 's|echo aout|echo elf|g' \
	    	configure
  # gcc44 build patch
  patch -Np0 -i ${srcdir}/aiksaurus-gcc44.patch

  ./configure --prefix=/usr --with-gtk
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make -j1 DESTDIR=${pkgdir} install
}
