# $Id: PKGBUILD 178957 2013-03-01 10:20:25Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=grantlee
pkgver=0.5.1
pkgrel=1
pkgdesc="A string template engine based on the Django template system and written in Qt"
arch=('i386' 'amd64')
url="http://www.gitorious.org/grantlee/pages/Home"
license=('LGPL3')
depends=('qt4')
makedepends=('cmake' 'doxygen')
source=("http://downloads.grantlee.org/${pkgname}-${pkgver}.tar.gz")
md5sums=('775f22dac0953029b414ed3b7379098c')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}

