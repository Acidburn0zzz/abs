#
# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=git
pkgver=1.9.1
pkgrel=1
pkgdesc="the fast distributed version control system"
arch=(i686 x86_64)
url="http://git-scm.com/"
license=('GPL2')
depends=('curl' 'expat>=2.0' 'perl-error' 'perl>=5.14.0' 'pcre')
makedepends=('python2' 'libgnome-keyring' 'xmlto' 'asciidoc' 'getopt') # 'emacs')
optdepends=('tk: gitk and git gui'
            'perl-libwww: git svn'
            'perl-term-readkey: git svn'
            'perl-mime-tools: git send-email'
            'perl-net-smtp-ssl: git send-email TLS support'
            'perl-authen-sasl: git send-email TLS support'
            'python2: various helper scripts'
            'subversion: git svn'
            'cvsps: git cvsimport'
            'gnome-keyring: GNOME keyring credential helper')
replaces=('git-core')
provides=('git-core')
backup=('etc/conf.d/git-daemon')
install=git.install
source=("https://www.kernel.org/pub/software/scm/git/git-$pkgver.tar.xz"
        init.d-git-daemon
        conf.d-git-daemon
        git_daemon.in)

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  gsed -i -e 's/-liconv//' Makefile
}

build() {
  export PYTHON_PATH='/usr/bin/python2'
  cd "$srcdir/$pkgname-$pkgver"

  gmake prefix=/usr gitexecdir=/usr/lib/git-core \
    CFLAGS="$CFLAGS" LDFLAGS="$LDFLAGS" \
    USE_LIBPCRE=1 \
    NO_CROSS_DIRECTORY_HARDLINKS=1 \
    MAN_BOLD_LITERAL=1 \
    all doc

  #gmake -C contrib/emacs prefix=/usr
  gmake -C contrib/credential/gnome-keyring
}

check() {
  export PYTHON_PATH='/usr/bin/python2'
  cd "$srcdir/$pkgname-$pkgver"
  local jobs
  #jobs=$(expr "$MAKEFLAGS" : '.*\(-j[0-9]*\).*')
  # build failures with multiple jobs in check, not ideal...
  jobs='-j1'
  mkdir -p "$srcdir/git-test"
  # We used to use this, but silly git regressions:
  #GIT_TEST_OPTS="--root=/dev/shm/" \
  # http://comments.gmane.org/gmane.comp.version-control.git/202020
  gmake prefix=/usr gitexecdir=/usr/lib/git-core \
    CFLAGS="$CFLAGS" LDFLAGS="$LDFLAGS" \
    USE_LIBPCRE=1 \
    NO_CROSS_DIRECTORY_HARDLINKS=1 \
    MAN_BOLD_LITERAL=1 \
    NO_SVN_TESTS=y \
    DEFAULT_TEST_TARGET=prove \
    GIT_PROVE_OPTS="$jobs -Q" \
    GIT_TEST_OPTS="--root=$srcdir/git-test" \
    test
}

package() {
  export PYTHON_PATH='/usr/bin/python2'
  cd "$srcdir/$pkgname-$pkgver"
  gmake -C contrib/credential/gnome-keyring
  gmake prefix=/usr gitexecdir=/usr/lib/git-core \
    CFLAGS="$CFLAGS" LDFLAGS="$LDFLAGS" \
    USE_LIBPCRE=1 \
    NO_CROSS_DIRECTORY_HARDLINKS=1 \
    MAN_BOLD_LITERAL=1 \
    INSTALLDIRS=vendor DESTDIR="$pkgdir" install install-doc

  # bash completion
  mkdir -p "$pkgdir"/usr/share/bash-completion/completions/
  install -m644 ./contrib/completion/git-completion.bash "$pkgdir"/usr/share/bash-completion/completions/git
  # fancy git prompt
  mkdir -p "$pkgdir"/usr/share/git/
  install -m644 ./contrib/completion/git-prompt.sh "$pkgdir"/usr/share/git/git-prompt.sh
  # emacs
  #gmake -C contrib/emacs prefix=/usr DESTDIR="$pkgdir" install
  # gnome credentials helper
  install -m755 contrib/credential/gnome-keyring/git-credential-gnome-keyring \
      "$pkgdir"/usr/lib/git-core/git-credential-gnome-keyring
  gmake -C contrib/credential/gnome-keyring clean
  # the rest of the contrib stuff
  cp -R ./contrib/* $pkgdir/usr/share/git/

  # scripts are for python 2.x
  gsed -i 's|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|' \
    $(find "$pkgdir" -name '*.py') \
    "$pkgdir"/usr/lib/git-core/git-p4 \
    "$pkgdir"/usr/share/git/gitview/gitview \
    "$pkgdir"/usr/share/git/remote-helpers/git-remote-bzr \
    "$pkgdir"/usr/share/git/remote-helpers/git-remote-hg
  gsed -i 's|#![ ]*/usr/bin/python$|#!/usr/bin/python2|' \
    "$pkgdir"/usr/share/git/svn-fe/svnrdump_sim.py

  # remove perllocal.pod, .packlist, and empty directories.
  rm -rf "$pkgdir"/usr/lib/perl5

  # git daemon script
    install -dm755 "$pkgdir"/etc/rc.d
    install -dm755 "$pkgdir"/etc/init.d
    install -dm755 "$pkgdir"/etc/conf.d
    install -m755 "$srcdir"/git_daemon.in "$pkgdir"/etc/rc.d/git_daemon
    install -m755 "$srcdir"/init.d-git-daemon "$pkgdir"/etc/init.d/git-daemon
    install -m644 "$srcdir"/conf.d-git-daemon "$pkgdir"/etc/conf.d/git-daemon
}

md5sums=('d65ca55041898c226e532425fc673035'
         'fc18331ec12a3529216f3b45f385b831'
         '66fb74fb87bfe83b7023e1d82bbf7f24'
         '6295012674e1cc4df42944145d00c415')
