# Port: devel/git
pkgname=git
pkgver=2.16.2
pkgrel=1
pkgdesc="the fast distributed version control system"
arch=('amd64')
url="http://git-scm.com/"
license=('GPL2')
depends=('curl' 'expat>=2.0' 'perl-error' 'perl>=5.14.0' 'pcre' 'libiconv')
makedepends=('python2' 'python' 'xmlto' 'asciidoc' 'getopt'
             'gettext-tools')
optdepends=('tk: gitk and git gui'
            'perl-libwww: git svn'
            'perl-term-readkey: git svn'
            'perl-mime-tools: git send-email'
            'perl-net-smtp-ssl: git send-email TLS support'
            'python2: various helper scripts'
            'subversion: git svn')
replaces=('git-core')
provides=('git-core')
backup=('etc/conf.d/git-daemon')
install=git.install
source=("https://www.kernel.org/pub/software/scm/git/git-$pkgver.tar.xz"
        'git_daemon.in'
        'git_daemon.initd'
        'git_daemon.confd')
sha256sums=('5560578bd21468d98637f41515c165d32f69caff0838b8989dee5ce10022c717'
            '77bf08cc035dea8b2b2c0075ceda4832251f197d659417bcb835bcb74429ed77'
            '9d2278b7bfb2dc78c7e4d8b1ebd60db5bee53bc1a8d5115da00ce8d55f5c32d3'
            '9825be4de7a69233f85b08cbaa4260707eb84cd4e7a8ef275c7b007e70c357e4')

build() {
  export PYTHON_PATH='/usr/bin/python2'
  cd "$srcdir/$pkgname-$pkgver"

  gsed -i 's|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|' \
	$(find "$srcdir" -name '*.py')

  gmake prefix=/usr gitexecdir=/usr/libexec/git-core \
    CFLAGS="$CFLAGS" LDFLAGS="$LDFLAGS" \
    USE_LIBPCRE=1 \
    NO_CROSS_DIRECTORY_HARDLINKS=1 \
    MAN_BOLD_LITERAL=1 \
		PERL_PATH=/usr/bin/perl \
    all doc

#  gmake -C contrib/credential/gnome-keyring
  gmake -C contrib/subtree prefix=/usr gitexctdir=/usr/lib/git-core all doc
}

check() {
  export PYTHON_PATH='/usr/bin/python2'
  cd "$srcdir/$pkgname-$pkgver"
  local jobs
  #jobs=$(expr "$MAKEFLAGS" : '.*\(-j[0-9]*\).*')
  # build failures with multiple jobs in check, not ideal...
  jobs=$(expr"$MAKEFLAGS" : '.*\(-j[0-9]*\).*') || true
  mkdir -p "$srcdir/git-test"
  # explictly specify SHELL toavoid a texst failurein t/t9903-bash-prompt.sh
  # which is caused by 'git rebase' trying to use builuser's SHELLinsid the
  # build jail
  SHELL=/bin/sh\
  gmake prefix=/usr gitexecdir=/usr/libexec/git-core \
    CFLAGS="$CFLAGS" LDFLAGS="$LDFLAGS" \
    USE_LIBPCRE=1 \
    NO_CROSS_DIRECTORY_HARDLINKS=1 \
    MAN_BOLD_LITERAL=1 \
    NO_SVN_TESTS=y \
    DEFAULT_TEST_TARGET=prove \
    GIT_PROVE_OPTS="$jobs -Q" \
    GIT_TEST_OPTS="--root=$srcdir/git-test" \
    PERL_PATH=/usr/bin/perl test || true
}

package() {
  export PYTHON_PATH='/usr/bin/python2'
  cd "$srcdir/$pkgname-$pkgver"

#  gmake -C contrib/credential/gnome-keyring

  for file in $(find "$srcdir" -name '*.py'); do
	gsed -i 's|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|' \
	${file}
  done

  gmake -j1 prefix=/usr gitexecdir=/usr/libexec/git-core \
    CFLAGS="$CFLAGS" LDFLAGS="$LDFLAGS" \
    PYTHON=/usr/bin/python2 \
    USE_LIBPCRE=1 \
    NO_CROSS_DIRECTORY_HARDLINKS=1 \
    MAN_BOLD_LITERAL=1 \
		PERL_PATH=/usr/bin/perl \
    INSTALLDIRS=vendor DESTDIR="$pkgdir" install

  # bash completion
  mkdir -p "$pkgdir"/usr/share/bash-completion/completions/
  install -m644 ./contrib/completion/git-completion.bash "$pkgdir"/usr/share/bash-completion/completions/git

  # fancy git prompt
  mkdir -p "$pkgdir"/usr/share/git/
  install -m644 ./contrib/completion/git-prompt.sh "$pkgdir"/usr/share/git/git-prompt.sh

  # the rest of the contrib stuff
  cp -R ./contrib/* $pkgdir/usr/share/git/

  # scripts are for python 2.x
#  gsed -i 's|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|' \
#    $(find "$pkgdir" -name '*.py') \
#    "$pkgdir"/usr/libexec/git-core/git-p4 \
#    "$pkgdir"/usr/share/git/gitview/gitview \
#    "$pkgdir"/usr/share/git/remote-helpers/git-remote-bzr \
#    "$pkgdir"/usr/share/git/remote-helpers/git-remote-hg
#  gsed -i 's|#![ ]*/usr/bin/python$|#!/usr/bin/python2|' \
#    "$pkgdir"/usr/share/git/svn-fe/svnrdump_sim.py

  # remove perllocal.pod, .packlist, and empty directories.
  rm -rf "$pkgdir"/usr/lib/perl5

  # git daemon script
    install -dm755 "$pkgdir"/etc/{rc.d,init.d,conf.d}
    install -m755 "$srcdir"/git_daemon.initd "$pkgdir"/etc/init.d/git-daemon
    install -m755 "$srcdir"/git_daemon.confd "$pkgdir"/etc/conf.d/git-daemon
    install -m755 "$srcdir"/git_daemon.in "$pkgdir"/etc/rc.d/git-daemon
}
