# Port: x11/xfce4-terminal
pkgname=xfce4-terminal
pkgver=0.8.4
pkgrel=1
pkgdesc="A modern terminal emulator primarly for the Xfce desktop environment"
arch=('amd64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'vte3' 'hicolor-icon-theme')
makedepends=('intltool')
conflicts=('terminal')
replaces=('terminal')
options=('gnu_configure')
source=("http://archive.xfce.org/src/apps/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('c5c1163b30e7a43d56ff92a25193bf9f29ce60e6cf43e5988530df79c84cfdc8')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/libexec/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --disable-debug \
    --without-gnome-default-appsdir
  gmake
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
