# $Id: PKGBUILD 173707 2012-12-21 16:29:38Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=freetype2
pkgver=2.5.3
pkgrel=8
pkgdesc="TrueType font rendering library"
arch=(i686 x86_64)
license=('GPL')
url="http://freetype.sourceforge.net"
options=('libtoolfix')
install=libtool-unfixed.install
port_patches=(
	patch-builds_unix_detect.mk
)
# harfbuzz<->freetype cycle
# freetype can be built first
depends=('zlib' 'libpng')
if ! ((BREAK_DEPENDENCY_CYCLES)); then
  depends+=('harfbuzz')
fi
source=(http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2
	freetype-2.2.1-enable-valid.patch
	fix_segfault_with_harfbuzz.diff
	freetype-2.5.1-enable-spr.patch
)
md5sums=('d6b60f06bfc046e43ab2a6cbfd171d65'
         '214119610444c9b02766ccee5e220680'
         '58adb4c2ecf7a060e56341f236445cc4'
         '80a14cce234f3f190cd936ca9060c398')

prepare() {
  cd "${srcdir}/freetype-${pkgver}"

  patch -Np1 -i "${srcdir}/freetype-2.2.1-enable-valid.patch"
  patch -Np1 -i "${srcdir}/freetype-2.5.1-enable-spr.patch"

  # fix segfaults # https://bugs.archlinux.org/task/39365
  # http://git.savannah.gnu.org/cgit/freetype/freetype2.git/commit/?id=23367ff97f33ef6a2b7e1fced1157c87a46d9596
  patch -Np1 -i "${srcdir}/fix_segfault_with_harfbuzz.diff"

  # Disabled for now due to resistance
  # Kept here for easier rebuilds via ABS
  # https://bugs.archlinux.org/task/35274
  #patch -Np1 -i "${srcdir}/freetype-2.5.1-enable-sph.patch"

}

build() {
  cd "${srcdir}/freetype-${pkgver}"
  ./configure --prefix=/usr --disable-static
  gmake -j1
}

check() {
  cd "${srcdir}/freetype-${pkgver}"
  gmake -k check
}

package() {
  cd "${srcdir}/freetype-${pkgver}"

  gmake -j1 DESTDIR="${pkgdir}" install

  install -dm755 "$pkgdir"/usr/lib/unfixed
  ln -s /usr/lib/libfreetype.so.6 "$pkgdir"/usr/lib/unfixed/libfreetype.so.17
  install -dm755 "$pkgdir"/usr/lib/ldconfig
  echo '/usr/lib/unfixed' > "$pkgdir"/usr/lib/ldconfig/freetype-unfixed
}
