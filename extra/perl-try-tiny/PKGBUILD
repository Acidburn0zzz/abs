# Maintainer: Justin Davis <jrcd83@gmail.com>
# $Id: PKGBUILD 180376 2013-03-20 18:37:55Z eric $

pkgname=perl-try-tiny
pkgver=0.22
pkgrel=2
pkgdesc="Minimal try/catch with proper localization of \$@"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl)
url="http://search.cpan.org/dist/Try-Tiny"
source=("http://search.cpan.org/CPAN/authors/id/D/DO/DOY/Try-Tiny-${pkgver}.tar.gz")
sha1sums=('3b4f180a97ac0b3fb5b6b54b0f7adeb33e193a34')

build() {
  cd Try-Tiny-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Try-Tiny-${pkgver}
  make test
}

package() {
  cd Try-Tiny-${pkgver}
  make DESTDIR="$pkgdir" install
  install -dm755  "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

