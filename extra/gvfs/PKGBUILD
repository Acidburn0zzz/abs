# $Id: PKGBUILD 199075 2013-11-08 14:49:13Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gvfs
pkgname='gvfs'
pkgver=1.26.1.1
pkgrel=10
arch=('i386' 'amd64')
license=('LGPL')
makedepends=('avahi' 'dbus-glib' 'intltool' 'libcdio-paranoia' 'libgphoto2' 'libimobiledevice' 'libsoup' 'smbclient' 'docbook-xsl' 'gtk3' 'libmtp' 'gcr' 'samba')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgbase/${pkgver:0:4}/$pkgbase-$pkgver.tar.xz)
sha256sums=('2dadf986e464db1d7f007b178a5432b30ba93ef0cb31a2795156540d496dc06d')

build() {
  cd "$pkgbase-$pkgver"
  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --disable-gudev \
      --disable-libsystemd-login \
      --disable-udisks2 \
      --disable-gdu \
      --disable-udev \
      --disable-goa \
      --disable-keyring \
      --disable-libmtp \
      --disable-gtk \
      --libexecdir=/usr/libexec/gvfs \
      --with-bash-completion-dir=/usr/share/bash-completion/completions \
      --disable-obexftp --disable-bluray --disable-afc LIBS="-lintl"
  gmake
}

package_gvfs() {
  pkgdesc="Userspace virtual filesystem implemented as a pluggable module for gio"
  depends=('avahi' 'dconf' 'libarchive' 'libcdio-paranoia' 'libsoup' 'libsecret' 'libbluray')
  install=gvfs.install

  cd "$pkgbase-$pkgver"
  gsed -e 's/^am__append_4/#am__append_4/' \
      -e 's/^am__append_5/#am__append_5/' \
      -e 's/^am__append_6/#am__append_6/' \
      -e 's/^am__append_7/#am__append_7/' \
      -i monitor/Makefile
  gmake DESTDIR="$pkgdir" install

}

