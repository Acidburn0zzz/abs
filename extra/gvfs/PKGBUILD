# $Id: PKGBUILD 199075 2013-11-08 14:49:13Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gvfs
pkgname=('gvfs' 'gvfs-smb' 'gvfs-afp')
pkgver=1.26.1.1
pkgrel=1
arch=('i386' 'amd64')
license=('LGPL')
makedepends=('avahi' 'dbus-glib' 'intltool' 'libarchive' 'libcdio-paranoia' 'libgphoto2' 'libimobiledevice' 'libsoup' 'smbclient' 'docbook-xsl' 'gtk3' 'libmtp')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgbase/${pkgver:0:4}/$pkgbase-$pkgver.tar.xz)
sha256sums=('2dadf986e464db1d7f007b178a5432b30ba93ef0cb31a2795156540d496dc06d')

build() {
  cd "$pkgbase-$pkgver"
  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --disable-gudev \
      --disable-libsystemd-login \
      --disable-udisks2 \
      --disable-gdu \
      --disable-udev \
      --disable-goa \
      --disable-keyring \
      --disable-libmtp \
      --disable-gtk \
      --libexecdir=/usr/libexec/gvfs \
      --with-bash-completion-dir=/usr/share/bash-completion/completions \
      --disable-obexftp --disable-bluray --disable-afc LIBS="-lintl"
  gmake
}

package_gvfs() {
  pkgdesc="Userspace virtual filesystem implemented as a pluggable module for gio"
  depends=('avahi' 'dconf' 'libarchive' 'libcdio-paranoia' 'libsoup' 'libsecret' 'libbluray')
  replaces=('gvfs-obexftp')
  optdepends=('gvfs-afc: AFC (mobile devices) support'
              'gvfs-smb: SMB/CIFS (Windows client) support'
              'gvfs-gphoto2: gphoto2 (PTP camera/MTP media player) support'
              'gvfs-afp: Apple Filing Protocol (AFP) support'
              'gvfs-mtp: MTP device support'
              'gtk3: Recent files support')
  install=gvfs.install

  cd "$pkgbase-$pkgver"
  gsed -e 's/^am__append_4/#am__append_4/' \
      -e 's/^am__append_5/#am__append_5/' \
      -e 's/^am__append_6/#am__append_6/' \
      -e 's/^am__append_7/#am__append_7/' \
      -i monitor/Makefile
  gmake DESTDIR="$pkgdir" install

  cd "$pkgdir"
  rm usr/libexec/gvfs/gvfsd-{smb,smb-browse,afp,afp-browse}
  rm usr/share/gvfs/mounts/{smb,smb-browse,afp,afp-browse}.mount
  rm usr/share/glib-2.0/schemas/org.gnome.system.smb.gschema.xml
  rm usr/share/GConf/gsettings/gvfs-smb.convert
}

package_gvfs-smb() {
  pkgdesc="SMB/CIFS (Windows client) backend for gvfs"
  depends=("gvfs=$pkgver" 'smbclient')
  install=gvfs-smb.install

  cd "$pkgbase-$pkgver/daemon"
  install -m755 -d "$pkgdir/usr/libexec/gvfs"
  install -m755 -d "$pkgdir/usr/share/gvfs/mounts"

  install -m755 .libs/gvfsd-smb{,-browse} "$pkgdir/usr/libexec/gvfs/"
  install -m644 smb{,-browse}.mount "$pkgdir/usr/share/gvfs/mounts/"

  install -dm755  "$pkgdir/usr/share/glib-2.0/schemas"
  install -m644 org.gnome.system.smb.gschema.xml \
    "$pkgdir/usr/share/glib-2.0/schemas/org.gnome.system.smb.gschema.xml"
  install -dm755  "$pkgdir/usr/share/GConf/gsettings"
  install -m644 gvfs-smb.convert \
    "$pkgdir/usr/share/GConf/gsettings/gvfs-smb.convert"
}

package_gvfs-afp() {
  pkgdesc="Apple Filing Protocol (AFP) backend for gvfs"
  depends=("gvfs=$pkgver")
  install=gvfs-module.install
  install -m755 -d "$pkgdir/usr/libexec/gvfs"
  install -m755 -d "$pkgdir/usr/share/gvfs/mounts"

  cd "$pkgbase-$pkgver/daemon"
  install -m755 .libs/gvfsd-afp{,-browse} "$pkgdir/usr/libexec/gvfs/"
  install -m644 afp{,-browse}.mount "$pkgdir/usr/share/gvfs/mounts/"
}
