#

pkgbase=python-crypto
_pkgname=pycrypto
pkgname=('python2-crypto' 'python-crypto')
pkgver=2.6.1
pkgrel=7
arch=('i386' 'amd64')
makedepends=('python2' 'python')
url="http://www.dlitz.net/software/pycrypto/"
license=('custom')
source=(http://ftp.dlitz.net/pub/dlitz/crypto/${_pkgname}/${_pkgname}-${pkgver}.tar.gz{,.asc})
sha256sums=('f2ce1e989b272cfcb677616763e0a2e7ec659effa67a88aa92b3a65528f60a3c'
            'c2ab0516cc55321e6543ae75e2aa6f6e56e97432870f32a7799f3b89f467dc1b')

prepare() {
  find ${_pkgname}-${pkgver}/LEGAL -type f -exec chmod 644 {} \;
  find ${_pkgname}-${pkgver}/LEGAL -type d -exec chmod 755 {} \;
 
  cp -r ${_pkgname}-${pkgver} ${_pkgname}-${pkgver}-py3
}

build() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py build

  cd ../${_pkgname}-${pkgver}-py3
  python setup.py build
}

package_python2-crypto() {
  pkgdesc="Collection of cryptographic algorithms and protocols, implemented for use from Python 2."
  depends=('python2' 'gmp')
  replaces=('pycrypto')
  conflicts=('pycrypto')
  provides=("pycrypto=${pkgver}")

  cd ${_pkgname}-${pkgver}
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/"
  cp -r LEGAL "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_python-crypto() {
  pkgdesc="Collection of cryptographic algorithms and protocols, implemented for use from Python 3."
  depends=('python' 'gmp')

  cd ${_pkgname}-${pkgver}-py3
  python setup.py install --root="${pkgdir}" --optimize=1
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/"
  cp -r LEGAL "${pkgdir}/usr/share/licenses/${pkgname}/"
}

check() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py test

  cd ../${_pkgname}-${pkgver}-py3
  python setup.py test
}
