#
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=msmtp
pkgname=('msmtp' 'msmtp-mta')
pkgver=1.4.32
pkgrel=1
arch=('i686' 'amd64')
license=('GPL3')
url="http://msmtp.sourceforge.net"
makedepends=('texlive-plainextra' 'gsasl' 'libgnome-keyring')
source=(http://download.sourceforge.net/sourceforge/msmtp/${pkgbase}-${pkgver}.tar.bz2)
sha1sums=('03186a70035dbbf7a31272a20676b96936599704')

prepare() {
  cd ${pkgbase}-${pkgver}
  gsed -i 's|/usr/bin/python|/usr/bin/python2|' scripts/msmtp-gnome-tool/msmtp-gnome-tool.py
}

build() {
  cd ${pkgbase}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --with-ssl=gnutls
  gmake
  gmake -C doc html pdf
}

package_msmtp() {
  pkgdesc="A mini smtp client"
  depends=('gsasl' 'libgnome-keyring')
  install=msmtp.install

  cd ${pkgbase}-${pkgver}
  gmake DESTDIR="${pkgdir}" install 
  gmake DESTDIR="${pkgdir}" -C doc install-html install-pdf

# Installing example configs and scripts to /usr/share/doc/msmtp
# as they are not installed by default (Debian and Gentoo do it this way)
  install -d "${pkgdir}/usr/share/doc/msmtp"
  cp -r scripts/{find_alias,msmtp-gnome-tool,msmtpqueue,msmtpq,set_sendmail} "${pkgdir}/usr/share/doc/msmtp/"
  install -dm755 "${pkgdir}/usr/share/doc/msmtp/"
  install -m644 doc/*.example "${pkgdir}/usr/share/doc/msmtp/"

  install -dm755 "${pkgdir}/usr/share/vim/vimfiles/syntax"
  install -m644 scripts/vim/msmtp.vim "${pkgdir}/usr/share/vim/vimfiles/syntax/msmtp.vim"
}

package_msmtp-mta() {
  pkgdesc="A mini smtp client - the regular MTA"
  depends=('msmtp')
  provides=('smtp-forwarder')
  conflicts=('smtp-forwarder')

  install -d "${pkgdir}"/usr/bin
  ln -s msmtp "${pkgdir}/usr/bin/sendmail"
}
