#
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=msmtp
pkgname=('msmtp' 'msmtp-mta')
pkgver=1.6.2
pkgrel=2
arch=('amd64')
license=('GPL3')
url="http://msmtp.sourceforge.net"
makedepends=('texlive-plainextra' 'gsasl' 'libgnome-keyring')
source=(http://download.sourceforge.net/sourceforge/msmtp/${pkgbase}-${pkgver}.tar.xz)
sha1sums=('2d2272d04851b9bc806a77f47344e61840185329')

prepare() {
  cd ${pkgbase}-${pkgver}
  sed -i '' -e 's|/etc/ssl/certs/ca\-certificates.crt|/usr/share/certs/ca\-root\-nss.crt|g' \
    doc/msmtp.1

}

build() {
  cd ${pkgbase}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --with-ssl=gnutls
  gmake
}

package_msmtp() {
  pkgdesc="A mini smtp client"
  depends=('gsasl' 'libgnome-keyring')
  install=msmtp.install

  cd ${pkgbase}-${pkgver}
  gmake DESTDIR="${pkgdir}" install 
}

package_msmtp-mta() {
  pkgdesc="A mini smtp client - the regular MTA"
  depends=('msmtp')
  provides=('smtp-forwarder')
  conflicts=('smtp-forwarder')

  install -d "${pkgdir}"/usr/bin
  ln -s msmtp "${pkgdir}/usr/bin/sendmail"
}
