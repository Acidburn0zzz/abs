# $Id$
# Maintainer : RÃ©my Oudompheng <remy@archlinux.org>

pkgname=python-pyelftools
_pypiname=pyelftools
pkgver=0.23
pkgrel=2
pkgdesc="Python library for analyzing ELF files and DWARF debugging information"
arch=('amd64')
url="http://pypi.python.org/pypi/pyelftools"
license=('custom')
depends=('python')
source=("http://pypi.python.org/packages/source/p/${_pypiname}/${_pypiname}-${pkgver}.tar.gz")
md5sums=('aa7cefa8bd2f63d7b017440c9084f310')

build() {
  cd ${srcdir}/${_pypiname}-${pkgver}
  python setup.py build
}

check() {
  cd ${srcdir}/${_pypiname}-${pkgver}
  python test/run_all_unittests.py
}

package() {
  cd ${srcdir}/${_pypiname}-${pkgver}
  python setup.py install --root=${pkgdir}
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m 644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

