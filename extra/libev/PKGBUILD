# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Thomas Haider <t.haider@vcnc.org>
# Port: devel/libev

pkgname=libev
pkgver=4.20
pkgrel=1
pkgdesc="A full-featured and high-performance event loop"
arch=('i386' 'amd64')
url="http://software.schmorp.de/pkg/libev.html"
license=('BSD')
source=(http://dist.schmorp.de/${pkgname}/${pkgname}-${pkgver}.tar.gz
        libev.pc.in
        )
md5sums=('1cac539cfc560f381a490c9fba940de9'
         '33624c88092d4b431477d78a3e93b0f7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -dm755 ${pkgdir}/usr/libdata/pkgconfig
  sed -e 's|%PREFIX%|/usr|g ; s|%VERSION%|'${pkgver}'|g' \
  	"${srcdir}/libev.pc.in" > "${pkgdir}/usr/libdata/pkgconfig/libev.pc"
  
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}/
  install -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
