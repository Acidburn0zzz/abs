# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Thomas Haider <t.haider@vcnc.org>
# Port: devel/libev

pkgname=libev
pkgver=4.22
pkgrel=1
pkgdesc="A full-featured and high-performance event loop"
arch=('i386' 'amd64')
url="http://software.schmorp.de/pkg/libev.html"
license=('BSD')
source=("http://dist.schmorp.de/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'libev.pc.in')
sha256sums=('736079e8ac543c74d59af73f9c52737b3bfec9601f020bf25a87a4f4d0f01bd6'
            '26150af1ce6b58b6e88d0f364e1ea7a7919d3c4d4f8fa6e40083b2fb43f5e6b6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -dm755 ${pkgdir}/usr/libdata/pkgconfig
  sed -e 's|%PREFIX%|/usr|g ; s|%VERSION%|'${pkgver}'|g' \
  	"${srcdir}/libev.pc.in" > "${pkgdir}/usr/libdata/pkgconfig/libev.pc"
  
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}/
  install -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
