# $Id: PKGBUILD 202652 2013-12-23 08:42:02Z foutrelis $
# Maintainer: Anthony Donnelly <amzo@archbsd.net>

pkgname=slim
pkgver=1.3.6
pkgrel=9
pkgdesc="Desktop-independent graphical login manager for X11"
arch=('i386' 'amd64')
url="http://slim.berlios.de/"
license=('GPL2')
depends=('libxmu' 'libpng' 'libjpeg' 'libxft' 'libxrandr' 'xorg-xauth' 'consolekit')
makedepends=('cmake' 'freeglut')
backup=('etc/slim.conf' 'etc/logrotate.d/slim' 'etc/pam.d/slim'
        'etc/slimlock.conf')
source=(http://downloads.sourceforge.net/project/slim.berlios/$pkgname-$pkgver.tar.gz
        slim-1.3.6-fix-libslim-libraries.patch
        slim-1.3.6-add-sessiondir.patch
        slim.pam
        slim.logrotate
        slimlock-freebsd.diff
        slim.in
	init.d-slim)
options=(emptydirs)

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix installation path of slim.service
  gsed -i 's|set(LIBDIR "/lib")|set(LIBDIR "/usr/lib")|' CMakeLists.txt

  patch -Np1 -i "$srcdir/slim-1.3.6-fix-libslim-libraries.patch"
  patch -Np1 -i "$srcdir/slim-1.3.6-add-sessiondir.patch"
  patch -Np1 -i "$srcdir/slimlock-freebsd.diff"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DUSE_PAM=yes \
    -DUSE_CONSOLEKIT=yes
  gmake
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  gmake DESTDIR="$pkgdir" install

  # need this to be created
  install -dm755 "$pkgdir/var/run"

  install -dm755 "$pkgdir/etc"
  install -dm755 "$pkgdir"/etc/{rc.d,init.d}
  install -dm755 "$pkgdir/etc/pam.d"
  install -dm755 "$pkgdir/etc/logrotate.d"
  install -m755 "$srcdir/slim.in"        "$pkgdir/etc/rc.d/slim"
  install -m755 "$srcdir/init.d-slim"    "$pkgdir/etc/init.d/slim"
  install -m644 "$srcdir/slim.pam"       "$pkgdir/etc/pam.d/slim"
  install -m644 "$srcdir/slim.logrotate" "$pkgdir/etc/logrotate.d/slim"
  install -m644 slimlock.conf            "$pkgdir/etc/slimlock.conf"

  # Provide sane defaults
  # NOTE: using vt07 causes some input problems...
  gsed -i -e 's|#xserver_arguments.*|xserver_arguments -nolisten tcp vt09|' \
          -e 's|/var/run/slim.lock|/var/run/slim.pid|' \
          -e '/^halt_cmd/s/-h/-p/' \
          -e 's|/etc/issue|/etc/motd|g' \
	  -e 's|/bin/bash|/usr/bin/bash|g' \
    "$pkgdir/etc/slim.conf"
}

# vim:set ts=2 sw=2 et:
sha256sums=('21defeed175418c46d71af71fd493cd0cbffd693f9d43c2151529125859810df'
            '3dfa697f8c058390c7e02e7aba769475057ef8ddde945dc43b8cb7f9724dbda0'
            '0dffd53a69eb9033a67fad964df6fc150ee7a483e29d8eb8b559010fbd14e5fd'
            'cddde3e20f1872e14d7cd30f8301c8366396b7ceb73b3bcd9384f932d1d194d3'
            '5bf44748b5003f2332d8b268060c400120b9100d033fa9d35468670d827f6def'
            '5971c7a9b9886ff0065aceef42239084191133b7421ec0386c8443ca5379e760'
            'e818fc6059cdebb65874754e18b772fe5ccc2c3cfc17c6066e8b97cf1b5b9af7'
            '1bfcc14ea3bf35ae55b502e7f1ce094c254c606d657cc986db3b72ea23ebc3b4')

