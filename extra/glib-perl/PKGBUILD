# $Id: PKGBUILD 183484 2013-04-21 22:21:04Z heftig $
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgname=glib-perl
pkgver=1.307
pkgrel=4
pkgdesc="Perl wrappers for glib 2.x, including GObject"
arch=(i386 amd64)
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
options=('!emptydirs')
depends=('glib2' 'perl')
makedepends=('perl-extutils-depends' 'perl-extutils-pkgconfig')
source=(http://downloads.sourceforge.net/sourceforge/gtk2-perl/Glib-${pkgver}.tar.gz)
md5sums=('2b2b0d20d87c6d2db3d98c8b733275aa')

build() {
  cd "${srcdir}/Glib-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/Glib-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/Glib-${pkgver}"
  make DESTDIR="${pkgdir}" install
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
