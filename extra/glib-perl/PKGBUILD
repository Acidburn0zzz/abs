# Port: devel/p5-Glib2
pkgname=glib-perl
pkgver=1.323
pkgrel=1
pkgdesc="Perl wrappers for glib 2.x, including GObject"
arch=(i386 amd64)
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
options=('!emptydirs')
depends=('glib2' 'perl')
makedepends=('perl-extutils-depends' 'perl-extutils-pkgconfig')
source=(http://downloads.sourceforge.net/sourceforge/gtk2-perl/Glib-${pkgver}.tar.gz)
md5sums=('66648e72313e8b258622a2959120b2d8')

build() {
  cd "${srcdir}/Glib-${pkgver}"
  PERL_USE_UNSAFE_INC=1 \
  perl Makefile.PL INSTALLDIRS=vendor
  PERL_USE_UNSAFE_INC=1 \
  gmake
}

check() {
  cd "${srcdir}/Glib-${pkgver}"
  gmake test
}

package() {
  cd "${srcdir}/Glib-${pkgver}"
  make DESTDIR="${pkgdir}" install
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
