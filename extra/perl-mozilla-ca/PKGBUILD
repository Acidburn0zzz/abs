# Packager: Anthony Donnelly <Amzo@PacBSD.org>
# $Id: PKGBUILD 180398 2013-03-20 22:20:14Z eric $

pkgname=perl-mozilla-ca
pkgver=20141217
pkgrel=1
pkgdesc="Mozilla's CA cert bundle in PEM format"
arch=(any)
license=(GPL2 LGPL2.1)
options=(!emptydirs)
depends=('perl>=5.006')
url=https://metacpan.org/release/Mozilla-CA
source=("http://search.cpan.org/CPAN/authors/id/A/AB/ABH/Mozilla-CA-$pkgver.tar.gz")
md5sums=('e0e5646886aeb3f71fb593f536295e2c')

build() (
  cd Mozilla-CA-$pkgver
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check() (
  cd Mozilla-CA-$pkgver
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package() (
  cd Mozilla-CA-$pkgver
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

