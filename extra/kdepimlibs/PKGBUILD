# $Id: PKGBUILD 193219 2013-08-19 09:54:12Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepimlibs
pkgver=4.11.0
pkgrel=3
pkgdesc="KDE PIM Libraries"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdepimlibs'
license=('GPL' 'LGPL')
depends=('nepomuk-core' 'gpgme' 'akonadi' 'libical' 'prison' 'qjson')
makedepends=('cmake' 'automoc4' 'boost' 'cyrus-sasl')
install='kdepimlibs.install'
port_patches=(
	patch-cmake-modules_FindLibical.cmake
)
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"
	${port_patches[@]})
sha1sums=('8fa13be4729184b50edf404cc7de8dbd97c23636'
          'b1027fc0493ce9fb830b9df543e9ee7673df3a06')

prepare() {
  cd ${pkgname}-${pkgver}

  for p in ${port_patches[@]}; do
    patch -p0 -i "${srcdir}/${p}"
  done
}

build() {
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
