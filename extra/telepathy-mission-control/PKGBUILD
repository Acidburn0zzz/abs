# $Id: PKGBUILD 191309 2013-07-23 13:29:40Z jgc $
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Contributor: Ju Liu <liuju86 at gmail dot com>
# Contributor: Bjorn Lindeijer <bjorn lindeijer nl>
# Contributor: Andreas Zwinkau <beza1e1@web.de>

pkgname=telepathy-mission-control
pkgver=5.16.2
pkgrel=3
pkgdesc="A Telepathy component providing abstraction of some of the details of connection managers."
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org/wiki/Mission Control"
license=('LGPL2.1')
depends=('telepathy-glib' 'libgnome-keyring' 'dconf')
makedepends=('libxslt' 'python2')
install=telepathy-mission-control.install
options=('libtoolfix')
source=(http://telepathy.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('8dc751da77a84abef3f9153bc422a17b')

build() {
    cd "$pkgname-$pkgver"
    PYTHON=python2 ./configure --prefix=/usr \
    	--libexecdir=/usr/lib/telepathy \
      --enable-gnome-keyring \
      --disable-schemas-compile \
      --disable-upower
    gmake
}

package() {
    cd "$pkgname-$pkgver"
    gmake DESTDIR="$pkgdir" install
}
