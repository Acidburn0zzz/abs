# $Id: PKGBUILD 166656 2012-09-14 14:39:16Z andyrtr $
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgbase=hyphen
pkgname=('hyphen' 'hyphen-en')
pkgver=2.8.8
pkgrel=1
arch=('i386' 'amd64')
url="http://hunspell.sf.net"
license=('GPL' 'LGPL' 'MPL')
makedepends=('libc' 'perl' 'gawk')
source=(http://downloads.sourceforge.net/hunspell/${pkgname}-${pkgver}.tar.gz)
md5sums=('5ade6ae2a99bc1e9e57031ca88d36dad')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  # libtool fix
#  autoreconf -vfi
  
  ./configure --prefix=/usr --disable-static ac_cv_prog_AWK=gawk
  make
}

package_hyphen() {
 pkgdesc="library for high quality hyphenation and justification"
 depends=('libc' 'perl')

  cd "$srcdir/$pkgbase-$pkgver"
  make DESTDIR="$pkgdir/" install-libLTLIBRARIES
  make DESTDIR="$pkgdir/" install-binSCRIPTS
  make DESTDIR="$pkgdir/" install-includeHEADERS
  
  # license
  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_hyphen-en() {
 pkgdesc="English hyphenation rules"
 # arch=('any')
 optdepends=('hyphen: offers hyphenation library functions')

  cd "$srcdir/$pkgbase-$pkgver"
  make DESTDIR="$pkgdir/" install-hyphDATA

  pushd ${pkgdir}/usr/share/hyphen/
   en_US_aliases="en_AG en_AU en_BS en_BW en_BZ en_CA en_DK en_GB en_GH en_HK en_IE en_IN en_JM en_NA en_NZ en_PH en_SG en_TT en_ZA en_ZW"
   for lang in $en_US_aliases; do
        ln -s hyph_en_US.dic hyph_$lang.dic
   done
  popd
  
  # the symlinks
  install -dm755 ${pkgdir}/usr/share/myspell/dicts
  pushd $pkgdir/usr/share/myspell/dicts
    for file in $pkgdir/usr/share/hyphen/*; do
      ln -sv /usr/share/hyphen/$(basename $file) .
    done
  popd
}
