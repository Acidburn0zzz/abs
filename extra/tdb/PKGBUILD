# $Id: PKGBUILD 172510 2012-12-04 14:40:09Z tpowa $
# Maintainer: Anthony donnelly <amzo@archbsd.net>

pkgname=tdb
pkgver=1.3.7
pkgrel=1
pkgdesc="A Trivial Database similar to GDBM but allows simultaneous commits"
arch=('amd64')
license=('GPL3')
url="http://tdb.samba.org/"
makedepends=('python2' 'libxslt' 'docbook-xsl')
optdepends=('python2: for python bindings')
source=(http://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.gz
        http://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.asc)
validpgpkeys=('9147A339719518EE9011BCB54793916113084025') #Samba Library Distribution Key <samba-bugs@samba.org>
md5sums=('e3741a19c427255acd1e92c5e09d9df7'
         'SKIP')

build() {
   cd ${srcdir}/${pkgname}-${pkgver}
   sed -i -e 's#python#python2#g' buildtools/bin/waf

   CFLAGS+=" -fno-color-diagnostics"

   export PYTHON=/usr/bin/python2

   ./configure --prefix=/usr \
               --localstatedir=/var \
               --sysconfdir=/etc/samba
   make
}

package() {
   cd ${srcdir}/${pkgname}-${pkgver}
   export PYTHON=/usr/bin/python2
   make DESTDIR=$pkgdir/ install
}
