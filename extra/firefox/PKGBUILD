# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Jakub Schmidtke <sjakub@gmail.com>

pkgname=firefox
pkgver=24.0
pkgrel=3
pkgdesc="Standalone web browser from mozilla.org"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2' 'mozilla-common' 'libxt' 'startup-notification' 'mime-types'
         'dbus-glib' 'alsa-lib' 'libvpx' 'libevent' 'nss>=3.15.1' 'hunspell' 'sqlite'
          'libnotify' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('unzip' 'zip' 'python2' 'yasm' 'mesa' 'imake'
             'xorg-server-xvfb' 'dos2unix' 'libidl2' 'autoconf2.13')
optdepends=('networkmanager: Location detection via available WiFi networks')
url="http://www.mozilla.org/projects/firefox"
install=firefox.install
options=(!emptydirs)
_port_patches=(
  patch-browser-app-nsBrowserApp.cpp
  patch-bug783463
  patch-bug791305
  patch-bug803480
  patch-bug807492
  patch-bug810716
  patch-bug826985
  patch-bug851992
  patch-bug876156
  patch-bug878791
  patch-bug886181
  patch-bug889699
  patch-bug893397
  patch-bug910875
  patch-bug916216
  patch-bug916589
  patch-build-pgo-profileserver.py
  patch-clang32-libc++-visibility-hack
  patch-duckduckgo
  patch-extensions-spellcheck-hunspell-src-mozHunspell.cpp
  patch-ipc-chromium-Makefile.in
  patch-ipc-chromium-src-base-message_pump_libevent.cc
  patch-media-webrtc-trunk-src-system_wrappers-source-spreadsortlib-spreadsort.hpp
  patch-no-videoio.h
  patch-security_manager_ssl_src_nsNSSComponent.cpp
  patch-toolkit_mozapps_installer_packager_mk
  patch-toolkit_xre_Makefile.in
)
source=(https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/$pkgver/source/firefox-$pkgver.source.tar.bz2
        mozconfig firefox.desktop vendor.js
        firefox-install-dir.patch
        shared-libs.patch
        firefox-20.0.1-fixed-loading-icon.png
        "${_port_patches[@]}"
)

export CC=gcc
export CPP=cpp
export CXX=g++
prepare() {
  cd mozilla-release
  dos2unix media/webrtc/trunk/webrtc/system_wrappers/source/spreadsortlib/spreadsort.hpp

  cp ../mozconfig .mozconfig
  patch -Np1 -i ../firefox-install-dir.patch
  patch -Np1 -i ../shared-libs.patch

  for i in "${_port_patches[@]}"; do
    msg "Patch $i"
    patch -p0 -i "${srcdir}/$i"
  done
  msg "Patching done"

  msg "sed mozpng"
  sed -i '' -e '/MOZPNG/s/=[0-9]*/=10511/' configure.in
  msg "preparing"

  # Fix PRE_RELEASE_SUFFIX
  gsed -i '/^PRE_RELEASE_SUFFIX := ""/s/ ""//' \
    browser/base/Makefile.in

  mkdir "$srcdir/path"

  # WebRTC build tries to execute "python" and expects Python 2
  ln -s /usr/bin/python2 "$srcdir/path/python"

  # configure script misdetects the preprocessor without an optimization level
  # https://bugs.archlinux.org/task/34644
  gsed -i '/ac_cpp=/s/$CPPFLAGS/& -O2/' configure

#  # Fix tab loading icon (flickers with libpng 1.6)
#  # https://bugzilla.mozilla.org/show_bug.cgi?id=841734
#  cp "$srcdir/firefox-20.0.1-fixed-loading-icon.png" \
#    browser/themes/gnomestripe/tabbrowser/loading.png

#  # ipc/app/Makefile doesn't get generated but is required for some reason...
#  sed -i '' \
#      -e "/^DIRS +=/,/]/ {/]/i\\
#    'app',
#}" \
#      ipc/moz.build
##export PYTHONPATH=$PP/mach:$PP/mozbuild:$PP/../addon-sdk/source/python-lib/markdown:$PP/../config
##python2 ../config.status -n --file="app/Makefile"
}

build() {
  cd mozilla-release

  if [[ -e /usr/lib/libgcov.a ]]; then
    echo "--- /usr/lib/libgcov.a exists - the build would fail"
    false
  fi

  export PATH="$srcdir/path:$PATH"
  export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/firefox"
  export PYTHON="/usr/bin/python2"
  export LIBS="$LIBS -lpthread"

  export DISPLAY=:99
  Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 $DISPLAY &
  #unset DISPLAY

  # otherwise we get undefined references to environ...
  autoconf-2.13
  cd js/src
  autoconf-2.13
  cd ../..

  if ! gmake -f client.mk build MOZ_PGO=1; then
    kill $!
    return 1
  fi

  kill $! || true
}

package() {
  cd mozilla-release
  gmake -j1 -f client.mk DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/usr/lib/firefox/defaults/preferences/"
  install -m644 ../vendor.js "$pkgdir/usr/lib/firefox/defaults/preferences/vendor.js"

  for i in 16 22 24 32 48 256; do
      install -dm755 "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/"
      install -m644 browser/branding/official/default$i.png \
        "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/firefox.png"
  done
  install -dm755 "$pkgdir/usr/share/icons/hicolor/64x64/apps"
  install -dm755 "$pkgdir/usr/share/icons/hicolor/128x128/apps"
  install -dm755 "$pkgdir/usr/share/icons/hicolor/210x210/apps"
  install -m644 browser/branding/official/content/icon64.png \
    "$pkgdir/usr/share/icons/hicolor/64x64/apps/firefox.png"
  install -m644 browser/branding/official/mozicon128.png \
    "$pkgdir/usr/share/icons/hicolor/128x128/apps/firefox.png"
  install -m644 browser/branding/official/content/about-logo.png \
    "$pkgdir/usr/share/icons/hicolor/210x210/apps/firefox.png"

  install -dm755 "$pkgdir/usr/share/applications/"
  install -m644 ../firefox.desktop \
    "$pkgdir/usr/share/applications/firefox.desktop"

  # Use system-provided dictionaries
  rm -rf "$pkgdir"/usr/lib/firefox/{dictionaries,hyphenation}
  ln -s /usr/share/hunspell "$pkgdir/usr/lib/firefox/dictionaries"
  ln -s /usr/share/hyphen "$pkgdir/usr/lib/firefox/hyphenation"

  # We don't want the development stuff
  #rm -r "$pkgdir"/usr/{include,lib/firefox-devel,share/idl}

  #workaround for now
  #https://bugzilla.mozilla.org/show_bug.cgi?id=658850
  ln -sf firefox "$pkgdir/usr/lib/firefox/firefox-bin"
}
sha256sums=('86a6efa501e1b106b597a8506757f3a2db9d3a5cfcc61ea29a9ad0c834bea83b'
            'c716283d84442ca97036c09a083089bc459d784ffb8148bd6aee6cb484f3ac38'
            'd1980760155fa1e2da773727fa5393f2f106d56dcfbe7131856722eef22fc008'
            '4b50e9aec03432e21b44d18c4c97b2630bace606b033f7d556c9d3e3eb0f4fa4'
            'ded67e8204bd5e1c0c5771c0d2c84ff80c998e1543711e7cd804cfe29e8dd1b0'
            'e2b4a00d14f4ba69c62b3f9ef9908263fbab179ba8004197cbc67edbd916fdf1'
            '68e3a5b47c6d175cc95b98b069a15205f027cab83af9e075818d38610feb6213'
            '7df536d99f1934dff49729ca187fd388e9169c56ba79a4c8b4997f6b2866cc1f'
            'a55ab2d308329097b6229c8c47c8939ee9a2509d45b5dd1cb97fed592cc1e6cd'
            'bef4a48c96419cb9734e3e93764d6a88d597f046d394b8659ca0e1050c16a06a'
            '97cac2182710fa29ff550528427d839ee0454a953c8ee551e2238eabf713427a'
            '19de819a7ac329156d7dd4f1c8bd99a5ee4f14171e0905051f2dd3a68fe16b87'
            '3a2ffe7a6196cda780315aa2b654a054b29b75a023b675faf90058da753d6e4f'
            'de6720f57d18078d8015e067861a333dfd505ba4dc0d47862cd335e4ebd47bec'
            'd983ef09269bfd0754e12d05a031eabf1050343ac355741e0670ba43cdca0169'
            '5dc7be2d2503bd7aea2027b89457c29d06be289c96c0f0a712d28ea3618bb3cb'
            '4c075d850382f6a9ff4c85a73c73e30c47ad8086d16354be148e81f54d500541'
            'c6ca5dfc5da3725b03c84fba9769f5a025fb20af9b5e2814c29cdcf71e6c4d8c'
            '02b9c67feefe9973c94b95b541e2aa69ad2376c446007a8b154371e65e6d288c'
            'ac2d210f3a1ff5d8aa816c4d0a5d522a2b873e1f82fe30ce1b538569aa9c04f3'
            '2aa28703d213048ed8b1eedc29e406b9cb1b4563322ddcf9403004d2dc2aba17'
            'c97bc717f3b2d2378db260c8df0903d8035523128ebac8b107519a969728542c'
            '7459339c11a603c3b4ed2f617d88c273dc10e40d10ebb16865919a7818a72f3b'
            '0c1d520152ef28e141d7a46dd689b5ffd80f382d3a6589d6c17f09781dd99294'
            'ded4520bc2e335966757f28819c281fb35b24c41527882b9efbf8fb1a664ce47'
            'a306dce3dd7c94e3928fa53468c75ed58ab8904756e8c7814a548d909f98baf4'
            '876799b56c9160005142fb6dfdded0c780df54fe0a8be8df1114582a39f35a1c'
            '89aaa6ac874c95b254e0c4d349cb7cc868a10a8a22eda67a85e4477e85301681'
            '2667776f33cb6bfabf356a209a30d3f02c7651b833f89da1490d10af16f1744f'
            '34603cd9991098f3f6a3635b24820080df1d5e7dae0a842a8dcf2882d06c3b3c'
            '9430c5cb4f352107005996357902e11aae58b85f5f16f3cb5be42a41a3fc2c28'
            '7c9234684820f7dc0eca3717241423ccd19917f6c7285b442585825557664969'
            'f1c931fe54a2c753c20743bb0b2d2727e135cd1d9511c4bf1281691e36191649'
            'c069725e1dd449e51861ca70dfd8464ed8525453fcc160129508b205adfd65ce')
