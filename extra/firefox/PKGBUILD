# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Jakub Schmidtke <sjakub@gmail.com>

pkgname=firefox
pkgver=27.0.1
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2' 'mozilla-common' 'libxt' 'startup-notification' 'mime-types'
         'dbus-glib' 'alsa-lib' 'libvpx' 'libevent' 'nss>=3.15.1' 'hunspell' 'sqlite'
          'libnotify' 'desktop-file-utils' 'hicolor-icon-theme' 'libv4l')
makedepends=('unzip' 'zip' 'python2' 'yasm' 'mesa' 'imake' 'gstreamer0.10' 'gstreamer0.10-base'
             'xorg-server-xvfb' 'dos2unix' 'libidl2' 'autoconf2.13' 'gstreamer0.10-base-plugins'
             'llvm') # llvm for PGO / libprofile_rt.a
optdepends=('networkmanager: Location detection via available WiFi networks')
url="http://www.mozilla.org/projects/firefox"
install=firefox.install
options=(!fbsd10fix !emptydirs clang)
_port_patches=(
        patch-browser-app-nsBrowserApp.cpp
        patch-bug783463
        patch-bug791305
        patch-bug810716
        patch-bug826985
        patch-bug878791
        patch-bug893397
        patch-bug916589
        patch-bug945046
        patch-bug946560
        patch-bug948946
        patch-build-pgo-profileserver.py
        patch-clang32-libc++-visibility-hack
        patch-duckduckgo
        patch-extensions-spellcheck-hunspell-src-mozHunspell.cpp
        patch-ipc-chromium-Makefile.in
        patch-ipc-chromium-src-base-message_pump_libevent.cc
        patch-media-webrtc-trunk-src-system_wrappers-source-spreadsortlib-spreadsort.hpp
        patch-no-videoio.h
        patch-security_manager_ssl_src_nsNSSComponent.cpp
        patch-toolkit_mozapps_installer_packager_mk
        patch-toolkit_xre_Makefile.in
)
source=(https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/$pkgver/source/firefox-$pkgver.source.tar.bz2
        mozconfig firefox.desktop vendor.js
        firefox-install-dir.patch
        firefox-20.0.1-fixed-loading-icon.png
        "${_port_patches[@]}"
)

_google_api_key=AIzaSyAO345WIdLIfX0L1hR-dNVVZHrblLkQGpU

prepare() {
  cd mozilla-release
  dos2unix media/webrtc/trunk/webrtc/system_wrappers/source/spreadsortlib/spreadsort.hpp

  cp ../mozconfig .mozconfig
  patch -Np1 -i ../firefox-install-dir.patch

  for i in "${_port_patches[@]}"; do
    msg "Patch $i"
    patch -p0 -i "${srcdir}/$i"
  done
  msg "Patching done"

  msg "sed mozpng"
  sed -i '' -e '/MOZPNG/s/=[0-9]*/=10511/' configure.in
  msg "preparing"

  # Fix PRE_RELEASE_SUFFIX
  gsed -i '/^PRE_RELEASE_SUFFIX := ""/s/ ""//' \
    browser/base/Makefile.in

  echo -n "$_google_api_key" >google-api-key
  echo "ac_add_options --with-google-api-keyfile=\"$PWD/google-api-key\"" >>.mozconfig

  mkdir "$srcdir/path"

  # WebRTC build tries to execute "python" and expects Python 2
  ln -s /usr/bin/python2 "$srcdir/path/python"

  ## otherwise we get undefined references to environ...
  ## and mozpng sed must be applied
  autoconf-2.13
  cd js/src
  autoconf-2.13
  cd ../..

  for f in \
      directory/c-sdk/config/FreeBSD.mk \
      directory/c-sdk/configure \
      security/coreconf/FreeBSD.mk \
      js/src/Makefile.in \
      js/src/configure \
      configure; do \
    if [[ -f "$f" ]] ; then
      sed -i '' -e 's|echo aout|echo elf|g' "$f"
    fi
  done

  # configure script misdetects the preprocessor without an optimization level
  # https://bugs.archlinux.org/task/34644
  gsed -i '/ac_cpp=/s/$CPPFLAGS/& -O2/' configure

  fbsd10fix

  # some last minute code fixes
  sed -i '' -e 's@include "\(audio_device_utility_linux.h\)"@include "linux/\1"@' \
    media/webrtc/trunk/webrtc/modules/audio_device/audio_device_impl.cc
  # problems with _Unwind_Backtrace
  sed -i '' -e 's@#elif HAVE_DLADDR @#elif 0 @' \
    xpcom/base/nsStackWalk.cpp

  # remember ipc/app/{Makefile,backend.mk} get deleted after the first build...
  # keep them somewhere nice...
}

build() {
  cd mozilla-release

  export PATH="$srcdir/path:$PATH"
  export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/firefox"
  export PYTHON="/usr/bin/python2"
  export LIBS="$LIBS -lpthread"

  #export CFLAGS="$CFLAGS -fPIC"
  #export CXXFLAGS="$CXXFLAGS -fPIC"

  export DISPLAY=:99
  Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 $DISPLAY &

  if ! gmake -f client.mk build; then
    kill $!
    return 1
  fi

  kill $! || true
}

package() {
  cd mozilla-release
  gmake -j1 -f client.mk DESTDIR="$pkgdir" install
  msg "REST"

  install -dm755 "$pkgdir/usr/lib/firefox/defaults/preferences/"
  install -m644 ../vendor.js "$pkgdir/usr/lib/firefox/defaults/preferences/vendor.js"

  for i in 16 22 24 32 48 256; do
      install -dm755 "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/"
      install -m644 browser/branding/official/default$i.png \
        "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/firefox.png"
  done
  install -dm755 "$pkgdir/usr/share/icons/hicolor/64x64/apps"
  install -dm755 "$pkgdir/usr/share/icons/hicolor/128x128/apps"
  install -dm755 "$pkgdir/usr/share/icons/hicolor/210x210/apps"
  install -m644 browser/branding/official/content/icon64.png \
    "$pkgdir/usr/share/icons/hicolor/64x64/apps/firefox.png"
  install -m644 browser/branding/official/mozicon128.png \
    "$pkgdir/usr/share/icons/hicolor/128x128/apps/firefox.png"
  install -m644 browser/branding/official/content/about-logo.png \
    "$pkgdir/usr/share/icons/hicolor/210x210/apps/firefox.png"

  install -dm755 "$pkgdir/usr/share/applications/"
  install -m644 ../firefox.desktop \
    "$pkgdir/usr/share/applications/firefox.desktop"

  # Use system-provided dictionaries
  rm -rf "$pkgdir"/usr/lib/firefox/{dictionaries,hyphenation}
  ln -s /usr/share/hunspell "$pkgdir/usr/lib/firefox/dictionaries"
  ln -s /usr/share/hyphen "$pkgdir/usr/lib/firefox/hyphenation"

  # We don't want the development stuff
  #rm -r "$pkgdir"/usr/{include,lib/firefox-devel,share/idl}

  #workaround for now
  #https://bugzilla.mozilla.org/show_bug.cgi?id=658850
  ln -sf firefox "$pkgdir/usr/lib/firefox/firefox-bin"
}
sha1sums=('29dc759e36f2b9381e80ac48ce59635ea25033dc'
          '4e3e194328550529e9004a4e226e4fd28c449293'
          '27a7cd91a9faac2d2b07d729cd0fb3a2d5243629'
          '7739fbea2838cb9148a1d77090ace46eeb397f79'
          'db2d93a36184cc8e2edd4149052da382b5b43bfd'
          '0c3900ffe21bfc3d96b80ade334613bc7e76fe27'
          'd51e4f9de1439b80ab9007b986afd2ff7e7e2bf7'
          'fa57b5a7fbfe3320771af3c44efbe73353158a67'
          'f00c57829eae47020addb66db6812113c2fee122'
          'febe116fef6c592ea357aa51054a9cb4a3371c92'
          'c8a0a6f2f665bce5223fd390c511ffd319353afa'
          'd02931cf578b67c6d318488b6e826816d6041b61'
          '2cabb38380e127d81f5e1e7a6a7ec2680f276546'
          'f0e6d2955179a25e54ec0414389bda37a19a6e13'
          '80a1e5ca810e92e5ba3cfe2161a981b865b65756'
          'cfad1256daae3572d4f2ed37ec9d2c4c63c3cc54'
          '3dfd21d46e2e7343e50a9d13a0ab04402f02e3db'
          'b8c5cd11363933f77a2436a2d557cabb383ce566'
          '7087a6d21a756bb8eca139a12aec08db37d71664'
          'ca7d350b870b76b425d36f9b79a068f3e68af672'
          '82756e2de7df8ef8ebdff788b047fd8344566ade'
          'aa4e98231abae8fb95a57c215ca1a42a43a6ee1b'
          '2fd94061f151a73cbb535b1ae45e0aab3f3ab903'
          '037ace8f22a76ed5a2622975d6d7062536824334'
          'a86a38da1b9a34c4f350c96d804a7ccf157d5550'
          '7ded0cf09cd2ff759b46b3dec0330cf7d5dfef39'
          '26bf45e98a52ff0943113a901f4f539a149c0c3d'
          '54c305a3130038d9b768e34df465a49ef0c91d05')
