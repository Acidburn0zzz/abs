# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Jakub Schmidtke <sjakub@gmail.com>

pkgname=firefox
pkgver=47.0.1
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org"
arch=('i386' 'amd64')
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2' 'mozilla-common' 'libxt' 'startup-notification' 'mime-types' 'soundtouch'
         'dbus-glib' 'alsa-lib' 'libvpx' 'libevent' 'nss>=3.15.1' 'hunspell' 'sqlite' 'rust'
         'libnotify' 'desktop-file-utils' 'hicolor-icon-theme' 'libv4l' 'opus' 'v4l-compat')
makedepends=('unzip' 'zip' 'python2' 'yasm' 'mesa' 'imake' 'gstreamer0.10' 'gstreamer0.10-base' 'autoconf213'
             'xorg-server-xvfb' 'dos2unix' 'libidl2' 'gstreamer0.10-base-plugins xorg-xauth' 'gconf')
optdepends=('networkmanager: Location detection via available WiFi networks')
url="http://www.mozilla.org/projects/firefox"
install=firefox.install
options=(!fbsd10fix !emptydirs clang)
patchoptions="-p0"
source=(https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/$pkgver/source/firefox-$pkgver.source.tar.xz
        mozconfig
        firefox.desktop
        firefox-install-dir.patch
        vendor.js
        freetype261.patch
        firefox-fixed-loading-icon.png)
sha256sums=('5ac36c3481dde80ef2e36237badef6cb8ec5fe7e3b5ac1728839477de0cc034c'
            'd73e05d189bc7f76f579709bcb0a6ad8f1739d472b747eefd3737e2e2446ba65'
            'c202e5e18da1eeddd2e1d81cb3436813f11e44585ca7357c4c5f1bddd4bec826'
            'd86e41d87363656ee62e12543e2f5181aadcff448e406ef3218e91865ae775cd'
            '4b50e9aec03432e21b44d18c4c97b2630bace606b033f7d556c9d3e3eb0f4fa4'
            'd3796311f93328fc8494d75a2123d29e51d52c3f01ffea0977efbfbe12171809'
            '68e3a5b47c6d175cc95b98b069a15205f027cab83af9e075818d38610feb6213')
validpgpkeys=('2B90598A745E992F315E22C58AB132963A06537A')

_google_api_key=AIzaSyAO345WIdLIfX0L1hR-dNVVZHrblLkQGpU

prepare() {
  cd "${pkgname}-${pkgver}"

  cp ../mozconfig .mozconfig
  patch -Np1 -i ../firefox-install-dir.patch

  sed -i '' -e '/MOZPNG/s/=[0-9]*/=10511/' \
    configure.in
  echo -n "$_google_api_key" >google-api-key
  echo "ac_add_options --with-google-api-keyfile=\"$PWD/google-api-key\"" >>.mozconfig

  mkdir "$srcdir/path"

  # WebRTC build tries to execute "python" and expects Python 2
  ln -s /usr/bin/python2 "$srcdir/path/python"

  # configure script misdetects the preprocessor without an optimization level
  # https://bugs.archlinux.org/task/34644
  gsed -i '/ac_cpp=/s/$CPPFLAGS/& -O2/' configure

  # Fix tab loading icon (doesn't work with libpng 1.6)
  # https://bugzilla.mozilla.org/show_bug.cgi?id=841734
  cp "$srcdir/firefox-fixed-loading-icon.png" \
    browser/themes/linux/tabbrowser/loading.png

  autoconf-2.13
  cd js/src && autoconf-2.13
  cd ../../
  # some last minute code fixes
  sed -i '' -e 's@include "\(audio_device_utility_linux.h\)"@include "linux/\1"@' \
    media/webrtc/trunk/webrtc/modules/audio_device/audio_device_impl.cc

}

build() {
  cd "${pkgname}-${pkgver}"

  # doesn't get set when using noextract
  export PATH="$srcdir/path:$PATH"
  export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/firefox"
  export PYTHON="/usr/bin/python2"
  export LIBS="$LIBS -pthread"

  export CFLAGS="$CFLAGS -fPIC"
  export CXXFLAGS="$CXXFLAGS -fPIC"

  # Do PGO
  # xvfb-run -a -s "-extension GLX -screen 0 1280x1024x24" \
    gmake -f client.mk build

}

package() {
  cd "${pkgname}-${pkgver}"
  gmake -j1 -f client.mk DESTDIR="$pkgdir" install
  msg "REST"

  install -dm755 "$pkgdir/usr/lib/firefox/defaults/preferences/"
  install -m644 ../vendor.js "$pkgdir/usr/lib/firefox/defaults/preferences/vendor.js"

  for i in 16 22 24 32 48 256; do
      install -dm755 "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/"
      install -m644 browser/branding/official/default$i.png \
        "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/firefox.png"
  done
  install -dm755 "$pkgdir/usr/share/icons/hicolor/64x64/apps"
  install -dm755 "$pkgdir/usr/share/icons/hicolor/128x128/apps"
  install -dm755 "$pkgdir/usr/share/icons/hicolor/210x210/apps"
  install -m644 browser/branding/official/content/icon64.png \
    "$pkgdir/usr/share/icons/hicolor/64x64/apps/firefox.png"
  install -m644 browser/branding/official/mozicon128.png \
    "$pkgdir/usr/share/icons/hicolor/128x128/apps/firefox.png"
  install -m644 browser/branding/official/content/about-logo.png \
    "$pkgdir/usr/share/icons/hicolor/210x210/apps/firefox.png"

  install -dm755 "$pkgdir/usr/share/applications/"
  install -m644 ../firefox.desktop \
    "$pkgdir/usr/share/applications/firefox.desktop"

  # Use system-provided dictionaries
  rm -rf "$pkgdir"/usr/lib/firefox/{dictionaries,hyphenation}
  ln -s /usr/share/hunspell "$pkgdir/usr/lib/firefox/dictionaries"
  ln -s /usr/share/hyphen "$pkgdir/usr/lib/firefox/hyphenation"

  # We don't want the development stuff
  #rm -r "$pkgdir"/usr/{include,lib/firefox-devel,share/idl}

  #workaround for now
  #https://bugzilla.mozilla.org/show_bug.cgi?id=658850
  ln -sf firefox "$pkgdir/usr/lib/firefox/firefox-bin"
}
