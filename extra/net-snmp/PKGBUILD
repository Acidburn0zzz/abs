pkgname=net-snmp
pkgver=5.7.3
pkgrel=1
pkgdesc="A suite of applications used to implement SNMP v1, SNMP v2c and SNMP v3 using both IPv4 and IPv6"
arch=('i386' 'amd64')
url="http://www.net-snmp.org/"
license=('BSD')
depends=('pciutils')
makedepends=('python2-setuptools')
optdepends=('perl-term-readkey: for snmpcheck application'
            'perl-tk: for snmpcheck and tkmib applications'
            'python2: for the python modules')
options=('!emptydirs' '!makeflags')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc}
        libnl32.patch snmpd.conf snmpd.in snmpd.init-d
	snmptrapd.conf snmptrapd.in snmptrapd.init-d)
sha1sums=('97dc25077257680815de44e34128d365c76bd839'
          'SKIP'
          '74a9848b95f63378eb1753fc309d2b74de5afb0f'
          '51ebdca93c68ca04b7f402fad4f504422ec2c6cf'
          '00ce6983d60a96c369d4a965be149be68ae06ba2'
          '658c671b0c6ed135d2ec6aa8b6221861db1a5a73'
          '0d296caa01c789455c3cd4c67cd81aca5547681d'
          '29e1dae125561e33dbd3d8b87f62e47c2577e3a7'
          'b59e952065202afefae40def8fe50ae5f32662f3')
prepare() {
  cd ${pkgname}-${pkgver}
  
  mkdir "${srcdir}/bin"
  ln -s /usr/bin/gmake "${srcdir}/bin/make"
  export PATH="${srcdir}/bin:$PATH"
}

build() {
  cd ${pkgname}-${pkgver}
  PYTHONPROG=/usr/bin/python2 ./configure --prefix=/usr \
    --sysconfdir=/etc --sbindir=/usr/sbin \
    --mandir=/usr/share/man \
    --enable-ucd-snmp-compatibility \
    --enable-ipv6 \
    --with-python-modules \
    --with-default-snmp-version="3" \
    --with-sys-contact="root@localhost" \
    --with-sys-location="Unknown" \
    --with-logfile="/var/log/snmpd.log" \
    --with-mib-modules="host disman/event-mib smux mibII/mta_sendmail mibII/tcpTable ucd-snmp/diskio" \
    --with-persistent-directory="/var/net-snmp" \
    --disable-static --with-ldflags="-lm -lkvm -ldevstat"
  make NETSNMP_DONT_CHECK_VERSION=1
}

package() {
  cd ${pkgname}-${pkgver}
  gsed -i -e "s:install --basedir=\$\$dir:install --basedir=\$\$dir --root=${pkgdir}:" Makefile
  make DESTDIR="${pkgdir}" INSTALL_PREFIX="${pkgdir}" INSTALLDIRS=vendor install

  rm -f "${pkgdir}"/usr/share/snmp/mibs/BRIDGE-MIB.txt
  
  install -dm755 "${pkgdir}"/etc/{rc.d,init.d,conf.d}
  
  for file in snmpd.in snmptrapd.in; do
    install -m755 "${srcdir}"/$file "${pkgdir}"/etc/rc.d/${file%%.*}
  done
  
  for conf in snmpd.conf snmptrapd.conf; do
    install -m755 "${srcdir}"/$conf "${pkgdir}"/etc/conf.d/$conf
  done

  for file in snmpd.init-d snmptrapd.init-d; do
    install -m755 "${srcdir}"/$file "${pkgdir}"/etc/init.d/${file%%.*}
  done

  chmod +x "${pkgdir}"/etc/{rc.d,init.d}/{snmpd,snmptrapd}

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
