# $Id: PKGBUILD 191316 2013-07-23 14:05:15Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=colord
pkgver=1.2.2
pkgrel=3
pkgdesc="System daemon for managing color devices"
arch=(i686 x86_64)
url="http://www.freedesktop.org/software/colord"
license=(GPL2)
depends=(lcms2 polkit shared-color-profiles sqlite dconf dbus)
makedepends=(intltool gobject-introspection vala sane bash-completion)
optdepends=('sane: UseSANE support [/etc/colord.conf]')
install=colord.install
backup=(etc/colord.conf)
source=($url/releases/$pkgname-$pkgver.tar.xz)
md5sums=('a03f06f20a1c70cbf30fc8bd59b99678')

prepare() {
  cd $pkgname-$pkgver

  sed -i '' -e 's|-Wclobbered||g' \
		-e 's|-Wempty-body||g' \
		-e 's|-Wignored-qualifiers||g' \
		-e 's|-Wtype-limits||g' \
		-e 's|-Wmissing-parameter-type||g' \
		-e 's|LIBS="-lusb-1.0|LIBS="-lusb|g' \
		configure
}

build() {
  cd $pkgname-$pkgver
  
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-static \
    --enable-vala --disable-sane \
    --with-daemon-user=colord --disable-systemd-login \
    --disable-udev --disable-gusb \
    USB_CFLAGS="-I/usr/include" \
    USB_LIBS="-L/usr/lib -lusb" LIBS="-lintl"
  gmake
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
