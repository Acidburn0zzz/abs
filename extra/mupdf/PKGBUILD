# $Id: PKGBUILD 92586 2013-06-09 13:32:10Z bpiotrowski $
# Maintainer:  BartÅomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Pierre-Paul Paquin <pierrepaulpaquin@gmail.com>
# Contributor: xduugu (.desktop and install files)

pkgname=mupdf
pkgver=1.5
pkgrel=2
pkgdesc='Lightweight PDF and XPS viewer'
arch=('i686' 'x86_64')
url='http://mupdf.com'
license=('GPL3')
depends=('freetype2' 'libjpeg' 'jbig2dec' 'openjpeg' 'libxext' 'desktop-file-utils' 'xdg-utils')
makedepends=('gmake')
install=mupdf.install
options=('staticlibs')
patchdir=(mupdf-$pkgver-source/)
source=(http://distcache.FreeBSD.org/ports-distfiles/mupdf-$pkgver-source.tar.gz
        mupdf-1.5-openjpeg-2.1.0.patch)
md5sums=('89bd4ddc74c266062ebf2702741bb173'
         '8e71587ad9b86e10c9144618ab43149b')
sha256sums=('e41fa8951de3b44a5ec95052b8897fbcca1eb721e0fd0289862e31e2f03f846a'
            '6ea44355ac1309cbd6605a8478e7245f2c0f17f61b30bb72d8ec79f65732452d')

prepare() {
  cd $pkgname-$pkgver-source
  sed -i '' -e 's|libopenjp2|libopenjpeg|g' \
		Makerules
	sed -i '' -e 's/-pipe -O2 //' \
		-e 's|/usr/local|/usr|' \
		-e 's/Linux/FreeBSD/' \
		Makerules

  rm -rf thirdparty/{curl,freetype,jpeg,zlib,jbig2dec,openjpeg}
}

build() {
  CFLAGS+=' -fPIC'
  CXXFLAGS+=' -fPIC'

  cd $pkgname-$pkgver-source
  gmake build=release CURL_LIBS='-lcurl -pthread'
}

package() {
	cd $pkgname-$pkgver-source
	gmake build=release prefix="$pkgdir"/usr install

	sed -i '' 's/mupdf.xpm/mupdf/' debian/mupdf.desktop
	sed -i '' 's/application\/x-pdf/application\/x-pdf/' debian/mupdf.desktop
	install -dm755 "${pkgdir}/usr/share/applications"
	install -dm755 "${pkgdir}/usr/share/pixmaps"
	install -m644 debian/mupdf.desktop "$pkgdir"/usr/share/applications/mupdf.desktop
	install -m644 debian/mupdf.xpm "$pkgdir"/usr/share/pixmaps/mupdf.xpm

	chmod 644 "$pkgdir"/usr/lib/libfitz.a
}
