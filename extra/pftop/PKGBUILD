#

pkgname=pftop
pkgver=0.7
pkgrel=1
pkgdesc="Utility for real-time display of statistics for pf"
arch=('i386' 'amd64')
url="http://www.eee.metu.edu.tr/~canacar/pftop/"
license=('BSD')
_port_patches=(
        patch-aa
        patch-pftop.c
        patch-sf-scanner.l
        extra-patch-cache.c
        extra-patch-cache.h
        extra-patch-config.h
        extra-patch-engine.c
        extra-patch-pftop.c
        extra-patch-sf-gencode.c
)
source=("http://www.eee.metu.edu.tr/~canacar/${pkgname}-${pkgver}.tar.gz"
        "${_port_patches[@]}"
        queue.h
        )
md5sums=('2fdef1e3fffc38ae40f27aa2dfdcf6fc'
         'c14532e6eaf5b659351eab6af36b49fc'
         '1d2f8cd955bfab2f0007433926e22653'
         'e15b7b16c2302d584ca86c39c2c37616'
         '2e7c2439cca3f43165cf676bb31e83bd'
         'ced57d28ec9acf9d3ef9206002b87216'
         'dcd145f5d2d3d3bcd40b928fcb3a85db'
         '8e01193ce95c6da264828f17cba79ff4'
         'f2ff2a0c235442b0174d765d7ba173c1'
         'f8354e7e892ac3e94b5011d6acb3d4d5'
         '13c349d856e356783e68f2a5ebb1eac6')

prepare() {
  cd ${pkgname}-${pkgver}
  for i in "${_port_patches[@]}"; do
    patch -p0 -i "${srcdir}/${i}"
  done

  sed -i '' -e "s|<sys/queue.h>|\"${srcdir}/queue.h\"|g" engine.c
  sed -i '' -e 's|__dead|__dead2|g' sf-gencode.{c,h}
  sed -i '' -e 's|#include <net/if_pflog.h>||' sf-gencode.c
}

build() {
  cd ${pkgname}-${pkgver}
  export CFLAGS="${CFLAGS} -DHAVE_ALTQ=1 -DHAVE_SNPRINTF=1 -DHAVE_VSNPRINTF=1"
  # since 10:
  export CFLAGS="${CFLAGS} -DHAVE_FINE_GRAINED_LOCKING=1"
  make OSLEVEL=45 LOCALBASE=/usr CSTD=gnu89 __MAKE_CONF=/dev/null
}

package() {
  cd ${pkgname}-${pkgver}

  install -dm755 "${pkgdir}/usr/sbin"
  install -dm755 "${pkgdir}/usr/share/man/man8"

  install -m755 pftop "${pkgdir}/usr/sbin/pftop"
  install -m644 pftop.8 "${pkgdir}/usr/share/man/man8/pftop.8"
}
