pkgbase=nautilus
pkgname=(nautilus libnautilus-extension)
pkgver=3.16.2
pkgrel=2
pkgdesc="GNOME file manager"
arch=('amd64')
license=(GPL)
depends=(libexif gnome-desktop exempi gvfs desktop-file-utils gnome-icon-theme dconf 
         libtracker-sparql libnotify nautilus-sendto)
makedepends=(intltool gobject-introspection python tracker)
url="http://www.gnome.org"
options=('!emptydirs')
source=(http://download.gnome.org/sources/$pkgbase/${pkgver%.*}/$pkgbase-$pkgver.tar.xz)
sha256sums=('3e7ecdda3a47b6ad03098270940aa506782866fa3602d91e711d99f96741478f')

build() {
  cd $pkgbase-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/libexec/nautilus \
      --disable-update-mimedb \
      --disable-packagekit \
      --disable-schemas-compile \
      LIBS="-pthread -lintl"
  gmake
}

package_nautilus() {
  depends+=(libnautilus-extension)
  groups=(gnome)
  install=nautilus.install

  cd $pkgbase-$pkgver
  gmake DESTDIR="$pkgdir" install

### Split libnautilus-extension
  cd ..
  mkdir -p n-e/usr/{lib,share}
  mv "$pkgdir"/usr/include n-e/usr
  mv "$pkgdir"/usr/{lib/girepository-1.0,libdata/pkgconfig} n-e/usr/lib
  mv "$pkgdir"/usr/lib/libnautilus-extension.* n-e/usr/lib
  mv "$pkgdir"/usr/share/{gir-1.0,gtk-doc} n-e/usr/share
}

package_libnautilus-extension() {
  pkgdesc="Library for extending the $pkgdesc"
  depends=(gtk3)

  mv n-e/* "$pkgdir"
}
