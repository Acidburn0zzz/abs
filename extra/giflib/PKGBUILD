# Port: graphics/giflib
pkgname=giflib
pkgver=5.1.4
pkgrel=1
pkgdesc="A library for reading and writing gif images"
url="http://sourceforge.net/projects/giflib/"
arch=('i386' 'amd64')
license=('MIT')
makedepends=('xmlto' 'docbook-xml' 'docbook-xsl')
provides=("libungif=${pkgver}")
conflicts=('libungif')
replaces=('libungif')
options=('libtoolfix')
source=(http://downloads.sourceforge.net/sourceforge/giflib/${pkgname}-${pkgver}.tar.bz2)
md5sums=('2c171ced93c0e83bb09e6ccad8e3ba2b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --disable-static
  gmake

}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
