pkgbase=gtk2
pkgname=('gtk2' 'gtk-update-icon-cache')
pkgver=2.24.24
pkgrel=3
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
makedepends=('atk' 'pango>=1.34.1-3' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage'
             'shared-mime-info' 'cairo' 'gdk-pixbuf2>=2.28.2-2' 'gobject-introspection')
license=('LGPL')
options=('libtoolfix')
_port_patches=(
        patch-Makefile.in
        patch-gtk_gtkbuilderparser.c
        patch-gtk_updateiconcache.c
)
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/2.24/gtk+-$pkgver.tar.xz
        xid-collision-debug.patch
        gdk-thread-hack.diff
        "${_port_patches[@]}"
)
sha256sums=('12ceb2e198c82bfb93eb36348b6e9293c8fdcd60786763d04cfec7ebe7ed3d6d'
            '88a5f572a78f3f2095e8ad3fca5003103f71b25c8071fd0261ade8c431aaa248'
            'ba22671269e3d20dc3c0013ee1ac36bbb1cde890e4597ee4a60badc101b7dced'
            '3a090e37a543b71ee7fd37fffbbaeadd212dd0ad9f84ad4f8788488c14dfa2d9'
            '7a2089acc9553f48c73a18d31bd302b82a65b06d7a0a61f72077961ddb4b33ee'
            '5e082f9e0109a1a63e713d1997d277251a3f7760efedccd4c9253e5e9aaae8a9')
prepare() {
    cd gtk+-$pkgver
    patch -Np1 -i ../xid-collision-debug.patch
    for i in "${_port_patches[@]}"; do
      patch -p0 -i "${srcdir}/$i"
    done

}

build() {
    cd gtk+-$pkgver

    CXX=/usr/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-xinput=yes \
        --disable-cups LDFLAGS="-lintl" 

    gmake
}

package_gtk2() {
    pkgdesc="GTK+ is a multi-platform toolkit (v2)"
    install=gtk2.install
    depends=('atk' 'pango>=1.34.1-3' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'shared-mime-info' 'cairo' 'libcups' 'gtk-update-icon-cache')
    depends=('atk' 'pango>=1.34.1-3' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'shared-mime-info' 'cairo' 'gtk-update-icon-cache')
    backup=(etc/gtk-2.0/gtkrc)
    replaces=('gtk2-docs')

    cd gtk+-$pkgver

    gmake DESTDIR="$pkgdir" install
    sed -i '' \
     -e "s#env python#env python2#" $pkgdir/usr/bin/gtk-builder-convert

    echo 'gtk-fallback-icon-theme = "gnome"' > "$pkgdir/etc/gtk-2.0/gtkrc"

    #split this out to use with gtk3 too
    rm $pkgdir/usr/bin/gtk-update-icon-cache

}
package_gtk-update-icon-cache() {
    pkgdesc="The GTK+ update icon cache tool"
    depends=('gdk-pixbuf2>=2.24.1-1')

    cd gtk+-$pkgver/gtk

    install -dm755 $pkgdir/usr/bin/
    install -m755 gtk-update-icon-cache $pkgdir/usr/bin/
}
