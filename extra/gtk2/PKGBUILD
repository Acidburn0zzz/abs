# Port: x11-toolkits/gtk20
pkgname=('gtk2')
pkgver=2.24.31
pkgrel=1
arch=('i386' 'amd64')
url="http://www.gtk.org/"
depends=(atk pango libxcursor libxinerama libxrandr libxi libxcomposite libxdamage 
         shared-mime-info cairo gtk-update-icon-cache)
makedepends=(gobject-introspection python2)
optdepends=('gnome-themes-standard: Default widget theme')
license=(LGPL)
install=gtk2.install
options=('libtoolfix')
patchdir=(gtk+-$pkgver)
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/2.24/gtk+-$pkgver.tar.xz
        xid-collision-debug.patch
        gdk-thread-hack.diff
        gtk-query-immodules-2.0.hook
        gtkrc)
sha256sums=('68c1922732c7efc08df4656a5366dcc3afdc8791513400dac276009b40954658'
            'd758bb93e59df15a4ea7732cf984d1c3c19dff67c94b957575efea132b8fe558'
            'ba22671269e3d20dc3c0013ee1ac36bbb1cde890e4597ee4a60badc101b7dced'
            '9656a1efc798da1ac2dae94e921ed0f72719bd52d4d0138f305b993f778f7758'
            'b77a427df55a14182c10ad7e683b4d662df2846fcd38df2aa8918159d6be3ae2')

prepare() {
    cd gtk+-$pkgver
    patch -Np1 -i ../xid-collision-debug.patch

    sed -i '' -e 's|[{]libdir[}]/locale|{datadir}/locale|g' \
		configure
    sed -i '' -e 's|file,cups|file,cups,lpr|' \
		gtk/Makefile.in

}

build() {
    cd gtk+-$pkgver

    CXX=/usr/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-xinput=yes \
        --disable-cups LDFLAGS="-lintl" 

    # https://bugzilla.gnome.org/show_bug.cgi?id=655517
    gsed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    gmake
}

package_gtk2() {
    cd gtk+-$pkgver
    depends+=('adwaita-icon-theme')

    gmake DESTDIR="$pkgdir" install
    sed -i '' \
     -e "s#env python#env python2#" $pkgdir/usr/bin/gtk-builder-convert

    install -m644 "$srcdir/gtkrc" "$pkgdir/usr/share/gtk-2.0/gtkrc"
}
