pkgname=ri-li
pkgver=2.0.1
pkgrel=1
pkgdesc="Drive a toy wood engine in many levels and collect all the coaches to win"
arch=('i386' 'amd64')
url="http://ri-li.sourceforge.net/"
license=('GPL')
depends=('gcc-libs' 'sdl_mixer')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/Ri-li-$pkgver.tar.bz2 ri-li.desktop)
md5sums=('57a2ff50a0c704786da8adf61d78bf52' 'ff57f8b04b212af3e6f8c600c3e0c35b')

prepare() {
  cd $srcdir/Ri-li-$pkgver

  sed -i '' -e 's|-lSDL |`sdl-config --libs`|' configure
  sed -i '' -e 's| gentoo||' Makefile.in
  sed -i '' -e '/include/ s|iostream.h|iostream|' src/*.cc
  sed -i '' -e 's|cerr|std::&|g; s|endl|std::&|g' src/*.cc

}
build() {
  cd $srcdir/Ri-li-$pkgver
  ./configure --prefix=/usr
  gmake
}

package() {
  cd $srcdir/Ri-li-$pkgver
  gmake DESTDIR=${pkgdir} install

  install -dm755 $pkgdir/usr/share/{applications,pixmaps}
  install -m644 $srcdir/ri-li.desktop $pkgdir/usr/share/applications/ri-li.desktop
  install -m644 data/Ri-li-icon-48x48.png $pkgdir/usr/share/pixmaps/ri-li.png
}
