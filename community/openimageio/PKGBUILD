# $Id: PKGBUILD 118893 2014-09-12 21:07:51Z svenstaro $
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgname=openimageio
pkgver=1.4.16
pkgrel=5
pkgdesc="A library for reading and writing images, including classes, utilities, and applications"
arch=('i386' 'amd64')
url="http://www.openimageio.org/"
license=('custom')
depends=('openexr' 'boost-libs' 'jasper' 'glew' 'libtiff' 'opencolorio' 'intel-tbb')
makedepends=('cmake' 'qt4' 'python2' 'boost' 'mesa' 'git')
optdepends=('qt4: iv image viewer'
            'python2: bindings support')
source=("git+git://github.com/OpenImageIO/oiio.git#tag=Release-${pkgver}"
        'ustring-cxx11-std-string.patch'
        'fix-compiler-warning.patch'
        'stdmath.patch')
sha256sums=('SKIP'
            '67cb1d5cb520821d97cd2ba355f7696a96ad5a32b0edf6350e83a86f34f4dd04'
            'adc78209477a50d7c68c6b89db3628c402ac6ac1cc4bb19115fb5ac7193dca65'
            'f837bc1fa618431868c07b746077f515ad221d07c9bdc3f82fd1c375cc315bbb')

prepare() {
  cd oiio
  patch -Np1 < "${srcdir}/ustring-cxx11-std-string.patch"
  patch -Np1 < "${srcdir}/fix-compiler-warning.patch"
  patch -Np1 < $srcdir/stdmath.patch
}

build() {
  cd oiio

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DPYLIB_INSTALL_DIR=lib/python2.7/site-packages \
      -DUSE_EXTERNAL_TBB=ON \
      -DOIIO_BUILD_TESTS=OFF \
      ..
  make
}

package() {
  cd oiio/build

  make DESTDIR="$pkgdir" install

  # license
  cd ..
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
