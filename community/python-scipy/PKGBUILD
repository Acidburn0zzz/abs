pkgname=('python-scipy' 'python2-scipy')
pkgver=0.16.0
pkgrel=2
pkgdesc="SciPy is open-source software for mathematics, science, and engineering."
arch=('i386' 'amd64')
url="http://www.scipy.org/"
license=('BSD')
makedepends=('gcc-fortran' 'gnu-binutils' 'python-numpy' 'python2-numpy' 'python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("https://pypi.python.org/packages/source/s/scipy/scipy-${pkgver}.tar.gz")
md5sums=('eb95dda0f36cc3096673993a350cde77')

build() {
  # Changing the arithmetic parameter (Thanks to Fabrizio Castellano)
#  sed -i "s/\#define\ UNK\ 1/\#define\ IBMPC\ 1/" \
#    scipy-${pkgver}/scipy/special/cephes/mconf.h
  export LDFLAGS="-Wall -shared"
  # freebsd libgcc_s fuckup:
#  export LD_LIBRARY_PATH="/usr/lib:${LD_LIBRARY_PATH}"

  # 2 builds
  cp -R scipy-${pkgver} scipy-${pkgver}-py2

  # build for python3
  cd scipy-${pkgver}
  python3 setup.py config_fc --fcompiler=gnu95 build

  # build for python2
  cd ../scipy-${pkgver}-py2

  for file in $(find . -name '*.py' -print); do
       gsed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
       gsed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  python2 setup.py config_fc --fcompiler=gnu95 build
}

check() {
  # we need to do a temp install so we can import scipy
  # also, the tests must not be run from the scipy source directory
  export LDFLAGS="-Wall -shared"

  cd ${srcdir}/scipy-${pkgver}
  python3 setup.py config_fc --fcompiler=gnu95 install \
    --prefix=/usr --root=${srcdir}/test --optimize=1
  export PYTHONPATH=${srcdir}/test/usr/lib/python3.5/site-packages
  cd ${srcdir}
  python -c "from scipy import test; test('full')"

  cd ${srcdir}/scipy-${pkgver}-py2
  python2 setup.py config_fc --fcompiler=gnu95 install \
    --prefix=/usr --root=${srcdir}/test --optimize=1
  export PYTHONPATH=${srcdir}/test/usr/lib/python2.7/site-packages
  cd ${srcdir}
  python2 -c "from scipy import test; test('full')"
}

package_python-scipy() {
  depends=('python-numpy')
  provides=('python3-scipy' 'scipy')

  cd scipy-${pkgver}
  export LDFLAGS="-Wall -shared"

  python3 setup.py config_fc --fcompiler=gnu95 install \
    --prefix=/usr --root=${pkgdir} --optimize=1

  install -dm755 "${pkgdir}/usr/share/licenses/python-scipy"
  install -m644 LICENSE.txt \
    "${pkgdir}/usr/share/licenses/python-scipy/LICENSE"
}

package_python2-scipy() {
  depends=('python2-numpy')
  conflicts=('python-scipy<0.9.0')

  cd scipy-${pkgver}-py2
  export LDFLAGS="-Wall -shared"

  python2 setup.py config_fc --fcompiler=gnu95 install \
    --prefix=/usr --root=${pkgdir} --optimize=1

  install -dm755 "${pkgdir}/usr/share/licenses/python2-scipy"
  install -m644 LICENSE.txt \
    "${pkgdir}/usr/share/licenses/python2-scipy/LICENSE"
}

# vim:set ts=2 sw=2 et:
