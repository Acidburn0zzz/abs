pkgname=('wireshark-cli' 'wireshark-common' 'wireshark-gtk' 'wireshark-qt')
pkgbase=wireshark
pkgver=2.0.0
pkgrel=1
arch=('i386' 'amd64')
license=('GPL2')
makedepends=('qt5-base' 'gtk3' 'bash' 'gnutls' 'portaudio'
             'lua52' 'python' 'desktop-file-utils' 'hicolor-icon-theme')
url='http://www.wireshark.org/'
source=("http://www.wireshark.org/download/src/${pkgbase}-${pkgver}.tar.bz2")
sha1sums=('f8b226b40e36e4c431b4f70bc3f523b6dd53a501')

prepare() {
  cd ${pkgbase}-${pkgver}
  gsed -i 's/$(AM_V_RCC)rcc/&-qt4/p' ui/qt/Makefile.am
}

build() {
  cd ${pkgbase}-${pkgver}

  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --with-qt=5 \
      --with-gtk3=yes \
      --with-pcap \
      --with-zlib \
      --with-lua \
      --with-portaudio \
      --with-ssl \
      --with-krb5
  gmake all
}

package_wireshark-cli() {
  pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - CLI version'
  depends=('libgcrypt' 'gnutls' 'glib2' 'lua52')
  install='wireshark.install'
  conflicts=('wireshark')

  cd ${pkgbase}-${pkgver}

  gmake DESTDIR="${pkgdir}" install
  gmake DESTDIR="${pkgdir}" uninstall-local

  #wireshark uid group is 150
  chgrp 150 "${pkgdir}/usr/bin/dumpcap"
  chmod 754 "${pkgdir}/usr/bin/dumpcap"
  rm "${pkgdir}/usr/bin/wireshark" "${pkgdir}/usr/bin/wireshark-gtk"

  # Headers
  install -dm755 "${pkgdir}"/usr/include/${pkgbase}/{epan/{crypt,dfilter,dissectors,ftypes,wmem},wiretap,wsutil}

  install -m644 color.h config.h register.h ws_symbol_export.h "${pkgdir}/usr/include/${pkgbase}"
  for d in epan epan/crypt epan/dfilter epan/dissectors epan/ftypes wiretap wsutil; do
    install -m644 ${d}/*.h "${pkgdir}"/usr/include/${pkgbase}/${d}
  done
}

package_wireshark-common() {
  pkgdesc='Common files used by wireshark-gtk and wireshark-qt'
  depends=('hicolor-icon-theme' 'shared-mime-info' 'xdg-utils')
  install=wireshark-common.install

  cd "${pkgbase}-${pkgver}"

  gmake DESTDIR="${pkgdir}" install-data-local
  rm "${pkgdir}"/usr/share/applications/wireshark{,-gtk}.desktop
}

package_wireshark-gtk() {
  pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - GTK frontend'
  depends=('gtk3' 'portaudio' 'wireshark-cli' 'desktop-file-utils'
  'wireshark-common')
  replaces=(wireshark)
  conflicts=(wireshark)
  install='wireshark-gui.install'

  cd ${pkgbase}-${pkgver}

  install -dm755 "${pkgdir}/usr/bin/"
  install -m755 .libs/wireshark-gtk "${pkgdir}/usr/bin/wireshark-gtk"
  install -dm644 "${pkgdir}/usr/share/applications"
  install -Dm644 wireshark-gtk.desktop "${pkgdir}/usr/share/applications/wireshare-gtk.desktop"
}

package_wireshark-qt() {
  pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - Qt frontend'
  depends=('desktop-file-utils' 'qt5-base' 'wireshark-cli' 'wireshark-common')
  install='wireshark-gui.install'

  cd ${pkgbase}-${pkgver}

  install -dm755 "${pkgdir}/usr/bin/"
  install -Dm755 .libs/wireshark "${pkgdir}/usr/bin/wireshark"
  install -dm644 "${pkgdir}/usr/share/applications"
  install -Dm644 wireshark.desktop "${pkgdir}/usr/share/applications/wireshark.desktop"
  #gsed -e 's/^Name.*=Wireshark/& (QT)/g' -e '/Exec=/s/wireshark/&-qt/g' wireshark.desktop > "${pkgdir}/usr/share/applications/wireshark-qt.desktop"
}

