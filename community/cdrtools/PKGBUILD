pkgname=cdrtools
pkgver=3.02a06
pkgrel=1
pkgdesc="Original cdrtools supporting CD, DVD and BluRay burning"
arch=('i386' 'amd64')
url="http://cdrecord.berlios.de/private/cdrecord.html"
license=('CDDL')
conflicts=('cdrkit')
provides=('cdrkit')
makedepends=('gcc')
options=(!makeflags)
backup=('etc/default/cdrecord' 'etc/default/rscsi')
install=cdrtools.install
source=("http://downloads.sourceforge.net/cdrtools/cdrtools-${pkgver}.tar.bz2")
sha256sums=('ed79ab99414352ea9305163660b52b6a82394466bae03aebdbe2150997835eb1')

build() {
  cd "$srcdir"/cdrtools-${pkgver%%a*}

if [[ "$CARCH" == "amd64" ]]; then
	ln  -sf "$srcdir"/cdrtools-${pkgver%%a*}/RULES/i386-freebsd-cc.rul \
		"$srcdir"/cdrtools-${pkgver%%a*}/RULES/$CARCH-freebsd-cc.rul
else
        ln  -sf "$srcdir"/cdrtools-${pkgver%%a*}/RULES/i386-freebsd-cc.rul \
                "$srcdir"/cdrtools-${pkgver%%a*}/RULES/$CARCH-freebsd-cc.rul
fi

  export GMAKE_NOWARN=true
  gmake INS_BASE=/usr INS_RBASE=/
}

package() {
  cd "$srcdir"/cdrtools-${pkgver%%a*}

  export GMAKE_NOWARN=true
  gmake INS_BASE=/usr INS_RBASE=/ DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/include
  rm -rf "$pkgdir"/usr/lib/{*.a,profiled}

  # create symlinks for cdrkit compatibility
  cd "$pkgdir"/usr/bin
  ln -s cdrecord wodim
  ln -s readcd readom
  ln -s mkisofs genisoimage
  ln -s cdda2wav icedax
}

# vim:set ts=2 sw=2 et:
