# $Id: PKGBUILD 110972 2014-05-11 14:27:16Z bgyorgy $
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: kiefer <jorgelmadrid@gmail.com>

pkgname=lxdm
pkgver=0.5.0
pkgrel=1
pkgdesc='Lightweight X11 Display Manager'
arch=('i686' 'x86_64')
url="https://sourceforge.net/projects/lxdm/"
license=('GPL')
groups=('lxde')
depends=('gtk2' 'xorg-server' 'libexecinfo')
makedepends=('intltool' 'iso-codes')
optdepends=('gtk-engines: default GTK+ theme'
            'librsvg: display the default background')
install=$pkgname.install
backup=('etc/lxdm/lxdm.conf' 'etc/pam.d/lxdm' 'etc/lxdm/Xsession'
        'etc/lxdm/PreLogin' 'etc/lxdm/LoginReady' 'etc/lxdm/PostLogin'
        'etc/lxdm/PostLogout' 'etc/lxdm/PreReboot' 'etc/lxdm/PreShutdown')
source=(http://downloads.sourceforge.net/lxdm/$pkgname-$pkgver.tar.xz
        default-config.patch
        lxdm.pam
        Xsession)
options=(debug !strip)
md5sums=('a51686720e606ca456d7f56ae4159d1f'
         '302d4238a53f5084675a2d2a09971f3d'
         'c941ef896248bc7c03901b513490425c'
         'd9c8f8c9e6de52dbc389696454c8f572')

prepare(){
  cd "$srcdir/$pkgname-$pkgver"

  unset LDFLAGS

  # Adjust Arch-specific settings
  patch -Np1 -i ../default-config.patch

  # Use our custom pam and Xsession files
  cp ../lxdm.pam pam/lxdm
  cp ../Xsession data/Xsession

  # Support for pulseaudio
  echo 'test -x /usr/bin/pax11publish && /usr/bin/pax11publish -r' >>data/PostLogout.in

  sed -i '' -e 's|<shadow.h>|<xorg/shadow.h>|g' src/pam.c src/lxcom.c src/auth.c
  sed -i '' -e 's|/proc/|/compat/linux/proc|g' src/lxdm.c src/pam.c
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --libexecdir=/usr/lib/lxdm \
		--sysconfdir=/etc --localstatedir=/var --with-xconn=xlib --enable-debug \
		CFLAGS="${CFLAGS} `pkg-config --cflags pixman-1`" \
		LDFLAGS="${LDFLAGS} `pkg-config --libs pixman-1`" \
		LIBS="-lintl -lexecinfo"
  gmake
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install
  chmod 644 "$pkgdir/etc/lxdm/lxdm.conf"

  # Home directory
  install -dm 755 "$pkgdir/var/lib/lxdm"
  echo 'GDK_CORE_DEVICE_EVENTS=true' > "$pkgdir"/var/lib/lxdm/.pam_environment
  chown -R 121:121 "$pkgdir/var/lib/lxdm"
}
