#
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

pkgname=chrpath
# 0.16 depends on byteswap.h which we don't have in freebsd
pkgver=0.16
pkgrel=1
pkgdesc="Change or delete the rpath or runpath in ELF files"
arch=('i386' 'amd64')
url="http://directory.fsf.org/project/chrpath/"
license=('GPL2')
depends=('libc')
_port_patches=(patch-testsuite__Makefile.in
               patch-elf.c)
source=("https://alioth.debian.org/frs/download.php/latestfile/813/chrpath-$pkgver.tar.gz")
md5sums=('2bf8d1d1ee345fc8a7915576f5649982')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver
  for i in "${_port_patches[@]}"; do
    msg "patch $i"
    patch -p0 -i "${srcdir}/$i"
  done
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man
  gmake
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver
  gmake DESTDIR="${pkgdir}" docdir=/usr/share/doc/chrpath install
}
