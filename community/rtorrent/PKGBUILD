pkgname=rtorrent
pkgver=0.9.6
pkgrel=1
pkgdesc='Ncurses BitTorrent client based on libTorrent'
url='http://libtorrent.rakshasa.no/'
license=('GPL')
arch=('i386' 'amd64')
depends=('libtorrent=0.13.6' 'curl' 'xmlrpc-c' 'libsigc++' 'ncurses')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/rakshasa/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('8ca89ca9e8f0cf984198d030203087e93d24743dfa158091a5d225a70ca4c8cf')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# turn into c++11
	find . -name '*.h' -o -name '*.cc' | xargs gsed -i \
		-e 's/tr1::/std::/g' \
		-e 's/std::std::/std::/g' \
		-e '/include/s,tr1/,,' \
		-e '/namespace tr1/d'
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

  gsed '/AM_PATH_CPPUNIT/d' -i configure.ac
  ./autogen.sh

	export CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing -std=c++11"
	./configure \
		--prefix=/usr \
		--disable-debug \
		--with-xmlrpc-c \

	gmake
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -dm755 ${pkgdir}/usr/share/doc/${pkgname}
	install -m644 doc/rtorrent.rc "${pkgdir}"/usr/share/doc/rtorrent/rtorrent.rc
}
