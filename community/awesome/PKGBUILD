# $Id: PKGBUILD 98638 2013-10-16 01:00:11Z seblu $
# Maintainer: SÃ©bastien Luttringer
# Contributor: xduugu
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Vesa Kaihlavirta

pkgname=awesome
pkgver=3.5.8
pkgrel=1
pkgdesc='Highly configurable framework window manager'
arch=('i386' 'amd64')
url='http://awesome.naquadah.org/'
license=('GPL2')
depends=('cairo' 'dbus' 'gdk-pixbuf2' 'imlib2' 'libxdg-basedir' 'lua' 'lua-lgi'
         'pango' 'startup-notification' 'xcb-util-cursor' 'xcb-util-keysyms'
         'xcb-util-wm' 'xorg-xmessage' 'libpng')
makedepends=('asciidoc' 'cmake' 'docbook-xsl' 'doxygen' 'imagemagick' 'ldoc' 'xmlto')
optdepends=('rlwrap: readline support for awesome-client'
            'dex: autostart your desktop files'
            'vicious: widgets for the Awesome window manager')
provides=('notification-daemon')
backup=('etc/xdg/awesome/rc.lua')
options=('iconv')
source=("$url/download/$pkgname-$pkgver.tar.xz")
sha256sums=('7c30c96c57bd72cfed630bbccc344054fe44b37c0a79bdd98075c507af672ec6')

build() {
  cd $pkgname-$pkgver
  cmake \
    -DCMAKE_BUILD_TYPE=RELEASE \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc \
    -DLUA_LIBRARY=/usr/lib/liblua.so.5.3 \
    "${cmake_iconv_args}"
  gmake V=1
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
  # install desktop file so you can start awesome from your login manager
  install -dm755 "$pkgdir/usr/share/xsessions"
  install -m644 awesome.desktop "$pkgdir/usr/share/xsessions/awesome.desktop"
}

# vim:set ts=2 sw=2 et:
