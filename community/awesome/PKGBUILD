# $Id: PKGBUILD 98638 2013-10-16 01:00:11Z seblu $
# Maintainer: SÃ©bastien Luttringer
# Contributor: xduugu
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Vesa Kaihlavirta

pkgname=awesome
pkgver=3.5.2
pkgrel=2
pkgdesc='Highly configurable framework window manager'
arch=('i686' 'x86_64')
url='http://awesome.naquadah.org/'
license=('GPL2')
depends=(
  'cairo'
  'dbus'
  'gdk-pixbuf2'
  'imlib2'
  'libxdg-basedir'
  'lua'
  'lua-lgi'
  'pango'
  'startup-notification'
  'xcb-util-cursor'
  'xcb-util-keysyms'
  'xcb-util-wm'
  'xorg-xmessage'
)
makedepends=(
  'asciidoc'
  'cmake'
  'docbook-xsl'
  'doxygen'
  'imagemagick'
  'ldoc'
  'xmlto'
  'getopt'
)
optdepends=(
  'rlwrap: readline support for awesome-client'
  'dex: autostart your desktop files'
  'vicious: widgets for the Awesome window manager'
)
provides=('notification-daemon')
backup=('etc/xdg/awesome/rc.lua')
port_patches=(patch-CMakeLists.txt
	patch-awesomeConfig.cmake
	patch-naughty)
source=("$url/download/$pkgname-$pkgver.tar.xz"
	${port_patches[@]})
md5sums=('c16eaaaddf6f56b4e041007952e2a4fe'
         '39bdf017d2cc1a7668224dc982efbec9'
         '2a3a53656075aef064a25755dabf195a'
         '8f9deca3085bb5f9ccfb3d1a863f4731')

prepare() {
  cd $pkgname-$pkgver

  for p in ${port_patches[@]}; do
	patch -p0 -i "${srcdir}/${p}"
  done
}

build() {
  cd $pkgname-$pkgver
  cmake \
    -DCMAKE_BUILD_TYPE=RELEASE \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc \
    -DLUA_LIBRARY=/usr/lib/liblua.so.5.2
  gmake
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
  # install desktop file so you can start awesome from your login manager
  install -Dm644 awesome.desktop "$pkgdir/usr/share/xsessions/awesome.desktop"
}

# vim:set ts=2 sw=2 et:
