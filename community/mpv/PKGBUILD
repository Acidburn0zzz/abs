# $Id$
# Maintainer:  Bart.omiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Eivind Uggedal <eivind@uggedal.com>

pkgname=mpv
pkgver=0.3.7
pkgrel=1
pkgdesc='Video player based on MPlayer/mplayer2'
arch=('i686' 'x86_64')
license=('GPL')
url='http://mpv.io'
# - lirc-utils
depends=(
         'portaudio' 'ffmpeg' 'lcms2' 'libdvdread' 'libcdio-paranoia' 'libquvi' 'libgl'
         'libxinerama' 'mpg123' 'libxv' 'libxkbcommon' 'libva'
         'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'lua' 'libdvdnav'
         'libv4l'
        )
optdepends=('pulseaudio')
makedepends=('mesa' 'python-docutils' 'pulseaudio')
options=(!emptydirs)
source=(https://github.com/mpv-player/$pkgname/archive/v$pkgver.tar.gz)
md5sums=('0e1398c324775998b0e60fdb11b9e06d')

prepare() {
  cd $pkgname-$pkgver
  sed -i '' -e 's/\#include <alloca.h>//' audio/out/ao_alsa.c
  ./bootstrap.py
}


build() {
  unset CFLAGS
  unset LDFLAGS
  cd $pkgname-$pkgver

  ./waf configure --prefix=/usr \
    --confdir=/etc/mpv \
    --disable-joystick \
    --disable-radio \
    --disable-radio-capture \
    --disable-radio-v4l2 \
    --disable-libbs2b \
    --disable-direct3d \
    --disable-corevideo \
    --disable-cocoa \
    --disable-coreaudio
  ./waf build
}

package() {
  cd $pkgname-$pkgver
  ./waf install --destdir="$pkgdir"

  install -d "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 etc/{input,example}.conf \
    "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "$pkgdir"/usr/share/doc/mpv
}
