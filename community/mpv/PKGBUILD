# $Id$
# Maintainer:  Bart.omiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Eivind Uggedal <eivind@uggedal.com>

pkgname=mpv
pkgver=0.1.7
pkgrel=1
pkgdesc='Video player based on MPlayer/mplayer2'
arch=('i686' 'x86_64')
license=('GPL')
url='http://mpv.io'
depends=('portaudio' 'ffmpeg' 'lcms2' 'libxinerama' 'libquvi' 'mpg123' 'libxv' 'libxkbcommon' 'libva')
makedepends=('mesa' 'python-docutils')
options=(!emptydirs)
source=("https://github.com/mpv-player/$pkgname/archive/v$pkgver.tar.gz")

build() {
  unset CFLAGS
  unset LDFLAGS
  sed -i '' -e 's/\#include <alloca.h>//' ${srcdir}/$pkgname-$pkgver/audio/out/ao_alsa.c
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
    --confdir=/etc/mpv \
    --disable-lircc \
    --disable-joystick \
    --disable-radio \
    --disable-radio-capture \
    --disable-radio-v4l2 \
    --disable-rpath \
    --disable-libbs2b \
    --disable-direct3d \
    --disable-corevideo \
    --disable-cocoa \
    --disable-coreaudio
  gmake
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install

  install -d "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 etc/{input,example,encoding-example-profiles}.conf \
    "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "$pkgdir"/usr/share/doc/mpv
  install -m755 TOOLS/mpv_identify.sh "$pkgdir"/usr/bin
}
md5sums=('69185e255fe6e8b6b13dcf6ea9b2fefb')
