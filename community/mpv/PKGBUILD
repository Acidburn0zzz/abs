# $Id$
# Maintainer:  Anthony Donnelly <amzo@archbsd.net>

pkgname=mpv
pkgver=0.11.0
pkgrel=1
pkgdesc='Video player based on MPlayer/mplayer2'
arch=('i386' 'amd64')
license=('GPL')
url='http://mpv.io'
depends=(
         'ffmpeg' 'lcms2' 'libdvdread' 'libcdio-paranoia' 'libquvi' 'libgl' 'enca'
         'libxinerama' 'mpg123' 'libxv' 'libxkbcommon' 'libva'
         'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'lua' 'libdvdnav'
         'libguess' 'libv4l' 'portaudio'
        )
makedepends=('mesa' 'python-docutils' 'ladspa')
options=('!emptydirs' '!buildflags')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mpv-player/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('a2157174e46db46dad5deb1fde94283e72ebe922fd15447cb16a2a243fae5bda')

prepare() {
  cd $pkgname-$pkgver
  sed -i '' -e 's/\#include <alloca.h>//' audio/out/ao_alsa.c
  ./bootstrap.py
}


build() {
  cd $pkgname-$pkgver

  export CFLAGS="-I/usr/local/include"
  export LDFLAGS="-L/usr/local/lib -liconv"
  ./waf configure --prefix=/usr \
    --confdir=/etc/mpv \
    --enable-zsh-comp \
    --enable-libmpv-shared \
    --enable-cdda

  ./waf build
}

package() {
  cd $pkgname-$pkgver
  ./waf install --destdir="$pkgdir"

  install -d "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 etc/{input,example}.conf \
    "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "$pkgdir"/usr/share/doc/mpv
}
