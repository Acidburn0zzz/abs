# $Id: PKGBUILD 90092 2013-05-06 19:39:38Z foutrelis $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=snes9x
pkgname=('snes9x' 'snes9x-gtk')
pkgver=1.53
pkgrel=3
arch=('i386' 'amd64')
url="https://code.google.com/p/snes9x-gtk/"
license=('custom')
makedepends=('intltool' 'nasm' 'mesa' 'libpulse'  'libpng' 'sdl' 'gtk2' 'libxv' \
             'desktop-file-utils' 'gnome-icon-theme')
port_patches=(patch-gtk-configure
	patch-gtk-src-gtk__s9x.cpp
	patch-screenshot.cpp)
source=(http://snes9x-gtk.googlecode.com/files/${pkgbase}-${pkgver}-src.tar.bz2
        snes9x-gtk2.24.diff.gz
	${port_patches[@]})
sha1sums=('57be6480369f66b9aaaeb722789ce114f58ed1e2'
          '680dbe0a70f1821bc7931539e72f5a1a95e3938c'
          'eef5016a604e1d42399e90ccc53480e88765252d'
          '1e8e85a7c458fff6598aa57beb8d260c9de9e333'
          '85fc7e46a484993d5d2a37aad4d065d1d6732cda')

prepare() {
  cd ${pkgbase}-${pkgver}-src/gtk

  unset LDFLAGS

  for i in ${port_patches[@]}; do
	patch -p0 -i "${srcdir}/$i"
  done

  cd ..
  patch -p1 -i "${srcdir}/snes9x-gtk2.24.diff"
}

build() {
  cd ${pkgbase}-${pkgver}-src/unix
  ./configure --prefix=/usr --with-sdd1-decomp --enable-netplay LDFLAGS="${LDFLAGS} -lintl"
  gmake

  cd ../gtk
  ./configure --prefix=/usr --with-sdd1-decomp --with-netplay --with-opengl LDFLAGS="${LDFLAGS} -lintl"
  gmake
}

package_snes9x() {
  pkgdesc="A portable Emulator for the Super Nintendo Entertainment System"
  depends=('gcc-libs' 'libpng' 'libxext' 'libsm')

  cd ${pkgbase}-${pkgver}-src/unix
  install -dm 755 "${pkgdir}/usr/bin"
  install -m755 snes9x "${pkgdir}/usr/bin/snes9x"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -d -m755  "${pkgdir}/usr/share/doc/${pkgname}/"
  install -m644 ../docs/{snes9x.conf.default,{control-inputs,controls,snapshots}.txt} \
    "${pkgdir}/usr/share/doc/${pkgname}/"
  install -dm755  "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644 "${srcdir}/${pkgbase}-${pkgver}-src/docs/snes9x-license.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_snes9x-gtk() {
  pkgdesc="A portable Emulator for the Super Nintendo Entertainment System - GTK version"
  license=('custom' 'LGPL')
  depends=('sdl' 'libgl' 'libpulse' 'gtk2' 'libxv' 'desktop-file-utils' 'gnome-icon-theme')
  install=snes9x-gtk.install

  cd ${pkgbase}-${pkgver}-src/gtk
  make DESTDIR="${pkgdir}" install
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 ../docs/{snes9x.conf.default,{control-inputs,controls,snapshots}.txt} \
    "${pkgdir}/usr/share/doc/${pkgname}/"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/${pkgbase}-${pkgver}-src/docs/snes9x-license.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
