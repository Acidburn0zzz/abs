# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgname=libvirt
pkgver=1.2.20
pkgrel=9
pkgdesc="API for controlling virtualization engines (openvz,kvm,qemu,virtualbox,xen,etc)"
arch=('i386' 'amd64')
url="http://libvirt.org/"
license=('LGPL')
depends=('e2fsprogs' 'gnutls'  'libxml2'   'python2'
	 'avahi' 'polkit'  'libpciaccess'  'dbus-core' 'libxau' 'libxdmcp' 
	 'curl' 'libsasl' 'libgcrypt' 'libgpg-error' 'openssl' 'libxcb' 'gcc-libs'
	'libx11' 'libssh2' 'libgcrypt' 'libxml2' 'qemu' 'yajl')
makedepends=( 'dnsmasq' 'automake' 'autoconf' 'gm4' 'perl' 'pkg-config')
optdepends=('bridge-utils: for briged networking (default)'
	    'dnsmasq: for NAT/DHCP for guests'
	    'openbsd-netcat: for remote management over ssh'
	    'radvd'
	    'dmidecode'
	    'ebtables')
options=('emptydirs' )
backup=('etc/libvirt/libvirtd.conf'
	'etc/libvirt/qemu.conf'
	'etc/sasl2/libvirt.conf')
install="libvirt.install"
source=("http://libvirt.org/sources/$pkgname-$pkgver.tar.gz"
	'https://gitweb.gentoo.org/repo/gentoo.git/plain/app-emulation/libvirt/files/libvirtd.confd-r5'
	'https://gitweb.gentoo.org/repo/gentoo.git/plain/app-emulation/libvirt/files/libvirt-guests.confd'
	'https://gitweb.gentoo.org/repo/gentoo.git/plain/app-emulation/libvirt/files/libvirt-guests.init-r1'
	'https://gitweb.gentoo.org/repo/gentoo.git/plain/app-emulation/libvirt/files/libvirtd.init-r15')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export PYTHON=`which python2`

  for file in $(find . -name '*.py' -print); do
    sed -i'' -e 's_#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i'' -e 's_#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done
  export LDFLAGS=-lX11
  export RADVD=/usr/bin/radvd
 [ -f Makefile ] || SSH2_LIBS="-L/usr/lib -lssh2 -lssl" SH2_CFLAGS="-I/usr/include" \
		./configure --prefix=/usr --libexec=/usr/libexec/"$pkgname" \
		--sbindir=/usr/bin CFLAGS="${CFLAGS} -fPIC " \
		--without-polkit \
		--disable-werror \
		--with-pic \
		--with-qemu-user=nobody --with-qemu-group=nobody \
		--without-sasl \
		--without-avahi \
		--without-hal \
		--without-udev \
		--without-netcf \
		--without-network \
		--without-sysctl \
		--enable-nls  --with-libintl-prefix=/usr
  gmake

  sed -i'' -e 's|#group =.*|group="kvm"|' src/qemu/qemu.conf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  gmake DESTDIR="$pkgdir" install

  install -dm755 ${pkgdir}/etc/{rc.d,init.d,conf.d}
  install -m755 ${srcdir}/libvirtd.in ${pkgdir}/etc/rc.d/libvirtd
  install -m755 ${srcdir}/libvirtd.init-r15 ${pkgdir}/etc/init.d/libvirtd
  install -m755 ${srcdir}/libvirt-guests.init-r1 ${pkgdir}/etc/init.d/libvirt-guests
  install -m644 ${srcdir}/libvirtd.confd-r5 ${pkgdir}/etc/conf.d/libvirtd
  install -m644 ${srcdir}/libvirt-guests.confd ${pkgdir}/etc/conf.d/libvirt-guests

  rm ${pkgdir}/usr/lib/charset.alias
}
md5sums=('b1058b8062a9b76f55928ce87eb0f5fe'
         'e8f6ee334f35a10035b744f266f443ee'
         'd1cb1b2c05bce214a6c4f94a6f755658'
         '578165930288bdadc6999cbc4566bd45'
         '469b2603a87a97598c78176751c25ac9')

