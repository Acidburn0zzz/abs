pkgname=tint2
pkgver=0.12.3
pkgrel=1
pkgdesc="A basic, good-looking task manager for WMs"
arch=('i386' 'amd64')
url="https://gitlab.com/o9000/tint2"
license=('GPL2')
conflicts=('ttm-svn')
depends=('gtk2' 'imlib2' 'startup-notification')
makedepends=('cmake' 'startup-notification' 'git' 'ninja')
source=("$pkgname-$pkgver.tgz::https://gitlab.com/o9000/tint2/repository/archive.tar.gz?ref=$pkgver"
        'zombie-fix.patch')
sha256sums=('7f1f7cdb8eecfaaac620589c327bf29def457a9d38cee587769c93aacd2a3e36'
            'a1e942ca3bef05885989f899c0eaaf63f538f7b84cb43d1517d5d39aa64a4ef9')

prepare() {
  mv "${pkgname}-${pkgver}"-*/ "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # Applying all available patches
  for f in ../*.patch; do
    echo -e "--- ${f##*/} ---"
    patch -Np1 -i $f && echo "${f##*/} OK!" || echo "${f##*/} FAILED!"
  done
}

build() {
  cd "${pkgname}-${pkgver}"

  # Generating Ninja project files
  mkdir -p build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DENABLE_TINT2CONF=1 -G Ninja

  # Compiling
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C "$pkgname-${pkgver}/build" install
}
