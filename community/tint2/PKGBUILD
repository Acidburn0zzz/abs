# $Id: PKGBUILD 64903 2012-02-18 20:04:04Z bpiotrowski $
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Blue Peppers <bluepeppers@archlinux.us>
# Contributor: Stefan Husmann< stefan-husmann@t-online.de>

pkgname=tint2
pkgver=0.12.1
pkgrel=1
pkgdesc="A basic, good-looking task manager for WMs"
arch=('i686' 'amd64')
url="https://gitlab.com/o9000/tint2"
license=('GPL2')
conflicts=('ttm-svn')
depends=('gtk2' 'imlib2' 'startup-notification')
makedepends=('cmake' 'startup-notification' 'git' 'ninja')
source=("${pkgname}-${pkgver}.tgz::https://gitlab.com/o9000/tint2/repository/archive.tar.gz?ref=${pkgver}"
        'zombie-fix.patch')
sha256sums=('097131b0c6bb2d7e366e0b15ba0a9174e826dc03e4eace004f5e66a29a5ac8bc'
            '499c0d867bebe59128c1b59424510da77b14b8fd9907a2822a2c318dbfb74d66')

prepare() {
  cd "${pkgname}.git"
  # Applying all available patches
  for f in ../*.patch; do
    echo -e "--- ${f##*/} ---"
    patch -Np1 -i $f && echo "${f##*/} OK!" || echo "${f##*/} FAILED!"
  done
}

build() {
  cd "${pkgname}.git"

  # Generating Ninja project files
  mkdir -p build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DENABLE_TINT2CONF=1 -G Ninja

  # Compiling
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C "$pkgname.git/build" install
}
