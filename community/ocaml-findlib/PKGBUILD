pkgname=ocaml-findlib
pkgver=1.5.6
pkgrel=8
license=('MIT')
arch=('i386' 'amd64')
pkgdesc='Objective Caml (OCaml) package manager'
url='http://projects.camlcity.org/projects/findlib.html'
depends=('ocaml')
makedepends=('gcc')
source=("http://download.camlcity.org/download/findlib-$pkgver.tar.gz")
options=('!strip' 'zipman') # otherwise the bytecode gets broken
patchdir="findlib-$pkgver"
md5sums=('91585dd5459cb69bfd9a0689bf222403')

build() {
  cd "$srcdir/findlib-$pkgver"

  ./configure -config /etc/findlib.conf -sitelib /usr/lib/ocaml -mandir /usr/share/man
  gmake all opt
}

package () {
  cd "$srcdir/findlib-$pkgver"

  gmake prefix="$pkgdir" install 

  # add the old site-lib to the path to maintain compatibility with old style packages
  sed -i '' -e 's/path=\"\/usr\/lib\/ocaml\"/path="\/usr\/lib\/ocaml:\/usr\/lib\/ocaml\/site-lib"/' \
    "${pkgdir}/etc/findlib.conf"

  install -m755 src/findlib/ocamlfind_opt "$pkgdir/usr/bin/"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
