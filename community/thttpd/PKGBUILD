# $Id: PKGBUILD 93680 2013-07-08 11:41:45Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Contributor: Stewart Starbuck <stewart@stewartstarbuck.co.uk>

pkgname=thttpd
pkgver=2.26
pkgrel=1
pkgdesc='Simple, small, portable, fast, and secure HTTP server'
url='http://www.acme.com/software/thttpd/'
license=('custom:BSD')
arch=('i686' 'amd64')
backup=('etc/thttpd.conf')
source=("http://www.acme.com/software/thttpd/${pkgname}-${pkgver}.tar.gz"
        'forwarded-for.patch'
        'discreet.patch'
        'crypt.patch'
        'logrotate.d'
        'config'
        'thttpd.rc.d'
        'thttpd.in'
        'patch-mmc.c')
sha256sums=('4693d7c421fe1bd25553a8891cbb447d2bab3e0df01c029dadd79596b9bdb4c5'
            'eb3be98c65b8f9297867205772cd9939f526ac1c2227c24ba7e8ff754dd2216c'
            '9935d23a4de441b6e34f04fc402839faef82209a0ebb78bfed8b94b2a29f5b71'
            'c832bd07a132ba12fa9941fcfa019e65caa2453de792b631ec2e63bed4dadb1d'
            '9dacfbd65674efa70b172f55184fa172c325c0014dc769200bbf8e050db6538d'
            '50d7ca4b26f6052aa8f2a7eb6200dca43182999061c0778d675f562170d830fb'
            'a3398bfa75abcd92bdec991fbcd146f27819840ba4614193acb00afa634ac45a'
            'b0dee5688fcc1e2a2d50cd9e0aa97a8e9bf81022dc49d5570987775274dcb339'
            '03ad604039c5efecccdff1dfe3d0c6e820edac237540490b67af03513e017dbc')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../forwarded-for.patch
	patch -p1 -i ../discreet.patch
	patch -p1 -i ../crypt.patch
	patch -p1 -i ../patch-mmc.c
	echo 'mkv	video/x-matroska' >> mime_types.txt
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	CPPFLAGS= ./configure --prefix=/usr --mandir=/usr/share/man
	gsed \
		-e 's/^CFLAGS =/CFLAGS +=/' \
		-e '/^STATICFLAG =/c STATICFLAG =' \
		-i Makefile */Makefile
	gmake
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	install -d "${pkgdir}"/usr/share/man/man{1,8}
	gmake \
		BINDIR="${pkgdir}"/usr/bin \
		WEBDIR="${pkgdir}"/srv/http \
		MANDIR="${pkgdir}"/usr/share/man \
		WEBGROUP=wheel install

	rm -fr "${pkgdir}"/srv
	chown -R root:wheel "${pkgdir}"
	chmod -R 755 "${pkgdir}"/usr/bin # strip needs u+w

	install -dm755 "${pkgdir}"/etc
	install -m644 ../config "${pkgdir}"/etc/thttpd.conf
	install -dm755 "${pkgdir}"/etc/logrotate.d
	install -m644 ../logrotate.d "${pkgdir}"/etc/logrotate.d/thttpd

	install -d "${pkgdir}"/var/log/thttpd
	install -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
	head -n 26 thttpd.c > "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE

	# conflicts with apache
	mv "${pkgdir}"/usr/bin/htpasswd{,-thttpd}
	mv "${pkgdir}"/usr/share/man/man1/htpasswd{,-thttpd}.1

	cd "${srcdir}"
	# openrc
	install -dm755 "${pkgdir}"/etc/init.d
	install -m755 thttpd.rc.d "${pkgdir}"/etc/init.d/thttpd
	# freebsd-init
	install -dm755 "${pkgdir}"/etc/rc.d
	install -m755 thttpd.in "${pkgdir}"/etc/rc.d/thttpd
}
