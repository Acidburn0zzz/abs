# $Id: PKGBUILD 93680 2013-07-08 11:41:45Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Contributor: Stewart Starbuck <stewart@stewartstarbuck.co.uk>

pkgname=thttpd
pkgver=2.25b
pkgrel=11
pkgdesc='Simple, small, portable, fast, and secure HTTP server'
url='http://www.acme.com/software/thttpd/'
license=('custom:BSD')
arch=('i686' 'amd64')
backup=('etc/thttpd.conf')
source=("http://www.acme.com/software/thttpd/${pkgname}-${pkgver}.tar.gz"
        'forwarded-for.patch'
        'discreet.patch'
        'getline.patch'
        'logrotate.d'
        'config'
        'thttpd.rc.d'
        'thttpd.in')
sha1sums=('cd0e823233ad73e34ee1c9b871b01050feb9db52'
          '1293fbaf83a489c65d6d2b107d797c687233d96a'
          '1e73fca7d9ac2579da317f985c8666a181dc37c1'
          '5ac14035a10b9155d813b363520e9aa74c5d91fc'
          '36ffeefd1675ca4920605b1b5ca32dd5141a8f23'
          '98f5b5783c9527aebc09648b38a4a487542eb906'
          '54a4faa45ddecea1cc4f753680c6d6d4c6fb5ffd'
          '159231908bb170297942d19b3faab90380f0379e')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../forwarded-for.patch
	patch -p1 -i ../discreet.patch
	patch -p1 -i ../getline.patch
	echo 'mkv	video/x-matroska' >> mime_types.txt
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	CPPFLAGS= ./configure --prefix=/usr --mandir=/usr/share/man
	gsed \
		-e 's/^CFLAGS =/CFLAGS +=/' \
		-e '/^STATICFLAG =/c STATICFLAG =' \
		-i Makefile */Makefile
	gmake
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	install -d "${pkgdir}"/usr/share/man/man{1,8}
	gmake \
		BINDIR="${pkgdir}"/usr/bin \
		WEBDIR="${pkgdir}"/srv/http \
		MANDIR="${pkgdir}"/usr/share/man \
		WEBGROUP=wheel install

	rm -fr "${pkgdir}"/srv
	chown -R root:wheel "${pkgdir}"
	chmod -R 755 "${pkgdir}"/usr/bin # strip needs u+w

	install -dm755 "${pkgdir}"/etc
	install -m644 ../config "${pkgdir}"/etc/thttpd.conf
	install -dm755 "${pkgdir}"/etc/logrotate.d
	install -m644 ../logrotate.d "${pkgdir}"/etc/logrotate.d/thttpd

	install -d "${pkgdir}"/var/log/thttpd
	install -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
	head -n 26 thttpd.c > "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE

	# conflicts with apache
	mv "${pkgdir}"/usr/bin/htpasswd{,-thttpd}
	mv "${pkgdir}"/usr/share/man/man1/htpasswd{,-thttpd}.1

	cd "${srcdir}"
	# openrc
	install -dm755 "${pkgdir}"/etc/init.d
	install -m755 thttpd.rc.d "${pkgdir}"/etc/init.d/thttpd
	# freebsd-init
	install -dm755 "${pkgdir}"/etc/rc.d
	install -m755 thttpd.in "${pkgdir}"/etc/rc.d/thttpd
}
