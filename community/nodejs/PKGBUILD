pkgname=nodejs
pkgver=5.1.0
pkgrel=1
pkgdesc='Evented I/O for V8 javascript'
arch=('i386' 'amd64')
url='http://nodejs.org/'
license=('MIT')
depends=( 'libexecinfo')
makedepends=('python2' 'gcc' 'gsed')
checkdepends=('curl')
options=('!emptydirs')
source=("http://nodejs.org/dist/v$pkgver/node-v$pkgver.tar.gz")
sha256sums=('25b2d3b7dd57fe47a483539fea240a3c6bbbdab4d89a45a812134cf1380ecb94')

prepare() {
  cd node-v$pkgver

  msg 'Fixing for python2 name'
  find . -type f -exec gsed \
    -e 's_^#!/usr/bin/env python$_&2_' \
    -e 's_^\(#!/usr/bin/python2\).[45]$_\1_' \
    -e 's_^#!/usr/bin/python$_&2_' \
    -e "s_'python'_'python2'_" -i {} \;
  find test/ -type f -exec gsed 's_python _python2 _' -i {} \;
  #find ./deps -type f -path ./deps/v8 -prune -o  -exec gsed -i "s\python \python2 \g" -i {} \;
  gsed -i "s/python /python2 /g" ./tools/gyp/pylib/gyp/generator/xcode.py
  gsed -i "s/python -c/python2 -c/g" ./deps/v8/build/toolchain.gypi
  gsed -i "s/python -c/python2 -c/g" ./deps/v8/build/standalone.gypi
  gsed -i "s/python -c/python2 -c/g" ./deps/v8/Makefile.nacl
  gsed -i "s/python -c/python2 -c/g" ./deps/v8/Makefile
  gsed -i "s/python -c/python2 -c/g" ./deps/v8/Makefile.android
  #sed -i "s/python /python2 /g" Makefile.build
}

build() {
  cd node-v$pkgver

  export PYTHON=python2
  ./configure \
    --prefix=/usr \
    --without-npm

  gmake -j4
}

check() {
  cd node-v$pkgver
  gmake test || true
}

package() {
  cd node-v$pkgver

  gmake DESTDIR="$pkgdir" install

  # install docs as per user request
  install -d "$pkgdir"/usr/share/doc/nodejs
  cp -r doc/api/{*.html,assets} \
    "$pkgdir"/usr/share/doc/nodejs

  install -dm755 "$pkgdir"/usr/share/licenses/nodejs/
  install -m644 LICENSE \
   "$pkgdir"/usr/share/licenses/nodejs/LICENSE

  mv "${pkgdir}/usr/man" "${pkgdir}/usr/share/man"
}

# vim:set ts=2 sw=2 et:
