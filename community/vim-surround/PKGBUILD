# $Id: PKGBUILD 82310 2013-01-14 20:58:57Z thestinger $
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Aaron Schaefer <aaron@elasticdog.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=vim-surround
pkgver=2.1
pkgrel=1
pkgdesc='Provides mappings to easily delete, change, and add paired "surroundings"'
arch=('any')
url='https://github.com/tpope/vim-surround'
license=('custom:vim')
groups=('vim-plugins')
depends=('vim')
install=vimdoc.install
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/tpope/vim-surround/archive/v${pkgver}.tar.gz"
        'license.txt')
md5sums=('85ffd09f75fe308f0bde43b0ce39bc2c'
         'cd49910a3dde6fe0a7e24ebe8cbf7066')

package() {
  cd "${pkgname}-${pkgver}"

  local installpath="$pkgdir/usr/share/vim/vimfiles"

  install -dm755 $installpath/{doc,plugin}
  install -dm755 $pkgdir/usr/share/licenses/$pkgname

  install -m644 doc/surround.txt "$installpath/doc/surround.txt"
  install -m644 plugin/surround.vim "$installpath/plugin/surround.vim"
  install -m644 ../license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
