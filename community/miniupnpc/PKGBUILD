pkgname=miniupnpc
pkgver=1.9.20151008
pkgrel=1
pkgdesc="A small UPnP client library/tool to access Internet Gateway Devices"
arch=('i386' 'amd64')
url="http://miniupnp.free.fr"
license=('BSD')
depends=('freebsd-world')
source=("http://miniupnp.free.fr/files/$pkgname-$pkgver.tar.gz")
sha256sums=('e444ac3b587ce82709c4d0cfca1fe71f44f9fc433e9f946b12b9e1bfe667a633')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  gmake
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  gmake PREFIX="$pkgdir/" install
  install -dm755 "${pkgdir}"/usr/share/man/man3
  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}

  install -Dm644 man3/miniupnpc.3 "${pkgdir}"/usr/share/man/man3/miniupnpc.3
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
