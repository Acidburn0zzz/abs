#
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=youtube-dl
pkgver=2016.01.09
pkgrel=1
pkgdesc="A small command-line program to download videos from YouTube.com and a few more sites"
arch=('i386' 'amd64')
url="http://rg3.github.com/youtube-dl/"
license=('custom')
depends=('python')
makedepends=('python-setuptools')
optdepends=('ffmpeg: for video post-processing')
source=(http://youtube-dl.org/downloads/${pkgver}/${pkgname}-${pkgver}.tar.gz{,.sig})
validpgpkeys=('7D33D762FD6C35130481347FDB4B54CBA4826A18'
              '428DF5D63EF07494BB455AC0EBF01804BCF05F6B')
sha256sums=('463cfed59a1e2c6609a8e3d3354e0494833f1b249fe1979984b06a51ac5c904d'
            'SKIP')

prepare() {
  cd ${pkgname}
  gsed -i 's|etc/bash_completion.d|share/bash-completion/completions|' setup.py
}

package() {
  cd "${srcdir}/${pkgname}"
  python setup.py install --root="${pkgdir}/" --optimize=1
  mv "${pkgdir}/usr/share/bash-completion/completions/youtube-dl"{.bash-completion,}
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
