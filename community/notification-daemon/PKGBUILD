# $Id: PKGBUILD 99929 2013-10-31 02:31:50Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Mark Rosenstand <mark@borkware.net>

pkgname=notification-daemon
pkgver=0.7.6
pkgrel=1
pkgdesc="Notification daemon for the desktop notifications framework"
arch=(i686 x86_64)
license=(GPL)
url="http://www.galago-project.org/specs/notification/"
depends=(gtk3 libcanberra)
makedepends=(pkgconfig intltool)
options=('!emptydirs')
source=(http://download.gnome.org/sources/${pkgname}/0.7/${pkgname}-${pkgver}.tar.xz
	"org.gnome.Notifications.service")
sha256sums=('64d0ce6fb12c94c3b73b701502c804c408cb5a94580bcae5ac37607639f7a0b3'
            '75b67e1e4d9cfa1c2316a4a35f2468f6c9977c314322449023af0ef1bbf82ca4')

build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr --sysconfdir=/etc \
        --libexecdir=/usr/libexec/notification-daemon-1.0 \
        --localstatedir=/var --disable-static
    gmake
}

check() {
    cd ${pkgname}-${pkgver}
    gmake check
}

package() {
    cd ${pkgname}-${pkgver}
    gmake DESTDIR=${pkgdir} install

    install -dm755 "${pkgdir}/usr/share/dbus-1/services"
    install -m755 "${srcdir}/org.gnome.Notifications.service" "${pkgdir}/usr/share/dbus-1/services/"
}
