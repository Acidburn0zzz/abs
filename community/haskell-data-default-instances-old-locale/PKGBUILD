# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Wolfgang Bumiller <archbsd at bumiller dot com>
_hkgname=data-default-instances-old-locale
pkgname=haskell-data-default-instances-old-locale
pkgver=0.0.1
pkgrel=4
pkgdesc="Default instances for types in old-locale"
url="http://hackage.haskell.org/package/${_hkgname}"
license=('custom:BSD3')
arch=('i386' 'amd64')
depends=('ghc=7.10.2' 'haskell-data-default-class' 'haskell-old-locale=1.0.0.7')
options=('strip' 'staticlibs')
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
install=${pkgname}.install
md5sums=('43bd868049d870ee722eda6bdd89fc28')

build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    runhaskell Setup configure -O ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } --enable-split-objs --enable-shared \
       --prefix=/usr --docdir=/usr/share/doc/${pkgname} --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    gsed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}
package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -d -m755               ${pkgdir}/usr/share/haskell/${pkgname}
    install    -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
    install -d -m755         ${pkgdir}/usr/share/licenses/${pkgname}
    install    -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
