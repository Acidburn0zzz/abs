pkgname=pugixml
pkgver=1.7
pkgrel=1
pkgdesc="Light-weight, simple and fast XML parser for C++ with XPath support"
url='http://pugixml.org'
arch=('amd64' 'i386')
license=('MIT')
makedepends=('cmake')
source=("http://github.com/zeux/pugixml/releases/download/v${pkgver}/pugixml-${pkgver}.tar.gz")
sha256sums=('fbe10d46f61d769f7d92a296102e4e2bd3ee16130f11c5b10a1aae590ea1f5ca')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake "../pugixml-${pkgver}/scripts" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED_LIBS=ON
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
