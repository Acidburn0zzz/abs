# $Id: PKGBUILD 180567 2013-03-23 15:06:13Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=gnutls
pkgver=3.2.8
pkgrel=2
pkgdesc="A library which provides a secure layer over a reliable transport layer"
arch=('i386' 'amd64')
license=('GPL3' 'LGPL')
url="http://www.gnutls.org/"
install=gnutls.install
options=('!libtool' '!zipman')
depends=('gcc-libs>=4.7.1-5' 'libtasn1>=3.0' 'readline' 'nettle>=2.4' 'p11-kit>=0.12')
makedepends=('valgrind')
source=(ftp://ftp.gnutls.org/gcrypt/gnutls/v3.1/${pkgname}-${pkgver}.tar.xz{,.sig})

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  sed -i '' -e "/^ltmain=/!s|\$$ac_aux_dir/ltmain.sh|/usr/share/libtool/config/ltmain.sh|g" \
      -e "/^LIBTOOL=/s|\$$\(top_builddir\)/libtool|/usr/bin/libtool|g" \
      aclocal.m4
  find . -name "Makefile.in" | \
    xargs sed -i '' -e 's|^LIBTOOL[ ]*=.*|LIBTOOL=/usr/bin/libtool|g'
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
    --with-zlib \
    --disable-static \
    --disable-guile \
    --disable-valgrind-tests

  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
  
  # lots of .png files are put into infodir and are gzipped by makepkg! this may need to be fixed by using !zipman
  # gzip -9 all files in infodir and manpages manually
  find "$pkgdir/usr/share/info" -name '*.info*' -exec gzip -9 {} \;
  find "$pkgdir/usr/share/man" -exec gzip -9 {} \;
}
md5sums=('a961705dbe0cbbc7220b6c2d2d9e0028'
         'SKIP')
