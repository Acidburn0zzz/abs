# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Daniele Paolella <dp@mcrservice.it>
# Contributor: Claudiu Traistaru <claudiu2004@gmail.com>

pkgname=supervisor
pkgver=3.2.0
pkgrel=1
pkgdesc="A system for controlling process state under UNIX"
arch=('any')
url="http://supervisord.org"
license=('custom')
depends=('python2-meld3' 'python2-setuptools')
backup=('etc/supervisord.conf')
source=("http://pypi.python.org/packages/source/s/$pkgname/$pkgname-$pkgver.tar.gz"
        'supervisord.conf'
        'supervisord'
        'supervisord.in')
sha256sums=('522d7dad95c4b26a448bf39430a5404542000c15b45a2cf188c2fe05821965ce'
            '2fe8a2e8ad6d410949999cbd1a6825b7720070a6bfd7d03b7e767fa6c702478d'
            '518e8dbb2e81b701369a3156e56c640626bb92ae656dcdf7b6ef15cc8f5da90d'
            '9b609b27a0e0372243f60357b0c7cfcd54e4b8c870e2f0e2778951367c0959a1')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"
	rm -rf "supervisor/medusa/"{debian,demo,docs,test}
	find ./ -type f -name "*.py" -exec sed -i '' -e 's|/usr/bin/env python|/usr/bin/env python2|' {} \;
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	python2 setup.py build
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	python2 setup.py install -O1 --skip-build --prefix=/usr --root="$pkgdir"
	install -dm755 "$pkgdir/etc/"
	install -m600 "$srcdir/supervisord.conf" "$pkgdir/etc/supervisord.conf"
	#install -dm755 "$pkgdir/etc/logrotate.d"
	#install -m644 "$srcdir/supervisor.logrotate" "$pkgdir/etc/logrotate.d/supervisor"
   	
	install -m755 -d "$pkgdir/etc/init.d/"
	install -m755 ../supervisord "$pkgdir/etc/init.d/supervisord"
	install -m755 -d "$pkgdir/usr/local/etc/rc.d"
	install -m755 ../supervisord.in "$pkgdir/usr/local/etc/rc.d/supervisord"
 
	install -d "$pkgdir/usr/share/licenses/$pkgname"
	install -d "$pkgdir/var/log/$pkgname"
	install -d "$pkgdir/etc/supervisor.d"
	mv LICENSES.txt COPYRIGHT.txt "$pkgdir/usr/share/licenses/$pkgname"
}
