# $Id$
# Maintainer: Alexander R.dseth <rodseth@gmail.com>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: David Moore <davidm@sjsoft.com>

pkgbase=python-simplejson
pkgname=("$pkgbase" 'python2-simplejson')
pkgver=3.7.2
pkgrel=3
pkgdesc='Simple, fast, extensible JSON encoder/decoder for Python'
license=('MIT')
arch=('i386' 'amd64')
url='http://undefined.org/python/#simplejson'
makedepends=('python-setuptools' 'python2-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/simplejson/simplejson/tarball/v${pkgver}")
sha256sums=('80746b1d27b1557bef290aeb8dbe155872b081e52b160b84b95b5aef81283cd3')

package_python-simplejson() {
  depends=('python')

  cd "$srcdir/simplejson-simplejson-"*
  python setup.py install --root="$pkgdir"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname" 
  install -m644 "$srcdir/simplejson-simplejson"-*/LICENSE.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-simplejson() {
  depends=('python2')

  cd "$srcdir/simplejson-simplejson-"*
  python2 setup.py install --root="$pkgdir"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname" 
  install -m644 "$srcdir/simplejson-simplejson"-*/LICENSE.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
