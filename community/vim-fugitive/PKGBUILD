# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: M Rawash <mrawash@gmail.com>
# Contributor: János Illés <ijanos@gmail.com>

pkgname=vim-fugitive
pkgver=2.2
pkgrel=1
pkgdesc="A git wrapper so awesome, it should be illegal."
arch=('any')
url="https://github.com/tpope/vim-fugitive"
license=('custom:vim')
groups=('vim-plugins')
depends=('vim')
install=vimdoc.install
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/tpope/${pkgname}/archive/v${pkgver}.tar.gz"
        'license.txt')
md5sums=('d1c245be66c6a7817f3dab47ebdddfd8'
         'cd49910a3dde6fe0a7e24ebe8cbf7066')

package() {
  cd "${pkgname}-${pkgver}"

  local installpath="$pkgdir/usr/share/vim/vimfiles"

  install -dm755 $installpath/{doc,plugin}
  install -dm755 $pkgdir/usr/share/licenses/$pkgname

  install -m644 doc/fugitive.txt "$installpath/doc/fugitive.txt"
  install -m644 plugin/fugitive.vim "$installpath/plugin/fugitive.vim"
  install -m644 ../license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
