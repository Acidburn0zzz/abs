pkgbase=python-pluggy
pkgname=(python-pluggy python2-pluggy)
pkgver=0.3.1
pkgrel=3
pkgdesc="Plugin and hook calling mechanisms for python"
arch=('any')
url="https://www.pluggypayments.com/docs/python"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
#checkdepends=('python-pytest' 'python2-pytest')
source=("https://pypi.python.org/packages/source/p/pluggy/pluggy-$pkgver.tar.gz")
sha512sums=('ebff28ef6d5eb78d8043fcf3860e43dd80bd800c12f6f744ae2a8757815f94034c858dbd6f4ab35dd4c609d4446aeb1783ace4f54ea6c3de6ec295ec06c7abb8')

prepare() {
  cp -a pluggy-$pkgver{,-py2}
}

build() {
  cd pluggy-$pkgver
  python setup.py build

  cd ../pluggy-$pkgver-py2
  python2 setup.py build
}

##disabling until we have the check deps on i386
#check() {
#  cd pluggy-$pkgver
#  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test

#  cd ../pluggy-$pkgver-py2
#  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test2
#}

package_python-pluggy() {
  depends=('python')

  cd pluggy-$pkgver
  python setup.py install -O1 --root "${pkgdir}"

  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pluggy() {
  depends=('python2')

  cd pluggy-$pkgver-py2
  python2 setup.py install -O1 --root "${pkgdir}"

  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
