# $Id: PKGBUILD 94892 2013-08-01 06:55:08Z bpiotrowski $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:Biginoz < biginoz AT free point fr>
# Contributor: Ignacio Galmarino <igalmarino@gmail.com>
# Contributor: Matthias Sobczyk <matthias.sobczyk@googlemail.com>
# Contributor: Kamil Kaminski <kyle@kkaminsk.com>

pkgname=minidlna
pkgver=1.1.5
pkgrel=2
pkgdesc="A DLNA/UPnP-AV Media server (aka ReadyDLNA)"
arch=('i386' 'amd64')
url="http://sourceforge.net/projects/minidlna/"
license=('GPL')
depends=('libexif' 'libjpeg' 'libid3tag' 'flac' 'libvorbis' 'ffmpeg' 'sqlite')
backup=('etc/minidlna.conf')
install=minidlna.install
source=(http://downloads.sourceforge.net/project/minidlna/minidlna/$pkgver/minidlna-$pkgver.tar.gz
        minidlna.in)
sha256sums=('8477ad0416bb2af5cd8da6dde6c07ffe1a413492b7fe40a362bc8587be15ab9b'
            'c3106ce0f3596917e1640a674cdc3f78f855732f1473a6e85bba96ffa314e80b')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  for i in "${_port_patches[@]}"; do
    patch -p0 -i "${srcdir}/$i"
  done

  gsed -i 's|#user=.*|user=minidlna|g' minidlna.conf
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  DESTDIR=$pkgdir make install
  install -dm755 ${pkgdir}/etc/rc.d
  install -m644 minidlna.conf ${pkgdir}/etc/minidlna.conf
  install -m755 "${srcdir}/minidlna.in" ${pkgdir}/etc/rc.d/minidlna
}
