#

pkgname=vim-ide
pkgver=0.4.4
_scriptid=19207
pkgrel=1
pkgdesc="vim-project based IDE like plugin"
arch=('any')
url="http://www.vim.org/scripts/script.php?script_id=69"
license=('custom')
depends=('vim')
groups=('vim-plugins')
install=vimdoc.install
options=('!emptydirs')
_name=${pkgname#vim-}
source=(${_name}-$pkgver.tar.gz::http://www.vim.org/scripts/download_script.php?src_id=$_scriptid
	'LICENSE')
md5sums=('c6f1cb9f42d78a7fa7aa6238d4daa0fc'
         '5f8e6286cb497ec9a8fe2b24dacabf5d')

build() {
	cd ${srcdir}

	tar xzf ${_name}-${pkgver}.tar.gz

	# We are installing this as package - fixup the default icon folder path
	sed -i '' -e 's@\(call s:Set("g:IDE_IconFolder",'\''\)~/.vim/@\1vimfiles/@' \
	  ide/ide.vim
}

_icons=(ideClose.png ideEdit.png ideOpen.png ideReadOnly.png)
_plugins=(ide.vim ideSyntax.pl)
_doc=(ide.txt)

package() {
	cd ${srcdir}/${_name}

	installpath="${pkgdir}/usr/share/vim/vimfiles"

	install -dm755 $pkgdir/usr/share/{licenses/$pkgname,vim/vimfiles/{doc,plugin,icons}}

	for i in ${_doc[@]}; do
		install -m644 "${i}" "${installpath}/doc/${i}"
	done
	for i in ${_icons[@]}; do
		install -m644 "${i}" "${installpath}/icons/${i}"
	done
	for i in ${_plugins[@]}; do
		install -m644 "${i}" "${installpath}/plugin/${i}"
	done

	install -m644 "${srcdir}/LICENSE" ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
