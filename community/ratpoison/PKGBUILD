# $Id: PKGBUILD 88374 2013-04-16 20:26:08Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: juergen <juergen@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=ratpoison
pkgver=1.4.8
pkgrel=1
pkgdesc="A simple keystroke-driven window manager"
arch=('i386' 'amd64')
license=('GPL')
depends=('libxinerama' 'readline' 'bash' 'perl' 'libxtst' 'libxft' 'texinfo')
url="http://www.nongnu.org/ratpoison/"
install="${pkgname}.install"
source=("http://savannah.nongnu.org/download/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.desktop")
md5sums=('2e31e70f08e3a52d4fd9abe9ade36e83'
         '29c3cb9be59758e39d8471391231a74a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  gmake CFLAGS="$CFLAGS -DHAVE_GETLINE"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  # fix permissions
  chmod a+x "${pkgdir}/usr/share/ratpoison/"{allwindows.sh,clickframe.pl,rpshowall.sh,rpws,split.sh}

  # Not useful outside the source tree. Kill it
  rm "${pkgdir}/usr/share/ratpoison/genrpbindings"

  cd contrib
  ./genrpbindings
  install -dm755 "${pkgdir}/usr/share/ratpoison/bindings"
  install -m644 {Ratpoison.pm,ratpoison-cmd.el,ratpoison.rb,ratpoison.lisp,ratpoison.py} \
    "${pkgdir}/usr/share/ratpoison/bindings/"

  install -dm755 "${pkgdir}/etc/X11/sessions"
  install -m644 "${srcdir}/${pkgname}.desktop" \
    "${pkgdir}/etc/X11/sessions/${pkgname}.desktop"

  install -dm755 "${pkgdir}/usr/share/emacs/site-lisp"
}
