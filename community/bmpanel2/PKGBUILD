# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: nsf <no.smile.face@gmail.com>
# Contributor: Claudiu Traistaru <claudiu2004@gmail.com>

pkgname=bmpanel2
pkgver=2.1pre1
pkgrel=6
pkgdesc="A NETWM-compatible panel for X11"
arch=("i386" "amd64")
url="http://bmpanel2.googlecode.com/"
license=('custom:MIT')
depends=('libxext' 'pango')
optdepends=('pygtk: configuration GUI')
makedepends=('cmake' 'asciidoc' 'python2')
source=("${url}files/$pkgname-$pkgver.tar.gz")
md5sums=('766ce5d3d6be427192fa11883b230eed')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  [ -d bld ] || mkdir bld && cd bld
  sed -i '' -e 's/#include <alloca.h>/#include <stdlib.h>/' $srcdir/$pkgname-$pkgver/widget-utils.c
  sed -i '' -e 's/uint/unsigned/' $srcdir/$pkgname-$pkgver/args.c
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=RELEASE \
           -DPYTHON_EXECUTABLE=/usr/bin/python2
  gmake
}

package() {
  cd "$srcdir/$pkgname-$pkgver/bld"
  make DESTDIR="$pkgdir/" install

  # python2 fix
  sed -i '' -e 's/env python/&2/' `find ./ -name "*.py"`

  # license
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 ../COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
} 
