# $Id: PKGBUILD 102612 2013-12-16 18:31:48Z seblu $
# Maintainer: SÃ©bastien Luttringer

pkgname=opensmtpd
pkgver=5.4.1p1
pkgrel=4
pkgdesc='Free implementation of the server-side SMTP protocol'
arch=('i686' 'x86_64')
url='http://www.opensmtpd.org/'
license=('custom')
depends=('libevent' 'ca_root_nss')
provides=('smtp-server' 'smtp-forwarder')
conflicts=('smtp-server' 'smtp-forwarder')
backup=('etc/smtpd/smtpd.conf' 'etc/smtpd/aliases')
options=('emptydirs')
install=opensmtpd.install
source=("http://www.opensmtpd.org/archives/$pkgname-$pkgver.tar.gz"
	'smtpd.in')
md5sums=('74551342985b78eb687995f7953d1790'
         'c1844480f63a6b46aa7fb587b5aeb6fc')

prepare() {
  gsed -ri 's,/etc/mail,/etc/smtpd,g' $pkgname-$pkgver/smtpd/smtpd.conf
}

build() {
  cd $pkgname-$pkgver
  # Remove _FORTIFY_SOURCES: FS#38124
  export CPPFLAGS=''
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/smtpd \
    --sbindir=/usr/bin \
    --libexecdir=/usr/libexec/smtpd \
    --with-maildir=/var/spool/mail \
    --with-privsep-path=/var/empty \
    --with-sock-dir=/run \
    --with-ca-file=/usr/share/certs/ca-root-nss.crt \
    --with-privsep-user=smtpd \
    --with-queue-user=smtpq \
    --with-pam
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install

  # Remove binaries provided by world
	rm -f ${pkgdir}/usr/bin/{mailq,newaliases,sendmail,makemap}

	# Remove the uneeded man pages which world distributes
  rm -f ${pkgdir}/usr/share/man/man5/{aliases.5,forward.5}
  rm -f ${pkgdir}/usr/share/man/man8/{makemap.8,sendmail.8}

  # license
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

	install -dm755 ${pkgdir}/etc/rc.d
	install -m655 ${srcdir}/smtpd.in ${pkgdir}/etc/rc.d/smtpd

	# Create an empty aliases fiel to prevent errors
	touch ${pkgdir}/etc/smtpd/aliases
}

# vim:set ts=2 sw=2 et:
