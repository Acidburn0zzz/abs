pkgname=opensmtpd
pkgver=5.7.3p2
pkgrel=1
pkgdesc='Free implementation of the server-side SMTP protocol'
arch=('i386' 'amd64')
url='http://www.opensmtpd.org/'
license=('custom')
depends=('libasr' 'libevent' 'ca_root_nss' 'db')
provides=('smtp-server' 'smtp-forwarder')
conflicts=('smtp-server' 'smtp-forwarder')
backup=('etc/smtpd/smtpd.conf' 'etc/smtpd/aliases')
options=('emptydirs')
install=opensmtpd.install
source=("http://www.opensmtpd.org/archives/$pkgname-$pkgver.tar.gz"
	'smtpd.in'
	'opensmtpd.initd')
sha256sums=('0d2973008d0f66bebb84bed516be6c32617735241cc54dd26643529281a8e52b'
            '03b398d8004c3e9a12c109c1cf2d2086f73b7d7026d5328655c7e380723e2985'
            '8cc1d5af51c35064fd22ffb351eeb025b576bfceb16a1f950ad09606a08529c9')

prepare() {
  gsed -ri 's,/etc/mail,/etc/smtpd,g' $pkgname-$pkgver/smtpd/smtpd.conf
}

build() {
  cd $pkgname-$pkgver
  # Remove _FORTIFY_SOURCES: FS#38124
  export CPPFLAGS=''
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/smtpd \
    --sbindir=/usr/bin \
    --libexecdir=/usr/libexec/smtpd \
    --with-maildir=/var/spool/mail \
    --with-privsep-path=/var/empty \
    --with-sock-dir=/var/run \
    --with-ca-file=/usr/share/certs/ca-root-nss.crt \
    --with-privsep-user=smtpd \
    --with-queue-user=smtpq \
    --with-pam
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install

  # Remove binaries provided by world
	rm -f ${pkgdir}/usr/bin/{mailq,newaliases,sendmail,makemap}

	# Remove the uneeded man pages which world distributes
  rm -f ${pkgdir}/usr/share/man/man5/{aliases.5,forward.5}
  rm -f ${pkgdir}/usr/share/man/man8/{makemap.8,sendmail.8}

  # license
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  install -dm755 ${pkgdir}/etc/{rc.d,init.d}
  install -m755 ${srcdir}/smtpd.in ${pkgdir}/etc/rc.d/opensmtpd
  install -m755 ${srcdir}/opensmtpd.initd ${pkgdir}/etc/init.d/opensmtpd

  # install an empty aliases file (used by the default config)
  install -dm755 "$pkgdir/etc/smtpd"
  install -m644 /dev/null "$pkgdir/etc/smtpd/aliases"
}

# vim:set ts=2 sw=2 et:
