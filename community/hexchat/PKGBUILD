pkgname=hexchat
pkgver=2.10.2
pkgrel=2
pkgdesc='A popular and easy to use graphical IRC (chat) client'
arch=('i386' 'amd64')
url='http://hexchat.github.io/'
license=('GPL')
depends=('dbus-glib' 'desktop-file-utils' 'gtk2' 'libcanberra' 'libnotify' 'libproxy' 'pciutils')
replaces=('xchat')
makedepends=('intltool' 'iso-codes' 'perl' 'python')
optdepends=('enchant: Spell check'
            'perl: Perl plugin'
            'python: Python plugin')
install="${pkgname}.install"
source=("http://dl.hexchat.net/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('87ebf365c576656fa3f23f51d319b3a6d279e4a932f2f8961d891dd5a5e1b52c')

prepare() {
  cd ${pkgname}-${pkgver}

  gsed 's/CFLAGS="$PERL_CFLAGS"/CFLAGS="$PERL_CFLAGS -fuse-ld=gold"/' -i configure
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix='/usr' --enable-{perl,python='python3.4',textfe}
  gmake
}

package() {
  cd ${pkgname}-${pkgver}

  gmake DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
