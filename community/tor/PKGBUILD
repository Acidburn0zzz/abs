# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: simo <simo@archlinux.org>
# Contributor: Sid Karunaratne

pkgname=tor
pkgver=0.2.6.10
pkgrel=1
pkgdesc='Anonymizing overlay network.'
arch=('i386' 'amd64')
url='http://www.torproject.org/'
license=('BSD')
depends=('libevent' 'bash')
optdepends=('torsocks: for torify')
backup=('etc/tor/torrc'
        'etc/tor/torrc-dist'
        'etc/tor/tor-tsocks.conf')
install='tor.install'
_port_patches=()
source=("http://www.torproject.org/dist/${pkgname}-${pkgver}.tar.gz"{,.asc}
        'torrc'
        'tor.in'
        ${_port_patches[@]})
md5sums=('04f919e7882d1ca80f835545af562bad'
         'SKIP'
         'c1dd4004c63edaeaa829e01be5413cfc'
         '631b0458ee48320000245d9c5b9a6284')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  for _p in ${_port_patches[@]}; do
    patch -p0 < ../${_p}
  done
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --with-openssl-dir=/usr/share/certs
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -dm0700 "${pkgdir}/var/lib/tor"

  install -dm0755 "${pkgdir}/etc/tor/"
  mv "${pkgdir}/etc/tor/torrc.sample" "${pkgdir}/etc/tor/torrc-dist"
  install -m0644 "${srcdir}/torrc" "${pkgdir}/etc/tor/torrc"

  install -dm0755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -dm755 "${pkgdir}/etc/rc.d/"
  install -m644 "${srcdir}/tor.in" "${pkgdir}/etc/rc.d/tor"
}
