# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: simo <simo@archlinux.org>
# Contributor: Sid Karunaratne

pkgname=tor
pkgver=0.2.7.6
pkgrel=1
pkgdesc='Anonymizing overlay network.'
arch=('i386' 'amd64')
url='http://www.torproject.org/'
license=('BSD')
depends=('libevent' 'bash')
optdepends=('torsocks: for torify')
backup=('etc/tor/torrc'
        'etc/tor/torrc-dist'
        'etc/tor/tor-tsocks.conf')
install='tor.install'
source=("http://www.torproject.org/dist/${pkgname}-${pkgver}.tar.gz"{,.asc}
        'torrc'
        'tor.in')
validpgpkeys=('B35BF85BF19489D04E28C33C21194EBB165733EA') # Nick Mathewson
sha256sums=('493a8679f904503048114aca6467faef56861206bab8283d858f37141d95105d'
            'SKIP'
            '44093eaf790c6c1cad8690ee002829481fb9b22ca58663f524f5792521222406'
            'dd20955172a87dfb06a2ae714d7576194435017db2063207955a0482fb624e27')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  for _p in ${_port_patches[@]}; do
    patch -p0 < ../${_p}
  done
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --with-openssl-dir=/usr/share/certs
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -dm0700 "${pkgdir}/var/lib/tor"

  install -dm0755 "${pkgdir}/etc/tor/"
  mv "${pkgdir}/etc/tor/torrc.sample" "${pkgdir}/etc/tor/torrc-dist"
  install -m0644 "${srcdir}/torrc" "${pkgdir}/etc/tor/torrc"

  install -dm0755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -dm755 "${pkgdir}/etc/rc.d/"
  install -m644 "${srcdir}/tor.in" "${pkgdir}/etc/rc.d/tor"
}
