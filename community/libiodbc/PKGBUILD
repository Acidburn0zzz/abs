# $Id: PKGBUILD 4453 2009-10-23 09:11:19Z spupykin $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=libiodbc
pkgver=3.52.9
pkgrel=1
pkgdesc='Independent Open DataBase Connectivity for Linux'
arch=('amd64')
url="http://www.iodbc.org/dataspace/iodbc/wiki/iODBC/"
license=('LGPL')
depends=('libc')
makedepends=('chrpath')
replaces=('iodbc')
conflicts=('iodbc')
options=('!libtool')
source=("http://downloads.sourceforge.net/iodbc/${pkgname}-${pkgver}.tar.gz")
md5sums=('98a681e06a1df809af9ff9a16951b8b6')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
  sed -i '' -e 's|-ldl||g' admin/libiodbc.pc.in

  sed -i '' -e "/^ltmain=/!s|\$$ac_aux_dir/ltmain.sh|/usr/share/libtool/config/ltmain.sh|g" \
    -e "/^LIBTOOL=/s|\$$\(top_builddir\)/libtool|/usr/bin/libtool|g" \
    ${srcdir}/${pkgname}-${pkgver}/aclocal.m4
  find ${srcdir}/${pkgname}-${pkgver} -name "Makefile.in" | xargs sed -i '' -e 's|^LIBTOOL[ ]*=.*|LIBTOOL=/usr/bin/libtool|g'

	./configure --prefix=/usr \
		--disable-static \
		--includedir=/usr/include/libiodbc \
		--disable-gui \
		--disable-libodbc
	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
	chrpath -d ${pkgdir}/usr/bin/iodbctest{,w}
	rm -rf ${pkgdir}/usr/share/libiodbc/
}
