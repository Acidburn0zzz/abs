# $Id: PKGBUILD 92586 2013-06-09 13:32:10Z bpiotrowski $
# Maintainer:  BartÅomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Pierre-Paul Paquin <pierrepaulpaquin@gmail.com>
# Contributor: xduugu (.desktop and install files)

pkgname=mupdf
pkgver=1.7a
pkgrel=1
pkgdesc='Lightweight PDF and XPS viewer'
arch=('i686' 'amd64')
url='http://mupdf.com'
license=('GPL3')
depends=('freetype2' 'libjpeg' 'jbig2dec' 'openjpeg2' 'libxext' 'desktop-file-utils' 'xdg-utils' 'curl')
makedepends=('gmake')
install=mupdf.install
options=('staticlibs')
patchdir=(mupdf-$pkgver-source/)
source=(http://mupdf.com/downloads/mupdf-${pkgver/_/}-source.tar.gz
        mupdf.desktop
        mupdf.xpm
        mupdf-1.5-openjpeg-2.1.0.patch)
md5sums=('319fda2cc5301bb3ec2e1d82c3329986'
         '39b54f82a763aac54e352315a0ee9037'
         'f3f35e7320bafde331250de1c99186a1'
         '8e71587ad9b86e10c9144618ab43149b')

prepare() {
  cd $pkgname-$pkgver-source
	sed -i '' -e 's/-pipe -O2 //' \
		-e 's|/usr/local|/usr|' \
		-e 's/Linux/FreeBSD/' \
		Makerules

  rm -rf thirdparty/{curl,freetype,jpeg,zlib,jbig2dec,openjpeg}
	patch -p1 -i ../mupdf-1.5-openjpeg-2.1.0.patch
}

build() {
  CFLAGS+=' -fPIC'
  CXXFLAGS+=' -fPIC'

  cd $pkgname-$pkgver-source
  gmake build=release CURL_LIBS='-lcurl -pthread'
}

package() {
  cd $pkgname-$pkgver-source
  gmake build=release prefix="$pkgdir"/usr install
	install -dm755 "$pkgdir"/usr/lib
  install -m644 build/release/libmujs.a "$pkgdir"/usr/lib

  mv "$pkgdir"/usr/bin/mupdf-x11-curl "$pkgdir"/usr/bin/mupdf
  rm "$pkgdir"/usr/bin/mupdf-x11

	install -dm755 "$pkgdir"/usr/share/{applications,pixmaps}
  install -m644 ../mupdf.desktop "$pkgdir"/usr/share/applications/mupdf.desktop
  install -m644 ../mupdf.xpm "$pkgdir"/usr/share/pixmaps/mupdf.xpm

  find "$pkgdir"/usr/include "$pkgdir"/usr/share "$pkgdir"/usr/lib \
    -type f -exec chmod -v 0644 {} +
}

