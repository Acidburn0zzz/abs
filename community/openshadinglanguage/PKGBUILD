# $Id: PKGBUILD 118688 2014-09-09 00:34:44Z foutrelis $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=openshadinglanguage
pkgver=1.5.10
pkgrel=2
pkgdesc="Advanced shading language for production GI renderers"
arch=(i686 x86_64)
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
depends=('boost-libs' 'openimageio>=1.4.13' 'openexr' 'intel-tbb' 'freetype2' 'libpng' 'libtiff' 'llvm')
makedepends=('boost' 'cmake')
_port_patches=(
        patch-CMakeLists.txt
        )
source=(https://github.com/imageworks/OpenShadingLanguage/archive/Release-${pkgver}.tar.gz
        https://github.com/imageworks/OpenShadingLanguage/commit/163df11a3af32295757cfe008d1ba2b757cc7b7b.patch
        https://github.com/imageworks/OpenShadingLanguage/pull/412.patch
        "${_port_patches[@]}")
md5sums=('b828366f056e115b539ee1780883fc7d'
         'e980ed0a9480958307fa048544154887'
         'd21cab675ce13c9f0640e1f6a662d2b9'
         '56cd40335d80ec8b57c4bd0affc61320')

prepare() {
  cd OpenShadingLanguage-Release-$pkgver

  for i in "${_port_patches[@]}"; do
    msg "patch $i"
    patch -p0 -i "${srcdir}/$i"
  done

  # Fails building this file on i686
  #sed -i "/src\/testshade/d" CMakeLists.txt

  patch -Np1 < $srcdir/412.patch
  patch -Np1 < $srcdir/163df11a3af32295757cfe008d1ba2b757cc7b7b.patch
  sed -i '' "s|#!/bin/bash|#!/usr/bin/bash|" src/liboslexec/serialize-bc.bash
}

build() {
  cd OpenShadingLanguage-Release-$pkgver

  cd src

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake ../.. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLLVM_STATIC=0 \
    -DCMAKE_CXX_FLAGS="-std=c++11 -Wno-deprecated-register -Wno-c++11-narrowing"
  make
}

package() {
  cd OpenShadingLanguage-Release-$pkgver/src/build

  make DESTDIR="$pkgdir/" install

  mkdir -p "$pkgdir"/usr/share/OSL/
  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  mv "$pkgdir"/usr/LICENSE "$pkgdir"/usr/share/licenses/$pkgname # TODO: Tell upstream about this shit
  mv "$pkgdir"/usr/{CHANGES,README.md,INSTALL} "$pkgdir"/usr/share/OSL/
  mv "$pkgdir"/usr/doc "$pkgdir"/usr/share/OSL/doc
  mv "$pkgdir"/usr/shaders "$pkgdir"/usr/share/OSL/shaders
}

# vim:set ts=2 sw=2 et:
