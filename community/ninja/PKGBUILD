pkgname=ninja
pkgver=1.6.0
pkgrel=2
pkgdesc='Small build system with a focus on speed'
arch=(i386 amd64)
url='http://martine.github.io/ninja/'
license=(Apache)
depends=(gcc-libs)
makedepends=(asciidoc python2 re2c emacs-nox)
source=(https://github.com/martine/ninja/archive/v$pkgver.zip)
sha1sums=('a4dc454f421ff0e0cf193b34d03e3e20db4ebba2')

build() {
  cd ninja-$pkgver

  python2 ./bootstrap.py
  emacs -Q --batch -f batch-byte-compile misc/ninja-mode.el
  asciidoc doc/manual.asciidoc
}

package() {
  cd ninja-$pkgver

  install -dm755 "$pkgdir/usr/bin"
  install -m755 ninja "$pkgdir/usr/bin/ninja"
  install -dm755 "$pkgdir/usr/share/doc/ninja"
  install -m644 doc/manual.asciidoc "$pkgdir/usr/share/doc/ninja/manual.asciidoc"
  install -m644 doc/manual.html "$pkgdir/usr/share/doc/ninja/manual.html"

  install -dm755 "$pkgdir/usr/share/emacs/site-lisp"
  install -m644 misc/ninja-mode.el "$pkgdir/usr/share/emacs/site-lisp/ninja-mode.el"
  install -m644 misc/ninja-mode.elc "$pkgdir/usr/share/emacs/site-lisp/ninja-mode.elc"

  install -dm755 "$pkgdir/usr/share/bash-completion/completions"
  install -m644 misc/bash-completion "$pkgdir/usr/share/bash-completion/completions/ninja"
  install -dm755 "$pkgdir/usr/share/zsh/site-functions"
  install -m644 misc/zsh-completion "$pkgdir/usr/share/zsh/site-functions/_ninja"
}
