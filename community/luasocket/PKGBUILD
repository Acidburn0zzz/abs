# $Id: PKGBUILD 99083 2013-10-24 09:31:56Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Anders Bergh <anders1@gmail.com>

pkgbase=luasocket
pkgname=(lua-socket lua51-socket)
pkgver=20151008
pkgrel=6
pkgdesc='Networking support library for the Lua language'
arch=('i386' 'amd64')
url='https://github.com/diegonehab/luasocket'
license=('MIT')
makedepends=('lua' 'lua51')
source=("git+https://github.com/diegonehab/luasocket.git#commit=d1ec29be7f982db75864155dd61a058902e1cae2")
md5sums=('SKIP')

build() {
  cp -a luasocket-${pkgver/rc/-rc} luasocket-${pkgver/rc/-rc}-52
  msg2 'Building with lua 5.2'
  pushd luasocket-${pkgver/rc/-rc}-52
  gmake LUAV=5.2
  popd
  msg2 'Building with lua 5.1'
  cd luasocket-${pkgver/rc/-rc}
  find . -type f -name \*.[ch] -exec sed -i '' \
    -e 's|include "lua.h|include "lua5.1/lua.h|g' \
    -e 's|include "lualib.h|include "lua5.1/lualib.h|g' \
    -e 's|include "luaconf.h|include "lua5.1/luaconf.h|g' \
    -e 's|include "lauxlib.h|include "lua5.1/lauxlib.h|g' \
    {} \;
  gmake LUAV=5.1
}

package_lua-socket() {
  depends=('lua')
  cd luasocket-${pkgver/rc/-rc}-52
  gmake DESTDIR="$pkgdir/" LUAV=5.2 prefix=/usr install-unix
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua51-socket() {
  depends=('lua51')
  replaces=('luasocket')
  conflicts=('luasocket')

  cd luasocket-${pkgver/rc/-rc}
  gmake DESTDIR="$pkgdir/" LUAV=5.1 prefix=/usr install-unix
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
