pkgname=wesnoth
pkgver=1.12.5
pkgrel=1
pkgdesc="A turn-based strategy game on a fantasy world"
arch=('i386' 'amd64')
license=('GPL')
url="http://www.wesnoth.org/"
makedepends=('boost' 'cmake' 'sdl_ttf' 'sdl_net' 'sdl_mixer' 'sdl_image' 'fribidi' 'boost-libs' 'pango' 'lua52' 'dbus-core' 'python2')
install=wesnoth.install
# Uses openmp - so no clang support
options=(!emptydirs)
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2"
	'wesnothd.tmpfiles.conf'
	'wesnothd.initd'
	'wesnoth-boost.patch')
sha256sums=('599aad40da4b271c5138ac5a4391d9ff9c367ee51f5c30ca138f6baf92aea81d'
            'dfb0bd9ed935f8b6f45d0f75f846ab2897fe32c57ba93fdcaaf04fb4b91d7a53'
            'ed19251a7bdaff22300ec44021df02ae4a7d7dfd41a6149f8f20874240d96581'
            'ccacb1049a71935392b46f919c4045b11936b232522ed2763561fbc0fb1e40b7')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Try this again in a new version when they fix their linking to boost
  patch -Np1 < ${srcdir}/wesnoth-boost.patch

  mkdir build && cd build
  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DENABLE_OMP=OFF \
      -DENABLE_TOOLS=ON \
      -DMANDIR=share/man \
      -DFIFO_DIR=/var/run/wesnothd
  gmake
}

package() {
  depends=('sdl_ttf' 'sdl_net' 'sdl_mixer' 'sdl_image' 'fribidi' 'boost-libs' 'pango' 'lua52' "wesnoth-data" 'dbus-core' 'python2')
  cd "$srcdir/$pkgname-$pkgver"

  cd build
  gmake DESTDIR="$pkgdir" install

  rm -r $pkgdir/usr/share/applications
  rm -r $pkgdir/usr/share/doc
  rm -r $pkgdir/usr/share/pixmaps
  rm -r $pkgdir/usr/share/wesnoth
  install -dm755 "${pkgdir}"/{usr/lib/tmpfiles.d,etc/init.d}
  install -m755 "${srcdir}/wesnothd.initd" "${pkgdir}/etc/init.d/wesnothd"
  install -m644 "$srcdir/wesnothd.tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/wesnothd.conf"
}
