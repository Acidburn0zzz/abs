# $Id$
# Maintainer:  Bart³omiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Claudiu Traistaru <claudiu2004@gmail.com>

pkgname=cmus
pkgver=2.7.1
pkgrel=1
pkgdesc='Very feature-rich ncurses-based music player'
arch=('i386' 'amd64')
url="http://cmus.sourceforge.net/"
license=('GPL')
depends=('ncurses' 'libcue')
makedepends=('faad2' 'ffmpeg' 'flac' 'libao' 'libmad' 'libmodplug'
              'libmp4v2' 'libmpcdec' 'libpulse' 'libvorbis' 'wavpack' 'gcc')
optdepends=('alsa-lib: for ALSA output plugin support'
            'libao: for AO output plugin support'
            'libpulse: for PulseAudio output plugin support'
            'faad2: for AAC input plugin support'
            'ffmpeg: for ffmpeg input plugin support'
            'flac: for flac input plugin support'
            'libmad: for mp3 input plugin support'
            'libmodplug: for modplug input plugin support'
            'libmp4v2: for mp4 input plugin support'
            'libmpcdec: for musepack input plugin support'
            'libvorbis: for vorbis input plugin support'
            'wavpack: for wavpack input plugin support')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/cmus/cmus/archive/v${pkgver}.tar.gz")
sha256sums=('8179a7a843d257ddb585f4c65599844bc0e516fe85e97f6f87a7ceade4eb5165')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure prefix=/usr
  gmake 

}

package() {
  cd $srcdir/$pkgname-$pkgver
  gmake DESTDIR=$pkgdir install
  install -dm755  $pkgdir/usr/share/zsh/site-functions
  install -m644 contrib/_cmus $pkgdir/usr/share/zsh/site-functions/_cmus
}
