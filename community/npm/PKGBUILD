# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=npm
pkgver=3.7.5
pkgrel=1
pkgdesc='A package manager for javascript'
arch=('i386' 'amd64')
url='https://www.npmjs.com/'
license=('custom:Artistic')
depends=('nodejs')
provides=('nodejs-node-gyp')
makedepends=('git')
optdepends=('python2: for node-gyp')
options=('!emptydirs')
source=("git+https://github.com/npm/npm.git#tag=v${pkgver}")
sha256sums=('SKIP')

build() {
  cd npm
  gmake -j1 # Build fails if number of jobs > 1 ¯\_(ツ)_/¯
}

package() {
  cd npm
  gmake NPMOPTS="--prefix=\"$pkgdir/usr\"" install

  # Why 777? :/
  chmod -R u=rwX,go=rX "$pkgdir"

  # Fix files owned by nobody:
  chown -R root "$pkgdir/usr/lib/node_modules" 

  # Fix wrong symlinks
  for _dir in man1 man5 man7; do
    rm -f "$pkgdir"/usr/share/man/$_dir/*
    cd "$pkgdir"/usr/lib/node_modules/npm/man/$_dir
    for _file in *; do
      ln -s /usr/lib/node_modules/npm/man/$_dir/$_file "$pkgdir"/usr/share/man/$_dir/
    done
  done

  # Provide node-gyp executable
  cp "${pkgdir}/usr/lib/node_modules/npm/bin/node-gyp-bin/node-gyp" \
    "${pkgdir}/usr/bin/node-gyp"

  # Experimental dedup
  # cd "${pkgdir}/usr/lib/node_modules/${pkgname}/node_modules"
  # for dep in semver; do
  #   rm -r "${dep}";
  #   node "${srcdir}/npm/cli.js link ${dep}";
  # done

  install -dm644  "$pkgdir/usr/share/licenses/$pkgname/"
  install -m644 "$srcdir/npm/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
