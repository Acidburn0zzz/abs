# $Id: PKGBUILD 69385 2012-04-15 09:02:58Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kwpolska <kwpolska@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Laszlo Papp <djszapi @ gmail at com>

pkgname=vim-nerdtree
pkgver=5.0.0
_scriptid=17123
pkgrel=1
pkgdesc='A tree explorer plugin for navigating the filesystem'
arch=('any')
url='http://www.vim.org/scripts/script.php?script_id=1658'
license=('custom:WTFPL')
depends=('vim')
groups=('vim-plugins')
install='vimdoc.install'
source=("${pkgname}-${pkgver}.zip::http://www.vim.org/scripts/download_script.php?src_id=${_scriptid}"
        'COPYING')
sha256sums=('16e8e0684d5964ececa47cbd4ac714d3ccf34247559af534f4b512baf5e0bf56'
            '7637386b5f81e8a719ca336233149005e5fa28b5e6054ea7b67de49355b0ad40')

package() {
  install -dm755 ${pkgdir}/usr/share/vim/vimfiles/{doc,nerdtree_plugin,plugin,syntax}

  install -m644 ${srcdir}/doc/NERD_tree.txt \
    ${pkgdir}/usr/share/vim/vimfiles/doc/

  install -m644 ${srcdir}/nerdtree_plugin/{exec_menuitem,fs_menu}.vim \
    ${pkgdir}/usr/share/vim/vimfiles/nerdtree_plugin/

  install -m644 ${srcdir}/plugin/NERD_tree.vim \
    ${pkgdir}/usr/share/vim/vimfiles/plugin/

  install -m644 ${srcdir}/syntax/nerdtree.vim \
    ${pkgdir}/usr/share/vim/vimfiles/syntax/

  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ${srcdir}/COPYING \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
