# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: jepaan <jepaan at hotmail dot com>

pkgname=assimp
pkgver=3.0.1270
pkgrel=2
pkgdesc="Portable Open Source library to import various well-known 3D model formats in an uniform manner"
arch=('i686' 'x86_64')
license=('BSD')
depends=('libxmu' 'zlib' 'freeglut')
makedepends=('cmake' 'boost')
url=('http://assimp.sourceforge.net/index.html')
source=("http://downloads.sourceforge.net/assimp/assimp--$pkgver-source-only.zip"
	patch-clang)
md5sums=('52aa4cf4e34e6b2a9c5f6c0b3c319af1'
         '1b26a05bbf3be0e75b00b94cacf4e50e')

prepare() {
	cd ${srcdir}/assimp--3.0.1270-source-only
	patch -p0 -i ${srcdir}/patch-clang
}

build() {
	cd ${srcdir}
	mkdir build
	cd build

	cmake ../${pkgname}--${pkgver}-source-only \
		-DCMAKE_INSTALL_PREFIX=/usr
	make 
}

package()  {
	cd ${srcdir}/build

	make DESTDIR=$pkgdir install
	install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m644 ${srcdir}/${pkgname}--${pkgver}-source-only/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
