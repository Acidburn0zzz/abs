# $Id: PKGBUILD 84791 2013-02-24 15:55:43Z svenstaro $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=vim-ultisnips
pkgver=3.1
pkgrel=1
pkgdesc='TextMate-style snippets for Vim.'
arch=('any')
url='https://github.com/SirVer/ultisnips'
license=('GPL2')
depends=('vim' 'python2')
groups=('vim-plugins')
replaces=('vim-snipmate')
install=vimdoc.install
makedepends=('git')
source=("${pkgname}.tar.gz"::"https://github.com/SirVer/ultisnips/archive/${pkgver}.tar.gz"
        git+https://github.com/honza/vim-snippets.git)
sha256sums=('32ea72d321f07755e878f640398c72f0a072e56a980a66bdc7bfb41ba85fa6e7'
            'SKIP')

package() {
  cd "${srcdir}/ultisnips-${pkgver}"

  install -dm755 "$pkgdir/usr/share/vim/vimfiles/"
  cp -r pythonx after doc ftdetect autoload ftplugin plugin syntax utils \
    "$pkgdir/usr/share/vim/vimfiles/"

  cd "${srcdir}/vim-snippets"

  install -dm755 "${pkgdir}/usr/share/vim/vimfiles/"
  cp -r UltiSnips autoload snippets "${pkgdir}/usr/share/vim/vimfiles/"

  # complie .pyc and .pyo fiels
  find "${pkgdir}" -name '*.py' -exec python2 -mpy_compile {} +
  find "${pkgdir}" -name '*.py' -exec python2 -O -mpy_compile {} +
}
