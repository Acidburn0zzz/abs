# $Id$
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: fancris3 <fancris3 at gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=terminator
pkgver=0.98
pkgrel=2
pkgdesc='Terminal emulator that supports tabs and grids'
arch=('amd64')
url='http://www.tenshu.net/p/terminator.html'
license=('GPL2')
# Note: the package named 'vte3' is actually vte for GTK 3
# and terminator seems to require vte for GTK 2
depends=('vte' 'pygtk' 'xdg-utils' 'python2-notify' 'python2-keybinder2')
makedepends=('desktop-file-utils' 'gettext' 'intltool')
install=terminator.install
source=("http://launchpad.net/terminator/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz")

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    # python2 fix
    for file in terminatorlib/{,plugins/}*.py; do
        sed -i '' -e 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
        sed -i '' -e 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
    done

    python2 setup.py build
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}

    python2 setup.py install --skip-build --root=${pkgdir}

    rm -f ${pkgdir}/usr/share/applications/${pkgname}.desktop
    rm -f ${pkgdir}/usr/share/icons/hicolor/icon-theme.cache
    install -dm755 ${pkgdir}/usr/share/applications/
    install -m644 ${srcdir}/${pkgname}-${pkgver}/data/${pkgname}.desktop.in ${pkgdir}/usr/share/applications/${pkgname}.desktop
    sed -i '' -e 's|_||' ${pkgdir}/usr/share/applications/${pkgname}.desktop

    mv "${pkgdir}/usr/man" "${pkgdir}/usr/share/man" 
}
sha256sums=('0a6d8c9ffe36d67e60968fbf2752c521e5d498ceda42ef171ad3e966c02f26c1')
