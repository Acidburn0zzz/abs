# $Id: PKGBUILD 90569 2013-05-13 09:41:28Z bisson $

pkgname=libtorrent
pkgver=0.13.4
pkgrel=1
pkgdesc='BitTorrent library with a focus on high performance and good code'
url='http://libtorrent.rakshasa.no/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('gcc-libs')
source=("http://distcache.FreeBSD.org/ports-distfiles/${pkgname}-${pkgver}.tar.gz")
sha1sums=('3a3ca87054d020bc376abe2c1ea15bbbaef31131')
options=('libtoolfix')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# Make it compile with clang as c++11 code
	find . -name '*.h' -o -name '*.cc' | xargs gsed -i \
		-e 's/tr1::/std::/g' \
		-e 's/std::std::/std::/g' \
		-e '/namespace tr1/d' \
		-e '/include/s,tr1/,,'
	# last one there is not actually "needed" but makes sense

	gsed -i -e 's/\.assign/.fill/' \
		src/torrent/utils/extents.h \
		src/torrent/utils/log.cc
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	export CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing -std=c++11"

	./configure \
		OPENSSL_CFLAGS="-I/usr/include/openssl" OPENSSL_LIBS="-L/usr/lib -lssl -lcrypto" \
		STUFF_CFLAGS="-I/usr/include/openssl" STUFF_LIBS="-L/usr/lib -lssl -lcrypto" \
		--prefix=/usr \
		--with-kqueue \
		--enable-ipv6

	gmake
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	gmake DESTDIR="${pkgdir}" install
}
