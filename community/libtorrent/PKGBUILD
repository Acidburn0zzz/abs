# $Id: PKGBUILD 90569 2013-05-13 09:41:28Z bisson $

pkgname=libtorrent
pkgver=0.13.6
pkgrel=1
pkgdesc='BitTorrent library with a focus on high performance and good code'
url='http://libtorrent.rakshasa.no/'
arch=('i386' 'amd64')
license=('GPL')
depends=('gcc-libs')
makedepends=(gcc)
source=("http://distcache.FreeBSD.org/ports-distfiles/${pkgname}-${pkgver}.tar.gz")
sha1sums=('9ca6ca9698f81c758fe934b52374f23588a7cc78')
options=('libtoolfix')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# Make it compile with clang as c++11 code
	find . -name '*.h' -o -name '*.cc' | xargs gsed -i \
		-e 's/tr1::/std::/g' \
		-e 's/std::std::/std::/g' \
		-e '/namespace tr1/d' \
		-e '/include/s,tr1/,,'
	# last one there is not actually "needed" but makes sense

	gsed -i -e 's/\.assign/.fill/' \
		src/torrent/utils/extents.h \
		src/torrent/utils/log.cc

	gsed '/AM_PATH_CPPUNIT/d' -i configure.ac
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	export CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing -std=gnu++11"
	./autogen.sh

	./configure \
		OPENSSL_CFLAGS="-I/usr/include/openssl" OPENSSL_LIBS="-L/usr/lib -lssl -lcrypto" \
		STUFF_CFLAGS="-I/usr/include/openssl" STUFF_LIBS="-L/usr/lib -lssl -lcrypto" \
		--prefix=/usr \
		--with-kqueue \
		--enable-ipv6

	gmake
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	gmake DESTDIR="${pkgdir}" install
}
