# $Id: PKGBUILD 91661 2013-05-26 09:24:52Z bluewind $
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgname=perl-curses
pkgver=1.32
pkgrel=5
pkgdesc="Character screen handling and windowing"
arch=('i386' 'amd64')
url="http://search.cpan.org/dist/Curses"
license=('GPL' 'PerlArtistic')
depends=('perl' 'ncurses')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/G/GI/GIRAFFED/Curses-$pkgver.tgz)
md5sums=('e4d9066bfc5a69cb2cee3e1dfc3209b3')

build() {
  cd  $srcdir/Curses-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  $srcdir/Curses-$pkgver
  make pure_install doc_install DESTDIR=$pkgdir
  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete

# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;

}

