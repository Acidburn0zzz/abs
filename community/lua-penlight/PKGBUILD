# $Id$
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>
# Contributor: Claudiu Traistaru <claudiu2004@gmail.com>

pkgbase=penlight
pkgname=('lua-penlight')
pkgver=1.3.2
pkgrel=1
pkgdesc='Lua libraries focusing on input data handling'
arch=('any')
url='https://github.com/stevedonovan/penlight'
license=('custom')
makedepends=('lua' 'lua-filesystem')
source=("$pkgname-$pkgver.tar.gz::https://github.com/stevedonovan/penlight/archive/$pkgver.tar.gz")
md5sums=('0315a39834bb6fab07741ec04ede1bf4')

check() {
  cd Penlight-$pkgver
  export LUA_PATH="$PWD/lua/?/init.lua;$PWD/lua/?.lua;$(lua -e 'print(package.path)')"
  lua run.lua
}

package_lua-penlight() {
  depends=('lua' 'lua-filesystem')

  cd Penlight-$pkgver
  install -dm755 "$pkgdir/usr/share/lua/5.3/pl/platf"
  install -m644 lua/pl/*.lua "$pkgdir/usr/share/lua/5.3/pl"

  # copy docs
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  install -m644 CHANGES.md README.md doc/manual/* "$pkgdir/usr/share/doc/$pkgname"
  # copy license
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
