pkgname=lxpanel
pkgver=0.6.2
pkgrel=5
pkgdesc='Lightweight X11 desktop panel for LXDE'
arch=('i386' 'amd64')
license=('GPL2')
url='http://lxde.org/'
groups=('lxde')
depends=('gtk2' 'menu-cache' 'lxmenu-data' 'libwnck' 'libfm' 'libsysinfo')
makedepends=('intltool' 'docbook-xml' 'docbook-xsl' 'git')
optdepends=('wireless_management: wifimgr')
source=(http://downloads.sourceforge.net/sourceforge/lxde/lxpanel-$pkgver.tar.gz)
sha256sums=('f9ba6d0b825f7b99de045c3371738792bf9f3604af66bef4d98d783461c60a48')

prepare() {
  cd $srcdir/$pkgname-$pkgver

	sed -i '' -e '/g_thread_create/ s|.*|g_thread_new("tf", (GThreadFunc)thread_func,thread_data);|' \
		src/gtk-run.c

	sed -i '' -e '/g_thread_init/d' \
		src/panel.c

	sed -i '' -e 's|structure_size :|.structure_size =|; s|structure_version :|.structure_version =|' \
		src/plugin.h

	sed -i '' -e '/gdk_color_parse(b/ s|;||; /gdk_color_parse(b/{p;s/.*/;/;}' \
		src/plugins/batt/batt.c

	sed -i '' -e 's|/sys|/compat/linux&|' \
		src/plugins/batt/batt_sys.h

	sed -i '' -e 's|/proc|/compat/linux&|' \
		src/plugins/cpu/cpu.c \
		src/plugins/monitors/monitors.c \
		src/plugins/thermal/thermal.c

	sed -i '' -e 's|%lld|%lu|g; s|bytes >=|bytes >|' \
		src/plugins/netstatus/netstatus-dialog.c

	sed -i '' -e '/NETSTATUS_ERROR, code/ s|error_message|"%s", &|' \
		src/plugins/netstatus/netstatus-iface.c

	sed -i '' -e '611s/signal_strength)/*&/' \
		src/plugins/netstatus/netstatus-sysdeps.c

	sed -i '' -e 's|linux|sys|' \
		src/plugins/volume/volume-impl.c

}
build() {
  cd $srcdir/$pkgname-$pkgver

  unset CC CPP CXX
  CFLAGS="${CFLAGS} -I$srcdir/$pkgname-$pkgver -Wno-return-type -std=gnu89"

  ./configure \
      --sysconfdir=/etc \
      --prefix=/usr \
      --with-plugins=netstatus,volume,volumealsa,deskno,batt,kbled,xkb,thermal,cpu,cpufreq,monitors,wnckpager \
      --enable-man \
      --datarootdir=/usr/share
    gmake
}

package() {
  cd $srcdir/$pkgname-$pkgver
  gmake DESTDIR=$pkgdir install
}
