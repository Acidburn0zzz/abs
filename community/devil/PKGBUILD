# $Id: PKGBUILD 96404 2013-08-28 00:08:35Z eric $
# Maintainer: Laurent Carlier <lordheavym@gmail.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: TheHoff <forums>

pkgname=devil
pkgver=1.7.8
pkgrel=11
pkgdesc="Library for reading several different image formats"
arch=('i686' 'amd64')
url="http://openil.sourceforge.net/"
depends=('libpng' 'libmng' 'jasper' 'lcms' 'openexr')
makedepends=('gcc')
install=devil.install
options=('!docs' '!emptydirs' !clang)
license=('GPL')
source=("http://downloads.sourceforge.net/openil/DevIL-${pkgver}.tar.gz"
        'gcc-5.0.patch')
sha256sums=('682ffa3fc894686156337b8ce473c954bf3f4fb0f3ecac159c73db632d28a8fd'
            'bd1c96a588b1d086d7d4b0d631f707147d58d62dbe08d8d930086594df09d253')

prepare() {
  cd ${srcdir}/devil-$pkgver

  sed -i '' -e '/^pkgdir=/s|$$(libdir)/|$$(prefix)/libdata/|g' \
    data/Makefile.am
  sed -i '' -e 's|<malloc\.h>|<stdlib.h>|g' \
    src-ILU/ilur/ilur.c
  gpatch -Np1 -i "${srcdir}/gcc-5.0.patch"
}

build() {
  cd ${srcdir}/devil-$pkgver

  if [[ $CARCH == x86_64 ]]; then
    ./configure --prefix=/usr --enable-ILU --disable-allegro \
      --disable-directx8 --disable-directx9 \
      --without-libsquish
  else
    ./configure --prefix=/usr --enable-ILU --disable-sse3
  fi

  make
}

package() {
  cd ${srcdir}/devil-$pkgver

  make prefix=${pkgdir}/usr install
}
