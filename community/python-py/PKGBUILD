#
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgbase=python-py
pkgname=('python-py' 'python2-py')
pkgver=1.4.28
pkgrel=1
pkgdesc="library with cross-python path, ini-parsing, io, code, log facilities"
arch=('any')
license=('MIT')
url="http://pylib.readthedocs.org/en/latest/"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest')
source=("http://pypi.python.org/packages/source/p/py/py-$pkgver.tar.gz")
sha512sums=('0f65ec7114d5d634cf8b0be0c91dee9c7ce7b4795143d1394f1e625586d852c3c0941b7e9490a1eb6a1d3ad5db7c64ab72b6532793f22fdda705a347d4945814')

prepare() {
  cp -R py-${pkgver}{,-py2}
}

build() {
  cd "$srcdir/py-${pkgver}"
  python setup.py build

  cd "$srcdir/py-${pkgver}-py2"
  python2 setup.py build
}

check() {
  ## export this as passing LANG= doesn't get picked up by the test
  ## using os.environ to get LANG
  export LC_ALL=en_GB.UTF-8

  cd "$srcdir/py-${pkgver}"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test

  cd "$srcdir/py-${pkgver}-py2"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test2
}

package_python-py() {
  depends=('python')

  cd py-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-py() {
  depends=('python2')

  cd py-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
