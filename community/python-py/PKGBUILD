#
# Maintainer: Anthony Donnelly <Amzo@PacBSD.org>

pkgbase=python-py
pkgname=('python-py' 'python2-py')
pkgver=1.4.30
pkgrel=5
pkgdesc="library with cross-python path, ini-parsing, io, code, log facilities"
arch=('i386' 'amd64')
license=('MIT')
url="http://pylib.readthedocs.org/en/latest/"
makedepends=('python-setuptools' 'python2-setuptools')
#checkdepends=('python-pytest' 'python2-pytest')
source=("http://pypi.python.org/packages/source/p/py/py-$pkgver.tar.gz")
sha512sums=('dd9d92f8eaf8e5fc61bfb7feb4b07df5ae1745ebf88bcbe9d62589caa9e1f7d8beae5b2d20b407520919e47391cd50cf2d5323556fa5bd90a37e120b1d394d7a')

prepare() {
  cp -R py-${pkgver}{,-py2}
}

build() {
  cd "$srcdir/py-${pkgver}"
  python setup.py build

  cd "$srcdir/py-${pkgver}-py2"
  python2 setup.py build
}

#check() {
  ## export this as passing LANG= doesn't get picked up by the test
  ## using os.environ to get LANG
#  export LC_ALL=en_GB.UTF-8

#  cd "$srcdir/py-${pkgver}"
#  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test

#  cd "$srcdir/py-${pkgver}-py2"
#  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test2
#}

package_python-py() {
  depends=('python')

  cd py-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-py() {
  depends=('python2')

  cd py-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
