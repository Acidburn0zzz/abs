# $Id: PKGBUILD 125190 2015-01-02 22:45:13Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: minder

pkgbase=python-pillow
pkgname=(python-pillow python2-pillow)
pkgver=3.0.0
pkgrel=1
_sanever=2.8.2
_appname=Pillow
_py2basever=2.7
_py3basever=3.5m
pkgdesc="Python Imaging Library (PIL) fork. Python3 version."
arch=('i686' 'amd64')
url="http://python-pillow.github.io/"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'lcms' 'libwebp' 'tk' 'sane')
source=("http://pypi.python.org/packages/source/P/$_appname/$_appname-$pkgver.zip"
        "Sane-$_sanever.tar.gz::https://github.com/python-pillow/Sane/archive/v$_sanever.tar.gz")
sha256sums=('0024fce578547a3da92579a163bae09b1d9f0baf3cd8e4a893dc6c11e4e624bb'
            '102cd25a1696ebd8639066dfe5fba8ac82373a9a1d50376c10df88e34ccc2485')

prepare() {
  cd "${srcdir}"

  # allow sane to build
  gsed -i "s|os.path.join|'../libImaging', &|" "${srcdir}/Sane-${_sanever}/setup.py"
  cp -r "${srcdir}/Sane-${_sanever}" "${srcdir}/${_appname}-${pkgver}/Sane"
  # py2 and py3
  cp -r "${srcdir}/${_appname}-${pkgver}" "${srcdir}/${_appname}2-${pkgver}"
}

package_python-pillow() {
  depends=('python' 'lcms' 'libwebp')
  optdepends=('tk: for the ImageTK module'
              'sane: for the Sane module'
              'python-pyqt4: for the ImageQt module')
  cd "$srcdir/$_appname-$pkgver"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  pushd Sane
    python3 setup.py install --root="$pkgdir/" --optimize=0
  popd
  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname"
  install -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  install -dm755 "$pkgdir/usr/include/python$_py3basever/"
  install -m644 libImaging/*.h  "$pkgdir/usr/include/python$_py3basever/"

  # clean up bins
  cd "$pkgdir/usr/bin"
  for f in *.py; do
    mv "$f" "${f%.py}"
  done
}

package_python2-pillow() {
  pkgdesc="Python Imaging Library (PIL) fork. Python2 version."
  depends=('python2' 'lcms' 'libwebp')
  optdepends=('tk: for the ImageTK module'
              'sane: for the Sane module'
              'python2-pyqt4: for the ImageQt module')
  provides=('python-imaging' 'python2-imaging')
  conflicts=('python-imaging' 'python2-imaging')
  replaces=('python2-imaging')
  cd "$srcdir/${_appname}2-$pkgver"
  gsed -i 's|/usr/local/bin/python$|/usr/bin/env python2|' PIL/OleFileIO.py
  python2 setup.py install --root="$pkgdir/" --optimize=0
  pushd Sane
    python2 setup.py install --root="$pkgdir/" --optimize=0
  popd

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname"
  install -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  install -dm755 "$pkgdir/usr/include/python$_py2basever/"
  install -m644 libImaging/*.h  "$pkgdir/usr/include/python$_py2basever/"

  # clean up bins
  cd "$pkgdir/usr/bin"
  for f in *.py; do
    mv "$f" "${f%.py}2"
  done
}
