pkgname=libosinfo
pkgver=0.3.0
pkgrel=1
pkgdesc="GObject based library API for managing information about operating systems, hypervisors and the (virtual) hardware devices they can support"
arch=('i386' 'amd64')
url="http://libosinfo.org/"
license=('GPL' 'LGPL')
depends=('glib2' 'libxslt')
makedepends=('check' 'gobject-introspection' 'intltool' 'libsoup' 'vala' 'python2')
options=('!libtool' 'libtoolfix')
source=("https://fedorahosted.org/releases/l/i/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('538a3468792e919edf5364fe102d751353ae600a92ad3a24f024424a182cefbc')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-udev \
              --with-usb-ids-path=/usr/share/hwdata/usb.ids \
              --with-pci-ids-path=/usr/share/hwdata/pci.ids
  gmake
}

check() {
  cd $pkgname-$pkgver
  gmake check
}

package() {
  cd $pkgname-$pkgver
  gmake DESTDIR="$pkgdir" install
}
