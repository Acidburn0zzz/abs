pkgname=wesnoth-data
pkgver=1.12.5
pkgrel=1
pkgdesc="A turn-based strategy game on a fantasy world (data files)"
arch=('any')
license=('GPL')
url="http://www.wesnoth.org/"
makedepends=('boost' 'cmake' 'sdl_image' 'sdl_ttf' 'sdl_mixer' 'sdl_net' 'lua' 'pango')
options=(!emptydirs)
source=("http://downloads.sourceforge.net/sourceforge/wesnoth/wesnoth-${pkgver}.tar.bz2"
        "wesnoth-boost.patch")
sha256sums=('599aad40da4b271c5138ac5a4391d9ff9c367ee51f5c30ca138f6baf92aea81d'
            'ccacb1049a71935392b46f919c4045b11936b232522ed2763561fbc0fb1e40b7')

build() {
  cd "${srcdir}/wesnoth-${pkgver}"

  # Try this again in a new version when they fix their linking to boost
  patch -Np1 < ${srcdir}/wesnoth-boost.patch

  mkdir build && cd build
  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DENABLE_OMP=OFF \
      -DENABLE_TOOLS=ON \
      -DMANDIR=share/man \
      -DFIFO_DIR=/var/run/wesnothd
  gmake
}

package() {
  cd "${srcdir}/wesnoth-${pkgver}"

  cd build
  gmake DESTDIR="$pkgdir" install

  rm -r $pkgdir/usr/bin
  rm -r $pkgdir/usr/share/man

  for file in $(grep -l "python" $pkgdir/usr/share/wesnoth/data/tools/*); do
    gsed -i "s|#!/usr/bi#n/env python|#!/usr/bin/env python2|" $file;
  done
}
