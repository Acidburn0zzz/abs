# $Id$
# Maintainer: Jan de Groot  <jgc@archlinux.org>
pkgname=libunique
pkgver=1.1.6
pkgrel=5
pkgdesc="Library for writing single instance applications"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gtk2')
makedepends=('gtk-doc' 'gobject-introspection')
options=('!libtool')
url="http://live.gnome.org/LibUnique"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/1.1/${pkgname}-${pkgver}.tar.bz2
        unique-gdbus.patch
        remove_G_CONST_RETURN.patch)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/unique-gdbus.patch"
  patch -Np1 -i "${srcdir}/remove_G_CONST_RETURN.patch"
  sed -i ''  -e 's|libdir)/pkgconfig|prefix)/libdata/pkgconfig|g' \
                ${srcdir}/${pkgname}-${pkgver}/configure \
		${srcdir}/${pkgname}-${pkgver}/Makefile.in
  sed -i '' -e 's|G_CONST_RETURN|const|g' \
                ${srcdir}/${pkgname}-${pkgver}/unique/uniqueapp.c \
                ${srcdir}/${pkgname}-${pkgver}/unique/uniquebackend.[ch] \
                ${srcdir}/${pkgname}-${pkgver}/unique/uniqueinternals.h \
                ${srcdir}/${pkgname}-${pkgver}/unique/uniquemessage.[ch]
  autoreconf --force --install
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --disable-dbus
  gmake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gmake DESTDIR="${pkgdir}" install

  # Remove documentation, provided by libunique3
  rm -r "$pkgdir/usr/share/gtk-doc"
}
md5sums=('7955769ef31f1bc4f83446dbb3625e6d'
         '02580c54ee840d202278e73da7f0f5a4'
         '4b718287ba6250d30fd4a6a121d25102')
