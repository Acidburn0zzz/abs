# $Id: PKGBUILD 77704 2012-10-13 15:48:59Z bpiotrowski $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:  TDY <tdy@gmx.com>
# Contributor: Grigorios Bouzakis <grbzks[at]gmail[dot]com>

pkgname=tmux
pkgver=2.0
pkgrel=1
pkgdesc='A terminal multiplexer'
url='http://tmux.sourceforge.net/'
arch=('i686' 'amd64')
license=('BSD')
depends=('libevent')
source=(http://downloads.sourceforge.net/tmux/tmux-$pkgver.tar.gz
		LICENSE
		tmux.install)
install=tmux.install

build() {
	cd "$srcdir/$pkgname-$pkgver"
	unset LDFLAGS
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make install DESTDIR=$pkgdir
	install -dm755 "$pkgdir/usr/share/licenses/tmux/"
	install -m644 ../LICENSE "$pkgdir/usr/share/licenses/tmux/LICENSE"

	install -dm755 "$pkgdir/usr/share/examples/tmux/"
	cp -R examples/* "$pkgdir/usr/share/examples/tmux/"
	find "$pkgdir/usr/share/examples/tmux/" -type d -exec chmod 755 '{}' ';'
	find "$pkgdir/usr/share/examples/tmux/" -type f -exec chmod 644 '{}' ';'
	install -dm755 "$pkgdir/usr/share/vim/vimfiles/syntax"
	install -m644 examples/tmux.vim "$pkgdir/usr/share/vim/vimfiles/syntax/tmux.vim"

	install -dm755 $pkgdir/etc/bash-completion.d/
	mv $pkgdir/usr/share/examples/tmux/bash_completion_tmux.sh $pkgdir/etc/bash-completion.d/tmux
}
md5sums=('9fb6b443392c3978da5d599f1e814eaa'
         '71601bc37fa44e4395580b321963018e'
         'd6ebb75682036c2b630d4ff9fe97216c')
